;It Came From The Desert - Greek Translation V1.0 installer by Spyridon Papakotsis
(if (= @installer-version 0)
    (set #esc "")
    (set #esc "[2p")
)
(set #showw 0)
(set @abort-button "Abort Translation")
(set #string_game "It Came From The Desert")
(set #pn "icfd_gr")
(set #ver "V1.0")

(if (= @language "greek")
    (
        (set @abort-button "Ακύρωση Μετάφρασης")
        (set #intro (cat ("\n%s - Ελληνική Μετάφραση (%s)\nΣπυρίδων Παπακώτσης\n\n" #string_game #ver)
                         ("Αυτό το πρόγραμμα θα μεταφράσει την εγκατάσταση του \"%s\", που έχετε στο σκληρό σας δίσκο, στα Ελληνικά.\n" #string_game)
                          "\nΟι υποστηριζόμενες εκδόσεις είναι:\n1. Πρωτότυπη Aμερικάνικη έκδοση\n2. Πρωτότυπη Eυρωπαϊκή έκδοση (IPF#14).\n3. Εκδοση της Hyperion για το AmigaOS4." )
        )
        (set #source_prompt ("Παρακαλώ, επιλέξτε το συρτάρι στο οποίο έχετε εγκαταστήσει το \"%s\"" #string_game))
        (set #source_help (cat ("Εντοπίστε κι επιλέξτε το συρτάρι μέσα στο οποίο βρίκεται εγκατεστημένο το \"%s\". Επειτα, πατήστε το κουμπί \"Συνέχεια\", για να πραγματοποιηθεί η μετάφραση.\n" #string_game)
                       "\nΑν παίζετε την έκδοση του WHDLoad, το πρόγραμμα θα μεταφράσει τ' αρχεία εντός του συρταριού \"data\", εκτός κι αν το έχετε μετονομάσει.\n"
                       "Σε αυτήν την περίπτωση, θα πρέπει να επιλέξετε το συγκεκριμένο συρτάρι και να πατήσετε \"Συνέχεια\", έτσι ώστε να πραγματοποιηθεί η μετάφραση."))
        (set #copy_prompt ("Θέλετε να γίνει η μετάφραση στο συγκεκριμένο συρτάρι, ή να δημιουργηθεί αντίγραφό του;"))
        (set #copy_here "Σε αυτό το συρτάρι")
        (set #copy_bak "Σε αντίγραφό του")
        (set #copy_help (cat "Αν επιθυμείτε να γίνει η μετάφραση του παιχνιδιού στο επιλεγμένο συρτάρι, αντικαθιστώντας την Αγγλική έκδοση,  "
                   ("παρακαλώ ενεργοποιήστε την επιλογή \"%s\".\n" #copy_here)
                   ("\nΑν εναλλακτικά, προτιμάτε να έχετε την Ελληνική έκδοση σε ξεχωριστό συρτάρι, κι επομένως να διατηρήσετε και την Αγγλική έκδοση άθικτη, τότε ενεργοποιήστε την επιλογή \"%s\". Σε αυτήν την περίπτωση,  τα αρχεία θα αντιγραφούν πρώτα σε νέο συρτάρι." #copy_bak)))
        (set #bak_conf "\nΘα δημιουργηθεί αντίγραφο του \"%s\" στο συρτάρι:\n\n%s\n\nκαι η ελληνική μετάφραση θα πραγματοποιηθεί μέσα σε αυτό.")
        (set #working_txt "Γίνεται Μετάφραση...")
        (set #whdl_txt "\nΦαίνεται πως επιλέξατε μια εγκατεστημένη έκδοση WHDLoad.\n\nΗ μετάφραση θα πραγματοποιηθεί εντός του:\n%s")
        (set #hdesert_txt (cat "\nΤο αρχείο \"HDesert\" θα αντικατασταθεί από νέο, το οποίο δεν δεν απαιτεί την ύπαρξη της δισκέτας τους παιχνιδιού στο DF0:, όταν παίζετε από το σκληρό δίσκο.\n"
                      "\nΓια να ξεκινήσετε το παιχνίδι, θα πρέπει να κάνετε διπλό κλικ στο εικονίδιο του HDesert.\n"
                      "\n(Αυτό δεν αφορά τις εγκαταστάσεις WHDLoad)"))
        (set #gnf_txt ("\nΤο \"%s\" δεν βρέθηκε στο επιλεγμένο συρτάρι.\n\nΠαρακαλώ, σιγουρευτείτε πως επιλέξατε το σωστό συρτάρι του παιχνιδιού." #string_game))
    )
    (
        (set #intro (cat ("\n%s - Greek Translation (%s)\nby Spyridon Papakotsis\n\n" #string_game #ver)
                         ("This program will translate your \"%s\" Hard Disk installation to Greek.\n" #string_game)
                          "\nThe supported versions are:\n1. Original U.S. release\n2. Original European release (IPF#14)\n3. Hyperion release for AmigaOS4" )
        )
        (set #source_prompt ("Please select the drawer in which \"%s\" is installed" #string_game))
        (set #source_help (cat ("Navigate to your \"%s\" installation drawer and click on Proceed to apply the patch.\n" #string_game)
                       "\nIf you play the WHDLoad version, the patch should find the files in the \"data\" drawer, unless you have renamed it to something else.\n"
                       "In that case, you'll have to navigate to the renamed data drawer and click on Proceed to apply the translation."))
        (set #copy_prompt ("Do you want to apply the translation to this drawer, or make a copy first?"))
        (set #copy_here ("To this drawer"))
        (set #copy_bak ("To a backup copy"))
        (set #copy_help (cat "If you want to have the game in the selected drawer translated to Greek, thus replacing the English version, "
                   ("please select the \"%s\" option.\n" #copy_here)
                   ("\nIf on the other hand, you prefer having a separate Greek installation and keep your English version intact, then select the \"%s\" option, and the files will be copied to a new drawer first." #copy_bak)))
        (set #bak_conf "\nYour \"%s\" installation will be copied to:\n\n%s\n\nand the Greek translation will be applied there.")
        (set #working_txt "Translating...")
        (set #whdl_txt "\nThis looks like a WHDLoad installation.\n\nThe translation will be applied to:\n%s")
        (set #hdesert_txt (cat "\nThe HDesert script will be replaced by a new one, which doesn't require Disk-1 to be inserted into DF0: in order to play from Hard Disk.\n"
                      "\nTo start the game just double-click on the HDesert icon.\n"
                      "\n(This does not apply to WHDLoad users)."))
        (set #gnf_txt ("\n\"%s\" was not found in the selected drawer.\n\nPlease make sure you select the correct game drawer." #string_game))
    )
)

(set #instdir "")
(procedure P_check
    (if (AND (<> (exists (tackon #instdir "dshell") (noreq))  1)  (<> (exists (tackon #instdir "data/dshell") (noreq))  1) )
       ((abort #gnf_txt))
    )
)

(set #stripslash_arg1 "")
(procedure P_STRIPSLASH
    (if (=  (substr #stripslash_arg1 (- (strlen #stripslash_arg1) 1)) "/")
        ((substr #stripslash_arg1 0 (- (strlen #stripslash_arg1) 1)))
        ((#stripslash_arg1))
    )
)

(message (#intro))

(if #showw (welcome))


(set #instdir
        (askdir
            (prompt (#source_prompt))
            (default "Work:")(disk)
            (help (#source_help))
        )
)

(P_check)

(set #backup
    (askchoice
        (prompt (#copy_prompt))
        (choices
            ((cat esc #copy_here))
            ((cat #copy_bak))
        )
        (help #copy_help)
        (default 0)
    )
)

(if (= #backup 1)
    (
        (set #stripslash_arg1 #instdir)
        (set #instdir (P_STRIPSLASH))
        (set #instdir2 (cat #instdir "_GR"))
        (message (#bak_conf #string_game #instdir2))
        (makedir #instdir2 (infos))
        (copyfiles
            (source #instdir)
            (dest #instdir2)
            (all)
        )
        (set #instdir #instdir2)
    )
)

(set #iswhd 0)
(set #whdldat (tackon #instdir "data"))
(if (= (exists(#whdldat (noreq)))  2)
    ((set #iswhd 1)(set #instdir #whdldat)(message (#whdl_txt #instdir)))
)

(P_check)

(if (= #iswhd 0)
    (
        (message #hdesert_txt)
        (copyfiles (source "extra/HDesert") (dest #instdir) (infos))
    )
)

(working #working_txt)

(set #pn_out ("%s.run" #pn))
(set #res (run ("%s > ENV:%s \"%s\"" #pn #pn_out #instdir)))
(set #pn_log (getenv #pn_out))
(delete ("ENV:%s" #pn_out))
(if (> #res 0)
    (abort #pn_log)
    ((if (= @installer-version 0)
         (  (working #pn_log)
            (set #res (run "C:wait 8"))
            (if (> #res 0)(exit #pn_log)(exit (quiet)))
         )
         (exit #pn_log (quiet))
    ))    
)
