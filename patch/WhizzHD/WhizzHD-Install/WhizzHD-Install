;$VER: Whizz Install 1.2 (2.2.96)
;
;Install-Script & HD-Loader ©1996 Thomas Kessler (kessler@fim.uni-erlangen.de)
;All rights reserved.
;Feel free to contact me via email if you have any questions
;or if you want to publish this installer-script on disk or cd.
;
;Whizz ©1995/96 Flair Software(tm) & Amiga Technologies(tm) GmbH
;All rights reserved.

(set #install-version "1.2")
(set old-level @user-level)
; --- english by Thomas Kessler ---
(set #msg_welcome (cat
	"\n\nWhizz HD-Installer "#install-version"\n\n"
	"for the Whizz AGA disk-version\n"
	"from Flair Software(tm)\n"
	"provided free with the Amiga(tm)\n"
	"from Amiga Technologies(tm) GmbH\n\n"
	"Script & HD-Loader ©1996 Thomas Kessler\nAll rights reserved."))
(set #msg_nomem "\nYou need a memory-expansion for 'WhizzHD'.")
(set #msg_notrackread "Can't find required file:\n'Data/TrackReader'")
(set #msg_choosedir (cat
	"Choose a destination-directory on your harddisk.\n"
	"A new drawer for Whizz will be created there."))
(set #msg_space1 "\nApproximately 1810K of harddisk space\nis needed on your volume ")
(set #msg_space2 "\n\nFree space: ")
(set #msg_space3 "\n\nDelete or transfer some files and try again.")
(set #msg_choose_insert "\nPlease select a disk-drive and\ninsert the disk labeled ")
(set #msg_nodrive1 "\nCan't find DF")
(set #msg_nodrive2 ":\nPlease select another disk-drive.")
(set #msg_baddisk1 "\nPlease insert the disk labeled ")
(set #msg_baddisk2 "\nin your drive DF")
(set #msg_reading1 "Reading disk-image")
(set #msg_reading2 "from")
(set #msg_reading3 "\nPlease wait...")
(set #msg_readerr1 "\nCan't create disk-image")
(set #msg_readerr2 ".\nCheck everything and try again.")
(set #msg_exit1 (cat
	"Note: WhizzHD needs about 1.8M continous memory\n"
	"and does not return to Workbench.\n\n"))
(set #msg_exit2 (cat
	"Whizz was designed for a 'normal' A1200. It should\n"
	"also work on an A4000/030 or better (but then make\n"
	"sure that '68040.library' or similar is not installed\n"
	"or try to start WhizzHD right after a system-reset)."))
(set #na "\n Sorry, no help available.")
; --- deutsch by Thomas Kessler ---
(if (= @language "deutsch")
(
(set #msg_welcome (cat
	"\n\nWhizz HD-Installation "#install-version"\n\n"
	"für die Whizz AGA Disketten-Version\n"
	"von Flair Software(tm), die dem\n"
	"Amiga(tm) von Amiga Technologies(tm) GmbH\n"
	"kostenlos beigelegt wird.\n\n"
	"Skript & HD-Ladeprogramm ©1996 Thomas Kessler\nAlle Rechte vorbehalten."))
(set #msg_nomem "\nSie benötigen eine Speicher-Erweiterung\nfür 'WhizzHD'.")
(set #msg_notrackread "Kann benötigte Datei nicht finden:\n'Data/TrackReader'")
(set #msg_choosedir (cat
	"Wählen Sie ein Verzeichnis auf der Festplatte aus.\n"
	"Dort wird eine neue Schublade für Whizz angelegt."))
(set #msg_space1 "\nEtwa 1810K Speicherplatz wird auf\nder Festplatte ")
(set #msg_space2 " benötigt.\n\nFreier Platz: ")
(set #msg_space3 "\n\nLöschen oder verschieben Sie einige Dateien\nund versuchen Sie es dann erneut.")
(set #msg_choose_insert "\nBitte wählen Sie ein Disketten-Laufwerk und\nlegen Sie folgende Diskette ein: ")
(set #msg_nodrive1 "\nKonnte Disketten-Laufwerk DF")
(set #msg_nodrive2 ": nicht finden.\nBitte wählen sie ein anderes Laufwerk.")
(set #msg_baddisk1 "\nBitte legen Sie die Diskette ")
(set #msg_baddisk2 "\nin Ihr Disketten-Laufwerk DF")
(set #msg_reading1 "Lese Disketten-Datei")
(set #msg_reading2 "von")
(set #msg_reading3 "\nBitte warten ...")
(set #msg_readerr1 "\nKonnte Disketten-Datei")
(set #msg_readerr2 " nicht anlegen.\nÜberprüfen Sie alles und versuchen Sie es erneut.")
(set #msg_exit1 (cat
	"Wichtig: WhizzHD braucht etwa 1.8M fortlaufenden\n"
	"Speicher und kommt nicht auf die Workbench zurück.\n\n"))
(set #msg_exit2 (cat
	"Whizz wurde für einen 'normalen' A1200 entwickelt.\n"
	"Es sollte auch auf einem A4000/030 (oder besser)\n"
	"laufen (stellen Sie sicher, daß die '68040.library'\n"
	"nicht installiert ist oder versuchen Sie, WhizzHD\n"
	"gleich nach einem System-Reset zu starten)."))
))
; --- français d'Anna Grömer ---
(if (= @language "français")
(
(set #msg_welcome (cat
	"\n\nWhizz HD-Installer "#install-version"\n\n"
	"pour la version disquette de Whizz AGA\n"
	"de Flair Software(tm)\n"
	"joint gratuitement avec le Amiga(tm)\n"
	"de Amiga Technologies(tm) GmbH\n\n"
	"Script & HD-Loader ©1996 Thomas Kessler\nTous Droits réservés."))
(set #msg_nomem "\nVous avez besoin d'un expansion\nde mémoire pour 'WhizzHD'.")
(set #msg_notrackread "Fichier demandé introuvable:\n'Data/TrackReader'")
(set #msg_choosedir (cat
	"Choisir un tiroir de déstination.\n"
	"Un nouveau tiroir y sera creé pour Whizz."))
(set #msg_space1 "\nWhizzHD utilisera environ 1810K de votre\ndisque ")
(set #msg_space2 "\n\nCapacité libre: ")
(set #msg_space3 "\n\nSupprimez ou deplaçez quelques fichiers\net recommencez.")
(set #msg_choose_insert "\nChoisissez l'unité à lire et\ninsérez votre disquette ")
(set #msg_nodrive1 "\nImpossible d'accéder à l'unité DF")
(set #msg_nodrive2 ":\nChoissisez un autre lecteur.")
(set #msg_baddisk1 "\nInsérez la disquette ")
(set #msg_baddisk2 "\ndans l'unité DF")
(set #msg_reading1 "Lecture de la disquette dans le fichier\n")
(set #msg_reading2 "d'unité")
(set #msg_reading3 "\nAttendez s.v.p...")
(set #msg_readerr1 "\nCréation du fichier")
(set #msg_readerr2 " impossible.\nVerifiez tout et recommencez.")
(set #msg_exit1 (cat
	"Important: WhizzHD a besoin de 1.8M de mémoire\ncontinu "
	"et ne reviendra pas sur le Workbench.\n\n"))
(set #msg_exit2 (cat
	"Whizz a étè developé pour un A1200 'standard'. Le\n"
	"déroulement devrais être aussi possible sur d'autres\n"
	"Amigas (mais assurez vous que le '68040.library'\n"
	"n'est pas installé ou essayez de debuter WhizzHD\n"
	"immédiatement après le system-reset)."))
))

; --- italiano ---
;(if (= @language "italiano")
;(
;
;))
; ------------------

(complete 0)
(user 2)

(message #msg_welcome)
(complete 1)

(if (> 1000 (- (database "total-mem") (database "graphics-mem"))) (abort #msg_nomem))
(complete 3)

(if (NOT (exists "Data/TrackReader")) (abort #msg_notrackread))
(complete 5)

(set #whizzdir
	(askdir (prompt #msg_choosedir) (help @askdir-help) (default "Work:")))
(set @default-dest (tackon #whizzdir "WhizzHD"))
(complete 7)

(if (<> (getdevice #whizzdir) "RAM")
	(if (< (getdiskspace #whizzdir) 18100000)
		(abort (cat #msg_space1 (getdevice #whizzdir)":" #msg_space2 (/ (getdiskspace #whizzdir) 1024)"K" #msg_space3))
	)
)
(complete 9)

(makedir (tackon #whizzdir "WhizzHD") (prompt "Creating dir") (help #na) (infos) (safe))
(complete 10)

(set #rc 5)
(while (<> #rc 0)
	(
		(set #diskrc 5)
		(while (<> #diskrc 0)
			(
				(set #diskdrive
					(askchoice
						(prompt (cat #msg_choose_insert "\"Whizz 1\"\n"))
						(help @askchoice-help) (choices "DF0:" "DF1:" "DF2:" "DF3:") (default 0)
					)
				)
				(complete 12)
				(if (<> "" (getassign (cat "df" #diskdrive) "d"))
					(set #diskrc 0)
					(message (cat #msg_nodrive1 #diskdrive #msg_nodrive2))
				)
			)
		)
		(set #rc2 5)
		(while (<> #rc2 0)
			(
				(set #rc2
					(run
						(prompt "Checking") (help #na)
						(cat "Data/TrackReader UNIT=" #diskdrive " CHKSUM=0x82afebeb QUIET")
					)
				)
				(if (<> #rc2 0)
					(message (cat #msg_baddisk1 "'Whizz 1'" #msg_baddisk2 #diskdrive":"))
				)
			)
		)
		(complete 15)
		(working (cat #msg_reading1 " 'whizz1.img' " #msg_reading2 " DF"#diskdrive":\n" #msg_reading3))
		(set #rc
			(run
				(prompt "Reading") (help #na)
				(cat "Data/TrackReader " (tackon #whizzdir "WhizzHD/") "whizz1.img UNIT=" #diskdrive)
			)
		)
		(if (<> #rc 0) (message (cat #msg_readerr1 " 'whizz1.img'" #msg_readerr2)))
	)
)
(complete 50)

(set #rc 5)
(while (<> #rc 0)
	(
		(set #diskrc 5)
		(while (<> #diskrc 0)
			(
				(set #diskdrive
					(askchoice
						(prompt (cat #msg_choose_insert "\"Whizz 2\"\n"))
						(help @askchoice-help) (choices "DF0:" "DF1:" "DF2:" "DF3:") (default #diskdrive)
					)
				)
				(complete 52)
				(if (<> "" (getassign (cat "df" #diskdrive) "d"))
					(set #diskrc 0)
					(message (cat #msg_nodrive1 #diskdrive #msg_nodrive2))
				)
			)
		)
		(set #rc2 5)
		(while (<> #rc2 0)
			(
				(set #rc2
					(run
						(prompt "Checking") (help #na)
						(cat "Data/TrackReader UNIT=" #diskdrive " CHKSUM=0x00000000 QUIET")
					)
				)
				(if (<> #rc2 0)
					(message (cat #msg_baddisk1 "'Whizz 2'" #msg_baddisk2 #diskdrive":"))
				)
			)
		)
		(complete 55)
		(working (cat #msg_reading1 " 'whizz2.img' " #msg_reading2 " DF"#diskdrive":\n" #msg_reading3))
		(set #rc
			(run
				(prompt "Reading") (help #na)
				(cat "Data/TrackReader " (tackon #whizzdir "WhizzHD/") "whizz2.img UNIT=" #diskdrive)
			)
		)
		(if (<> #rc 0) (message (cat #msg_readerr1 " 'whizz2.img'" #msg_readerr2)))
	)
)
(complete 90)

(copyfiles
	(prompt "Copy files") (help #na) (source "Data/WhizzHD") (dest (tackon #whizzdir "WhizzHD"))
	(infos) (optional force) (safe) (nogauge)
)
(complete 95)

(copyfiles
	(prompt "Copy files") (help #na) (source "Data/WhizzHD_drawer.info") (dest #whizzdir)
	(newname "WhizzHD.info") (optional force) (safe) (nogauge)
)
(complete 100)

(exit (cat #msg_exit1 (if (<> (database "cpu") "68020") #msg_exit2)))
(user old-level)
(welcome "")
