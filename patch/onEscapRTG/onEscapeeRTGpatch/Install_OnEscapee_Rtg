;---------------------------------------------------------------
; Script to install onEscapee Rtg Patch 
; by Ákos Diviánszky / Invictus
;---------------------------------------------------------------

(message "O N E S C A P E E   R T G\nVersion 1.06 (06.feb.98)\n\nThis scripth will install a gfx-card supporting patch for the game 'onEscapee'.\n This will only work with the commercial release, so don't try it with the old demo releases!\nA Spanish translation of the ReadMe file also included in this package.")

(if
	(< (getversion) (* 39 65536))
	(message "You need at least KickStart 3.0 to run onEscapee!\nAnyway this patch can be installed now.")
)

(if
	(patmatch "68000|68010" (database "cpu"))
	(message "You need at least a Motorola M68020 CPU to run onEscapee!\nMind this after installing this patch!")
)

(welcome)

(if	(= 2 (exists( "DH0:Games" (noreq)))) 
	(set #pgpath "DH0:Games") 
	(if	(= 2 (exists( "DH1:Games" (noreq)))) 
		(set #pgpath "DH1:Games") 
		(if	(= 2 (exists( "DH2:Games" (noreq)))) 
			(set #pgpath "DH2:Games") 
			(if	(= 2 (exists( "DH3:Games" (noreq)))) 
				(set #pgpath "DH3:Games") 
				(if	(= 2 (exists( "Game:" (noreq)))) 
					(set #pgpath "Game:") 
					(if	(= 2 (exists( "Games:" (noreq)))) 
						(set #pgpath "Games:") 
						(set #pgpath "Games_Collection:")
					)
				)
			)
		)
	)
)



(set #OEPath
	(askdir
		(prompt "Choose a directory for the onEscapee starter files.\nNO drawer will be created!")
		(help "It should be beside other game directories eg.: 'Entertain:Games/onEscapee'.\n"
		      "If you have copied onEscapee to your harddrive, select the game's root directory.")
		(default #pgpath)
	)
)

(set @default-dest #OEPath)

(complete 30)

(set #spwanted 
	(askbool
		(prompt	"Do you want to copy the Spanish Doc for onEscapee?\n(Spanish version translated by Fernando Brischetto)")
		(help	"You should have this file if you know or want to learn Spanish!")
	)
)

(copyfiles (source "onEscapee/Start") (dest #OEPath) (infos) )

(copyfiles (source "onEscapee/Game") (dest #OEPath) )

(complete 60)

(copyfiles (source "onEscapee/EscMode") (dest #OEPath) (infos) )


(if (= 1 #spwanted) 
	(
		(copyfiles 
		(source "onEscapee/ReadMe") 
		(dest (tackon #OEPath "ReadMe") )
		(all)
		)
		(copyfiles (source "onEscapee/ReadMe.info") (dest #OEPath) )
	)
) 


(complete 90)

(if (= 1 (askbool
	 (prompt	"The internal format of the preferences file have been changed since v1.02.\nYou must run the new EscMode program or your current setups will be ignored!\n\nDou you want to set the preferences now?")
     	 (help	"You should answer 'Yes'.")
	 )
    )
    (run (tackon @default-dest "EscMode") )
)

(complete 99)

(message	"Special Thanks to the people who helped developing & correcting this patch:\n\nPaul Burkey\nLaurent Baroukh\nMarcin Orlowski")

(complete 100)

(exit	(cat 	"All the files have been copied to the location \""
		#OEPath
		"\"\n\nIf you want play onEscapee, just click on the icon\n\""
		(tackon #OEPath "Start")
		"\"\n\n Have Fun!"
	)
)
