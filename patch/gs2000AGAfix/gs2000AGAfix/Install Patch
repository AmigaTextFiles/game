;Gunship 2000 AGA/CD32 Fix V1.0 installer by Spyridon Papakotsis
(if (= @installer-version 0)
    (set #esc "")
    (set #esc "[2p")
)
(set #showw 0)
(set @abort-button "Abort Patch")
(set #string_game "Gunship 2000 AGA/CD32")
(set #pn "gs2000AGAfix")

(message ((cat ("\n%s Fix (v1.0)\nby Spyridon Papakotsis\n\n" #string_game)
    "This Program disables the notorious feature present in AGA/CD32 versions, "
    "which renders the player\'s missiles useless in Flight and Campaign modes.\n"
    "When patched, the weapons will work again as they do in the ECS version."))
)

(if #showw (welcome))

(set #inst_media
    (askchoice
        (prompt ("Where do you play %s from?" #string_game))
        (choices
            (cat #esc "Hard Disk")
            (cat #esc "Floppy Disks")
        )
        (help (cat "If you have got the AGA or the CD32 version installed on your Hard Disk (including WHDLoad installations), "
                   "please select the \"Hard Disk\" option and you will be asked for the game\'s location when you click on Proceed.\n"
                   "\nIf you play the AGA version from floppies, select the \"Floppy Disks\" option and you will be asked to insert a disk when you click on Proceed."))
        (default 0)
    )
)

(if (= #inst_media 0)
    (set #instdir
        (askdir
            (prompt ("Please select the drawer where %s is installed" #string_game))
            (default "Work:")(disk)
            (help (cat ("Navigate to your %s installation drawer and click on Proceed to apply the patch.\n" #string_game)
                       "\nIf you use the WHDLoad version, the patch should find the files in the \"data\" drawer unless you have renamed it to something else.\n"
                       "In that case, navigate to the renamed data drawer and click on Proceed to apply the patch."))
        )
    )
    (
        (set #instdir "gs_dsk2:")
        (askdisk
            (prompt "\nPlease insert Gunship 2000 AGA - Disk 2 in any drive.\n\nThe volume name should be \"gs_dsk2\".\n\nUse a backup disk.\nDon\'t insert your original disk! ")
            (dest "gs_dsk2")
            (help (cat "Please insert Disk 2 of the game (named \"gs_dsk2:\") and make sure it is not write protected.\n"
                       "\nDo not insert your original disk, but use a write enabled backup disk instead.\n"
                       "You can make a copies of your original disks by using the installer provided on \"Disk 1\".\n"
                       "\nYou can also patch a disk image by mounting it, if you prefer."))
        )
    )
)

(working "Applying Patch...")

(set #pn_out ("%s.run" #pn))
(set #res (run ("%s > ENV:%s \"%s\"" #pn #pn_out #instdir)))
(set #pn_log (getenv #pn_out))
(delete ("ENV:%s" #pn_out))
(if (> #res 0)
    (abort #pn_log)
    ((if (= @installer-version 0)
         (  (working #pn_log)
            (set #res (run "C:wait 8"))
            (if (> #res 0)(exit #pn_log)(exit (quiet)))
         )
         (exit #pn_log (quiet))
    ))    
)
