;
;$VER: GDTrainer Installer script v2.01 (c) 1997 John Girvin
;

(welcome "Welcome to the Gloom Trainer Installer v2.01")
(set @app-name "GDTrainer")
(set #GDESTDIR "DH0:")

(set #GUSEGUI 0)
(set #GDEFTRAINERS 0)

(if
	(<> @user-level 2)
	(abort "You must select 'Expert' user level")
)

(message "\n\nWelcome to the GDTrainer Installer\n"
		 "(c) 1997 John Girvin\n\n"
		 "Please read the documentation thoroughly before"
		 " attempting to use this installer!\n\n"
		 "This is version 2.01\n\n"
		 "Click 'Proceed' to begin."
)

(set #GDESTDIR
	(askdir
		(prompt "Where do you have Gloom Deluxe installed?\n"
				"(select the directory containing the 'playgloom' program)"
		)
		(help @askdir-help)
		(default #GDESTDIR)
	)
)
(set @default-dest #GDESTDIR)

(set #GUSEGUI
	(askchoice
		(prompt "Do you want the cheat GUI to appear every time you load GDTrainer?\n"
				"(this may be changed later by editing the tooltypes of GDTrainer)"
		)
		(help @askchoice-help)
		(choices "Yes" "No")
		(default #GUSEGUI)
	)
)

(set #GDEFTRAINERS
	(askoptions
		(prompt "Please select the default trainers to use:\n"
				"(these may be changed later by either\n"
				"editing the tooltypes of GDTrainer\n"
				"or using the cheat GUI)"
		)
		(help @askoptions-help)
		(choices "Infinite lives"
				 "Maximum gun power"
				 "Infinite invisibility"
				 "Infinite thermo glasses"
				 "Infinite bouncy bullets"
				 "Super gun boosts"
    	)
		(default #GDEFTRAINERS)
	)
)

(copyfiles
	(prompt "Copying trainer program")
	(help @copyfiles-help)
	(source "GDTrainer")
	(dest #GDESTDIR)
	(nogauge)
	(noposition)
	(optional fail force)
)

(copyfiles
	(prompt "Copying trainer program icon")
	(help @copyfiles-help)
	(source "GDTrainer_icon")
	(newname "GDTrainer.info")
	(dest #GDESTDIR)
	(nogauge)
	(optional fail force)
)

(if (= #GUSEGUI 1)
	(tooltype
		(prompt "Setting trainer program icon tooltypes")
		(dest (tackon #GDESTDIR "GDTrainer"))
		(settooltype "NOGUI" "")
	)
)

(if (<> 0 (IN #GDEFTRAINERS 0))
	(tooltype
		(prompt "Setting trainer program icon tooltypes")
		(dest (tackon #GDESTDIR "GDTrainer"))
		(settooltype "INF_LIVES" "")
	)
)

(if (<> 0 (IN #GDEFTRAINERS 1))
	(tooltype
		(prompt "Setting trainer program icon tooltypes")
		(dest (tackon #GDESTDIR "GDTrainer"))
		(settooltype "INF_BIGGUN" "")
	)
)

(if (<> 0 (IN #GDEFTRAINERS 2))
	(tooltype
		(prompt "Setting trainer program icon tooltypes")
		(dest (tackon #GDESTDIR "GDTrainer"))
		(settooltype "INF_INVIS" "")
	)
)

(if (<> 0 (IN #GDEFTRAINERS 3))
	(tooltype
		(prompt "Setting trainer program icon tooltypes")
		(dest (tackon #GDESTDIR "GDTrainer"))
		(settooltype "INF_THERMO" "")
	)
)

(if (<> 0 (IN #GDEFTRAINERS 4))
	(tooltype
		(prompt "Setting trainer program icon tooltypes")
		(dest (tackon #GDESTDIR "GDTrainer"))
		(settooltype "INF_BOUNCY" "")
	)
)

(if (<> 0 (IN #GDEFTRAINERS 5))
	(tooltype
		(prompt "Setting trainer program icon tooltypes")
		(dest (tackon #GDESTDIR "GDTrainer"))
		(settooltype "MAXBOOSTS" "")
	)
)

(copyfiles
	(prompt (cat "Copying trainer loader"))
	(help @copyfiles-help)
	(source "GDTBoot")
	(dest #GDESTDIR)
	(nogauge)
	(optional fail force)
)
