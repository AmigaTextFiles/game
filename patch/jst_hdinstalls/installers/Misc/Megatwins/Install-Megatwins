
;----------------------------

;try to figure out a place where the user usually installs his games
(if (exists "Games:" (noreq) )
    (set @default-dest "Games:")
    (if (exists "SYS:Games" (noreq) )
        (set @default-dest "SYS:Games")
        (if (exists "Work:Games" (noreq) )
            (set @default-dest "Work:Games")
            (if (exists "JEUX:" (noreq) )
               (set @default-dest "JEUX:")
               (set @default-dest "SYS:")
            )
        )
    )
)

(message "\n\n\nThis loader needs the JST program (NOT INCLUDED)\n to be copied in your path\n\n(if you don't have it already)\n\nJST is available from aminet (game/patch) or on my site")


(set default-dest
(askdir
	(prompt ("Where should %s installed ?\nA drawer \"%s\" will automatically created." @app-name @app-name))
	(help @askdir-help)
	(default @default-dest)
	(disk)
)
)

(set #dest (tackon default-dest @app-name))

(set #CI_unit
	(askchoice
		(prompt "From which disk unit do you want\nto install the game")
		(help	@askoptions-help)
		(choices
		   "DF0:"
		   "DF1:"
		   "DF2:"
		   "DF3:"
		)
	)
)

(set #CI_drive ("DF%ld:" #CI_unit))

(makedir #dest
	(help @makedir-help)
	(infos)
)

;----------------------------

(copyfiles
	(help @copyfiles-help)
	(source "MegatwinsHD")
	(dest #dest)
	(infos)
)

(copyfiles
	(help @copyfiles-help)
	(source "megatwinshd.readme")
	(dest #dest)
	(infos)
)

(message ("\nInsert %s disk 1 into drive %s !" @app-name #CI_drive))
	(if
		(= 0 (run ("readmtdisk %ld \"%s/megatwins.d1\" 2 159 >con:0/0/450/200//CLOSE/WAIT" #CI_unit #dest)))
		("")
		(abort "\"readmtdisk\" program not found !")
	)

(message ("\nInsert %s disk 2 into drive %s !" @app-name #CI_drive))
	(if
		(= 0 (run ("readmtdisk %ld \"%s/megatwins.d2\" 2 159 >con:0/0/450/200//CLOSE/WAIT" #CI_unit #dest )))
		("")
		(abort "\"readmtdisk\" program not found !")
	)

(exit)

