(set GameDir "Zool2")

;try to figure out a place where the user usually installs his games
(if (exists "Games:" (noreq) )
    (set @default-dest "Games:")
    (if (exists "SYS:Games" (noreq) )
        (set @default-dest "SYS:Games")
        (if (exists "Work:Games" (noreq) )
            (set @default-dest "Work:Games")
            (if (exists "JEUX:" (noreq) )
               (set @default-dest "JEUX:")
               (set @default-dest "SYS:")
            )
        )
    )
)

;ask the user to select a directory to install the game into
(set default-dest
     (tackon (askdir (prompt "Where would you like " @app-name " installed?\n"
                             "A drawer called " GameDir " will be created.")
                     (help @askdir-help)
                     (default @default-dest)
                     (newpath)
             )
     GameDir
     )
)
(set @default-dest default-dest)

;create the selected directory
(makedir @default-dest
         (infos)
)

(makedir (tackon @default-dest "Z2HD1")
)

(makedir (tackon @default-dest "Z2HD2")
)

(askdisk
	(prompt	"\n\n\n\n\n\nPlease insert Zool disk 1 in any drive")
	(dest		"z2d1")
	(help		"Insert disk 1 in any floppy drive\nor you'll wait for a while!")
)

(copyfiles (source "z2d1:")
           (dest (tackon @default-dest "Z2HD1"))
	   (pattern "#?")
           (files)
)

(askdisk
	(prompt	"\n\n\n\n\n\nPlease insert Zool disk 2 in any drive")
	(dest		"z2d2")
	(help		"Insert disk 2 in any floppy drive\nor you'll wait for a while!")
)

(copyfiles (source "z2d2:")
           (dest (tackon @default-dest "Z2HD2"))
	   (pattern "#?")
           (files)
)

;copy all extra files to this directory

(copyfiles (source "Zool2")
           (dest @default-dest)
)

(copyfiles (source "jscaches")
           (dest @default-dest)
)

(copyfiles (source "Zool2.icon")
           (dest @default-dest)
	   (newname "Zool2.info")
)

(message "\n\n\n\n\nI will now decrunch and patch the executable")

	(if

		(= 0 (run ("xfddecrunch >NIL: %s %s" (tackon @default-dest "Z2HD1/z2") (tackon @default-dest "z2") )) )
		("")
		(abort "Install XFDDecrunch package first\n\n(util/pack/XFDDecrunch.lha on aminet)")
	)

	(if

		(= 0 (run ("patchexe %s" (tackon @default-dest "z2") )) )
		("")
		(abort "patchexe could not be executed!!")
	)



; cleanup

(delete (tackon @default-dest "Z2HD1/z2"))
(delete (tackon @default-dest "Z2HD2/installer"))
(delete (tackon @default-dest "Z2HD2/install_zool2"))

(complete 100)

