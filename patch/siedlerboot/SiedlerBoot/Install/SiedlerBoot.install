; $VER: SiedlerBoot.install 3.56 (24.03.97)
; Install script for SiedlerBoot (Workbench 2.0 or higher)
;
(COMPLETE 0)
;(USER 2)
;(DEBUG)
(SET #ApplicationName "SiedlerBoot")
(SET #ApplicationDir "sys:")
(SET #ApplicationDir1 "work:")
(SET #ApplicationDir2 "t:")
(SET #ApplicationDir3 "game:")
(SET #ApplicationDir4 "work:games")
(SET #ArchiveDir "/")
(SET #Language -1)
(SET #Documents -1)
(SET #ApplicationKeyfile (CAT #ApplicationName ".key"))

;=============================================================================
; German strings


(SET #App-InstallMsg
(CAT "\n\%s Installationsskript.\n"
        "Dieses Skript installiert %s auf Ihrem Amiga.\n\n"
        "Lesen Sie das Anleitungs-File für weitere Informationen\n"
        " über den Vertrieb von %s.\n\n"
        "%s © 1997 Guido Mersmann\n"
        "All Rechte reserviert."
))

(SET #App-Where
(CAT "Wo möchten Sie %s installieren ?\n"
        "(Ein Verzeichnis wird angelegt)"
))

(SET #App-Update
(CAT "\nEin %s-Verzeichnis existiert im ausgewählen"
        " Verzeichnis. Wenn Sie fortfahren,"
        " bestätigen Sie, daß Sie die alte %s-Installation"
        " mit der neuesten Version updaten wollen.\n"
))

(SET #App-Exit "\n%s installiert !!")




;=============================================================================
; English strings

(IF (<> @language "deutsch")
(
(SET default_lang 4)


(SET #App-InstallMsg
(CAT "\n\%s installation script.\n"
        "This script installs %s on your Amiga.\n\n"
        "Read the DOK-file for more information\n"
        " on the distribution of %s.\n\n"
        "%s © 1997 Guido Mersmann\n"
        "All rights reserved."
))

(SET #App-Where
(CAT "Where would you like to install %s ?\n"
        "(A drawer will be created)"
))

(SET #App-Update
(CAT "\nA %s drawer does already exist in the selected "
        " directory. By selecting \"Proceed\", you"
        " indicate that you want to update the %s"
        " installation with the most recent version.\n"
))

(SET #App-Exit "\n%s installed !!")


))
;----------------------------------------------------------------------------
;----------------------------------------------------------------------------
;       Copy_File (Name)
;----------------------------------------------------------------------------
;----------------------------------------------------------------------------
(PROCEDURE P_CopyFile #Dummy
                   (COPYFILES
        (SOURCE (TACKON #ArchiveDir #Dummy))
        (DEST #ApplicationDir)
        (HELP @copyfiles-help)
        (OPTIONAL <force>)
        (ALL)
        (INFOS)
                   )
) ;PROCEDURE
;----------------------------------------------------------------------------
;----------------------------------------------------------------------------
;       GetApplicationDir
;----------------------------------------------------------------------------
;----------------------------------------------------------------------------
(PROCEDURE P_GetApplicationDir
(SET #Dummy #ApplicationDir)
(if (= (exists (TACKON #ApplicationDir1 #ApplicationName) (NOREQ)) 2) (SET #Dummy #ApplicationDir1))
(if (= (exists (TACKON #ApplicationDir2 #ApplicationName) (NOREQ)) 2) (SET #Dummy #ApplicationDir2))
(if (= (exists (TACKON #ApplicationDir3 #ApplicationName) (NOREQ)) 2) (SET #Dummy #ApplicationDir3))
(if (= (exists (TACKON #ApplicationDir4 #ApplicationName) (NOREQ)) 2) (SET #Dummy #ApplicationDir4))

(SET #ApplicationDir
        (TACKON
                (ASKDIR
                        (PROMPT (#App-Where #ApplicationName))
                        (HELP @askdir-help)
                        (default #Dummy)
                )
                #ApplicationName
        )
)
(SET @default-dest #ApplicationDir)
) ;PROCEDURE
;----------------------------------------------------------------------------
;----------------------------------------------------------------------------
;       P_Update
;----------------------------------------------------------------------------
;----------------------------------------------------------------------------
(PROCEDURE P_Update
(if (= (exists #ApplicationDir) 2)
        (message (#App-Update #ApplicationName #ApplicationName #ApplicationName))
)
) ;PROCEDURE
;----------------------------------------------------------------------------
;----------------------------------------------------------------------------
;       P_CopyArchiveIcon
;----------------------------------------------------------------------------
;----------------------------------------------------------------------------
(PROCEDURE P_CopyArchiveIcon
                   (COPYFILES
        (SOURCE (CAT (CAT (TACKON #ArchiveDir "/") #ApplicationName) ".info"))
        (DEST (TACKON #ApplicationDir "/"))
        (HELP @copyfiles-help)
                   )
) ;PROCEDURE
;----------------------------------------------------------------------------
;----------------------------------------------------------------------------
;       MAIN
;----------------------------------------------------------------------------
;----------------------------------------------------------------------------
(message (#App-installmsg #ApplicationName #ApplicationName #ApplicationName #ApplicationName))
(welcome)

 (SET #ApplicationDir (P_GetApplicationDir))

(COMPLETE 1)
 (P_Update)

;***** Delete old Data and Prefs
(MAKEDIR #ApplicationDir (INFOS))

 (P_CopyFile #ApplicationName)
 (P_CopyFile "Siedler")
 (P_CopyFile "Readme")
 (P_CopyArchiveIcon)

 (COMPLETE 100)
;(exit #App-exit)
