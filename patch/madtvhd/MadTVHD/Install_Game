(set GameDir "MadTV")

;try to figure out a place where the user usually installs his games
(if (exists "Games:" (noreq) )
    (set @default-dest "Games:")
    (if (exists "SYS:Games" (noreq) )
        (set @default-dest "SYS:Games")
        (if (exists "Work:Games" (noreq) )
            (set @default-dest "Work:Games")
            (if (exists "JEUX:" (noreq) )
               (set @default-dest "JEUX:")
               (set @default-dest "SYS:")
            )
        )
    )
)

;ask the user to select a directory to install the game into
(set default-dest
     (tackon (askdir (prompt "Where would you like " @app-name " installed?\n"
                             "A drawer called " GameDir " will be created.")
                     (help @askdir-help)
                     (default @default-dest)
                     (newpath)
             )
     GameDir
     )
)
(set @default-dest default-dest)

;create the selected directory
(makedir @default-dest
         (infos)
)

;create the saves directory
(makedir (tackon @default-dest "saves")
         (infos)
)

(copyfiles (source "Game")
           (dest @default-dest)
)

(copyfiles (source "Game.icon")
           (dest @default-dest)
	   (newname "Game.info")
)

(askdisk
	(prompt	("\n\n\n\n\n\nPlease insert %s disk 1 in any drive" @app-name))
	(dest		"MTV1")
	(help		"Insert disk in any floppy drive\nor you'll wait for a while!")
)

(copyfiles (source "MTV1:")
           (dest @default-dest)
	   (files)
	   (pattern "#?")
)

(askdisk
	(prompt	("\n\n\n\n\n\nPlease insert %s disk 2 in any drive" @app-name))
	(dest		"MTV2")
	(help		"Insert disk in any floppy drive\nor you'll wait for a while!")
)

(copyfiles (source "MTV2:")
           (dest @default-dest)
	   (files)
	   (pattern "#?")
)

(delete (tackon @default-dest ".info"))
(delete (tackon @default-dest "install.info"))
(delete (tackon @default-dest "add21"))



