;****************************

(set #last-disk 1)          ;amount of disks
(set #disk-size 901120)     ;size of each disk

;****************************
;----------------------------
; Checks if given program is reachable via the path
; if not abort install
; IN:  #program - to check
; OUT: -

(procedure P_chkrun
  (if
    (= 0 (run ("cd SYS:\nWhich %s" #program)))
    ("")
    (abort ("You must install \"%s\" first !\nIt must be accessible via the path.\nYou can find it in the whdload package." #program))
  )
)

;----------------------------
; Create disk-Image using DIC
; IN:  #dest        - DestinationPath
;      #CI_diskname - DiskName
;      #CI_diskno   - DiskNumber
;      #CI_drive    - DriveToReadFrom
; OUT: -

(procedure P_image
  (message ("\nInsert \"%s\" into drive %s !\n\n(make sure it's the right disk because it will not checked)" #CI_diskname #CI_drive))
  (if
    (= 0 
      (run ("cd \"%s\"\nDIC %s FD=%ld LD=%ld SIZE=%ld >CON:///1000//CLOSE" #dest #CI_drive #CI_diskno #CI_diskno #disk-size))
    )
    ("")
    (abort "\"DIC\" has failed to create a diskimage")
  )
)

(set #program "DIC")
(P_chkrun)

; in expert mode ask for source drive
(if
  (= @user-level 2)
  (
    (set #CI_drive
      (askchoice
	(prompt "Select source drive for diskimages")
	(default 0)
	(choices "DF0:" "DF1:" "RAD:" "Enter Device")
	(help @askchoice-help)
      )
    )
    (select #CI_drive
      (set #CI_drive "DF0:")
      (set #CI_drive "DF1:")
      (set #CI_drive "RAD:")
      (set #CI_drive
        (askstring
          (prompt "Select source drive for diskimages")
          (default "DF0:")
          (help @askstring-help)
        )
      )
    )
  )
  (set #CI_drive "DF0:")
)

(set @default-dest
  (askdir
    (prompt ("Where is your Dungeon Master installed ?"))
    (help @askdir-help)
    (default @default-dest)
    (disk)
  )
)
(set #dest (@default-dest))

;----------------------------

(set #CI_diskno 1)
(while
  (<= #CI_diskno #last-disk)
  (
    (set #CI_diskname ("%s Disk %ld" @app-name #CI_diskno))
    (P_image)
    (run ("FileNote Disk.%ld %s Quiet" #CI_diskno @app-name))
    (rename ("%s/disk.%ld" #dest #CI_diskno) ("%s/UNIT%ld" #dest #CI_diskno))
    (set #CI_diskno (+ #CI_diskno 1))
  )
)

;----------------------------

(exit)
