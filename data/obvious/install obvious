; $VER: install obvious 1.0 (8.4.2001)
;
; Installer script for obvious style for dynAMIte
;
; Written by Thomas Aglassinger <agi@rieska.oulu.fi>
; Feel free to modify this script and include it with your own style,
; provided you indicate that you are the new author.


; some stuff you should change if you use this script for another style

(set #style "obvious")
(set #description "This style makes it easy to relate player figures to names in the title and chat.")

(set #exit-message (cat "The \"" #style "\" style has been installed."))


; ensure that minimum installer version to be installed

(set #minimum-installer-version 43)
(set #minimum-installer-revision 3)
(set #minimum-installer-id
   (+ #minimum-installer-revision
       (* #minimum-installer-version 65536)
   )
)
(debug "  minimum-installer-id = " #minimum-installer-id)
(debug "  current-installer-id = " @installer-version)

(if (< @installer-version #minimum-installer-id)
  (
    (set #message-wrong-installer
    (cat "You have an old version of the program `Installer' on your Amiga.\n\n"
         "The installation procedure needs at least Installer "
         #minimum-installer-version "." #minimum-installer-revision ".\n\n"
         "Please obtain a newer version from Aminet.\n\n"
    ))

    (debug "installer too old")
    (abort #message-wrong-installer)
  )
)


; welcome screen

(set #welcome-message
   (cat "\nThis installer script installs and activates the \"" #style "\" style for dynAMIte.\n\n"
        #description
))

(welcome #welcome-message)

; ask where dynAMIte is installed

(set #dynamite-drawer
   (askdir
      (prompt "Select the directory in which dynAMIte is installed")
      (help "This is the directory where the dynAMIte program and icon are located")
      (default "work:")
   )
)


; resolve information

(set #gfx-file      (cat #style ".gfx"))
(set #dynamite-file (tackon #dynamite-drawer "dynAMIte"))
(set #style-drawer  (tackon #dynamite-drawer "styles"))

(debug ("gfx-file=\"%s\"\ndynamite-file=\"%s\"\nstyle-drawer=\"%s\""
        #gfx-file #dynamite-file #style-drawer
))


; copy graphics

(copyfiles
	(prompt "Copying graphics")
	(help "This file represents the graphics of the style.")
	(source #gfx-file)
	(dest #style-drawer)
	(confirm)
)


; set tooltype

(tooltype
   (prompt (cat "Making \"" #style "\" the default style "
                "by setting tooltype \"STYLE=" #style "\"."))
   (help   (cat "If you skip this, dynAMIte will continue to use the current style. "
                "You can manually set this tooltype later.\n\n"
                "If continue, you can later revert to current style by removing the tooltype."))
   (settooltype "STYLE" #style)
   (dest #dynamite-file)
   (confirm)
)


; exit

(exit #exit-message
	(quiet)
)
