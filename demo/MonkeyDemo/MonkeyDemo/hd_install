. HD Install script for Monkey Island Demo (05.12.91) jmc
.    
.key drive,file
.bra {
.ket }
.def file "MonkeyDemo"
echo "The Secret of Monkey Island Demo install script."
echo ""
echo "This script will install the demo on the HD partition"
echo "that is supplied as an argument to it when started."
echo "If none was given it will stop shortly."
echo ""
if "{drive}" EQ ""
   skip flop
endif
if not exists {drive}
   echo "uh-oh...problems..."
   skip probs
endif
if not exists {file}
   skip wrongplace
endif
. All is well on we go
makedir {drive}MonkeyIsle
makedir {drive}MonkeyIsle/Rooms
copy MonkeyIsle.info to {drive}
copy MonkeyDemo#? to {drive}MonkeyIsle/
copy Rooms/#? to {drive}MonkeyIsle/Rooms/
echo "Let's see if all the files made it."
echo ""
dir {drive} all
echo ""
echo "All done!"
skip exit
. All done
lab flop
   echo "No HD name specified. Need DH0:, DH1:, etc."
   echo "Exiting..."
   skip exit
lab probs
   echo "Parameters supplied are no good. Device {drive} is not there!"
   echo "Exiting..."
   skip exit
lab wrongplace
   echo "You have not cd'ed to the directory containing the demo files!"
   echo "Exiting..."
   skip exit
lab exit
