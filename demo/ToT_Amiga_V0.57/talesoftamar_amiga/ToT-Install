;$VER: ToT-Install (2002-11-25)
;Sebastian Huebner <cyco@baud.de>

(onerror (P_CleanUp))

(procedure P_CleanUp
  (run "Goodies/SoundQuit.exe" (safe))
)

;check for installer version >=44 (OS3.5))
(set #instver (/ @installer-version 65536))
(if (>= #instver 44)
    (
      (effect "lower_right" "radial" $660000 $000000)
      (showmedia "symbol" "Goodies/Title.iff" "upper_left" "none" 0)
    )
)
;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~


(set #totversion "Client 'Enhanced' 0.57 R1")
(set #totdirname "TalesOfTamar" )
(set #totassign  "ToT" )
(set #deletedatafiles "(DEMO1_TCPIPReq.dat|DEMO1_TCPIPReq.RAWB|Kundschafter.RAWB|Waren.dat)")
(run "run >NIL: Goodies/Sound.exe" (safe))

;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
;~~~~~~~~~~~~~~~~~~alten Pfad suchen~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
; altes assign vorhanden? wenn nicht: "games:" vorhanden?
(set #oldplace (getassign #totassign))
(set #games    (getassign "games"   ))
(if (and (<> "" #oldplace) (exists #oldplace))
    ( (set #oldassignfound 1) (set @default-dest (pathonly #oldplace)) )
    (if (and (<> "" #games) (exists #games))
        (set @default-dest "Games:")
    )
)


(complete 0)


;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
;~~~~~~~~~~~~~~~~~~~~~~~Texte~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

;~~~~~~~~~~~~~~~~~~Englische Texte~~~~~~~~~~~~~~~~~~
(set #introduction
  (cat "\nWelcome to the Installation of\n\nT A L E S\nO F\nT A M A R\n\n\n\nVersion: "
       #totversion "\n\nhttp://www.tamar.net\nMartin Wolf <eternity@pride.de>"))

  (set #licensefile "ToT-License.guide")
  (set #license-request
    (cat "\n\n"
         "Please read the\n"
         "LICENSE AGREEMENT\n"
         "in the window above carefully.\n"
         "\n\n\n"
         "Do you accept all of the license terms and\n"
         "want to proceed with the installation,\n"
         "or\n"
         "do you reject at least one of the license terms\n"
         "and want to abort the installation?")
  )
  (set #license-request-old
    (cat "\n"
         "You are using an outdated version " #instver " of the installer (current: 44.10).\n"
         "Therefore the LICENSE AGREEMENT can't be displayed here directly.\n"
         "\n\n"
         "Please read \"" #licensefile "\" first,\n"
         "before you proceed with the installation.\n"
         "To do this you can abort the installation now\n"
         "and restart it after reading the agreement."
         "\n\n\n"
         "Have you read the license agreement, agree to it\n"
         "and would like to proceed with the installation?")
  )


(set #ask-path (cat "Where should the directory \"" #totdirname "\" be created?\n"
                    "An existing directory will be used."
               ))
(set #ask-path-help (cat "A new directory \"" #totdirname
                         "\" will be created or an existing one will be used."))
(set #ask-assign (cat "Should the assign \"" #totassign ":\" be added to your "
                      "\"S:user-startup\"?"))
(set #ask-assign-help (cat "The assign \"" #totassign ":\" can be added to your "
                           "\"S:user-startup\".\n\n"
                           "Because it is needed by " @app-name ", "
                           "you should really do this. Otherwise you have to "
                           "perform the addition by hand:\n\n   "
                           ))
(set #fileatplace (cat "Unable to create new directory:"
                       "A file exists at that position ") )

(set #goodbye "\n\n\n\nGoodbye and have fun with Tales of Tamar!")


;~~~~~~~~~~~~~~~~~~Deutsche Texte~~~~~~~~~~~~~~~~~~
(if (= @language "deutsch")
 (
  (set #introduction
    (cat "\nWillkommen zur Installation von\n\nT A L E S\nO F\nT A M A R\n\n\n\nVersion: "
         #totversion "\n\nhttp://www.tamar.net\nMartin Wolf <eternity@pride.de>"))

  (set #licensefile "ToT-Lizenz.guide")
  (set #license-request
    (cat "\n\n"
         "Bitte lesen Sie den\n"
         "LIZENZVERTRAG\n"
         "im obigen Fenster aufmerksam durch.\n"
         "\n\n\n"
         "Möchten Sie dem Vertrag in vollem Umfang zustimmen\n"
         "und die Installation fortsetzen,\n"
         "oder\n"
         "möchten Sie den Vertrag ablehnen\n"
         "und die Installation abbrechen?")
  )
  (set #license-request-old
    (cat "\n"
         "Sie benutzen eine veraltete Version " #instver " des Installers (aktuell: 44.10).\n"
         "Deshalb kann der LIZENZVERTRAG hier leider nicht direkt angezeigt werden.\n"
         "\n\n"
         "Bitte lesen Sie erst \"" #licensefile "\",\n"
         "bevor Sie die Installation durchführen.\n"
         "Dazu können Sie die Installation an diesem Punkt unterbrechen\n"
         "und nach dem Lesen des Vertrages neu starten."
         "\n\n\n"
         "Haben Sie den Vertrag gelesen, stimmen ihm in vollem Umfang zu\n"
         "und möchten die Installation fortsetzen?")
  )



  (set #ask-path (cat "Wo soll das Verzeichnis \"" #totdirname "\" angelegt werden?\n"
                      "Ein schon existierendes Verzeichnis wird weiter benutzt."
                 ))
  (set #ask-path-help (cat "A neues Verzeichnis \"" #totdirname
                           "\" wird erstellt oder ein bereits vorhandenes weiter benutzt."))
  (set #ask-assign (cat "Soll die Zuweisung \"" #totassign ":\" zu Ihrer "
                      "\"S:user-startup\" hinzugefügt werden?"))
  (set #ask-assign-help (cat "Die Zuweisung \"" #totassign ":\" kann zu Ihrer "
                           "\"S:user-startup\" hinzugefügt werden.\n\n"
                           "Da die Zuweisung von " @app-name " benötigt wird, "
                           "sollten Sie die Ergänzung zulassen, andernfalls müssen sie "
                           "diese per Hand durchführen:\n\n   "
                           ))
  (set #fileatplace (cat "Kann neues Verzeichnis nicht erstellen:"
                         "Eine Datei befindet sich an der Position ") )

  (set #goodbye "\n\n\n\nViel Spaß mit Tales of Tamar!")
 )
)

; ~~~~~~~~~Cadenas en español 1.0 (25.11.02) © Dámaso D. Estévez ~~~~~~~~~~~~

(if (= @language "español")
 (
  (set #introduction
    (cat "\nBienvenido al guión instalador de\n\nT A L E S\nO F\nT A M A R\n\n\n\nVersión: "
         #totversion "\n\nhttp://www.tamar.net\nMartin Wolf <eternity@pride.de>"))

  (set #licensefile "ToT-Licencia.guide")
  (set #license-request
    (cat "\n\n"
         "Por favor, lea con atención\n"
         "EL ACUERDO DE LICENCIA\n"
         "que se muestra en la ventana superior.\n"
         "\n\n\n"
         "¿Acepta todos los puntos de éste\n"
         "y quiere proceder a la instalación,\n"
         "o\n"
         "rechaza algún punto de dicho acuerdo\n"
         "y quiere abortar la instalación?")
  )
  (set #license-request-old
    (cat "\n"
         "Está utilizando una versión anticuada del programa instalador (la versión " #instver ", cuando la actual es la 44.10).\n"
         "Por ello EL ACUERDO DE LICENCIA no puede ser mostrador desde aquí directamente.\n"
         "\n\n"
         "Por favor lea primero el fichero \"" #licensefile "\",\n"
         "antes de proceder a realizar la instalación.\n"
         "Para ello aborte la instalación ahora\n"
         "y vuelva a realizarla después de leer dicho acuerdo."
         "\n\n\n"
         "¿Ha leido el acuerdo de licencia, y está de acuerdo con todos\n"
         " sus puntos y quiere proceder con la instalación?"))

  (set #ask-path (cat "¿Dónde debe crearse el directorio \"" #totdirname "\"?\n"
                    "De existir previamente, se utilizará éste."
               ))
  (set #ask-path-help (cat "Un nuevo directorio \"" #totdirname
                         "\" se creará o se utilizará uno ya existente."))
  (set #ask-assign (cat "¿Debo añadir la asignación \"" #totassign ":\" en el fichero "
                      "\"S:user-startup\" de su sistema?"))
  (set #ask-assign-help (cat "La asignación \"" #totassign ":\" puede ser añadida a su "
                           "fichero \"S:user-startup\".\n\n"
                           "Debido a que es requerida por `" @app-name "', "
                           "debería aceptar. De otra forma tendrá que "
                           "realizar dicha tarea a mano:\n\n   "
                           ))
  (set #fileatplace (cat "Imposible crear un nuevo directorio: "
                       "ya existe un fichero en dicha posición ") )

  (set #goodbye "\n\n\n\n¡Hasta la próxima y que disfrute con `Tales of Tamar'!")

  )
)

;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
;~~~~~~~~~~~~~~~~~~Installation~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

(complete 1)

; introduction
(message #introduction (all))


(complete 2)


; show license agreement
(if (>= #instver 44)
    (
      (showmedia 'license' #licensefile 'upper_right' 'medium' 1 'wordwrap')
      (message #license-request (all))
      (closemedia license)
    )
    (
      ; installer is too old to show license directly
      (message #license-request-old (all))
    )
)


(complete 10)


;choose user level
(welcome)


(complete 20)


; wo installieren?
(set #totpath (askdir (prompt #ask-path) (help #ask-path-help)
                      (default @default-dest))
)

; tot-verzeichnis anhaengen
(set #totpath (tackon #totpath #totdirname))

(complete 30)

; platz schon belegt oder alte schublade vorhanden?
(set #checkplace (exists #totpath))
(if (= #checkplace 1)
    (abort (cat #fileatplace #totpath))
)

(if (>= #instver 44)
    (
      (closemedia symbol)
      (showmedia "symbol" "Goodies/Bild1.iff" "upper_left" "none" 0)
    )
)

(complete 50)

; evtl. alte files loeschen, die nicht mit neuen ueberschrieben werden
(if (= 2 #checkplace)
    (if #deletedatafiles
        (foreach (tackon #totpath "data")
                 #deletedatafiles
                 (delete (tackon (tackon #totpath "data") @each-name)
                         (prompt "") (help ""))
        )
    )
)

(complete 70)

; alles ausser #?install#? kopieren (wenn noetig: neues Verzeichnis)
(copyfiles (prompt "") (help "") (source "") (dest #totpath)
           (pattern "~(#?install#?)") (infos) (noposition)
)

(complete 90)

; assign in die user-startup und auch gleich aktivieren
(set #assigntext (cat "Assign \"" #totassign ":\" \"" #totpath "\""))
(startup @app-name (prompt #ask-assign)
                   (help (cat #ask-assign-help #assigntext))
         (command #assigntext))
(makeassign #totassign #totpath)

(complete 100)
; und weg...
(set @default-dest #totpath)

(P_CleanUp)

(exit #goodbye)
