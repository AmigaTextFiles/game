; ---TaverneR.isf---

:TaverneREinrichtung
WennGleich #11, 0
    Einrichtung 15, "TaverneR", 5
Abschluss
WennGleich #11, 1
    Einrichtung 15, "TaverneR_Loch", 5
    FeldUnsichtbar 3
Abschluss
    Feld 1, "Ausgang", 322, 400, 487, 479, 402, 408, 1
    Feld 2, "weiter", 0, 188, 32, 479, 0, 354, 1
    Feld 3, "Anker", 229, 149, 313, 257, 275, 330, 1
    Objekt 8, "Anker", "AnkerTaverne", 229, 211, 275, 341, 0
    Zierde 4, "Kerzenleuchter2", 60, 120, 1
    Zierde 5, "Kerzenflamme4", 600, 130, 1
    Zierde 6, "Kerzenleuchter3", 192, 50, 1
    Person 7, "", "Flamme", 229, 369, 1
    Person 10, "Helga", "Helga", 472, 244, 1
    WennGleich #12, 1
        PersonUnsichtbar 0
    Abschluss
    Person 22, "", "Musiker3", 141, 299, 1
    PersonProg 22
        Beginn
            Anim 22, 1, 4
            Anim 22, 2, 2
            Anim 22, 1, 1
            Anim 22, 2, 8
        Schleife
    Abschluss
EinrichtungEnde
Zurück

:TaverneR
SpringeSub TaverneREinrichtung
PersonProg 10
    Beginn
        Anim 10, 1, 3
        Anim 10, 3, 1
        Anim 10, 1, 6
        Anim 10, 3, 1
    Schleife
Abschluss

Liste
WennBenutzt 1
    SpieleSound 2, 100, 70
    SpringeOrt Straße1, 115, 409, 2
Abschluss
WennBenutzt 2
    WennGleich #8, 2
        LadeSound 3, "SndSchlucken"
        Rede 0, 3, "Moment!", "Blabla5c"
        Laufe 0, 100, 354, 2
        Rede 0, 4, "Ich hab da ja noch was!", "Blabla5b"
        Rede 0, 3, "Das gute Mittelchen vom Zauberer.", "Blabla5a"
        Anim 0, 33, 10
        SpieleSound 3, 100, 20
        InventarWeg 1026
        Rede 0, 3, "Nun bin ich gerüstet!", "Blabla5b"
        Richtung 0, 2
        Rede 0, 3, "Komme, was mag!", "Blabla5b"
        Laufe 0, 0, 354, 2
        SetzeVariable 8, 3
        WarteAuf 0
    Abschluss
    SpringeOrt TaverneL, 629, 343, 0
Abschluss
WennAngesehen 3
    Richtung 0, 3
    Rede 0, 3, "Das ist ein echter Anker.", "Blabla5b"
    Rede 0, 3, "Sieht aus wie frisch gepflückt.", "Blabla5b"
    Stopp
Abschluss
WennBenutzt 3
    Inaktiv 10
    Anim 10, 4, 1
    Richtung 0, 3
    Anim 0, 13, 1
    Rede 10, 5, "Finger weg!", "Blabla4c"
    Rede 10, 5, "Das ist meiner!", "Blabla4b"
    Anim 10, 4, 10
    Richtung 0, 1
    Rede 0, 3, "Tschuldigung.", "Blabla5c"
    Aktiv 10
    Stopp
Abschluss
WennBenutzt 8
    Inaktiv 10
    Rede 10, 5, "Das ist immer noch meiner!!!", "Blabla4b"
    Anim 10, 4, 3
    Aktiv 10
    Stopp
Abschluss
WennAngesehen 10
    RichteAuf 0, 10
    Rede 0, 3, "Netter Eindruck.", "Blabla5c"
    Stopp
Abschluss
WennBenutzt 10
    RichteAuf 0, 10
    Rede 0, 3, "Einen doppelten Korn mit Schuss!", "Blabla5b"
    Rede 10, 2, "Geh doch zu deiner Mama!", "Blabla4b"
    Rede 10, 2, "Du wirst hier nicht bedient.", "Blabla4a"
    Rede 0, 4, "Ich bin aber volljährig!", "Blabla5b"
    Rede 10, 2, "Volljährig ist man jetzt erst ab 80.", "Blabla4a"
    Rede 10, 2, "Neues Gesetz vom König.", "Blabla4b"
    Aktiv 10
    Stopp
Abschluss
WennBenutztMit 10, 1027
    RichteAuf 0, 10
    Rede 0, 3, "Guck mal, was ich hier habe!", "Blabla5b"
    Rede 10, 2, "Gib den sofort wieder her!", "Blabla4b"
    Rede 0, 3, "Vergiss es!", "Blabla5c"
    Aktiv 10
    Stopp
Abschluss
WennBenutztMit 0, 10
    Rede 10, 2, "Kein Bedarf!", "Blabla5c"
    Aktiv 10
    Stopp
Abschluss
Springe Global
