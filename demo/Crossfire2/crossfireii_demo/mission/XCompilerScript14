// Großangriff auf Frontbase

LadeRaumer AlienRaumer1
LadeRaumer AlienRaumer2
LadeRaumer RebelBomber3
LadeRaumer RebelBomber4
LadeRaumer Spieler4
LadeRaumer UWBomber4
LadeRaumer UWBomber2
LadeRaumer RebellenKBoot
LadeRaumer RebellenTurm2
LadeRaumer AlienKBoot
LadeRaumer AlienTurm

LadeBild "Mission/Bild14_1"
ZeigeText 1
Einblenden
WarteTaste
Ausblenden

NewShip 57, Transporter1, -1, 0, -1
NewShip 57, Spieler4, -1, 0, -1, WirTot
SetVar RS_Kohle(0), 1000
MachMaShop 24, "Grafik/Hangar2", 4

StartMission 14

// Spieler
NewShip 84, Spieler4, , 0, 1, WirTot
SetVar RS_Richtung(0), 40
// Mit-Spieler
//NewShip 45, UWBomber2, FolgeMir, 1, 1
//ChgAim 1, 0
//AddWaffe 1, 4  AddWaffe 1, 4  AddWaffe 1, 4
//AddWaffe 1, 9  AddWaffe 1, 9  AddWaffe 1, 9
//AddWaffe 1, 38  AddWaffe 1, 38  AddWaffe 1, 38

// die AlienStation
NewShip 71, StationsDummy1, DummSchlumpf, 9, 7
ChgCtrl 9, Computer1, , StationAktiv
SetVar RS_Fixed(9), -1
ChgFreund 6, 7, 0   ChgFreund 1, 7, 0
SetVar RS_PosX(9), 32512  SetVar RS_PosY(9), 37376
SetVar RS_Energie(9), 10000

// Abwehrtürme der AlienStation
NewShip 70, AlienRakStation, Station2, 10, 6, StationTot
ChgCtrl 10, DummSchlumpf, StationEntdeckt, StationAktiv
SetVar RS_PosX(10), 27136   SetVar RS_PosY(10), 54784
SetVar RS_Fixed(10), -1     SetVar RS_Richtung(10), 24

NewShip 71, AlienRakStation, Station2, 11, 6, StationTot
ChgCtrl 11, DummSchlumpf, StationEntdeckt, StationAktiv
SetVar RS_PosX(11), 45592   SetVar RS_PosY(11), 28160
SetVar RS_Fixed(11), -1     SetVar RS_Richtung(11), 24

// Grüne Station-Freunde
NewShip 61, AlienRaumer2, DummSchlumpf, 12, 6, StationTot
ChgCtrl 12, DummSchlumpf, StationEntdeckt, StationAktiv
Setvar RS_Richtung( 12), 24
NewShip 7, AlienRaumer2, DummSchlumpf, 13, 6, StationTot
ChgCtrl 13, DummSchlumpf, StationEntdeckt, StationAktiv
SetVar RS_Richtung( 13), 16

SetVar G, 0

// Patrouille
NewShip 100, AlienRaumer2, Patrol, 15, 6, PatrolTot
ChgCtrl 15, Patrol, PatrolEntdeckt, PatrolEntdeckt
//AddWaffe 15, 69
ChgAim 15, , 84

NewShip 107, AlienRaumer2, Patrol, 16, 6, PatrolTot
ChgCtrl 16, Patrol, PatrolEntdeckt, PatrolEntdeckt
//AddWaffe 16, 41   AddWaffe 16, 41   AddWaffe 16, 41
ChgAim 16, , 84

SpieleMusik "Musik/Story5"

Wait 20
Text 38, 50, 6
Wait 60
Text 38, 50, 7
ZeigeInfo Pfeil_N

Setvar F, 2

// Warten bis die Patrouille tot ist
SetMarke Hualp
Wait 10
if(F>0) { Goto Hualp }

SpieleMusik "Musik/Story5"

// Mitflieger wieder anleinen
//ChgCtrl 1, FolgeMir
//ChgChef 1, 0

Wait 20
Text 1, 50, 9, 0

// Warten, bis er die Station entdeckt hat
SetMarke HiffNick
Wait 10
if(G=0) { Goto HiffNick }

// Texten und zurückziehen
Text 1, 50, 11, 0
Wait 20
ChgCtrl 1, DummSchlumpf
ChgAim 1, , 28
Wait 50
Text 1, 50, 12, 0
Wait 90
Text 38, 50, 13
Wait 80
Text 1, 50, 14, 0
Wait 140
Text 1, 50, 15, 0

Wait 80
Text 38, 50, 16
Wait 60
Text 38, 50, 17
Wait 60
Text 38, 50, 18
Wait 60
Text 38, 50, 19
Wait 60
Text 38, 50, 20

Wait 90
Text 1, 50, 21, 0

Wait 200
Text 1, 50, 22, 0

// dicken Alienkreuzer erscheinen lassen
NewShip 69, AlienKBoot, PapaFaehrt, 20, 6, StationTot
SetVar RS_Richtung(20), 16
SetVar RS_Schatten(20), -1

NewShip 69, AlienTurm, Station2, 21, 6
Add RS_PosX( 21), -4500   Add RS_PosY(21), 1280
Link 20, 21
SetVar RS_Richtung(21), 0
Limit 21, 40, 24

NewShip 69, AlienTurm, Station2, 22, 6
Add RS_PosX( 22), 4500   Add RS_PosY(22), 1280
Link 20, 22
SetVar RS_Richtung(22), 32
Limit 22, 8, 56

// Begleiter
NewShip 69, AlienRaumer2, Computer5, 23, 6, StationTot
AddWaffe 23, 69   Add RS_PosY(23), 8000
NewShip 69, AlienRaumer2, Computer5, 24, 6, StationTot
AddWaffe 24, 72   Add RS_PosY(24), 8000  Add RS_PosX(24), -4000

Add G, 3

SpieleMusik "Musik/Story4"

Wait 60
Text 1, 50, 23, 0

Wait 100
Text 38, 50, 24

Wait 90
Text 1, 50, 25, 0

// Freds Einsatztruppe losjagen
Wait 2000
Text 28, 50, 26
SpieleMusik "Musik/Story3"

NewShip 72, RebellenKBoot, PapaFaehrt, 30, 1
SetVar RS_Richtung( 30), 48
Add RS_Energie( 30), 1000

NewShip 72, RebellenTurm2, Station2, 31, 1
Add RS_PosX( 31), 4500   Add RS_PosY(31), 1280
Link 30, 31
SetVar RS_Richtung(31), 32
Limit 31, 8, 56

NewShip 72, RebellenTurm2, Station2, 32, 1
Add RS_PosX( 32), -4500   Add RS_PosY(32), 1280
Link 30, 32
SetVar RS_Richtung(32), 0
Limit 32, 24, 40

NewShip 122, RebelBomber3, Computer5, 33, 1
AddWaffe 33, 38   AddWaffe 33, 38  AddWaffe 33, 38
Add RS_Energie( 33), 200
NewShip 122, RebelBomber4, Computer5, 34, 1
AddWaffe 34, 9  AddWaffe 34, 9  AddWaffe 34, 9
Add RS_Energie( 34), 200
Add RS_PosX(34), 5000
NewShip 121, UWBomber2, Computer5, 35, 1
AddWaffe 35, 4  AddWaffe 35, 4  AddWaffe 35, 4
Add RS_Energie( 35), 200

Wait 80
Text 1, 50, 27, 0

// Warten, bis alles tot
SetMarke HirchMirch
Wait 20
//Text 1, 20, 50, G
if(G>0) { Goto HirchMirch }

Wait 30
Text 28, 50, 28, 30

Wait 80
Text 1, 50, 29, 0
Wait 20
ZeigeInfo MSuccess
Wait 20
EndMission -1
Wait 100

// MissionsEnde
LadeBild "Mission/DemoEnde"
ZeigeText 30
Einblenden
WarteTaste
Ausblenden

Ende


// ******* Interrupts *******
// ein Patrouillenschiff ist verstorben
SetMarke PatrolTot
Sub F, 1
Return

// ein Stationsverteidiger ist verstorben
SetMarke StationTot
Sub G, 1
Return

// Die Patrouille hat uns entdeckt
SetMarke PatrolEntdeckt

// zum Angriff schreien
Text 1, 40, 8, 0

// die Aliens spitz machen
ChgCtrl 15, Computer5
ChgCtrl 16, Computer5

SpieleMusik "Musik/Story1"

// unseren Freund loslassen
//ChgCtrl 1, Computer5
Return

// die Station hat uns entdeckt
SetMarke StationEntdeckt

// Anzahl Verteidiger setzen
SetVar G, 4

// die Jungens halbaktiv setzen
ChgCtrl 10, Station2, , StationAktiv
ChgCtrl 11, Station2, , StationAktiv
ChgCtrl 12, Station2, , StationAktiv
ChgCtrl 13, Station2, , StationAktiv
ChgCtrl 9, Computer1
Return

// die Station wurde beschossen -> alle werden aggressiv
SetMarke StationAktiv

// Anzahl Verteidiger setzen
if(G=0) { SetVar G, 4 }

// die Jungens ganzaktiv setzen
ChgCtrl 10, Station2
ChgCtrl 11, Station2
ChgCtrl 12, Computer5
ChgChef 12, 9, 200
ChgCtrl 13, Computer5
ChgChef 13, 9, 200
ChgCtrl 9, Computer1
Return

// wir sind höchstselbst verstorben
SetMarke WirTot
Wait 20
ZeigeInfo MFailed
Wait 20
EndMission -1
Ende

