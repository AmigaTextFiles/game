; $VER: Install_SnapshotF1GP 1.1 (19.4.2000)
;   by Oliver Roberts (oliver.roberts@iname.com)

(if (= (exists "Env:Language") 1)
	(set @language (getenv "Language"))
)

;*****************
; English strings
;
(set #str-kick2 "You must be running Kickstart 2.04 or higher to use this software")
(set #str-update "Installing newer version of %s")
(set #str-progwhere "Select where the SnapshotF1GP program should be installed\n(a separate drawer will not be created)")
(set #str-docswhere "Select where the AmigaGuide documentation should be installed")
(set #str-prog "SnapshotF1GP program")
(set #str-installdoc "Installing AmigaGuide documentation")

;*************
; Here we go!
;
(if (< (/ (getversion) 65536) 37)
	(abort #str-kick2)
)

(set #destdir
	(askdir
		(prompt #str-progwhere)
		(help @askdir-help)
		(default @default-dest)
	)
)
(set @default-dest #destdir)
(set #docdir
	(askdir
		(prompt #str-docswhere)
		(help @askdir-help)
		(default @default-dest)
	)
)

(copylib
	(prompt (#str-update "f1gp.library"))
	(source "Libs/f1gp.library")
	(dest "LIBS:")
	(help @copylib-help)
	(confirm)
)

(copylib
	(prompt (#str-update "iff.library"))
	(source "Libs/iff.library")
	(dest "LIBS:")
	(help @copylib-help)
	(confirm)
)

(copylib
	(prompt (#str-update #str-prog))
	(source "SnapshotF1GP")
	(dest @default-dest)
	(help @copylib-help)
	(infos)
	(confirm)
)

(copyfiles
	(prompt #str-installdoc)
	(source "SnapshotF1GP.guide")
	(dest #docdir)
	(help @copyfiles-help)
	(infos)
)

(exit)
