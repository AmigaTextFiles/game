(complete 0)
; -------------------------
; SELECT LANGUAGE TEXTS
; -------------------------

 (if (= @language "polski")
(     (set #welcome
      (cat
           "\nWitam w programie instalacyjnym  'Puzle 3.0'  \n"
           "Ten program zainstaluje grë  'Puzle 3.0'  \n"
           "na twoim twardym dysku.\n\n"
           "P U Z L E\n"
           "\nCopyright (C) 1995-1996 Mirosîaw Siedlarz\n"
           "Wszelkie Prawa Zastrzeûone\n"
           "Wersja 3.0 - Data produkcji 07.06.1996\n\n"

           "Przeczytaj plik  'Puzle_polski.guide'  w celu\n"
           "otrzymania wiëkszej iloôci informacji o programie.\n\n"
      )
     )
	(set #destdir
     (cat
           "Gdzie mam zainstalowaê program  'Puzle 3.0'  \n"
           "UWAGA! Nie bëdzie stworzony ûaden katalog."
     )
     )
     (set #copylibrary "Kopiujë biblioteke")
	(set #chooselang  "Wybierz wersjë jëzykowâ programu")
	(set #chooselang-help "    :-)")
	(set #level "Poziom trudnoôci")
     (set #level-1 "îatwy - 20 kawaîków ukîadanki")
	(set #level-2 "trudny - 80 kawaîków ukîadanki")
	(set #level-help "    :-)")
	(set #sound "Dûwiëk")
	(set #sound-1 "wîâczony")
	(set #sound-2 "wyîâczony")
	(set #sound-help "    :-)")
	(set #prev "Podglâd ukîadanego obrazka podczas gry")
	(set #prev-help "    :-)")
     (set #guide "Czy chcesz skopiowaê instrukcjë do programu ?")
     (set #guide-help "    |-)")
     (set #yes "Tak")
     (set #no "Nie")
 )
  )

 (if (= @language "english")
  (
     (set #welcome
      (cat
           "\nWelcome to the  'Puzle 3.0'  Installer Utility\n"
           "This program will install the  'Puzle 3.0'  \n"
           "program on your hard disk.\n\n"
           "P U Z L E\n"
           "\nCopyright (C) 1995-1996 Miroslaw Siedlarz\n"
           "All Rights Reserved\n"
           "Version 3.0 - Release date 07.06.1996\n\n"

           "Please read the  'Puzle_english.guide'  document for\n"
           "more information about the program.\n\n"
      )
     )
	(set #destdir
      (cat
           "Select directory where to install  'Puzle 3.0'  .\n"
           "NOTE! Directory will not be created."
      )
     )
	(set #copylibrary "Copying library")
	(set #chooselang "Select language version of program")
     (set #chooselang-help "You can set default language for the program")
	(set #level "Select difficulty level")
	(set #level-1 "easy - 20 pieces of puzzle")
	(set #level-2 "hard - 80 pieces of puzzle")
	(set #level-help "    :-)")
	(set #sound "Sound in the game")
     (set #sound-1 "on")
	(set #sound-2 "off")
	(set #sound-help "    :-)")
	(set #prev "Preview picture in the game ?")
	(set #prev-help "    :-)")
     (set #guide "Do you want to copy AmigaGuide document for this game ?")
     (set #guide-help "    :-)")
     (set #yes "Yes")
     (set #no "No")
	  )
 )
; ---------
; MAIN CODE
; ---------

(message  #welcome)
(complete 5)
(welcome)
(set @default-dest "DH1:")
(set Puzle-dest
    (askdir
      (prompt #destdir)
      (help @askdir-help)
      (default @default-dest)
    )
)
(set @default-dest Puzle-dest)
(complete 10)

; --------------
; COPY LIBRARIES
; --------------
	(copylib
		(prompt #copylibrary)
		(help @copylib-help)
		(source "PUZLE_00:libs/amos.library")
		(dest "SYS:libs")
		(confirm)
	)
(complete 20)

	(copylib
		(prompt #copylibrary)
		(help @copylib-help)
		(source "PUZLE_00:libs/easylife.library")
		(dest "SYS:libs")
		(confirm)
	)
(complete 30)

; -----------------
; COPY MAIN PROGRAM
; -----------------
	(copyfiles
		(source "PUZLE_00:Puzle")
          (dest Puzle-dest)
		(infos)
	)
(complete 40)

; ------------
; COPY GUIDE
; ------------
(set gid
	(askchoice
		(prompt #guide)
		(choices #Yes #No)
		(help #guide-help)
		(default 0)
     )
)
     ( if (= gid 0)
     (
          (copyfiles
		(source "PUZLE_00:Puzle.guide")
		(dest @default-dest)
		(infos)
          )
          (copyfiles
		(source "PUZLE_00:data/")
		(dest (tackon @default-dest "data"))
          (all)
          )
)
)
(complete 50)


; ----------
; COPY FONTS
; ----------
	(copyfiles
		(source "PUZLE_00:fonts")
		(dest "SYS:Fonts")
		(all)
	)
(complete 60)

; ------------
; COPY OBRAZKI
; ------------
	(trap 3 (copyfiles
		(pattern "puz.#?")
		(source "PUZLE_00:obrazki")
		(dest (tackon Puzle-dest "obrazki"))
	))
(complete 70)

	(trap 3 (copyfiles
		(pattern "puz.#?")
		(source "PUZLE_01:obrazki")
		(dest (tackon Puzle-dest "obrazki"))
	))
(complete 90)

; -------------------
; USTAWIENIE TOOLTYPE
; -------------------
	(set jezyk
	(askchoice
		(prompt #chooselang)
		(choices "english" "polski")
		(help #chooselang-help)

		(if (= @language "english")
			(default 0)
			(default 1)
		)
	)
     )

(complete 96)

	(set poziom
	(askchoice
        	(prompt #level)
        	(choices #level-1 #level-2)
        	(help #level-help)
    	)
 	)
(complete 97)

 (set dzwiek
    (askchoice
        (prompt #sound)
        (choices #sound-1 #sound-2)
		(help #sound-help)
    )
 )
(complete 98)
 (set podglad
    (askchoice
        (prompt  #prev)
        (choices #sound-1 #sound-2)
        (help #prev-help)
    )
 )
(complete 99)
(tooltype
    (dest (tackon Puzle-dest "Puzle"))
    (if (= jezyk 0) (settooltype "LANGUAGE" "english"))
    (if (= jezyk 1) (settooltype "LANGUAGE" "polski"))
    (if (= poziom 0) (settooltype "LEVEL" "1"))
    (if (= poziom 1) (settooltype "LEVEL" "2"))
    (if (= podglad 0) (settooltype "PREVIEW" "ON"))
    (if (= podglad 1) (settooltype "PREVIEW" "OFF"))
    (if (= dzwiek 0) (settooltype "SOUND" "ON"))
    (if (= dzwiek 1) (settooltype "SOUND" "OFF"))
)
; ---------
(complete 100)


(exit)