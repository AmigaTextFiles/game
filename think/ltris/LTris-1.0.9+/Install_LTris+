; $VER: installation script for LTris 1.0.9+ (08.03.05)
; © Edgar Schwan
; Created with GoldED Studio AIX

(set #conflevel "expert")
(set #execname_os4 "LTris+")
(set #execpath_os4 (cat "src/" #execname_os4))
(set #iconpath "icons/")
(set #gfxpath "src/gfx/")
(set #soundpath "src/sounds/")


(                              ; english (default)
(set #txt_wrongversion
(cat "You have an old version of the program 'Installer' "
     "on your Amiga!\n\nThe installation procedure needs at least Installer 42.9.\n\n"
     "Please check your configuration!"))
(set #txt_welcome
(cat "Welcome to the Installation of LTris 1.0.9+ for AmigaOS4!"))
(set #txt_wrong_os
(cat "Incorrect OS version (LTris+ requires OS V50 or better)!"))
(set #txt_misc_error
(cat "An error has occurred during installation. Please check the log file to understand the error."))
(set #txt_choose_dir_head
(cat "Please choose the installation path.\nA new drawer named 'LTris+' will be created there!"))
(set #txt_overwrite
(cat "LTris+ has been installed before.\n\nWhat should I do with this installation?"))
(set #txt_abort
(cat "Abort"))
(set #txt_new_inst
(cat "Delete old installation and install it again"))
(set #txt_really_delete
(cat "Do you really want to delete LTris+?"))
(set #txt_delete
(cat "Deleting old installation..."))
(set #txt_ask_sources
(cat "Do you want to install the sources?"))
(set #txt_yes
(cat "yes"))
(set #txt_no
(cat "no"))
(set #txt_copy_graphics
(cat "Copy graphics..."))
(set #txt_copy_sounds
(cat "Copy sounds..."))
(set #txt_copy_figures
(cat "Copy figures..."))
(set #txt_copy_binary
(cat "Copy binary..."))
(set #txt_copy_documents
(cat "Copy documents..."))
(set #txt_copy_icon
(cat "Copy icons..."))
(set #txt_copy_sources
(cat "Copy sources..."))
(set #txt_create_dir
(cat "Create directory '%s'..."))
)

(if (OR (= @language "german") (= @language "deutsch"))   ; deutsch
(
(set #txt_wrongversion
(cat "Achtung! Es ist eine alte Version des Programmes 'Installer' "
     "auf Ihrem Amiga!\n\nDie Installation benötigt mindestens Installer 42.9.\n\n"
     "Bitte überprüfen Sie Ihre Konfiguration!"))
(set #txt_welcome
(cat "Willkommen zur Installation von LTris 1.0.9+ für AmigaOS4!"))
(set #txt_wrong_os
(cat "Inkorrekte OS-Version. LTris+ benötigt mindestens OS V50 oder besser."))
(set #txt_misc_error
(cat "Während der Installation trat ein Fehler auf. Bitte prüfen Sie das Log-File für weitere Informationen!"))
(set #txt_choose_dir_head
(cat "Bitte wählen Sie den Installations-Pfad.\nEin neues Verzeichnis namens 'LTris+' wird automatisch erzeugt!"))
(set #txt_overwrite
(cat "LTris+ wurde schon einmal installiert.\n\nWas soll mit dieser Installation geschehen?"))
(set #txt_abort
(cat "Abbrechen"))
(set #txt_new_inst
(cat "Alte Installation löschen und neu installieren"))
(set #txt_really_delete
(cat "Wollen Sie wirklich LTris+ löschen?"))
(set #txt_delete
(cat "Lösche die alte Installation..."))
(set #txt_ask_sources
(cat "Wollen Sie die Quelltext installieren?"))
(set #txt_yes
(cat "ja"))
(set #txt_no
(cat "nein"))
(set #txt_copy_graphics
(cat "Kopiere Grafiken..."))
(set #txt_copy_sounds
(cat "Kopiere Sounds..."))
(set #txt_copy_figures
(cat "Kopiere Figuren..."))
(set #txt_copy_binary
(cat "Kopiere Programm..."))
(set #txt_copy_documents
(cat "Kopiere Dokumente..."))
(set #txt_copy_icon
(cat "Kopiere Piktogramme..."))
(set #txt_copy_sources
(cat "Kopiere Quelltexte..."))
(set #txt_create_dir
(cat "Erzeuge Verzeichnis '%s'..."))
))

(if (OR (= @language "italian") (= @language "italiano"))            ; italiano
(
(set #txt_wrongversion
(cat "Mi dispiace, hai una vecchia versione del programma 'Installer' "
     "sul tuo Amiga!\n\nLa procedura di installazone necessita di almeno il programma Installer versione 42.9.\n\n"
     "Per favore controlla la tua configurazione!"))
(set #txt_welcome
(cat "Benvenuto nell'installazione di LTris 1.0.9+!"))
(set #txt_wrong_os
(cat "Mi dispiace, Sistema Operativo non supportato (LTris+ necessita di almeno OS V50)!"))
(set #txt_misc_error
(cat "C'è stato un problema durante l'installazione. Controlla il file di log per conoscere l'errore."))
(set #txt_choose_dir_head
(cat "Scegli il percorso dell'installazione.\nUn nuovo cassetto di nome 'LTris+' verrà creato!"))
(set #txt_yes
(cat "Si"))
(set #txt_no
(cat "No"))
(set #txt_copy_binary
(cat "Copio l'eseguibile..."))
(set #txt_copy_icon
(cat "Copio l'icona..."))
(set #txt_copy_graphics
(cat "Copio le immagini..."))
(set #txt_copy_sources
(cat "Copio il codice sorgente..."))
(set #txt_create_dir
(cat "Creo il cassetto '%s'..."))
))

(if (OR (= @language "polish") (= @language "polski"))            ; polski
(
(set #default_language %100)
))

(if (OR (= @language "spanish") (= @language "español"))          ; español
(
(set #default_language %100)
))

; check installer-version

(set ver (/ @installer-version 65536))
(set rev (- @installer-version (* ver 65536)))

;(debug ("Installer-Version: %ld.%ld\n" ver rev))

(if (< ver 42)
    (abort #txt_wrongversion)
)

; check if we are running under correct OS

(if (< (/ (getversion) 65536) 50)
    (abort #txt_wrong_os)
)

; error handling

(onerror
    (if (> @ioerr 0)
        (
            (message(#txt_misc_error))
        )
    )

    (exit (quiet))
)

(welcome #txt_welcome)

(
    (set @default-dest                ; ask installation-directory
        (askdir
            (prompt #txt_choose_dir_head)
            (help @askdir-help)
            (default @default-dest)
            )
        )
    (set #rootdir @default-dest)
    (set @default-dest (tackon @default-dest "LTris+"))
)

;(debug ("Zielpfad: '%s'" @default-dest))

; handling for installation-dir

(if (exists @default-dest)
  (
    (set #delete_old                  ; old installation found
        (askbool
            (prompt #txt_overwrite)
            (help "")
            (default 0)
            (choices #txt_new_inst #txt_abort)
            )
        )

    ;(debug ("#delete_old: %ld" #delete_old))

    (if (= #delete_old 1)
        (                                ; ** delete old installation **
            (message #txt_really_delete)
            (delete @default-dest        ; delete it
                (prompt #txt_delete)
                (help "")
                (optional force)
                (all)
            )
        )
        (exit (quiet))                   ; exit
    )
  )
)

; ask sources

(set #install_sources
    (askchoice
        (prompt #txt_ask_sources)
        (help @askchoice-help)
        (choices #txt_yes #txt_no)
        (default 1)
    )
)

;(debug ("#install_sources: %ld" #install_sources))
(complete 10)

; create new directories

(makedir @default-dest
    (prompt (#txt_create_dir @default-dest))
    (help @makedir-help)
)
(makedir (tackon @default-dest "share")
    (prompt (#txt_create_dir @default-dest))
    (help @makedir-help)
)
(makedir (tackon @default-dest "share/ltris")
    (prompt (#txt_create_dir @default-dest))
    (help @makedir-help)
)
(makedir (tackon @default-dest "var")
    (prompt (#txt_create_dir @default-dest))
    (help @makedir-help)
)

; copy files

(set #destpath (tackon @default-dest "share/ltris/gfx" ))
;(debug ("copy gfx: #destpath: %s" #destpath))
(complete 20)

((copyfiles                                      ; copy gfx-files
    (prompt #txt_copy_graphics)
    (help @copyfiles-help)
    (source #gfxpath)
    (dest #destpath)
    (pattern "~(Makefile.am|Makefile.in)")
    (optional askuser)
    (confirm #conflevel)
 )
)

(set #destpath (tackon @default-dest "share/ltris/sounds" ))
;(debug ("copy sounds: #destpath: %s" #destpath))
(complete 30)

((copyfiles                                      ; copy sound-files
    (prompt #txt_copy_sounds)
    (help @copyfiles-help)
    (source #soundpath)
    (dest #destpath)
    (pattern "~(Makefile.am|Makefile.in)")
    (optional askuser)
    (confirm #conflevel)
 )
)

(set #destpath (tackon @default-dest "share/ltris" ))
;(debug ("copy sounds: #destpath: %s" #destpath))
(complete 35)

((copyfiles                                      ; copy figures
    (prompt #txt_copy_figures)
    (help @copyfiles-help)
    (source "src")
    (dest #destpath)
    (pattern "(figures)")
    (optional askuser)
    (confirm #conflevel)
 )
)

(set #srcpath (pathonly @icon))
;(debug ("copy docs: #srcpath: %s" #srcpath))
(complete 40)

((copyfiles                                      ; copy doc-files
    (prompt #txt_copy_documents)
    (help @copyfiles-help)
    (source #srcpath)
    (dest @default-dest)
    (pattern "(AUTHORS|COPYING|README.amiga|README|TODO)")
    (optional askuser)
    (confirm #conflevel)
 )
)

(complete 50)

(copyfiles                                       ; copy binary
    (prompt #txt_copy_binary)
    (help @copyfiles-help)
    (source #execpath_os4)
    (dest @default-dest)
    (optional askuser)
    (confirm #conflevel)
    )

(complete 60)

((copyfiles                                      ; copy icons
    (prompt #txt_copy_icon)
    (help @copyfiles-help)
    (source #iconpath)
    (dest @default-dest)
    (pattern "(#?.info)")
    (optional askuser)
    (confirm #conflevel)
 )
)

(complete 70)

(set #drawericon (cat "\"" (tackon #rootdir "LTris+.info") "\""))   ; copy drawer-icon
;(debug ("#drawericon: %s" #drawericon))

(run (cat "c:copy /LTris-1.0.9+.info " #drawericon " FORCE QUIET NOREQ")   ; does not work with 'copyfiles'
    (prompt #txt_copy_icon)
    (help "")
)

(complete 80)

(set #srcpath (tackon #srcpath "src"))
;(debug ("copy sources: #srcpath: %s" #srcpath))
(set #destpath (tackon @default-dest "src" ))
;(debug ("copy sources: #destpath: %s" #destpath))
(complete 90)

(if (= #install_sources 0)
    ((copyfiles                                  ; copy sources
        (prompt #txt_copy_sources)
        (help @copyfiles-help)
        (source #srcpath)
        (dest #destpath)
        (pattern "~(ltris+|gfx|sounds)")
        (optional askuser)
        (confirm #conflevel)
     )
    )
)

(complete 100)

(exit)

