;
;  Magic Numbers 1.1 installer
;  *************************
;
;  (c) 2001 Magic Software
;  ***********************
;
;
; -------------------------------------------------------------------------
;
;
(if (= @language "english")
  (
    (set #select-destination
      (cat "Where do you want to install the program? A directory "
           "called \"MagicNumbers\" will be automatically created."
      )
    )
    (set #select-catalogs "Which translations do you want to install?")
    (set #copying-files "Copying files...")
    (set #copying-library "Copying utilities.library...")
  )
)
(if (= @language "polski")
  (
    (set #select-destination
      (cat "Gdzie chcesz zainstalowaê program? Katalog o nazwie "
           "\"MagicNumbers\" zostanie automatycznie utworzony."
      )
    )
    (set #select-catalogs "Które tîumaczenia chcesz zainstalowaê?")
    (set #copying-files "Kopiujë pliki...")
    (set #copying-library "Kopiujë utilities.library...")

  )
)
;
;
;--------------------------------------------------------------------------
;
; Ask about destination
;
(set #destinationParent
  (askdir
    (prompt  #select-destination)
    (help    @askdir-help)
    (default "SYS:")
  )
)

;
; Ask about catalogs
;
(set #catalogs
  (askoptions 
    (prompt  #select-catalogs)
    (help    @askoptions-help)
    (choices "Polski" "Deutsch")
    (default 0)
  )
)
;
;
; -------------------------------------------------------------------------
;
; Create destination directory
;
(set #destination
  (tackon #destinationParent "MagicNumbers"
  )
)

(makedir #destination
  (infos)
)

;
;  Copy files
;
(copyfiles
  (source  "/")
  (dest    #destination)
  (choices "MagicNumbers" "Docs" "Images")
  (prompt  #copying-files)
  (help    @copyfiles-help)
  (infos)
)

;
; Copy catalogs and catalog description
;
(makedir (cat #destination "/Catalogs") )

(copyfiles
  (source "/Catalogs.info")
  (dest   #destination)
  (prompt #copying-files)
  (help   @copyfiles-help)
)

(copyfiles
  (source  "/Catalogs/MagicNumbers.cd")
  (dest    (cat #destination "/Catalogs") )
  (prompt  #copying-files)
  (help    @copyfiles-help)
)

(set n 0)
(while (set #catalog (select n "polski" "deutsch" "") )
 (
  (if (IN #catalogs n)
   (
    (copyfiles
     (source (cat "/Catalogs/" #catalog) )
     (dest   (cat #destination "/Catalogs/" #catalog) )
     (all)
    )
   )
  )
  (set n (+ n 1) )
 )
)

;
;  Copy utilities.library
;
(copylib
  (source "/Libs/utilities.library")
  (dest   "Libs:")
  (prompt #copying-library)
  (help   @copylib-help)
  (confirm)
)

(exit)
