;
; $VER: Klondike ADPTools Installation Script v2.5 (05.08.97)
; Copyright © 1995-97 Lejardinier Olivier
; All Rights Reserved.
;

;***************************************************************************;

(set #product-infos
(cat "\n"
     "Klondike ADPTools v2.5 (05.08.97) - FREEWARE"
     "\n\n"
     "- The ADPro toolkit for Klondike -"
     "\n"
     "Copyright © 1995-97 Lejardinier Olivier"
     "\n"
     "All Rights Reserved"
     "\n\n"
))

;
; English texts
;

(set #bad-amigados-version
(cat "\n"
     "Sorry, this program required AmigaDOS 3.0 or above (v39+)."
))

(set #install-intro
(cat #product-infos
     "These ARexx programs for ADPro allow you to create easily Klondike cardsets."
     "\n\n\n"
     "Please, read the documentation to get more informations."
))

(set #adpro-not-found
(cat  "\n"
      "Assign ADPRO: not found !"
      "\n"
      "ADPro is not present in your system or is bad installed."
))

(set #ask-language
(cat "Select version to install :"
))

(set #french
(cat "French"
))

(set #english
(cat "English"
))

(set #unarchiving-arexx-programs
(cat "Unarchiving ARexx programs..."
))

(set #ask-destination
(cat "\n"
     "Where do you want to install 'Klondike_ADPTools' drawer ?"
     "\n"
     "('Klondike_ADPTools' drawer will be created)"
     "\n"
))

(set #select-files-to-copy
(cat "Select files to copy :"
))

(set #unarchiving-templates-files
(cat "Unarchiving 'Templates' files..."
))

(set #unarchiving-symbols-files
(cat "Unarchiving 'Symbols' files..."
))

(set #start-adpro
(cat "\n"
     "Now you can start ADPro to use Klondike ADPTools."
))

;***************************************************************************;

;
; French texts
;

(if (= @language "français")
(

(set #bad-amigados-version
(cat "\n"
     "Désolé, l'AmigaDOS 3.0 ou supérieur est nécessaire "
     "pour utiliser ce programme"
))

(set #install-intro
(cat #product-infos
     "Ces programmes ARexx pour ADPro vous permettent de créer facilement des jeux de cartes pour Klondike."
     "\n\n\n"
     "Consultez la documentation pour obtenir plus d'informations."
))

(set #adpro-not-found
(cat  "\n"
      "Assign ADPRO: non trouvé !"
      "\n"
      "ADPro n'est pas présent dans votre système ou est mal installé."
))

(set #ask-language
(cat "Sélectionnez la version à installer :"
))

(set #french
(cat "Française"
))

(set #english
(cat "Anglaise"
))

(set #unarchiving-arexx-programs
(cat "Désarchivage des programmes ARexx..."
))

(set #ask-destination
(cat "\n"
     "Où voulez-vous installer le répertoire 'Klondike_ADPTools' ?"
     "\n"
     "(Le répertoire 'Klondike_ADPTools' sera créé)"
     "\n"
))

(set #select-files-to-copy
(cat "Sélectionnez les fichiers à copier :"
))

(set #unarchiving-templates-files
(cat "Désarchivage des fichiers 'Templates'..."
))

(set #unarchiving-symbols-files
(cat "Désarchivage des fichiers 'Symbols'..."
))

(set #start-adpro
(cat "\nVous pouvez maintenant lancer ADPro pour utiliser Klondike ADPTools."
))

))

;***************************************************************************;

 (set amigados-version (/ (getversion) 65536))

 (if (< amigados-version 39)
  (abort #bad-amigados-version)
 )

 (complete 0)

 (message #install-intro)

 (if (not (exists "ADPRO:" (quiet)))
  (abort #adpro-not-found)
 )

 (set choice
  (askchoice
   (prompt #ask-language)
   (help @askchoice-help)
   (choices #french
            #english
   )
   (if (= @language "français")
    (default 0)
    (default 1)
   )
  )
 )

 (complete 30)
 
 (if (not (exists "ADPRO:Commands2"))
  (makedir "ADPRO:Commands2")
 )
 
 (set @default-dest "ADPRO:Commands2")

 (makedir "T:Klondike_ADPTools")
 
 (complete 40)

 (working #unarchiving-arexx-programs)

 (if (= choice 0)
  (run "LhEx -fwT:Klondike_ADPTools x /Datas/French.LHA")
  (run "LhEx -fwT:Klondike_ADPTools x /Datas/English.LHA")
 )

 (copyfiles
  (source "T:Klondike_ADPTools")
  (pattern "#?.adpro")
  (dest @default-dest)
  (files)
  (optional "oknodelete" "force")
 )
 
 (run "C:Delete T:Klondike_ADPTools/#?.adpro")

 (copyfiles
  (source "/Datas/Reko")
  (dest "C:")
  (files)
  (optional "oknodelete" "force")
 )
 
 (complete 50)
 
 (set @default-dest "ADPRO:")
 
 (set destination
  (askdir
   (prompt #ask-destination)
   (help @askdir-help)
   (default @default-dest)
  )
 )

 (set destination (tackon destination "Klondike_ADPTools"))

 (makedir destination (infos))
 
 (complete 60)

 (copyfiles
  (prompt #select-files-to-copy)
  (help @copyfiles-help)
  (if (= choice 0)
   (source "//Documents/Français")
   (source "//Documents/English")
  )
  (choices "Klondike_ADPTools.Guide"
           "Description"
           "Bugs_Report"
           "/Print.info"
  )
  (dest destination)
  (files)
  (infos)
  (noposition)
  (confirm)
  (optional "oknodelete" "force")
 )
 
 (complete 90)

 (working #unarchiving-templates-files)

 (run "LhEx -fwT:Klondike_ADPTools x /Datas/Templates.LHA")

 (copyfiles
  (source "T:Klondike_ADPTools")
  (pattern "#?")
  (dest (tackon destination "Templates"))
  (files)
  (infos)
  (noposition)
  (optional "oknodelete" "force")
 )

 (run "C:Delete T:Klondike_ADPTools/#?")

 (working #unarchiving-symbols-files)
 
 (run "LhEx -fwT:Klondike_ADPTools x /Datas/Symbols.LHA")

 (copyfiles
  (source "T:Klondike_ADPTools")
  (dest (tackon destination "Symbols"))
  (all)
  (infos)
  (noposition)
  (optional "oknodelete" "force")
 )

 (run "C:Delete T:Klondike_ADPTools ALL")

 (complete 100)
 
 (message #start-adpro)
 
 (exit)
 
