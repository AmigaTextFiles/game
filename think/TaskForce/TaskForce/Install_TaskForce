; $VER: Install_TaskForce 1.22 (27.03.99)

(complete 0)

; version check
(if (< (/ (getversion) 65536) 37)
	(abort #msg-bad-kick)
)

(welcome)

(set #lang
	(askchoice
		(help @askchoice-help)
		(prompt "Select your preferred language.")
		(choices "Deutsch" "English" "Polski")
		(default 1)
	)
)

;----------------------------------------------------------------------
; english strings

(if (= #lang 1) (
	(set @language "english")
	(set #msg-bad-kick (cat "You must be using Kickstart 2.04 or higher to run TaskForce."))
	(set #msg-ask-dir (cat "Please choose a directory where to install the game. "
						"A subdirectory named \"TaskForce\" will be created."))
	(set #msg-copy-files (cat "Copying game files..."))
	(set #msg-install-rt (cat "Installing reqtools.library..."))
))

;----------------------------------------------------------------------
; german strings

(if (= #lang 0) (
	(set @language "deutsch")
	(set #msg-bad-kick (cat "TaskForce benötigt Kickstart 2.04 oder höher."))
	(set #msg-ask-dir (cat "Wähle ein Verzeichnis, in dem das Spiel installiert "
						"werden soll. Ein Unterverzeichnis namens \"TaskForce\" "
						"wird automatisch erstellt."))
	(set #msg-copy-files (cat "Kopiere Programmdateien..."))
	(set #msg-install-rt (cat "Installiere reqtools.library..."))
))

;----------------------------------------------------------------------
; polish strings

(if (= #lang 2) (
	(set @language "polski")
	(set #msg-bad-kick (cat "Musisz mieê kickstart conajmniej 2.04 aby uruchomiê TaskForce."))
	(set #msg-ask-dir (cat "Wybierz katalog gdzie chcesz mieê TaskForce. "
						"Podkatalog \"TaskForce\" bëdzie utworzony."))
	(set #msg-copy-files (cat "Kopiowanie plików..."))
	(set #msg-install-rt (cat "Instalowanie reqtools.library..."))
))

;----------------------------------------------------------------------

(set @default-dest
	(askdir
		(prompt #msg-ask-dir)
		(help @askdir-help)
		(default @default-dest)
	)
)

(complete 20)

(set #dest-dir (tackon @default-dest "TaskForce") )

(makedir #dest-dir (infos))
(makedir (tackon #dest-dir "Games"))

(complete 30)

(copyfiles
	(prompt #msg-copy-files)
	(help @copyfiles-help)
	(source "")
	(dest #dest-dir)
	(choices "TaskForce" "Editor" "Data")
	(infos)
)

(complete 50)

(copyfiles
	(prompt #msg-copy-files)
	(help @copyfiles-help)
	(source (tackon "Docs" @language))
	(dest (tackon #dest-dir "Docs"))
	(pattern "#?.guide")
	(infos)
)

(complete 58)

(copyfiles
	(prompt #msg-copy-files)
	(help @copyfiles-help)
	(source "Docs")
	(dest (tackon #dest-dir "Docs"))
	(choices "History.guide")
)

(complete 60)

(copyfiles
	(prompt #msg-copy-files)
	(help @copyfiles-help)
	(source (tackon "Missions" @language))
	(dest (tackon #dest-dir "Missions"))
	(pattern "#?.?pm")
)

(complete 70)

(copyfiles
	(prompt #msg-copy-files)
	(help @copyfiles-help)
	(source (tackon "Briefings" @language))
	(dest (tackon #dest-dir "Briefings"))
	(pattern "#?")
	(files)
)

(complete 80)

(if (<> #lang 1) (
	(copyfiles
		(prompt #msg-copy-files)
		(help @copyfiles-help)
		(source "Catalogs")
		(dest (tackon #dest-dir "Catalogs"))
		(choices @language)
	)
))

(complete 90)

(copylib
	(prompt #msg-install-rt)
	(help @copylib-help)
	(source "Libs/reqtools.library")
	(dest "LIBS:")
	(confirm)
)

(complete 100)
