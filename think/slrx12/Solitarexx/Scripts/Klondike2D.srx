/**********************************\
** Klondike2D v1.0 for Solitarexx **
**      by Michal Szafranski      **
\**********************************/
OPTIONS RESULTS

tex = '"Klondike 2D v1.0"'
wintex = '"We Have a Winner"'
ADDBUTTON 0 10 "Start"
ADDTEXT 4 42 tex 6
ADDCYCLE 7 9 'No|Yes' 0 '"Move parts"' 14
ADDCYCLE 9 12 '"Inf.|Once|Twice|3 Times|4 Times|5 Times|6 Times"' 1 '"Rotate deck"' 16
ADDCYCLE 11 12 '"3-2-1|One|Two|Three"' 1 '"Cards at once"' 17
ADDBUTTON 12 10 "Abort"
ADDTEXT 14 42 tex 6
SELECTGUI 3

SCREENSIZE 3 18 9 0
DO i = 0 TO 8
	NEWSTACK 3 16+4 0 i 0 0 0 18
	stack.i = RESULT
END
DO i = 0 TO 3
	NEWSTACK 4 0 i+2 i+5 1 0 18
	base.i = RESULT
END
DO i = 4 TO 7
	NEWSTACK 4 0 i-2 i+1 2 0 18
	base.i = RESULT
END
NEWSTACK 1 0 1 0 1 0 18
deck = RESULT
NEWSTACK 2 32+8+1 1 1 1 0 18 2 2
waste = RESULT
ADDCARDS deck
ADDCARDS deck SHUFFLED

DO FOREVER
	ACTION
	PARSE VAR RESULT act rest
	IF act = 1 THEN EXIT
	IF act = 3 THEN CALL GAME
END

GAME:
	CLEANUP deck
	SETGADGET 14 STR tex
	SELECTGUI 4
	sel = 0
	time = 0
	fin = 0
	GETGADGET 9
	gadtimes = RESULT
	GETGADGET 11
	gadcards = RESULT
	GETGADGET 7
	moveparts = RESULT
	ile. = 0
	SELECT
		WHEN gadcards = 0 THEN DO
			ile.0 = 3
			ile.1 = 2
			ile.2 = 1
		END
		WHEN gadtimes = 0 THEN ile. = gadcards
		OTHERWISE DO i = 0 TO gadtimes-1
			ile.i = gadcards
		END
	END
	DO i=0 TO 8
		CARDSELECT deck i
		MOVECARDS deck stack.i
		CARDSELECT deck 1
		MOVECARDS deck stack.i REVERSE
	END
	DO FOREVER
		ACTION
		PARSE VAR RESULT act stack sid card
		IF act = 1 THEN EXIT
		IF act = 2 THEN SELECT
			WHEN sid=1 THEN CALL DODECK
			WHEN sel=0 & card>0 & sid~=4 THEN sel = stack
			WHEN sel>0 & (sel = stack | sid = 4) THEN CALL DOBASE
			WHEN sel>0 & sid = 3 THEN CALL DOSTACKS
			OTHERWISE ERRBEEP
		END
		IF act = 3 THEN DO
			SELECTGUI 3
			RETURN
		END
	END
RETURN
DODECK:
	IF sel=waste THEN sel=0
	CARDSELECT deck ile.time
	IF RESULT = '' THEN DO
		CARDSELECT waste 52
		MOVECARDS waste deck REVERSE
		time = time+1
	END
	ELSE MOVECARDS deck waste REVERSE
RETURN
DOSTACKS:
	CARDSELECT sel 0 RELATIVE
	PARSE VAR RESULT kolors wars xx
	IF moveparts = 0 THEN DO
		WHICHCARD sel VISIBLE
		PARSE VAR RESULT len lsel
		IF lsel ~= 1 & lsel ~= len THEN RETURN
	END
	CARDSELECT stack 1
	PARSE VAR RESULT kolor war xx
	IF kolor = '' THEN DO
		war = 13
		kolor = kolors+1
	END
	kolor = (kolor+kolors) // 2
	IF war = wars+1 & kolor = 1 THEN MOVECARDS sel stack ATONCE
	ELSE ERRBEEP
	sel = 0
RETURN
DOBASE:
	IF sel~=stack THEN CALL CHBASE
	ELSE DO i= 0 TO 7 UNTIL ok=0
		stack = base.i
		CALL CHBASE
	END
	IF ok=0 THEN DO
		fin = fin +1
		MOVECARDS sel stack
		IF fin = 104 THEN SETGADGET 14 STR wintex
	END
	ELSE ERRBEEP
	sel = 0
RETURN
CHBASE:
	CARDSELECT sel 1
	PARSE VAR RESULT kolors wars xx
	CARDSELECT stack 1
	PARSE VAR RESULT kolor war xx
	IF kolor = '' THEN DO
		kolor = kolors
		war = -1
	END
	IF kolor = kolors & (wars - war) = 1 THEN ok = 0
	ELSE ok = 1
RETURN
