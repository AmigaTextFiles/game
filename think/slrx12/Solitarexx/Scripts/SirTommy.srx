/********************************\
** SirTommy v1.0 for Solitarexx **
**     by Michal Szafranski     **
\********************************/
OPTIONS RESULTS

tex = '"Sr Tommy"'
wintex = '"We Have a Winner"'
ADDBUTTON 0 10 "Start"
ADDCYCLE 1 6 '0|1|2' 0 'Reshuffles' 12
ADDCYCLE 2 6 '4|5|6|7' 0 'Stocks' 8
ADDTEXT 4 10 tex 6
ADDBUTTON 12 10 "Abort"
ADDBUTTON 13 12 'Reshuffle'
ADDTEXT 14 30 tex 6
SELECTGUI 1

SCREENSIZE 2 15 7 0
DO i = 0 TO 3
	NEWSTACK 8 0 i+2 i+3
	base.i = RESULT
END
DO i = 0 TO 6
	NEWSTACK i 8 0 i 1 0 0 15
	stack.i = RESULT
END
NEWSTACK 7 16+4 1
deck = RESULT
ADDCARDS deck SHUFFLED

DO FOREVER
	ACTION
	PARSE VAR RESULT act rest
	IF act = 1 THEN EXIT
	IF act = 3 THEN CALL GAME
END

GAME:
	CLEANUP deck
	SETGADGET 14 STR tex
	SELECTGUI 4
	sel = 0
	fin = 0
	GETGADGET 1
	resh = RESULT
	GETGADGET 2
	max = RESULT+4
	DO FOREVER
		ACTION
		PARSE VAR RESULT act stack sid card
		SELECT
		WHEN act = 1 THEN EXIT
		WHEN act = 2 & sel = 0 & card>0 & sid<8 THEN sel = stack
		WHEN act = 2 & sel > 0 & (sel = stack | sid = 8) THEN CALL DOBASE
		WHEN act = 2 & sel = deck & sid < max THEN DO
			MOVECARDS sel stack
			sel = 0
		END
		WHEN act = 3 & resh>0 & stack = 13 THEN CALL RESHUFFLE
		WHEN act = 3 & stack = 12 THEN DO
			SELECTGUI 1
			RETURN
		END
		OTHERWISE ERRBEEP
		END
	END
RETURN
RESHUFFLE:
	DO i = 0 TO 6
		CARDSELECT stack.i 52
		MOVECARDS stack.i deck REVERSE
	END
	SHUFFLECARDS deck
	resh = resh - 1
RETURN
DOBASE:
	IF sel~=stack THEN CALL CHBASE
	ELSE DO i= 0 TO 3 UNTIL ok=0
		stack = base.i
		CALL CHBASE
	END
	IF ok=0 THEN DO
		fin = fin +1
		MOVECARDS sel stack
		IF fin = 52 THEN SETGADGET 14 STR wintex
	END
	ELSE ERRBEEP
	sel = 0
RETURN
CHBASE:
	CARDSELECT sel 1
	PARSE VAR RESULT kolors wars xx
	CARDSELECT stack 1
	PARSE VAR RESULT kolor war xx
	IF kolor = '' THEN DO
		kolor = kolors
		war = -1
	END
	IF kolor = kolors & (wars - war) = 1 THEN ok = 0
	ELSE ok = 1
RETURN
