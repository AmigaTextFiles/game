
; $VER: BallMaster Update Script 1.0 (27.09.98)

; Author: Damir Arh

; (c)1998 DIGITAL AMIGA DREAM


; updates BallMaster V1.0 to BallMaster V1.1


;-------------------------------------------------

; first let's set all the strings

(set #select-exe
    (cat "Please select your old program file.")
)

(set #select-exe-help
    (cat "Select the file you use to run the game "
        "(BallMasterAGA or BallMasterECS).\n\n")
)

(set #wrong-file
    (cat "You haven't selected your old program file")
)

(set #copy-program
    (cat "Copying new program file")
)

(set #copy-guide
    (cat "Copying updated guide")
)

(set #copy-gfx
    (cat "Copying new graphics")
)

(set #copy-sfx
    (cat "Copying new music files")
)

(set #delete
    (cat "Deleting unnecessary files")
)

(set #exit
    (cat "Congratulations!\n"
        "You have updated you copy of BallMaster.")
)


;-------------------------------------------------

(set #source-dir
    (tackon (pathonly @icon) "files")
)


;-------------------------------------------------

; ask, where the exe-file is located

(set #exe-file
    (askfile
        (prompt #select-exe)
        (help #select-exe-help @askdir-help)
        (default @default-dest)
    )
)

(set @default-dest
    (pathonly #exe-file)
)


;-------------------------------------------------

; check the name of the exe-file

(set #filename
    (fileonly #exe-file)
)

(if (= #filename "BallMasterAGA")
    (set #aga 1)
    (if (= #filename "BallMasterECS")
        (set #aga 0)
        (abort #wrong-file)
    )
)

(complete 20)


;------------------------------------------------

; copy the files

(copyfiles
    (prompt #copy-program)
    (source (tackon #source-dir #filename))
    (dest @default-dest)
)

(complete 30)

(copyfiles
    (prompt #copy-guide)
    (source (tackon #source-dir "BallMaster.guide"))
    (dest @default-dest)
)

(complete 40)

(copyfiles
    (prompt #copy-gfx)
    (source (tackon #source-dir
        (if (= #aga 1)
            ("gfxAGA/about.iff")
            ("gfxECS/about.iff")
        )
    ))
    (dest (tackon @default-dest "gfx"))
)

(complete 60)

(copyfiles
    (prompt #copy-sfx)
    (source (tackon #source-dir "sfx"))
    (dest (tackon @default-dest "sfx"))
    (all)
)

(complete 80)

(delete
    (tackon @default-dest "sfx/NewTNT.mus")
    (prompt #delete)
)

(complete 100)


;-------------------------------------------------

;installation is complete

(exit #exit)

