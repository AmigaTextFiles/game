! SUMMON.AGT
!    Copyright 1997,1999 Robert Masenten
!
!  This is a game fragment containing a couple of the examples from 
!  the file README.MAGX
!

version magx
#include stddecl.agt

title
SUMMONINGS

A Game Fragment with Examples
from the MAGX documentation
end_title

objflag creature
[is ghost]
end_objflag


Room [Narrow Path]
Narrow Path
E [Bar] 
End_room

Room_descr [Narrow Path]
This is a narrow path leading eastward to an inn.
end_room_descr

Noun [Live Tree]
tree
live
A tree is growing here.
unmovable 
location [Narrow Path] 
end_noun

Noun [Gold Tree]
tree
gold
A suprisingly lifelike gold tree is here.
unmovable
location [Nowhere]
end_noun

noun_descr [gold tree]
The tree seems to be made of pure gold. It looks remarkably natural.
end_noun_descr


COMMAND TOUCH TREE
 1 NOT Present NOUN 
   EndDisambig 0
   PrintMessage "You don't see the $noun$ here."
   DoneWithTurn
 1 Equal NOUN [Live Tree]
   PrintMessage "The tree turns to gold!"
   SwapLocations [Live Tree] [Gold Tree]
   SetIt [Gold Tree]   
   DoneWithTurn
 1 ! NOUN is not the live tree 
   PrintMessage "You touch the $noun$ but nothing seems to happen."
   DoneWithTurn         
END_COMMAND


Room [Bar]
The Blue Hawk
W [Narrow Path]
End_room

Room_descr [Bar]
This inn has seen better days, but is still well stocked with drink.
end_room_descr

noun [wine]
wine
red
There is a bottle of French red wine sitting here.
drinkable
location [bar]
noun_synonyms french bottle spirit
end_noun

noun [vodka]
vodka
russian
There is a bottle of Russian vodka here.
location [bar]
drinkable
noun_synonyms spirit bottle
end_noun

creature [captain black]
black
captain
The ghost of Captain Black hovers here, glaring at you.
noun_synonyms ghost spirit
[is ghost]
end_creature

creature [air spirit]
spirit
air
An air spirit hovers here, looking like a cloud.
[is ghost]
end_creature

verb
summon call invoke global_scope
touch poke 
end_verb

variable [drunk?]

command summon
  PrintMessage "That requires an object."
  DoneWithTurn
end_command

command summon any
  1 NOT ObjFlagOn NOUN.[is ghost]
    EndDisambig 0
    PrintMessage "You can't summon the $noun$." 
    DoneWithTurn
  1 Present NOUN
    EndDisambig 100
    PrintMessage "The $noun$ is already here."
    DoneWithTurn
  1 PrintMessage "You cast the spell of summoning!"
    PutInCurrentRoom NOUN
    DoneWithTurn
end_command 

command drink any
  1 NOT Present NOUN
    EndDisambig 0
    PrintMessage "That isn't here."
    DoneWithTurn
  1 NOT IsDrinkable Noun
    EndDisambig 50
    PrintMessage "You can't drink that!"
    DoneWithTurn
  1 PrintMessage "You take a sip of the $noun$." 
    Add [drunk?] 1
    DoneWithTurn
end_command 



