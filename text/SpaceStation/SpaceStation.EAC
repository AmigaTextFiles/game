 .--------------------.
 |                    |
 |   Space Station    |
 |                    |
 `--------------------'

DEC:knife,kicked,crate,passed,eat,paper,wore,eaten,beamoff
DEC:monseen,crowbar,isolated,shovel,dir,temp,nofloor,lift,cpu,roland,lamp
DEC:shoes,emblem,dummy,doorseen,idcard,burn,fuel,full,level=1
DEC:health=1000,result

TITLE:Space Station
AUTHOR:Robert Szacki
VERSION:1.4
COPYRIGHT:Public Domain
WINDOW:Primary objective:Find a spaceship.

 .-------.
 | Start |
 `-------'

PAGE:START

Welcome to my first ever adventure game for EAC.
I hope you will enjoy it. It's kept in so called
cyber-punk climate.
   It happened all of sudden. You were teleported
to a hotel area with special mission. The boss
advised you of the aim in few words: find a spaceship.
You woke up sitting on the ground in dimly lit corridor.
To be accurate, you have no idea where a space station is.
Both ways lead to unknown terrains. At the moment
you noticed a normal sheet of paper moving about with 
imperceptible wind. You took it and read it.
It's an advertisement of a titanium fuel
which is described as the most powerful. You threw the
paper away then began to lift yourself up.
[more]
PAUSE:
GOTO:START2
ENDPAGE:

 .---------.
 | Arrival |
 `---------'

PAGE:START2

ADDOPT:Look around:look_out
ENDPAGE:

PAGE:START3
WRITEIF:burn=1

At the moment you noticed something that you never
felt before. The plastic floor is a little slimy and elastic.
ENDWRITE:

ADDOPT:Go to east wing:go_east
ADDOPT:Go to hall:go_west
ADDOPTIF:knife=1 AND doorseen=0:Cut power supply cables:cut
ADDOPTIF:doorseen=1:Cut power supply cable from room 302:cut302
ADDOPTIF:burn=1 AND nofloor=0:Prize the floor:rise_floor
ADDOPTIF:nofloor=1 AND cpu=0:Pull out the CPU:pull_cpu
ADDOPT:Look around:look_out
ENDPAGE:

PAGE:look_out

Straight walls surrounding you are painted white,
but a green light from the ceiling floods whole the corridor.
There is a small hole in the wall where you can see
plenty of cables. The air is warm and it seems
that the heater is mounted under the floor. Interior
decoration is dull. The west way leads to bigger room
like hall, east way is long and you can't see what's
there from the distance.
GOTO:START3
ENDPAGE:

PAGE:cut

WRITEIF:isolated=1
Suddenly all lights went off. Whole corridor became
dark except areas being lighted by red glow of security 
lights. Police appeared on the stage and immediately arrested you.

<Unlucky! You should be more careful now...>
ENDWRITE:
GOIF:isolated=0:shaft
ENDPAGE:

PAGE:shaft
It was bad movement. As you reached the cable with your knife,
deadly stream shocked you.

<Oh dear! You will need to restart...>
ENDPAGE:

PAGE:cut302

WRITEIF:isolated=1
You have cut the appropriate cable and, luckily,
nothing strange occured. Now if all is OK room 302
hasn't got any power supply.
LET:doorseen=2
GOTO:START3
ENDWRITE:
GOTO:shaft
ENDPAGE:

PAGE:rise_floor

You found pipes red of hot and a CPU in the socket.
Hopefully it will work with most of devices at this sector.
But the way to gather this item isn't so easy, as the CPU
is located very deep.
LET:nofloor=1
LET:burn=0
GOTO:START3
ENDPAGE:

PAGE:pull_cpu

WRITEIF:temp>-2
Hot pipes prevents you from doing this.
You will need to disable the boiler in this instance.
GOTO:START3
ENDWRITE:
You pulled out the CPU without big problems.
Instantly, the whole heat of the air just disappeared.
LET:cpu=1
CLRTEXT:
ADDTEXT:You've got a CPU.
GOTO:START3
ENDPAGE:

 .------------------.
 | Station entrance |
 `------------------'

PAGE:go_east

Wandering through the corridor you met some people
of the country. They looked strange, each of them had
the same white uniform. Eventually you stopped
at the bottom of corridor. It looks exactly like
previous place except the blue carpet which lies on the floor,
and fluorescents lamps which fade sometimes. You are standing
at the big door above which there is a telebim showing short message:
   » Space station «.
WRITEIF:crate=0
You decided to look below the carpet and immediately noticed a trapdoor
with wooden hinges. Recycling symbol tells that this should be a trashcan.
ENDWRITE:
PAGE:at_door

ADDOPTIF:knife=1 AND crate=0:Saw the hinges:cut_crate
ADDOPTIF:crate=1:Jump down:to_acid
ADDOPTIF:idcard=0:Open the door:lock_door
ADDOPTIF:idcard=1:Insert ID card into the scanner:enter
ADDOPT:Go to arrival:go_back
ENDPAGE:

PAGE:lock_door

You will need an ID card here. It is automatically given to
clients of the hotel. Probably the only way to get in there is
to steal one.
GOTO:at_door
ENDPAGE:

PAGE:cut_crate

You have sawed wooden hinges with easiness and pushed metal block
aside opening a very long shaft.
LET:crate=1
GOTO:at_door
ENDPAGE:

PAGE:enter
GOIF:emblem=2:at_launch

You're in space station, but it's not a final of your trip.
The way to the hangar is guarded by some guys and they doesn't
want to let you in. It will cost some.

ADDOPTIF:emblem=1:Give them the emblem:give_emblem
ADDOPT:Leave the station:at_door
ENDPAGE:

PAGE:give_emblem

They found your emblem quite valueble and immediately disappeared
in a nearby corridor in strange reasons.
LET:emblem=2
GOTO:at_launch
ENDPAGE:

PAGE:at_launch

The hangar is extremaly large place where all the ships are docked ready to
start. After short walk, you found the ship owned by the same guy
which owns the ID card. Unlucky, the fuel gauge points to zero.

ADDOPTIF:fuel>0:Fuel up the ship:empty__full
ADDOPTIF:full>0:Launch the ship:launch
ADDOPT:Leave the hangar:at_door
ENDPAGE:

PAGE:launch

You entered the ship hoping to finish your task. The engine started
automatically making some noises
WRITEIF:full<>2
and exploded!. The ship burned together with you.

<Such a thing when I was pretty close !>
ENDWRITE:
WRITEIF:full=2
and turned on! You pulled down the lever. The hangar door opened
fast allowing you to fly away. The ship was auto-piloted during
further fly, therefore you have entered the hibernation capsule.
[more]
PAUSE:
PEN:3


.······» SPACE STATION «·········.
·                                |
·Scenario &                      |
·Design:    Robert Szacki        |
·                                |
·--------------------            |
·My address:                     |
·   Robert Szacki                |
·   ul. Sulejkowska 35/16        |
·   04-129 Warszawa              |
·   Poland                       |
`--------------------------------'
I hope, you have had great time playing it.
And please forgive me if my english isn't so good.
ENDWRITE:
ENDPAGE:

PAGE:empty__full
OK. Now the ship should start.
LET:full=fuel
LET:fuel=0
GOTO:at_launch
ENDPAGE:

 .------.
 | Hall |
 `------'

PAGE:go_west

You are in small dark hall. From here you can get at most of places
in the hotel. One passage leads to the emergency exit as the sign says.
You can see big picture of falcon on the ceiling.
There is a ladder leading upwards.
WRITEIF:kicked=0
You notice small recess where you can see a robot
with aerial on one's head.
ENDWRITE:
PAGE:at_west

ADDOPT:Go trough green coriddor:go_back
ADDOPT:Go to emergency exit:at_portal
ADDOPTIF:shoes=0:Climb the ladder:go_up_fail
ADDOPTIF:shoes=1:Climb the ladder:go_up
ADDOPTIF:kicked=0:Kick the robot:kick_rob
ENDPAGE:

PAGE:go_back

Travelling trough the passage you stood in
the place where the green lights beam from the ceiling.
GOTO:START3
ENDPAGE:

PAGE:kick_rob

Robot fell and started to send signal in Morse language. Probably
he wants to call help. If so, you have to be quick. In the moment
you noticed a knife in robot's backpack.

LET:kicked=1
ADDOPT:Pull out an aerial on his head:push_button
ADDOPT:Pull out the knife:cut_rob
ADDOPT:Stay away from him:away
ENDPAGE:

PAGE:away

While you were leaving the recess red light went slowly off the corner.
Shape of the guard with photon-launcher on the arm came into view.
He shot you in advance, before you tried to make a move.

<Oh dear! You will need more luck to finish this game...>
ENDPAGE:

PAGE:cut_rob

You managed to pull the knife out but it didn't budge.
You tried again and at the moment you were paralyzed by the robot.

<Unlucky! You should be more careful now...>
ENDPAGE:

PAGE:push_button

After a while, signal became silent, the robot eventually died.
Taking an opportunity you took the knife.
LET:knife=1
CLRTEXT:
ADDTEXT:You took not isolated knife.
GOTO:at_west
ENDPAGE:

PAGE:go_up_fail

In half-way you slept and fell on the ground
from five metres. After a while you stood up with pain.
SUB:health:50
GOIF:health<=0:zero_health
GOTO:at_west
ENDPAGE:

 .------.
 | Exit |
 `------'

PAGE:at_portal

You're at the exit of the hotel. It leads on the planet ground, however
you can't pass due to extremely tall door. You can see small window
beneath. Thanks to it the outside can be observed. All is dark. You can 
hear only sounds of starting or landing ships. Additional passage in one
of the walls uncovers long and lit passage where you can see a lift.

ADDOPT:Enter main hall:go_west
ADDOPTIF:lift=1:Use a lift:elevator
ENDPAGE:

 .-------------.
 | Upper stage |
 `-------------'

PAGE:go_up

You are on the upper stage now. It's mostly filled with hotel apartments.
The gold carpet covers the floor. One door leads to vehicle which extremely fast
transports people to different areas in the sector. It's using own rails
hidden in the buildings. But you need a special ticket to use it.
PAGE:at_up

ADDOPTIF:doorseen<>3:Look around:look_out2
ADDOPT:Climb down the ladder:go_west
ENDPAGE:

PAGE:look_out2

When you were travelling in the passage, you found room 302, mentioned
by your boss before teleporting.
There is deep silent in there. Probably the owner is not inside.

PAGE:at_hoteldoor

ADDOPTIF:doorseen<2:Open the door:fail_hoteldoor
ADDOPTIF:doorseen=2:Open the door:open_hoteldoor
ADDOPTIF:doorseen=0:Look closer:look_at_hoteldoor
ADDOPT:Climb down the ladder:go_west
ENDPAGE:

PAGE:look_at_hoteldoor

You noticed a small terminal near the door saying that
the owner is at the bar. Also, there are some data about the
installations in the room itself.
LET:doorseen=1
GOTO:at_hoteldoor
ENDPAGE:

PAGE:fail_hoteldoor

As you reached the knob, you were shocked by the stream and landed
on the opposite wall.
SUB:health:200
GOIF:health<=0:zero_health
GOTO:at_hoteldoor
ENDPAGE:

PAGE:open_hoteldoor

WRITEIF:lamp=1
You quickly turned on the torch and broke into room. When you found
an ID card in one of the drawers, picked it up and immediately left
the room.
LET:idcard=1
LET:doorseen=3
CLRTEXT:
ADDTEXT:You've got an ID card.
GOTO:at_hoteldoor
ENDWRITE:
It's totally dark inside!!! You need a torch or something before advancing.
GOTO:at_hoteldoor
ENDPAGE:

 .------------.
 | Acid river |
 `------------'

PAGE:to_acid
LET:crate=2

After long flight you reached place which looks like sewage system.
A river of acid flows across the whole sewer. Acid melts everything
on its way. 
PAGE:acid

You crossed short stretch.
GOIF:passed=3:land
RND:eat:4
WRITEIF:eat=0
Acid is melting your whole body.
SUB:health:200
ENDWRITE:
WRITEIF:eat=1
Acid is scalding your head and stomach.
SUB:health:100
ENDWRITE:
WRITEIF:eat=2
Acid is scalding your hands and legs.
SUB:health:50
ENDWRITE:
WRITEIF:eat=3
Acid is neutralized in this zone.
ENDWRITE:

GOIF:health<=0:zero_health
CLRTEXT:
ADDTEXT:Actual health:
ADDVAR:health
ADD:passed:1
ADDOPT:Swim further:acid
ENDPAGE:

 .------.
 | Land |
 `------'

PAGE:land

Finally you reached an end of this terrible place. You're in shock.
PAGE:at_land

ADDOPT:Rest a while:wait
ADDOPTIF:wore=0:Drop your clothes:drop_clothes
ENDPAGE:

PAGE:drop_clothes

You feel a lot better now.
LET:wore=1
ADD:health:25
GOTO:at_land
ENDPAGE:

 .-------------------.
 | Land (after rest) |
 `-------------------'

PAGE:wait

You fell asleep for an hour.
PAGE:at_trash

This place smells poor. The ceiling is invisible. Large red spotlights
create particular atmosphere. Some trash lies at the corner. Different
installations begins its way from one wall to end up in other wall.
You hear stream of the acid. The floor is built with hard, undestructible
rock.
PAGE:at_trash2

ADDOPT:Look at the corner:exam_trash
ADDOPT:Seek exit:seek
ENDPAGE:

PAGE:exam_trash

You can see a big stack of old trash.
WRITEIF:wore<>2
On the top there is an old uniform with grey helmet.
ENDWRITE:
WRITEIF:eaten=0
You notice broken meat around, however strange substance covers it.
ENDWRITE:

ADDOPTIF:wore<>2:Wear an uniform:wear_it
ADDOPTIF:wore>0 AND paper=0:Examine old clothes:exam_clothes
ADDOPTIF:eaten=0:Eat old food:eat_food
ADDOPTIF:shovel=1:Dig into the trash:dig_trash
ADDOPT:Seek exit:seek
ENDPAGE:

PAGE:exam_clothes

You found a box and toilet paper within. It sticked to clothes when
you were swimming in the acid.
LET:paper=1
CLRTEXT:
ADDTEXT:You took a toilet paper.
GOTO:at_trash2
ENDPAGE:

PAGE:dig_trash

WRITEIF:shoes=0
You found quite pretty shoes. You decided to put them on. There is also 
a crumpled unuseful hat.
LET:shoes=1
ENDWRITE:
WRITEIF:roland=2 AND emblem=0
After a while you noticed something sparkling.
Yes! It's the emblem you were searching.
LET:emblem=1
CLRTEXT:
ADDTEXT:You found an emblem.
ENDWRITE:
You didn't find anything more.
GOTO:at_trash2
ENDPAGE:

PAGE:wear_it

Oh! The new uniform provides automatic humidification of the skin.
Actually, you feel a lot better now.
LET:wore=2
CLRTEXT:
ADDTEXT:You wore an uniform.
GOTO:at_trash2
ENDPAGE:

PAGE:eat_food

Bleeh! It doesn't taste well...
LET:eaten=1
GOTO:at_trash2
ENDPAGE:

PAGE:seek

Wandering in this gloomy scenery, you found a nicer place at last.
Whole ceiling is now visible.
GOTO:into_machinery
ENDPAGE:

 .---------------------.
 | Basement stage 1    |
 `---------------------'

PAGE:into_machinery

This is a basement on the lowest stage. The lift leads both upwards and
downwards.
The interference of the lights creates interesting mosaic on
the wall. You hear noises of the hidden machinery. The air is clean,
you feel warm breath of air from the vent. Color of the walls is brown.
You see big shape of falcon on one of them, the same as in the hall.
WRITEIF:beamoff<>3
You see a hole in the wall with lever inside.
ENDWRITE:
WRITEIF:beamoff<2
Unlucky, red laser beams makes access impossible.
ENDWRITE:
PAGE:machinery

ADDOPT:Enter the lift:elevator
ADDOPTIF:beamoff=3:Enter the boiler-room:into_warmer
ADDOPTIF:beamoff<3:Put hand into the cell:cell_check
ADDOPT:Go to end of acid river:at_trash
ADDOPTIF:monseen=1:Enter the vent:into_vent
ENDPAGE:

PAGE:cell_check

WRITEIF:beamoff=0
You have burned a hand. Luckily, you have another one...
LET:beamoff=1
SUB:health:200
GOIF:health<=0:zero_health
GOTO:machinery
ENDWRITE:
WRITEIF:beamoff=2
As you pulled the lever, nearby wall began to raise showing new,
not explored terrain.
LET:beamoff=3
GOTO:machinery
ENDWRITE:
You lost both hands and you cannot complete the task!

Game Over
ENDPAGE:

PAGE:into_vent

You crawled into the vent and found that alien object - a crowbar.
WRITEIF:health>=500
LET:monseen=2
LET:crowbar=1
CLRTEXT:
ADDTEXT:You found a crowbar.
GOTO:machinery
ENDWRITE:
Unexpectedly you lost your mind and eventually died.

<Sorry! You should avoid any health-reducing traps>
ENDPAGE:

 .-------------.
 | Boiler room |
 `-------------'

PAGE:into_warmer

The boiler room consists of two stages. It's dark and sultry. In front
of you, there is a boiler and a valve. You can see splashed glue on the table.
WRITEIF:shovel=0
There was a shovel hanging on the wall, so you took it.
LET:shovel=1
CLRTEXT:
ADDTEXT:You took a shovel.
ENDWRITE:
PAGE:at_warmer

ADDOPT:Leave the boiler-room:into_machinery
ADDOPT:Rotate valve left:rotate
ADDOPT:Rotate valve right:rotate
ADDOPTIF:cpu=2:Get up to higher stage:high_warm
ADDOPTIF:cpu=0:Check out the elevator:out_of_order
ADDOPTIF:cpu=1:Put the CPU in elevator control:fix_elev
ADDOPTIF:paper=1:Stick the paper to the knife:isolate
ADDOPTIF:dummy=0 AND shoes=0:Fix your old shoes:fix_shoes
ENDPAGE:

PAGE:rotate
GETRESULT:dir
WRITEIF:dir=2
ADD:temp:1
ENDWRITE:
WRITEIF:dir=3
SUB:temp:1
ENDWRITE:

WRITEIF:temp=-2
The device is almost not working and it's pretty cool in here.
ENDWRITE:
WRITEIF:temp=-1
The temperature is lowered.
ENDWRITE:
WRITEIF:temp=0
It's normal temperature now.
ENDWRITE:
WRITEIF:temp=1
The temperature is very high.
ENDWRITE:
WRITEIF:temp=2
It's pretty hot in here. Plastic material surrounding pipes is begining
to melt.
LET:burn=1
ENDWRITE:
GOIF:temp>=-2 AND temp<=2:at_warmer
While you were rotating and rotating... the security alert met
the ear. Valve automatically returned to normal position. Square-built
robot appeared from the darkness and shot at you cascade of bullets.
Aargghhh... you screamed and began to escape...
[more]
PAUSE:
You hid in a dark area beneath the table.
Fortunatelly, the robot went away soon.
LET:temp=0
GOTO:into_machinery
ENDPAGE:

PAGE:out_of_order

The CPU has been stolen from the elevator control.
GOTO:at_warmer
ENDPAGE:

PAGE:fix_shoes

You tried to fix old shoes, but it did not help anything. Probably,
you shouldn't make it on your own.
LET:dummy=1
GOTO:at_warmer
ENDPAGE:

PAGE:fix_elev

You have put the CPU in the appropriate socket. The device began
to work properly.
LET:cpu=2
GOTO:at_warmer
ENDPAGE:

PAGE:isolate

Using a glue stain on the table you sticked the paper to knife's hand.
You created new possibilites for this tool.
LET:paper=2
LET:isolated=1
CLRTEXT:
ADDTEXT:You've got isolated knife.
GOTO:at_warmer
ENDPAGE:

 .---------------------------.
 | Boiler room - High storey |
 `---------------------------'

PAGE:high_warm

You're about twenty metres above the ground. Steel construction
prevents you from falling down. Somebody with torch in hand is working
at unit nearly you.
PAGE:at_high_warm

ADDOPTIF:roland=0:Talk to worker:talk1
ADDOPTIF:roland=1:Talk to worker once again:talk2
ADDOPT:Reach a lower stage:into_warmer
ENDPAGE:

PAGE:talk1

He turned about.
   » What are you doing here without a torch ?
     Here, take it. «
He gave you a torch.
   » Thank you « - you answered
CLRTEXT:
ADDTEXT:You received a torch.
LET:lamp=1
LET:roland=1
GOTO:at_high_warm
ENDPAGE:

PAGE:talk2

It seems that the guy lost his patience.
He picked up his torch and looked at you more closely.
   » Listen to me: You're working in storage section uniform
     and doesn't have needed emblem. Watch out for T-0 guards as they
     can kill everything that moves. You've been warned. «
   » It sounds grimly « - you thought.
LET:roland=2
GOTO:at_high_warm
ENDPAGE:

PAGE:elevator

ADDOPTIF:level<3:Take the lift to the upper stage:lift_up
ADDOPTIF:level>0:Take the lift to the lower stage:lift_down
ADDOPTIF:level=0:Exit the lift:warehouse
ADDOPTIF:level=1:Exit the lift:into_machinery
ADDOPTIF:level=2:Exit the lift:control_room
ADDOPTIF:level=3:Exit the lift:go_elev
CLRTEXT:
ADDTEXT:Actual floor-:
ADDVAR:level
ENDPAGE:

PAGE:lift_up
ADD:level:1
GOTO:elevator
ENDPAGE:

PAGE:lift_down
SUB:level:1
GOTO:elevator
ENDPAGE:

 .---------------------.
 | Basement stage 2    |
 `---------------------'

PAGE:control_room

It seems, that you're placed in technical supervising center. There are
many different control panels in this room. You see monitors and LCDs
hanging below the ceiling. You see lift door as well as flight of stairs
leading upwards.
PAGE:at_control

ADDOPT:Enter the lift:elevator
ADDOPT:Come closer to buttons:buttons
ADDOPTIF:beamoff<2:Come closer to levers:levers
ADDOPTIF:monseen=0:Look at monitors:monitors
ENDPAGE:

PAGE:buttons

ADDOPT:Push the green one:button
ADDOPT:Stay away:at_control
ENDPAGE:

PAGE:button

Nothing happens.
GOTO:at_control
ENDPAGE:

PAGE:levers

ADDOPTIF:crowbar=1:Pull that big lever with crowbar:lever
ADDOPTIF:crowbar=0:Pull that big lever:lever_weak
ADDOPT:Stay away:at_control
ENDPAGE:

PAGE:lever_weak

For all your trying the lever doesn't budge.
GOTO:at_control
ENDPAGE:

PAGE:lever

As you pulled the lever, nearby door opened showing hostile guys.
WRITEIF:wore=2
Thanks to uniform, they recognized you as the worker.
   » Why had you disable security system ? «
   » Err.. It's not me, but somebody else. «
   » Where is he ? «
   » Err.. I paralysed him and threw to acid. «
   » Good work, major. «
After seconds, they just disappeared. What idiots since they take
things on trust.
LET:beamoff=2
GOTO:at_control
ENDWRITE:
   » Who, the hell, are you ? «
   » Err.. I've been just wandering around. Bye then.«
Your escape did not last long. A sniper-robot appeared and killed you 
by one shot.

<Oops! It seems that you have to be more careful.>
ENDPAGE:

PAGE:monitors

The monitors show ventilation plan. You caught sight of one blue
circle - an alien object which stucked in the vent.
LET:monseen=1
GOTO:at_control
ENDPAGE:

PAGE:go_elev
GOIF:lift=1:at_portal

The way outside is blocked by black iron gate with yellow
diagonal strips.

ADDOPT:Enter the lift:elevator
ADDOPTIF:crowbar=1:Hit the gate with crowbar:hit_door
ENDPAGE:

PAGE:hit_door

Iron gate was destroyed into pieces. You crossed the threshold of the lift
and appeared on the hotel level, just near the security exit.
LET:lift=1
GOTO:at_portal
ENDPAGE:

 .-----------.
 | Warehouse |
 `-----------'

PAGE:warehouse

You stopped at the entrance of a storage. You noticed radioactivity
symbol over the gate. It is silent here.
GOIF:emblem=1:in_warehouse
But, once you stepped inside, the T-0 robots jumped out their holes and
aimed at you with miscellanous weapons. There was no way to defence.

<Sorry! As you see this world is dangerous.>
ENDPAGE:

PAGE:in_warehouse
You stepped inside and without being disturb reached to the restricted 
area with fuel tanks. You feel, that you are watching by T-0s, so you must
act quickly.

ADDOPT:Pick up uranium fuel:get_fuel
ADDOPT:Pick up titanium fuel:get_fuel
ADDOPT:Pick up iridium fuel:get_fuel
ENDPAGE:

PAGE:get_fuel

You found that the petrol can is really heavy, but you were strong enough to return to the lift
before anyone could have chance to see you.
GETRESULT:result
LET:fuel=result
CLRTEXT:
ADDTEXT:You gained a fuel.
GOTO:elevator
ENDPAGE:

 .------------------.
 | The Others       |
 `------------------'

PAGE:INVENTORY
CLRTEXT:
ADDTEXT:You've got-:
ADDTEXTIF:knife=1: Knife
ADDTEXTIF:paper=1: Paper
ADDTEXTIF:wore=2: Uniform
ADDTEXTIF:crowbar=1: Crowbar
ADDTEXTIF:shovel=1: Shovel
ADDTEXTIF:cpu=1: CPU
ADDTEXTIF:lamp=1: Torch
ADDTEXTIF:emblem=1: Emblem
ADDTEXTIF:fuel<>0: Fuel
ENDPAGE:

PAGE:HEALTH
CLRTEXT:
ADDTEXT:Your health is-:
ADDVAR:health
ADDTEXTIF:health>=800: (Very good)
ADDTEXTIF:health>=500 AND health<800: (Good)
ADDTEXTIF:health>=300 AND health<500: (Bad)
ADDTEXTIF:health<300: (Critical)
ENDPAGE:

PAGE:zero_health

<Unlucky! You died due to poor health.>
ENDPAGE:

