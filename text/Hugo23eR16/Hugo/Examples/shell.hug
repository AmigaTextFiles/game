!\---------------------------------------------------------------------------
	
	HUGO v2.3 SHELL
	by Kent Tessman (c) 1995-1997

---------------------------------------------------------------------------\!

! Uncomment the following to use a precompiled version of the Hugo Library:
! #set PRECOMPILED_LIBRARY

! Uncomment the following to include the HugoFix Debugging Library:
! #set DEBUG

! Uncomment the following to include verb stub routines:
! #set VERBSTUBS

#include "grammar.g"                    ! grammar must come first

#ifset PRECOMPILED_LIBRARY
#link "hugolib.hlb"
#endif
#ifclear PRECOMPILED_LIBRARY
#include "hugolib.h"
#endif

routine init
{
	counter = -1

	STATUSTYPE = 1                  ! score/turns
	TEXTCOLOR = DEF_FOREGROUND
	BGCOLOR = DEF_BACKGROUND
	SL_TEXTCOLOR = BRIGHT_WHITE
	SL_BGCOLOR = BLUE

	prompt = ">"
	color TEXTCOLOR, BGCOLOR

	cls
	Font(BOLD_ON | DEFAULT_FONT)
	"SHELL"
	Font(BOLD_OFF)
	"An Interactive Starting Point\n"
	print BANNER

	player = you                    ! player initialization
	location = emptyroom
	old_location = location        
	
	move player to location
	FindLight(location)
	DescribePlace(location)
	location is visited
	CalculateHolding(player)

#ifset USE_PLURAL_OBJECTS
	InitPluralObjects
#endif
}

#ifset PRECOMPILED_LIBRARY
replace main
{
#endif
#ifclear PRECOMPILED_LIBRARY
routine main
{
#endif
	counter = counter + 1
	PrintStatusLine
	run location.each_turn
	runevents
	RunScripts
	if parent(speaking)~=location
		speaking = 0
}

character you "you"
{
	nouns "me", "myself"
	pronouns "you", "you", "your", "yourself"
	capacity 100
	long_desc {"Looking good."}
	is plural                       ! for matching second-person verbs
	is hidden			! for location descriptions
}

room emptyroom "empty room"
{
}
