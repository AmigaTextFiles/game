; F1GP-Ed Installer script (Aminet shareware archive version)
; $VER: 1.00 (19.11.94)
; written by Oliver Roberts

(set OSVersion (/ (getversion) 65536))

(welcome
   ("This utility will attempt to, either, install F1GP-Ed for the first time ")
   ("or update an existing version, as best it can on your system.  \n\n")
   ("This program will update libraries if older versions exist, ")
   ("and copy all files needed.\n")
)

(if(set destdir
      (askdir
         (prompt "Select a drawer in which F1GP-Ed should be installed/updated?\n(a separate drawer will not be created!)")
         (help @askdir-help)
         (default @default-dest)
      )
   )
   (
      (set @default-dest destdir)
   )
)

(set libdir
   (askdir
      (prompt "Where are the system libraries?")
      (help "Select the drawer where your system libraries are stored.  This can usually be accessed by the LIBS: assign and is therefore the default.")
      (default "LIBS:")
   )
)

(complete 10)

(if (< OSVersion 36)
   (
      (set ReqToolsSource "libs13/reqtools.library")
      (copylib
         (prompt "Installing newer version of gadtools13.library")
         (source "libs13/gadtools13.library")
         (dest libdir)
         (help @copylib-help)
         (newname "gadtools13.library")
         (confirm)
      )
   )
   (
      (set ReqToolsSource "libs20/reqtools.library")
   )
)

(copylib
   (prompt "Installing newer version of reqtools.library")
   (source ReqToolsSource)
   (dest libdir)
   (help @copylib-help)
   (newname "reqtools.library")
   (confirm)
)

(complete 20)

(copylib
    (prompt "Copying F1GP-Ed...")
    (source "F1GP-Ed")
    (dest @default-dest)
    (help @copylib-help)
    (infos)
    (confirm)
)

(if (NOT(exists ("s:F1GP-Ed.config")))
   (
      (copyfiles
         (prompt "Copying default F1GP-Ed config file...")
         (source "s/F1GP-Ed.config")
         (dest "s:")
         (newname "F1GP-Ed.config")
         (help @copyfiles-help)
         (confirm)
      )
   )
)

(complete 40)

(copyfiles
    (prompt "Copying F1GP-Ed Documentation...")
    (source "F1GP-Ed.guide")
    (dest @default-dest)
    (help @copyfiles-help)
    (infos)
)

(copyfiles
    (prompt "Copying F1GP-Ed Registration Form...")
    (source "Registration.txt")
    (dest @default-dest)
    (help @copyfiles-help)
    (infos)
)

(complete 60)

(set opts
   (askoptions
      (prompt "Please select which optional files you want to copy (existing files will be overwritten)")
      (help @askoptions-help)
      (choices "Real-life 1994 F1GP-Ed datafile" "Replacement Engine sound sample" "F1GP-Ed V2.30 Demo" "F1GP 2 News")
   )
)

(complete 70)

(if (IN opts 0)
   (copyfiles
      (prompt "Copying real-life 1994 settings...")
      (source "1994.f1gp")
      (dest @default-dest)
      (help @copyfiles-help)
      (infos)
   )
)

(if (IN opts 1)
   (copyfiles
      (prompt "Copying example replacement Engine sound sample...")
      (source "EngineSample.RAW")
      (dest @default-dest)
      (help @copyfiles-help)
   )
)

(complete 80)

(if (IN opts 2)
   (copyfiles
      (prompt "Copying the demo of the registered F1GP-Ed...")
      (source "F1GP-Ed_Demo")
      (dest @default-dest)
      (help @copyfiles-help)
      (infos)
   )
)

(if (IN opts 3)
   (copyfiles
      (prompt "Copying F1GP 2 textfile...")
      (source "F1GP_2.README")
      (dest @default-dest)
      (help @copyfiles-help)
      (infos)
   )
)

(complete 100)

(exit
    ("Files installed.  Enjoy using F1GP-Ed!\n")
    ("Please email any bugs or suggestions to the author at ")
    ("O.J.C.Roberts@essex.ac.uk (see documentation).")
)
