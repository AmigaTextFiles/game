; $VER: MK II HD Install-Script V1.0 (28.11.94) by Roland for Sentinel

(set GameDir "MK_II")
(set TotalDisks 4)
(set CurrentDisk 0)
(set Percent 20)
(set DiskPercent (/ (- 100 Percent) TotalDisks))

;try to figure out a place where the user usually installs his games
(if (exists "Games:" (noreq) )
    (set @default-dest "Games:")
    (if (exists "SYS:Games" (noreq) )
        (set @default-dest "SYS:Games")
        (if (exists "Work:Games" (noreq) )
            (set @default-dest "Work:Games")
            (set @default-dest "SYS:")
        )
    )
)

;ask the user to select a directory to install the game into
(set default-dest
     (tackon (askdir (prompt "Where would you like " @app-name " installed?\n"
                             "A drawer called " GameDir " will be created.")
                     (help @askdir-help)
                     (default @default-dest)
                     (newpath)
             )
     GameDir
     )
)
(set @default-dest default-dest)
(complete 5)

;create the selected directory
(makedir @default-dest
         (infos)
)
(complete 10)

;copy all extra files to this directory
(copyfiles (source "Mortal Kombat II")
           (dest @default-dest)
)
(copyfiles (source "Mortal Kombat II.icon")
           (dest @default-dest)
           (newname "Mortal Kombat II.info")
)
(complete Percent)

;copy all the disks to the data directory
(until (= CurrentDisk TotalDisks)
       (set CurrentDisk (+ CurrentDisk 1))
       (message "\n\n\n\n\n\nPlease insert " @app-name " Disk "
                CurrentDisk " into DF0:"
       )
       (working "\n\n\n\nConverting disk " CurrentDisk
                "\n\n\n\nCall one of our boards:\n\n"
                "Neon City:    +31 (0)478012733\n"
                "              +31 (0)478014014\n"
                "Dream Scape:  +31 (0)52552372 \n"
                "State Dreams: +31 (0)206946550\n"
       )
       (run (cat "Read" CurrentDisk " " @default-dest))
       (set Percent (+ Percent DiskPercent))
       (complete Percent)
)
(complete 100)
