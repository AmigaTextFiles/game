; $VER: All Terrain Racing HD Install-Script V1.0 (2.2.95) by Cobra for ACCESS
; Original Script written by ROLAND for ACCESS

(set GameDir "AllTerrainRacing")
(set TotalDisks 2)
(set CurrentDisk 0)
(set Percent 20)
(set DiskPercent (/ (- 100 Percent) TotalDisks))
(set keuze 0)

;try to figure out a place where the user usually installs his games
(if (exists "Games:" (noreq) )
    (set @default-dest "Games:")
    (if (exists "SYS:Games" (noreq) )
        (set @default-dest "SYS:Games")
        (if (exists "Work:Games" (noreq) )
            (set @default-dest "Work:Games")
            (set @default-dest "SYS:")
        )
    )
)

;ask the user to select a directory to install the game into
(set default-dest
     (tackon (askdir (prompt "Where would you like " @app-name " installed?\n"
                             "A drawer called " GameDir " will be created.")
                     (help @askdir-help)
                     (default @default-dest)
                     (newpath)
             )
     GameDir
     )
)
(set @default-dest default-dest)
(complete 5)

;create the selected directory and a subdirectory called Data
(makedir @default-dest
         (infos)
)
(complete 10)

;copy all extra files to this directory
(copyfiles (source "AllTerrainRacing")
           (dest @default-dest)
)
(copyfiles (source "AllTerrainRacing.info")
           (dest @default-dest)
)
(copyfiles (source "ATR.EXE")
           (dest @default-dest)
)
(complete Percent)

;copy all the disks to the data directory
(until (= CurrentDisk TotalDisks)
       (set CurrentDisk (+ CurrentDisk 1))
       (message "\n\n\n\n\n\nPlease insert " @app-name " Disk "
                CurrentDisk " into DF0:"
       )
       (working "\n\n\n\nConverting disk " CurrentDisk "\n"
       )
       (run (cat "Read " (tackon @default-dest (cat "ATR.DAT" CurrentDisk) ) )
       )
       (set Percent (+ Percent DiskPercent))
       (complete Percent)
)
(complete 100)
