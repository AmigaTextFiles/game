/* "Live demos" room
/*
/* Room flags:
/*    2: First visit to room, regardless of topic. 0=yes, 1=been here
/*    3: Topic chosen from menu
/*    4: Clock testing

/* UPDATE: scroll_frames[;normal_frames]
/*
UPDATE: 6;1

/* SECTION: section_no
/*
SECTION: 1

/* BG_IFF: file
/*
BG_IFF: DemoRoom.iff

/* START_POS: startpos_no;image;x;y;L|M|R;floor_no
/*
START_POS: 1;14;20;104;L;4

/* FLOOR: floor_no;x1;y1;x2;y2;floormaps
/*
FLOOR: 1;0;127;153;169;1-1/2-2/3-2/4-2
FLOOR: 2;36;101;96;169;1-1/2-2/3-P1/4-4
FLOOR: 3;170;131;319;170;1-P1/2-P1/3-3/4-P1
FLOOR: 4;18;101;96;104;1-2/2-2/3-2/4-4

/* PATH: path_no;floor1;floor2;px1;py1;px2;py2;{more_points} (This statement is optional)
PATH: 1;2;3;96;102;110;100;225;103;250;115;220;132

/* EXIT: exit_no;x1;y1;x2;y2;x;y;description (This statement is optional)

CLPART: OlafDemo.iff
/* 1 is the table
ROOMBOBS: 1;1;15;158;117;30;0;0
/* 2 is a piece of wall hiding Olaf on entry into room
ROOMBOBS: 1;2;143;153;16;58;0;0
/* 3 is a table clock
ROOMBOBS: 1;3;66;189;12;44;0;0

CLPART: DemoThings.iff
/* 4 is the floor/path diagram
ROOMBOBS: 1;4;0;62;321;85;0;0

/* STATIC: bob;image;x;y (This statement is optional)
/* Table
STATIC: 30;RBOB1;167;124
/* Wall
STATIC: 31;RBOB2;17;112

/* ANIM: bob;image;anim_ch;anim;x;y (This statement is optional)

/* ROOMOBJ: obj;name;room;VIS|NVIS;bob;image;x;y;cx;cy;cimage;prep;PICK|NPICK;anim_ch;verb;icon;LOW|MID|HIGH;types;w1;w2;w3 (This statement is optional)
ROOMOBJ: 1;clock;5;NVIS;50;RBOB3;125;125;0;-2;12 ; ;NPICK;0;5; ;MID;-;a;this;it

/* Direct actions for the demo room
/*
/* Reset the flag dealing with the clock tutorial
DACT: SETRF 4=0
/*
/* Load the correct Olaf graphics for this room
DACT: CLPART OlafDemo.iff;BOBS 10,11,1,1,31,47,32,0;BOBS 10,21,1,49,31,47,32,0;BOBS 10,31,1,97,31,47,32,0;CLPART OFF
/*
/* Show the clock if that's what we're talking about
DACT: IFRF 3=2;SHOW ROBJ1,125,125,RBOB3
/*
/* Show the floor diagram
/*
DACT: IFRF 3=1;PBOB 0,84,RBOB4
/*
/* Start a timer making Olaf comment when he's been transferred to floor 3.
/*
DACT: IFRF 3=1;DOAFTER 5,2,2
/*
/* Print the tutorial title
DACT: IFRF 3=1;TYPE 4,3,4,9,BORDER,Movement Paths
DACT: IFRF 3=2;TYPE 4,3,4,9,BORDER,A Useless Clock
DACT: IFRF 3=3;TYPE 4,3,4,9,BORDER,Default Commands
DACT: IFRF 3=4;TYPE 4,3,4,9,BORDER,Direct Commands
DACT: IFRF 3=5;TYPE 4,3,4,9,BORDER,Layout Features
DACT: IFRF 3=6;TYPE 4,3,4,9,BORDER,EHB Colours
/*
/* Lights up - enter one Viking hero
DACT: LIGHTS ON;CMOVE 70,157,P
/*
/* First visit information
DACT: IFRF 2=0;CUTSCENE 6,H;SETRF 2=1
/*
/* Path and floor tutorial
/* (Also see the comment in the ACTION: -1;... statement, triggered by
/* timer 2)
/*
DACT: IFRF 3=1;SAY Studying the floors and the path, shown on the floor here, and the FLOOR: and PATH: statements in 5.room, should give you an idea of how floors and paths work.
DACT: IFRF 3=1;SAY By the way, the editor now handles path editing - the only tricky part is specifying which floors are at the path's end points, and of course, the usual "floor maps" mayhem...
DACT: IFRF 3=1;SAY I'm going to let you study this on your own. Click on various spots in the room, both within and outside the floors, to see how I move.
DACT: IFRF 3=1;SAY You will notice how I never stop along the path, but always inside a floor, as close as possible to the point you clicked.
DACT: IFRF 3=1;SAY Okay, click away! Just click one of the commands in the command area when you're finished!;EXIT
/*
/* Clock tutorial
DACT: IFRF 3=2;SAY As you may have noticed, there is a completely useless clock in the bottom right corner.;SAY It's there to show what the TIME_LAYOUT: statement can do.
DACT: IFRF 3=2;SAY The time shown is the GAME time, and it doesn't have anything to do with the REAL time out there in YOUR world.
DACT: IFRF 3=2;SAY You can set the game time at certain points in your adventure, or have a timer update it at regular intervals.;SAY More about that later.
DACT: IFRF 3=2;SAY Now right-click the clock on the table to get the time from me personally!;EXIT
/*
/* Default commands
DACT: IFRF 3=3;SAY Default commands - well, this program uses them EVERYWHERE.;SAY However, this is a special application - so how would you normally use them?
DACT: IFRF 3=3;SAY The most common default command is probably LOOK AT (or similar).;SAY I even recommend that all objects that can appear in the inventory has this default verb.
DACT: IFRF 3=3;SAY This makes browsing the inventory a more consistent affair, because only the object name and not the default verb is automatically shown in the sentence box.
DACT: IFRF 3=3;SAY Other common default commands could be TALK TO for persons, and OPEN for doors.
DACT: IFRF 3=3;SAY Just be careful not to make the game TOO automatic - you don't want to spoil the puzzles by giving the player default commands that solve them!;GOTO 3,1
/*
/* Direct commands
DACT: IFRF 3=4;SAY The three commands in the command area here - "Front page", "Next page" and "Quit" - are all examples of direct commands.
DACT: IFRF 3=4;SAY By the way, the diskette icon is also a direct command: "Save/load"
DACT: IFRF 3=4;SAY Look in graal.main and see how they are defined in the VERB_TEXT: statements.
DACT: IFRF 3=4;SAY All direct commands react immediately when you click them, and they work with both mouse keys.
DACT: IFRF 3=4;SAY Because direct commands don't check for selected objects, you should not normally use IFOBJ with them.
DACT: IFRF 3=4;SAY Check out the QUIT command set up and used in this program, and how the DISABLE_QUIT: statement in graal.main has disabled the normal "q" quit key.
DACT: IFRF 3=4;SAY Use this to end your adventure in a stylish way - before you give the QUIT command, you can jump to a room just containing and end-of-game screen, for example.
DACT: IFRF 3=4;SAY The save and load function has been implemented in a similar way. Click the diskette to see it in action.;GOTO 3,1
/*
/* Layout features
DACT: IFRF 3=5;SAY As you can see from the command area in this diskmag, there are almost no limits to what you can do with the layout in GRAAL 2.
DACT: IFRF 3=5;SAY Note that the inventory seems to be completely gone. In fact, it's just defined in such a way that it SEEMS to be non-existent.
DACT: IFRF 3=5;SAY Among other things, the inventory arrow images are small images consisting of nothing but background colour, which makes them totally transparent...
DACT: IFRF 3=5;SAY ...and the minute areas still occupied by the inventory have been placed where they do not interfere with the other stuff in the command area.;GOTO 3,1
/*
/* EHB colours
DACT: IFRF 3=6;SAY This room, and indeed most other rooms in the Herald, uses the extra half-brite mode to get 64 colours.
DACT: IFRF 3=6;SAY You may notice that the colours here are not dithered, which produces a whole different "feel" to the graphics than that of the GRAAL demo adventure.
DACT: IFRF 3=6;SAY One thing to remember: If the room is 64 colours, make sure the clipart graphics - including the main character - is, too!
DACT: IFRF 3=6;SAY If the images you use in a 64-colour room looks "transparent" in an odd way, you have missed this important fact!
DACT: IFRF 3=6;SAY Also, make sure you DO use the "EHB" mode in your paint program...;SAY ...and not a 64-colour AGA mode, which is something completely different.;GOTO 3,1
DACT: EXIT

/* LINE: dlg;line_no;sentence1;sentence2| ;{cond} (This statement is optional)

/* LACT: dlg;line_no;{cond|comm} (This statement is optional)

/* ACTION: verb_no;{cond|comm} (This statement is optional)
/*
ACTION: -2;CLPART OlafDemo.iff;BOBS 10,11,1,1,31,47,32,0;BOBS 10,21,1,49,31,47,32,0;BOBS 10,31,1,97,31,47,32,0;CLPART OFF;SHOW 0, , , ;EXIT

/* Check if Olaf is on floor 3 and make a comment if so - triggered by
/* timer device 2. Only do it once, so end this with a cancel of timer 2...
/*
ACTION: -1;IFOBJ 2;IFFLOOR 3;CUTSCENE 11,H;CANCEL 2;EXIT

/* Make sure to cancel timer 2 if we exit the room without having used
/* floor 3...
ACTION: 1;CANCEL 2
ACTION: 2;CANCEL 2
ACTION: 3;CANCEL 2

/* ACTION: 2 - Next page
ACTION: 2;IFRF 3=1;SETRF 3=2;GOTO 5,1
ACTION: 2;IFRF 3=2;SETRF 3=3;GOTO 5,1
ACTION: 2;IFRF 3=3;SETRF 3=4;GOTO 5,1
ACTION: 2;IFRF 3=4;SETRF 3=5;GOTO 5,1
ACTION: 2;IFRF 3=5;SETRF 3=6;GOTO 5,1
ACTION: 2;IFRF 3=6;SETRF 4,1=2;GOTO 4,1

/* Activating the table clock...
/*
ACTION: 5;IFOBJ ROBJ1;IFRF 4=0;MOBJ;SAY The time is #TIME.;CBOB 11;SAY That message contained a special string showing the time, and it was formatted according to the TIME_FORMAT: statement in the graal.main file...;CMOVE 70,157,P
ACTION: 5;IFOBJ ROBJ1;IFRF 4=0;SAY Let's test the current time. First, I'll set the clock to 0:00...;SETTIME 0,0;SAY ...now click the table clock. You will get different messages depending on whether you click it before 0:15 or after!;SETRF 4=1;EXIT
ACTION: 5;IFOBJ ROBJ1;IFRF 4=1;IFTIME <015;SAY Yes, it's not yet a quarter past!
ACTION: 5;IFOBJ ROBJ1;IFRF 4=1;IFTIME >010;SAY I think it's already a quarter past or more!
ACTION: 5;IFOBJ ROBJ1;IFRF 4=1;SAY The clock is run by a timer, started in room 1 with a DOAFTER command.;SAY The ACTION: statement triggered by the timer is placed in graal.main so that it works in every room.
ACTION: 5;IFOBJ ROBJ1;IFRF 4=1;SAY It contains an ADDTIME command that adds five minutes to the clock every time the timer elapses.;SAY That's it! Now all you have to do is think about ways to use times and dates in your adventure... or not.;GOTO 3,1
