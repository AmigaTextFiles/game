/* Outdoor Halucination
/*
/* 4.room
/*
/* RF 1=0 sun grim  =1 sun happy
/* RF 2=1 spacecraft here

/* UPDATE: scroll_frames;normal_frames
UPDATE: 6;1

/* SECTION: section number
SECTION: 2

3D: 150;131;106;0.3

/* BACKDROP: filename
BACKDROP: 4BG.IFF

/* START_POS: sposno;image;x;y;camera;floor
START_POS: 1;SIMG11;383;151;R;1
START_POS: 2;SIMG21;323;144;R;1
START_POS: 3;SIMG21;209;119;M;3
START_POS: 4;SIMG21;121;97;L;3
START_POS: 5;SIMG21;33;113;L;3
START_POS: 6;//SIMG11;95;166;L;2

/* FLOOR: floorno;x1;y1;x2;y2;floormap/.../floormap
FLOOR: 1;311;141;398;169;1-1/2-2/3-2/4-2
FLOOR: 2;11;160;398;169;1-1/2-2/3-3/4-4
FLOOR: 3;29;109;205;169;1-2/2-2/3-3/4-4
FLOOR: 4;72;93;155;169;1-2/2-2/3-3/4-4

/* EXIT: no;x1;y1;x2;y2;epointx;epointy;description
EXIT: 1;398;104;409;144;388;149;home\sweet home
EXIT: 2;318;114;328;135;323;143;house
EXIT: 3;207;81;221;109;205;119;bar
EXIT: 4;102;60;140;89;120;95;castle
EXIT: 5;17;80;39;103;35;112;laboratory
EXIT: 6;0;107;62;169;73;161;spacecraft

/* CLPART: filename
/*
CLPART: 4FG.IFF

/* ROOMIMGS: no_of_imgs;startrimg;grabx;graby;width;height;xoffset;hotsp
/* 1=fisbone
ROOMIMGS: 1;1;8;20;32;18;0;0
/* 2-4=flaganim
ROOMIMGS: 3;2;108;16;27;17;28;0
/* 5=sun   6=sun talk
/*ROOMIMGS: 1;5;51;3;41;41;0;0
/*ROOMIMGS: 1;6;196;3;41;41;0;0
/* 7,8=spacecraft
ROOMIMGS: 2;7;3;51;75;66;79;0/66

/* STATIC: bobno;image;putx;puty
/* ANIM: bobno;image;anim_ch;amalsequence;putx;puty
ANIM: 2;RIMG2;2;A 0,(RIMG2,12)(RIMG3,12)(RIMG4,12)(RIMG3,12);253;49
/* ROOMOBJ: roomobjno,object_definition.... (see graal.main documentation)
ROOMOBJ: 1;fishbone;4;VIS;1;RIMG1;226;147;0;11;SIMG33; ;NPICK; ;8; ;LOW;D;a;this;it

/* DACT: condition;...;action;...
/*
DACT: TRACK 4.mod,0,NOFILTER
DACT: IFRF 4,1=0;CLPART 4FG.iff;IMGS 1,RIMG5,51,3,41,41,0,0;IMGS 1,RIMG6,196,3,41,41,0,0;CLPART OFF
DACT: IFRF 4,1=1;CLPART 4FG.iff;IMGS 2,RIMG5,237,3,41,41,41,0;CLPART OFF
DACT: SHOW 5, , , 
DACT: IFSPOS 6;SETRF 2=1;SETRF 8,1=1
DACT: HIDEEXIT 6
DACT: IFRF 2=1;PIMG -1,105,RIMG8;BOBON 4,-1,171,RIMG7;SHOWEXIT 6
DACT: LIGHTS ON;EXIT

/* LINE: dlgno;lineno;line;reuseline;conditions
/*
/*   Specifies a line of dialogue
/*
/* LACT: dlgno;lineno;action;...;action
/*
/*   Specifies a line executed when the corresponding LINE
/*   has been selected by the player. LACT:s belonging to a
/*   certain LINE: must ALWAYS be placed directly below the
/*   corresponding LINE: statement as shown here.

LINE: 3;1;My name is Erica.;Have I mentioned yet that my name is Erica?; 
LACT: 3;1;RESP S,3,Leave me alone!;DSET 3,-1,+2,+3,-4,-5

LINE: 3;2;Why are you looking so grim?;You don't look very happy.; 
LACT: 3;2;RESP S,3,Oh, it's so boring!;RESP S,3,Shining all the time, I hate it.;DSET 3,-2,-3,+4,+5

LINE: 3;3;See you later!; ; 
LACT: 3;3;IFRF 1=0;RESP S,3,Grmpf, hope not.;EDLG;EXIT
LACT: 3;3;IFRF 1=1;RESP S,3,See you, my little Erica!;EDLG;EXIT

LINE: 3;4;But you are so beautiful and warm!; ; 
LACT: 3;4;RESP S,3,Really?;SAY Yes, and your light is so bright!
LACT: 3;4;RESP S,3,Sounds good!;SAY Finally, without you there would be no life on this planet.
LACT: 3;4;RESP S,3,You are right. Now I'm happy again!;HIDE 5;SETRF 4,1=1;CLPART 4FG.iff;IMGS 2,RIMG5,237,3,41,41,41,0;SHOW 5,359,54,RIMG5;EDLG;EXIT

LINE: 3;5;I don't think I can help you.; ; 
LACT: 3;5;RESP S,3,I didn't ask you to help me.;RESP S,3,No-one can help me!;DSET 3,+1,+2,+3,-4,-5

LINE: 3;6;I need something very bright. Can you help me?; ;IFOF 44,1=1
LACT: 3;6;RESP R,3,I could give you one of my rays if you like.
LACT: 3;6;SAY Yes, that would be nice.;EDLG;HANDLE HIGH;W 24;PICK 44;HANDLE -1
LACT: 3;6;SAY Thank you!;SETOF 44,1=2;EXIT

/* Room actions follow below ***
/*
/* ACTION: verbno;condition;...;command;...

/*
/* Action 0 - Exit
/* ===============
/*
ACTION: 0;IFOBJ 1;MEXIT;GOTO 3,1
ACTION: 0;IFOBJ 2;MEXIT;GOTO 5,1
ACTION: 0;IFOBJ 3;MEXIT;GOTO 8,1
ACTION: 0;IFOBJ 4;MEXIT;GOTO 6,1
ACTION: 0;IFOBJ 5;MEXIT;GOTO 7,1
ACTION: 0;IFOBJ 6;MEXIT;GOTO 25,3

ACTION: 2;IFOBJ ROBJ1;MOBJ;SAY I don't pick up all the crap I find, you know.;EXIT
ACTION: 5;IFOBJ ROBJ1;MOBJ;SAY Dead fish can't talk!;EXIT
ACTION: 5;IFOBJ 5;IFRF 4,1=0;MOBJ;SAY Hello Sun!;RESP S,3,Mhhhmm...;DSET 3,+1,+2,+3,-4,-5;EXIT
ACTION: 5;IFOBJ 5;IFRF 4,1=1;MOBJ;SAY Hello Sun!;RESP S,3,Hello Erica!;DSET 3,N1,N2,+3,N4,N5,+6;EXIT
ACTION: 6;IFOBJ ROBJ1;MOBJ;SAY No, thank you. I'm not that hungry.;EXIT
ACTION: 8;IFOBJ ROBJ1;MOBJ;SAY A dead fish.;EXIT
ACTION: 9;IFOBJ ROBJ1;MOBJ;SAY Take this!;HANDLE;W 12;HANDLE -1;W 12;HANDLE;W 12;HANDLE -1;EXIT

/* End of room file
