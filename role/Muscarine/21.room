/* Alco-Beamer Planet Surface
/* Room script for GRAAL
/*
/* RF 1=0 green snow invisible =1 green snow visible
/* RF 2=1 drunk
/* RF 3=0/1 enter via beam false/true
/* RF 4 beam counter
/* RF 5=1 real green snow picked up (bugfix 12-02-2000)

/* UPDATE: scroll_frames[;normal_frames]
/*
UPDATE: 6;1

/* 3D: max;mid;min;adjspeed (This statement is optional)
/*
/* SECTION: SAME|section_no
/*
SECTION: 2

/* BACKDROP: file
/*
BACKDROP: 21BG.iff

/* START_POS: startpos_no;image;x;y;L|M|R;floor_no
/*
START_POS: 1;SIMG11;49;155; ;1
START_POS: 2;SIMG21;259;127; ;2
START_POS: 3;SIMG21;203;93; ;3

/* FLOOR: floor_no;x1;y1;x2;y2;floormaps
/*
FLOOR: 1;48;133;319;169;1-1/2-2/3-3
FLOOR: 2;77;115;262;169;1-1/2-2/3-3
FLOOR: 3;144;77;235;169;1-1/2-2/3-3

/* PATH: path_no;floor1;floor2;px1;py1;px2;py2;[{more_points}] (This statement is optional)
/*
/* EXIT: exit_no;x1;y1;x2;y2;x;y;description (This statement is optional)
/*
EXIT: 1;165;17;243;73;203;87;mountains
EXIT: 2;252;76;265;114;259;126;door

/* CLPART: file (This statement is optional)
/*
CLPART: 21FG.iff

/* ROOMIMGS: number;start_bob;x1;y1;w;h;x-offset;hotspot (This statement is optional)
/* 1=blue 2=yellow 3=green snow
ROOMIMGS: 1;1;101;16;18;17;0;0
ROOMIMGS: 1;2;129;6;18;17;0;0
ROOMIMGS: 1;3;155;4;17;18;0;0
/* 4=button
ROOMIMGS: 1;4;144;38;13;13;0;0
CLPART: 20FG.iff
/* 5-13=beam anim
ROOMIMGS: 3;5;101;14;65;78;65;0
ROOMIMGS: 3;8;101;92;65;78;65;0
ROOMIMGS: 3;11;101;170;65;78;65;0

/* STATIC: bob;image;x;y (This statement is optional)
/*
/* ANIM: bob;image;anim_ch;anim|ptrn;x;y (This statement is optional)
/*
/* ROOMOBJ: obj;name;room;VIS|NVIS;bob;image;x;y;cx;cy;cimage;prep;PICK|NPICK;anim_ch;verb;icon;LOW|MID|HIGH;types;w1;w2;w3 (This statement is optional)
/*
ROOMOBJ: 1;blue\snow;21;VIS;1 ;RIMG1;182;45;0;43;SIMG31; ;PICK; ;8;33;HIGH; ;a;this;it
ROOMOBJ: 2;yellow\snow;21;VIS;2;RIMG2;210;35;-2;46;SIMG31; ;PICK; ;8;32;HIGH; ;a;this;it
ROOMOBJ: 4;button;21;VIS;4;RIMG4;26;120;23;35;SIMG11; ;NPICK; ;8; ;MID; ;a;this;it

/* DACT: {cond|comm}
/*
DACT: TRACK 21.mod,0,NOFILTER
DACT: IFRF 1=0;HIDE ROBJ1;HIDE ROBJ2;HIDE 32
DACT: IFRF 1=1;SHOW 32,236,34,RIMG3
DACT: LIGHTS ON;EXIT

/* LINE: dlg;line_no;sentence1;sentence2| ;{cond} (This statement is optional)
/*
/* LACT: dlg;line_no;{cond|comm} (This statement is optional)
/*
/* ACTION: verb_no;{cond|comm} (This statement is optional)
/*
/* Action 0 - Exit
ACTION: 0;IFOBJ 1;MEXIT;GOTO 26,1
ACTION: 0;IFOBJ 2;MEXIT;GOTO 22,1

/* Action 1 - Give
/*
/* Action 2- Pick up
ACTION: 2;IFOBJ ROBJ1;SAY I need green snow. This is blue.;EXIT
ACTION: 2;IFOBJ ROBJ2;SAY I need green snow. This is yellow.;EXIT
ACTION: 2;IFOBJ 32;SETRF 5=1

/* Action 3 - Use
ACTION: 3;IFOBJ ROBJ4;IFRF 4>=3;IFRF 2=1;MOBJ;HANDLE;W 18;HANDLE -1;CUTSCENE 7,H;SETRF 2=0;GOTO 25,1
ACTION: 3;IFOBJ ROBJ4;IFRF 4>=3;CUTSCENE 5,H;MOBJ;HANDLE;W 18;HANDLE -1;CUTSCENE 7,H;SETRF 2=0;GOTO 25,1
ACTION: 3;IFOBJ ROBJ4;IFRF 2=1;MOBJ;HANDLE;W 18;HANDLE -1;CUTSCENE 7,H;SETRF 2=0;SETRF 20,3=1;ADDRF 4;GOTO 20,2
ACTION: 3;IFOBJ ROBJ4;IFRF 4>=1;CUTSCENE 5,H;MOBJ;HANDLE;W 18;HANDLE -1;CUTSCENE 7,H;SETRF 2=0;SETRF 20,3=1;ADDRF 4;GOTO 20,2
ACTION: 3;IFOBJ ROBJ4;MOBJ;HANDLE;W 18;HANDLE -1;SAY Nothing happens.;EXIT
ACTION: 3;IFOBJ 20;VERB 6;REDO

/* Action 4 - Open
/*
/* Action 5 - Talk to
/*
/* Action 6 - Consume
ACTION: 6;IFOBJ 20;CUTSCENE 5,H;SETRF 2=1;EXIT

/* Action 7 - Close
/*
/* Action 8 - Look at
/*
/* Action 9 - Attack
/*
/*
/* end of room file
