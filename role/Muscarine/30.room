/* Northpole
/* Room script for GRAAL
/* RF 1=1 polar explorer gone
/* RF 2=1 pinguin question
/* RF 3=1 line 4 on =2 trap question =3 asked for trap

/* UPDATE: scroll_frames[;normal_frames]
/*
UPDATE: 6;1

/* 3D: max;mid;min;adjspeed (This statement is optional)
/*
/* SECTION: SAME|section_no
/*
SECTION: 2

/* BACKDROP: file
/*
BACKDROP: 30BG.iff

/* START_POS: startpos_no;image;x;y;L|M|R;floor_no
START_POS: 1;SIMG31;150;158; ;1

/* FLOOR: floor_no;x1;y1;x2;y2;floormaps
FLOOR: 1;0;150;319;169;1-1

/* PATH: path_no;floor1;floor2;px1;py1;px2;py2;[{more_points}] (This statement is optional)
/*
/* EXIT: exit_no;x1;y1;x2;y2;x;y;description (This statement is optional)
EXIT: 1;0;159;319;169;159;169;south

/* CLPART: file (This statement is optional)
CLPART: 30FG.iff

/* ROOMIMGS: number;start_bob;x1;y1;w;h;x-offset;hotspot (This statement is optional)
/* 1-4 polar explorer
ROOMIMGS: 4;1;0;0;57;92;63; 
/* 5,6,(1) polar expl. talk
ROOMIMGS: 2;5;0;93;57;92;63; 

/* STATIC: bob;image;x;y (This statement is optional)
/*
/* ANIM: bob;image;anim_ch;anim|ptrn;x;y (This statement is optional)
/*
/* ROOMOBJ: obj;name;room;VIS|NVIS;bob;image;x;y;cx;cy;cimage;prep;PICK|NPICK;anim_ch;verb;icon;LOW|MID|HIGH;types;w1;w2;w3 (This statement is optional)
/*
/* DACT: {cond|comm}
/*
DACT: IFRF 1=1;HIDE 34
DACT: SETRF 29,4=1;LIGHTS ON;EXIT

/* LINE: dlg;line_no;sentence1;sentence2| ;{cond} (This statement is optional)
/* LACT: dlg;line_no;{cond|comm} (This statement is optional)
/*
LINE: 16;1;What are you doing here?; ; 
LACT: 16;1;RESP R,16,I'm a famous polar explorer. I explore the polar regions of this planet.
LACT: 16;1;DSET 16,N1;EXIT

LINE: 16;2;Isn't it a bit cold to be out without shoes?; ; 
LACT: 16;2;RESP R,16,Yes, but I was a little bit absent-minded this morning and forgot my boots.
LACT: 16;2;NAME 34,absent-minded\polar explorer;DSET 16,N2;EXIT

LINE: 16;3;What do you know about penguins?; ;IFRF 2=1
LACT: 16;3;RESP R,16,When I explored the south-pole last year I caught some.
LACT: 16;3;SETRF 3=1;DSET 16,N3,+4;EXIT

LINE: 16;4;How do you catch penguins?; ;IFRF 3=1
LACT: 16;4;RESP R,16,I have a special penguin trap.;SETRF 3=2;DSET 16,N4,+5;EXIT

LINE: 16;5;Could I have your penguin trap, please?; ;IFRF 3>=2
LACT: 16;5;RESP R,16,I'll to trade it for some warm boots.;SETRF 3=3
LACT: 16;5;IFCARR 37;DSET 16,N5;EDLG;SETRF 1=1;SAY I have some sealskin boots here.;HANDLE HIGH;PUT 37,U,0;W 24;HANDLE -1;RESP R,16,Ok, thanks. Here, you have this penguin trap.;HANDLE HIGH;PICK 36;W 24;HANDLE -1;EXIT
LACT: 16;5;DSET 16,-5;EXIT

LINE: 16;6;Goodbye.; ; 
LACT: 16;6;RESP R,16,Goodbye, miss.;EDLG;EXIT

/* ACTION: verb_no;{cond|comm} (This statement is optional)
/*
/* Action 0 - Exit
ACTION: 0;IFOBJ 1;MEXIT;GOTO 29,2

/* Action 1 - Give
ACTION: 1;IFOBJ 37;IFOBJ2 34;IFRF 3=3;SETRF 1=1;MOBJ 34;SAY I have some sealskin boots here.;HANDLE HIGH;PUT 37,U,0;W 24;HANDLE -1;RESP R,16,Ok, thanks. Here, you have this penguin trap.;HANDLE HIGH;PICK 36;W 24;HANDLE -1;EXIT

/* Action 2- Pick up
/*
/* Action 3 - Use
/*
/* Action 4 - Open
/*
/* Action 5 - Talk to
ACTION: 5;IFOBJ 34;MOBJ;SAY Hello mister!;RESP R,16,Hello miss!;DSET 16,+1,+2,+3,+4,+5,+6;EXIT

/* Action 6 - Consume
/*
/* Action 7 - Close
/*
/* Action 8 - Look at
/*
/* Action 9 - Attack
/*
/*
/* end of room file
