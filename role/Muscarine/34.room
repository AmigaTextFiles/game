/* Beach (Diskworld)
/* Room script for GRAAL
/*
/* RF 1=1 cannibals here =2 cannibals away
/* RF 2=1 talked to cannibals

/* UPDATE: scroll_frames[;normal_frames]
/*
UPDATE: 6;1

/* 3D: max;mid;min;adjspeed (This statement is optional)
/*
/* SECTION: SAME|section_no
SECTION: 2

/* BACKDROP: file
BACKDROP: 34BG.iff

/* START_POS: startpos_no;image;x;y;L|M|R;floor_no
/*
START_POS: 1;//SIMG11;126;115; ;1  /* spacecraft
START_POS: 2;SIMG11;212;68; ;3     /* jungle

/* FLOOR: floor_no;x1;y1;x2;y2;floormaps
/*
FLOOR: 1;106;105;319;132;1-1/2-2/3-2
FLOOR: 2;165;100;319;154;1-1/2-2/3-P1
FLOOR: 3;211;67;213;69;1-P1/2-P1/3-3

/* PATH: path_no;floor1;floor2;px1;py1;px2;py2;[{more_points}] (This statement is optional)
PATH: 1;2;3;183;100;197;72;212;68

/* EXIT: exit_no;x1;y1;x2;y2;x;y;description (This statement is optional)
EXIT: 1;59;43;123;104;126;115;spacecraft
EXIT: 2;192;28;219;71;213;68;jungle

/* CLPART: file (This statement is optional)
CLPART: 34FG.iff

/* ROOMIMGS: number;start_bob;x1;y1;w;h;x-offset;hotspot (This statement is optional)
/* 1=Trees FG
ROOMIMGS: 1;1;0;0;75;83;0;0/84
/* 2=cannibals
ROOMIMGS: 1;2;0;94;69;106;0;0
/* 3-7 fat cannibal licks his lips (disgusting)
ROOMIMGS: 5;3;0;84;17;10;17;120
/* 8=lunch 9=cauldron empty
ROOMIMGS: 2;8;85;0;57;89;114;0
/* 10-13 yuppie cannibal talk  14-16 hit
ROOMIMGS: 4;10;71;97;35;73;35;1/71
ROOMIMGS: 3;14;209;145;36;107;35;0/104
/* 17,18 indian
ROOMIMGS: 2;17;256;0;32;49;32;100
/* 19 give amulet
ROOMIMGS: 1;19;271;52;29;23;0;100
/* 20-22 cannibal flee
ROOMIMGS: 3;20;68;197;43;59;43;8/80
/* 23 empty
ROOMIMGS: 1;23;49;220;8;8;0; 

/* STATIC: bob;image;x;y (This statement is optional)
STATIC: 1;RIMG1;192;100

/* ANIM: bob;image;anim_ch;anim|ptrn;x;y (This statement is optional)
ANIM: 3;RIMG3;3;A 0,(RIMG7,120)(RIMG3,6)(RIMG4,6)(RIMG5,6)(RIMG6,6);283;168

/* ROOMOBJ: obj;name;room;VIS|NVIS;bob;image;x;y;cx;cy;cimage;prep;PICK|NPICK;anim_ch;verb;icon;LOW|MID|HIGH;types;w1;w2;w3 (This statement is optional)
ROOMOBJ: 1;cannibals;34;VIS;2;RIMG2;286;141;-114;-14;//SIMG11; ;NPICK; ;5; ; ;G;some;this;them
ROOMOBJ: 2;lunch;34;VIS;4;RIMG8;263;154;-73;1;//SIMG11; ;NPICK; ;8; ; ;MV;a;this;him
ROOMOBJ: 3;yuppie\cannibal;34;VIS;5;RIMG10;202;150;-14;0;//SIMG13; ;NPICK;4;5; ; ;MV;a;this;him
ROOMOBJ: 4;indian;34;VIS;6;RIMG18;260;166;-50;-13;//SIMG11; ;PICK;5;8; ;MID;MV;a;this;him

/* DACT: {cond|comm}
/*
DACT: TRACK 34.mod,0,NOFILTER
DACT: IFRF 1=0;HIDE ROBJ1;HIDE ROBJ2;HIDE ROBJ3;HIDE ROBJ4;BOBOFF 3;HIDE 45
DACT: IFRF 1=1;HIDE ROBJ4;HIDE 45
DACT: IFRF 1=2;HIDE ROBJ1;HIDE ROBJ2;HIDE ROBJ3;HIDE ROBJ4;BOBOFF 3
DACT: IFRF 1=2;IFNOTCARR 45;IFOF 45,1=0;SHOW 45,263,154,RIMG9
DACT: LIGHTS ON;EXIT

/* LINE: dlg;line_no;sentence1;sentence2| ;{cond} (This statement is optional)
/*
/* LACT: dlg;line_no;{cond|comm} (This statement is optional)
/*
/* ACTION: verb_no;{cond|comm} (This statement is optional)
/*
/* Action 0 - Exit
ACTION: 0;IFOBJ 1;MEXIT;GOTO 25,2
ACTION: 0;IFOBJ 2;MEXIT;GOTO 35,1

/* Action 1 - Give
/*
/* Action 2 - Pick up
/*
/* Action 3 - Use
/*
/* Action 4 - Open
/*
/* Action 5 - Talk to
ACTION: 5;IFOBJ ROBJ1|ROBJ3;IFRF 2=1;MOBJ ROBJ3;SAY Hello!;RESP R,18,I told you to go away! We don't like stupid creeps like you!;EXIT
ACTION: 5;IFOBJ ROBJ1|ROBJ3;SETRF 2=1;MOBJ ROBJ3;SAY Hello, what are you doing here?;RESP S,18,Don't ask stupid questions! We are cooking our lunch, as you can plainly see.;RESP R,18,Now go away! We don't like people like you!;EXIT

/* Action 6 - Consume
/*
/* Action 7 - Close
/*
/* Action 8 - Look at
ACTION: 8;IFOBJ ROBJ2;MOBJ;SAY I'm afraid they're going to eat him, unless I do something.;EXIT
ACTION: 8;IFOBJ ROBJ1;MOBJ;SAY D'you see that fat cannibal licking his lips?;SAY Disgusting, isn't it?;EXIT
ACTION: 8;IFOBJ ROBJ3;MOBJ;SAY His white suit looks very expensive.;EXIT

/* Action 9 - Attack
ACTION: 9;IFOBJ ROBJ1|ROBJ3;CUTSCENE 16,S;SETRF 1=2;GOTO 25,2;EXIT

/* eof
/* end of room file
