/* Crash Landing
/* Room script for GRAAL
/* RF 1=0 first visit
/* RF 2=0 spacecraft wrecked =1 repaired
/* RF 3=1 petshop seen =2 asked brother question
/* RF 4=1 northpole seen =2 asked brother question

/* UPDATE: scroll_frames[;normal_frames]
/*
UPDATE: 6;1

/* 3D: max;mid;min;adjspeed (This statement is optional)
3D: 105;89;75;0.3

/* SECTION: SAME|section_no
/*
SECTION: 2

/* BACKDROP: file
/*
BACKDROP: 29BG.iff

/* START_POS: startpos_no;image;x;y;L|M|R;floor_no
START_POS: 1;SIMG11;97;134; ;1    /* crash
START_POS: 2;SIMG31;222;168; ;2   /* north
START_POS: 3;SIMG21;70;66; ;3     /* south
START_POS: 4;//SIMG11;21;152; ;1  /* west

/* FLOOR: floor_no;x1;y1;x2;y2;floormaps
FLOOR: 1;0;120;218;169;1-1/2-2/3-P1
FLOOR: 2;0;152;265;169;1-1/2-2/3-1
FLOOR: 3;41;49;77;66;1-P1/2-P1/3-3

/* PATH: path_no;floor1;floor2;px1;py1;px2;py2;[{more_points}] (This statement is optional)
PATH: 1;1;3;155;122;70;65

/* EXIT: exit_no;x1;y1;x2;y2;x;y;description (This statement is optional)
EXIT: 1;0;66;74;132;73;144;spacecraft
EXIT: 2;161;153;282;169;222;169;north
EXIT: 3;46;33;85;65;70;65;south
EXIT: 4;0;133;16;163;20;152;west

/* CLPART: file (This statement is optional)
CLPART: 29FG.iff

/* ROOMIMGS: number;start_bob;x1;y1;w;h;x-offset;hotspot (This statement is optional)
/* 1-3=mechanic
ROOMIMGS: 3;1;25;0;37;76;37; 
/* 4=spacecraft 5=wreck 6=signpost
ROOMIMGS: 1;4;142;1;75;65;0; 
ROOMIMGS: 1;5;224;2;95;72;0; 
ROOMIMGS: 1;6;0;76;84;60;0; 

/* STATIC: bob;image;x;y (This statement is optional)
/*
/* ANIM: bob;image;anim_ch;anim|ptrn;x;y (This statement is optional)
/*
/* ROOMOBJ: obj;name;room;VIS|NVIS;bob;image;x;y;cx;cy;cimage;prep;PICK|NPICK;anim_ch;verb;icon;LOW|MID|HIGH;types;w1;w2;w3 (This statement is optional)
ROOMOBJ: 1;wrecked\spacecraft;29;VIS;1;RIMG5;47;57;48;77;SIMG13; ;NPICK; ;8; ; ; ;a;this;it
ROOMOBJ: 2;signpost;29;VIS;3;RIMG6;175;50;4;89;SIMG31; ;NPICK; ;8; ; ; ;a;this;it

/* DACT: {cond|comm}
DACT: IFRF 2=0;HIDEEXIT 1
DACT: IFRF 2=1;HIDE ROBJ1;PIMG 1,62,RIMG4
DACT: TRACK 29.mod,0,NOFILTER;LIGHTS ON
DACT: IFRF 1=0;SETRF 1=1;TEXT -1,90,1,The Navigation Unit Incident;CIMG SIMG23;SAY Erica to mission control! We've got a problem!;CIMG SIMG13;W 50;CIMG SIMG23;SAY It wasn't my fault.;SAY The navigation unit is crap.;EXIT
DACT: EXIT

/* LINE: dlg;line_no;sentence1;sentence2| ;{cond} (This statement is optional)
/* LACT: dlg;line_no;{cond|comm} (This statement is optional)
LINE: 15;1;Do you repair spacecrafts?; ; 
LACT: 15;1;RESP R,15,Yes I do. But you have to pay for it.;DSET 15,N1,+2;EXIT

LINE: 15;2;What does it cost to repair a spacecraft?;How much did you say costs to repair a spacecraft?;IFNOTCARR 41;IFRF 2=0
LACT: 15;2;RESP R,15,One spacecraft repair is seven firpin.;DSET 15,+3;EXIT

LINE: 15;3;What's a firpin?;What's a firpin again?; 
LACT: 15;3;RESP R,15,Firpin is the local currency.
LACT: 15;3;RESP R,15,We have firpin and furpun. One firpin is worth six furpun.
LACT: 15;3;DSET 15,-3;EXIT

LINE: 15;4;Aren't you the same guy who owns the pet shop?; ;IFRF 3=1
LACT: 15;4;IFRF 4=2;RESP R,15,No, that's my other brother.;DSET 15,N4;EXIT
LACT: 15;4;RESP R,15,No, that's my brother.;SETRF 3=2;DSET 15,N4;EXIT

LINE: 15;5;Aren't you the famous polar explorer?; ;IFRF 4=1
LACT: 15;5;IFRF 3=2;RESP R,15,No, that's my other brother.;DSET 15,N5;EXIT
LACT: 15;5;RESP R,15,No, that's my brother.;SETRF 4=2;DSET 15,N5;EXIT

LINE: 15;6;I have a credit note for one free spacecraft\repair.; ;IFCARR 41
LACT: 15;6;RESP R,15,Ok, I'll do it.;LIGHTS OFF;EDLG;SETRF 2=1;SHOWEXIT 1;HIDE ROBJ1;PIMG 1,62,RIMG4
LACT: 15;6;W 150;PUT 41,U,0;LIGHTS ON;TEXT 20,30,1,Later...;RESP R,15,It's ready now.
LACT: 15;6;SAY Ok, thank you!;EXIT

LINE: 15;7;Goodbye.; ; 
LACT: 15;7;RESP R,15,Goodbye, Miss!;EDLG;EXIT

LINE: 15;8;Do you mend holes in cauldrons?; ;IFCARR 45;IFOF 45,1=1
LACT: 15;8;RESP R,15,Well, let's have a look at this cauldron!;HANDLE MID;W 12;HANDLE -1
LACT: 15;8;RESP R,15,Ok, no problem.;SAY How much does it cost?
LACT: 15;8;RESP R,15,For a good customer like you, I'll do it for free.;W 50
LACT: 15;8;RESP R,15,Ready! I have mended the hole.;HANDLE MID;ICON 45,21;SETOF 45,1=0;PICK 45;W 12;HANDLE -1
LACT: 15;8;SAY Thank you!;RESP R,15,But you shouldn't put any hot liquids into it.
LACT: 15;8;SAY Oh, but I want to fill it with lava.;RESP R,15,That won't work. You'll have to make it heat-resistable first.
LACT: 15;8;SAY Could you do that? Make it heat-resistable?;RESP R,15,No. This is only a repair shop, not a hi-tech laboratory.
LACT: 15;8;DSET 15,-8;EXIT

/* ACTION: verb_no;{cond|comm} (This statement is optional)
/*
/* Action 0 - Exit
ACTION: 0;IFOBJ 1;MEXIT;GOTO 25,5
ACTION: 0;IFOBJ 2;MEXIT;GOTO 30,1
ACTION: 0;IFOBJ 3;MEXIT;GOTO 31,1
ACTION: 0;IFOBJ 4;MEXIT;GOTO 32,1

/* Action 1 - Give
ACTION: 1;IFOBJ 41;IFOBJ2 33;MOBJ 33;SAY I have a credit note for one free spacecraft repair.
ACTION: =;RESP R,15,Ok, I'll do it.;LIGHTS OFF;SETRF 2=1;SHOWEXIT 1;HIDE ROBJ1;PIMG 1,62,RIMG4
ACTION: =;W 150;PUT 41,U,0;LIGHTS ON;TEXT 20,30,1,Later...;RESP R,15,It's ready now.
ACTION: =;SAY Ok, thank you!;EXIT

/* Action 2- Pick up
/*
/* Action 3 - Use
ACTION: 3;IFOBJ ROBJ1;SAY I can't use it anymore. It's broken!;EXIT
ACTION: 3;IFOBJ 29;IFOBJ ROBJ1;SAY I can't use it anymore. It's broken!;EXIT

/* Action 4 - Open
/*
/* Action 5 - Talk to
ACTION: 5;IFOBJ 33;MOBJ;SAY Hello mister!;RESP R,15,Good evening, miss!;DSET 15,+1,+4,+5,+6,+7,+8;EXIT

/* Action 6 - Consume
/*
/* Action 7 - Close
/*
/* Action 8 - Look at
ACTION: 8;IFOBJ ROBJ2;MOBJ;SAY Something is wrong here...;EXIT
ACTION: 8;IFOBJ ROBJ1;MOBJ;SAY Someone has to fix this or I'll have to stay on this planet forever.;EXIT

/* Action 9 - Attack
/*
/*
/* end of room file
