/* Inside Jail
/* Room script for GRAAL
/*
/* RF 1 =0 show brickwork =1 show brick =2 show hole
/* RF 2 =1 show trapdoor

/* UPDATE: scroll_frames[;normal_frames]
/*
UPDATE: 6;1

/* 3D: max;mid;min;adjspeed (This statement is optional)
/*
/* SECTION: SAME|section_no
/*
SECTION: 2

/* BACKDROP: file
/*
BACKDROP: 14BG.iff

/* START_POS: startpos_no;image;x;y;L|M|R;floor_no
/*
START_POS: 1;SIMG11;177;163;M;1
START_POS: 2;SIMG21;195;143;M;2

/* FLOOR: floor_no;x1;y1;x2;y2;floormaps
/*
FLOOR: 1;34;152;228;169;1-1/2-2/3-3
FLOOR: 2;141;120;223;169;1-1/2-2/3-3
FLOOR: 3;206;167;273;169;1-1/2-2/3-3

/* PATH: path_no;floor1;floor2;px1;py1;px2;py2;[{more_points}] (This statement is optional)
/*
/* EXIT: exit_no;x1;y1;x2;y2;x;y;description (This statement is optional)
EXIT: 1;187;103;203;112;196;123;hole

/* CLPART: file (This statement is optional)
/*
CLPART: 14FG.iff

/* ROOMIMGS: number;start_bob;x1;y1;w;h;x-offset;hotspot (This statement is optional)
/*
/* 1=toilet
ROOMIMGS: 1;1;266;0;55;41;0; 
/* 2=brickwork 4=brick 3=hole
ROOMIMGS: 1;2;262;49;54;42;0; 
ROOMIMGS: 2;3;271;97;18;11;18; 
/* 5-8=alien
ROOMIMGS: 4;5;0;0;63;95;64; 
/* 9=toilet spuel
ROOMIMGS: 1;9;0;106;36;42;0;71
/* 10=trapdoor
ROOMIMGS: 1;10;38;98;62;53;0; 
/* 11 shrink 12-16 shrink walk
ROOMIMGS: 1;11;107;102;43;59;0;0
ROOMIMGS: 5;12;151;135;22;26;22;0
/* 17,18=alien take,give
ROOMIMGS: 2;17;0;156;31;40;36; 

/* STATIC: bob;image;x;y (This statement is optional)
/*
/* ANIM: bob;image;anim_ch;anim|ptrn;x;y (This statement is optional)
/*
/* ROOMOBJ: obj;name;room;VIS|NVIS;bob;image;x;y;cx;cy;cimage;prep;PICK|NPICK;anim_ch;verb;icon;LOW|MID|HIGH;types;w1;w2;w3 (This statement is optional)
/*
ROOMOBJ: 1;brickwork;14;VIS;1;RIMG2;187;74;0;53;SIMG33; ;NPICK; ;8; ; ;S;a;this;it
ROOMOBJ: 2;trapdoor;14;VIS;5;RIMG10;193;85;1;62;SIMG33; ;NPICK; ;8; ;HIGH; ;a;this;it
/*ROOMOBJ: 3;hole;14;VIS;3;RIMG3;195;103;0;26;SIMG33; ;NPICK; ;8 ; ;LOW; ;a ;this ;it
ROOMOBJ: 4;toilet;14;VIS;4;RIMG1;260;121;-45;41;//SIMG13; ;NPICK; ;8; ;MID; ;a;this;it

/* DACT: {cond|comm}
/*
DACT: TRACK 14.mod,0,NOFILTER
DACT: IFRF 1=0;HIDE 23;HIDEEXIT 1
DACT: IFRF 1=1;HIDEEXIT 1;HIDE ROBJ1
DACT: IFRF 1=2;HIDE ROBJ1
DACT: IFRF 2=0;HIDE ROBJ2
DACT: IFRF 2=1;HIDE ROBJ1;HIDE 23;HIDEEXIT 1;FLOOR 2,141,142,223,169,1-1/2-2/3-3
DACT: IFRF 23,7=0;SETRF 23,6=1
DACT: LIGHTS ON;EXIT

/* LINE: dlg;line_no;sentence1;sentence2| ;{cond} (This statement is optional)
/*
/* LACT: dlg;line_no;{cond|comm} (This statement is optional)
/*
LINE: 10;1;I'm on a mission to destroy the Universe.; ;IFRF 23,3=0
LACT: 10;1;RESP R,10,Oh, then you must be Erica Evermore.
LACT: 10;1;RESP R,10,I've heard a lot about you and your mission. I wish you luck!
LACT: 10;1;SETRF 23,3=1;DSET 10,N1,+7;EXIT

LINE: 10;2;Why are you in jail?; ;IFRF 23,4=0
LACT: 10;2;RESP R,10,I'm an alien from outer space. I came in peace to this planet.;RESP R,10,All I wanted was some marijuana but they put me in jail!
LACT: 10;2;SETRF 23,4=1;DSET 10,N2;EXIT

LINE: 10;3;The spacecraft in front of that hotel, is it\yours?; ;IFRF 23,5=0
LACT: 10;3;RESP R,10,Yes, it is.;SETRF 23,5=1;SETRF 23,6=0;DSET 10,+4,N3;EXIT

LINE: 10;4;Would you let me borrow your spacecraft for a\while?; ;IFRF 23,6=0
LACT: 10;4;RESP R,10,Ok, you can use it if you bring me some marijuana.
LACT: 10;4;IFCARR 43;SETRF 23,6=1
LACT: 10;4;SETRF 23,7=0;DSET 10,-4,+5;EXIT

LINE: 10;5;Would you let me borrow your spacecraft if I\get you a joint?; ;IFRF 23,7=0;IFCARR 43
LACT: 10;5;RESP R,10,Yes, do you have one?;EDLG;HANDLE HIGH;BOBON 3,126,82,RIMG17;W 24;PUT 43,U,0;HANDLE -1
LACT: 10;5;SHOW 22,95,52,RIMG7;W 12;BOBON 3,126,82,RIMG18;W 12;BOBOFF 3;SHOW 22,95,52,A 1,(RIMG6,10)(RIMG7,10)(RIMG6,10)(RIMG7,10)(RIMG6,10)(RIMG7,10)(RIMG6,10)(RIMG7,10)(RIMG8,50);W 150
LACT: 10;5;RESP R,10,Here's the key for the spacecraft.;BOBON 3,126,82,RIMG17;W 12;HANDLE HIGH;W 24;PICK 29;HANDLE -1;BOBOFF 3;RESP R,10,Be careful with it, please.
LACT: 10;5;IFRF 23,8=0;IFRF 23,3=1;RESP R,10,By the way, you should visit Onius Ant.;RESP R,10,Onius Ant is very wise and he probably can help you to destroy the Universe.;RESP R,10,He lives on a planet surrounded by a red ring, not far from here.
LACT: 10;5;SETRF 23,6=1;SETRF 23,7=1;DSET 10,N4,N5;EXIT

LINE: 10;6;Do you know a way to escape from this prison?; ; 
LACT: 10;6;RESP R,10,No, sorry.;DSET 10,N6;EXIT

LINE: 10;7;Goodbye.; ; 
LACT: 10;7;RESP R,10,Goodbye.;EDLG;EXIT

/* ACTION: verb_no;{cond|comm} (This statement is optional)
/*
/* Action 0 - Exit
ACTION: 0;IFOBJ 1;MEXIT;SAY I can't get through this hole, I'm too big.;EXIT

/* Action 1 - Give
/*
/* Action 2- Pick up
ACTION: 2;IFOBJ 23;IFNOTCARR 23;MOBJ;HANDLE;W 12;PICK;HANDLE -1;PIMG 187,103,RIMG3;SHOWEXIT 1;SETRF 1=2;EXIT

/* Action 3 - Use
ACTION: 3;IFOBJ ROBJ4;CUTSCENE 4,H;GOTO 38,1

/* Action 4 - Open
/*
/* Action 5 - Talk to
ACTION: 5;IFOBJ 22;MOBJ;DSET 10,+1,+2,+3,+4,+5,+6,+7;EXIT

/* Action 6 - Consume
ACTION: 6;IFOBJ 40;IFRF 1=2;IFRF 2=0;CMOVE 196,123,C;CUTSCENE 10,H;SAY I can fit through the hole now.;SAY I'll go through now.;GOTO 15,5
ACTION: 6;IFOBJ 40;CUTSCENE 10,H;CUTSCENE 11,H;EXIT

/* Action 7 - Close
ACTION: 7;IFOBJ ROBJ2;MOBJ;HANDLE;W 12;HIDE ROBJ2
ACTION: 7;IFOBJ ROBJ2;IFRF 1=0;SHOW ROBJ1,187,74,RIMG2
ACTION: 7;IFOBJ ROBJ2;IFRF 1=1;SHOW 23,194,103,RIMG4
ACTION: 7;IFOBJ ROBJ2;IFRF 1=2;PIMG 187,103,RIMG3;SHOWEXIT 1
ACTION: 7;IFOBJ ROBJ2;HANDLE -1;SETRF 2=0;FLOOR 2,141,120,223,169,1-1/2-2/3-3;EXIT

/* Action 8 - Look at
ACTION: 8;IFOBJ ROBJ4;MOBJ;SAY A seatlike bowl, fixed to the floor and connected to a pipe.;CIMG SIMG23;SAY Come on, you know what this is used for.;EXIT
ACTION: 8;IFOBJ ROBJ1;MOBJ;SAY There is a loose brick in the lower row.;HIDE ROBJ1;SHOW 23,194,103,RIMG4;SETRF 1=1;EXIT

/* Action 9 - Attack
/*
/*
/* end of room file
