/* Grogg Greentooth's Home
/*
/* 5.room
/*
/* Roomflags:
/* RF 1 = 0 Shoe on foot | = 1 Barefoot | = 2 Foot cut up
/* RF 2 = 0 not yet talked to Drawing | = 1 Hint from Drawing
/* RF 3 = 0 first visit | = 1 was here

/* UPDATE: scroll_frames;normal_frames
/*
UPDATE: 1;1

/* SECTION: section number
/*
SECTION: 2

/* BACKDROP: filename
/*
BACKDROP: 5BG.IFF

/* START_POS: sposno;image;x;y;camera;floor
/*
START_POS: 1;SIMG11;296;142;M;1
START_POS: 2;SIMG21;46;141;M;4

/* FLOOR: floorno;x1;y1;x2;y2;floormap/.../floormap
/*
FLOOR: 1;248;130;319;169;1-1/2-2/3-3/4-2
FLOOR: 2;90;164;299;169;1-1/2-2/3-3/4-4
FLOOR: 3;248;93;265;169;1-1/2-2/3-3/4-2
FLOOR: 4;90;153;179;169;1-2/2-2/3-2/4-4

/* EXIT: no;x1;y1;x2;y2;epointx;epointy;description
/*
EXIT: 1;296;13;319;132;306;129;exit
EXIT: 2;24;92;62;134;47;135;tunnel

/* CLPART: filename
/*
CLPART: 5FG.IFF

/* ROOMIMGS: no_of_imgs;startrimg;grabx;graby;width;height;xoffset;hotsp
/*
/* 1=Shoe, 2=Foot
ROOMIMGS: 1;1;160;13;30;30;0;-1
ROOMIMGS: 1;2;244;3;63;49;0;-1
/* 3=Foot cut up
ROOMIMGS: 1;3;85;4;65;51;65;0
/* 4=Drawing
ROOMIMGS: 1;4;199;9;32;40;0;0
/* 5=Grogg 6,7,8=Grogg Talk Anim
ROOMIMGS: 4;5;0;60;80;68;80;0
/* 9=Window
ROOMIMGS: 1;9;18;142;32;39;0;0

/* STATIC: bobno;image;putx;puty
/*

/* ANIM: bobno;image;anim_ch;amalsequence;putx;puty
/*

/* ROOMOBJ: roomobjno,object_definition.... (see graal.main documentation)
/*
ROOMOBJ: 1;Window;5;VIS;10;RIMG9;63;38; ; ; ; ;NPICK; ;8; ;HIGH; ;a;this;it

/* DACT: condition;...;action;...
/*
/* This room uses the command area
/*

DACT: SETRF 40,2=0
DACT: IFRF 1=1;HIDE 1
DACT: IFRF 1=2;PIMG 54,120,RIMG3;HIDE 1;HIDE 3;FLOOR 4,5,155,179,169,1-2/2-2/3-2/4-4
DACT: TRACK 5.mod,0,NOFILTER
DACT: LIGHTS ON
DACT: IFRF 3=0;RESP R,1,Welcome! My name is Grogg Greentooth.;SETRF 5,3=1;EXIT
DACT: IFRF 3=1;IFRF 1=0;RESP R,1,Welcome again! My name is still Grogg Greentooth.;EXIT
DACT: EXIT

/* LINE: dlgno;lineno;line;reuseline;conditions
/*
/* LACT: dlgno;lineno;action;...;action
/*
/* 1 is grogg dialogue
LINE: 1;1;My name is Erica.;My name is still Erica.;
LACT: 1;1;IFOF 1=0;SETOF 1=1;RESP R,1,Erica is a beautiful name!;DSET 1;EXIT
LACT: 1;1;RESP S,1,Erica is still a beautiful name!;DSET 1,-1;EXIT

LINE: 1;2;Good bye for now.; ; 
LACT: 1;2;RESP S,1,Good bye!;EDLG;EXIT

LINE: 1;3;Doesn't your foot feel cold?;Shouldn't we amputate your leg before it freezes?; 
LACT: 1;3;IFOF 2=0;SETOF 2=1;RESP R,1,Oh yes, now as you mention it...;RESP R,1,It feels really cold!;DSET 1;EXIT
LACT: 1;3;SETOF 3,1=1;RESP R,1,Yes, I think so. It would be better. Cut it off!;DSET 1,N3;EDLG;EXIT

LINE: 1;4;What do you know about that drawing behind you?;What did you say about that drawing again?;IFOF 2,1=0;IFOF 2,2=0
LACT: 1;4;RESP R,1,I think it's somewhat magic.;DSET 1,-4;EXIT

LINE: 1;5;What do you know about the star nilende?; ;IFOF 9,2=1
LACT: 1;5;RESP R,1,It's useful to have when exploring dark areas.;DSET 1,-5;EXIT

LINE: 1;6;My mission is to have fun!; ;IFOF 8,1=0
LACT: 1;6;RESP R,1,Oh, that's very nice.;DSET 1,N6;EXIT

LINE: 1;7;My mission is to destroy the Universe!;Do you know how to destroy the Universe?;IFOF 8,1=1
LACT: 1;7;IFOF 3=0;SETOF 3=1;RESP R,1,Who told you that?;SAY King Cranium.;RESP R,1,Ah yes, then you have to do it!;DSET 1;EXIT
LACT: 1;7;IFOF 3=1;RESP R,1,No, I have no idea!;DSET 1,N7;EXIT

/* 2 is drawing dialogue
LINE: 2;1;How are you able to talk? You're just a drawing!;I still don't understand how you can talk.; 
LACT: 2;1;IFOF 2=0;RESP S,2,It's a very special trick. I can't explain it now.;SETOF 2=1;DSET 2;EXIT
LACT: 2;1;IFOF 2=1;RESP S,2,You are very curious, aren't you?;SETOF 2=2;DSET 2;EXIT
LACT: 2;1;IFOF 2=2;RESP S,2,Ok, there is someone sitting behind the wall. That's all! Satisfied?;SETOF 2=3;DSET 2,N1,+3;EXIT

LINE: 2;2;Why does Grogg only have one leg?;Tell me more about Grogg's other leg!; 
LACT: 2;2;IFOF 1=0;RESP S,2,This is very sad story. Some time ago he had a second leg.;SETOF 1=1;DSET 2;EXIT
LACT: 2;2;IFOF 1=1;RESP S,2,But because he only has one shoe his other leg froze.;RESP S,2,They had to amputate it.;SETRF 2=1;DSET 2,-2;EXIT

LINE: 2;3;Who is sitting behind the wall?; ;IFOF 2=3
LACT: 2;3;RESP S,2,Honestly? There is nobody behind the wall!
LACT: 2;3;RESP S,2,I'm just a magic drawing that can talk.
LACT: 2;3;RESP S,2,Believe it or not! That's the truth.;DSET 2,N3

LINE: 2;4;See you later!; ; 
LACT: 2;4;RESP S,2,Bye!;EDLG;EXIT

/* Room actions follow below ***
/*
/* ACTION: verbno;condition;...;command;...

/* Action 0 - Exit
ACTION: 0;IFOBJ 1;MEXIT;GOTO 4,2
ACTION: 0;IFOBJ 2;IFRF 1=2;MEXIT;GOTO 11,1
ACTION: 0;IFOBJ 2;SAY I can't reach the tunnel. Grogg's leg is in the way.;EXIT

/* Action 1 - Give
ACTION: 1;IFOBJ 1;IFOBJ2 4|3;IFRF 5,1=1;SETRF 5,1=0;CMOVE 90,169,C;HANDLE;PUT 1,U,5;SHOW 1,68,140,RIMG1;W 12;HANDLE -1;RESP R,1,Thank you!;EXIT

/* Action 2 - Pick Up
ACTION: 2;IFOBJ 1;IFNOTCARR;IFRF 5,1=0;SETRF 5,1=1;MOBJ;HANDLE;W 12;PICK;HANDLE -1;RESP R,1,Hey, that's my shoe. Give it back!;EXIT
ACTION: 2;IFOBJ 3;IFRF 2=0;SAY I can't pick up the whole Grogg Greentooth! He's way too big.;EXIT
ACTION: 2;IFOBJ 3;IFRF 2=1;SAY I need something sharp to cut it off!;EXIT

/* Action 3 - Use
ACTION: 3;IFOBJ 1;IFOBJ2 4|3;IFRF 5,1=1;VERB 1;REDO
ACTION: 3;IFOBJ 14;IFOBJ2 3;IFOF 3,1=1;IFNOTCARR 3;IFCARR 1;MOBJ 3;SETRF 1=2;HANDLE MID;RESP R,1,OUCH! AAHRG!;HANDLE -1;PICK 3;PIMG 54,120,RIMG3;FLOOR 4,5,155,179,169,1-2/2-2/3-2/4-4;RESP R,1,Owww, that hurts.;EXIT
ACTION: 3;IFOBJ 14;IFOBJ2 3;IFOF 3,1=1;IFNOTCARR 3;IFNOTCARR 1;MOBJ 3;SETRF 1=2;HANDLE MID;RESP R,1,My foot doesn't feel cold now.;RESP R,1,There is no need to amputate it.;EXIT
ACTION: 3;IFOBJ 17;IFOBJ2 3;IFOF 3,1=1;IFNOTCARR 3;MOBJ 3;RESP R,1,No! Not with a dirty old knife like this.;EXIT
ACTION: 3;IFOBJ 14|17;IFOBJ2 3;IFOF 3,1=0;IFNOTCARR 3;SAY Perhaps I should ask him before I do this.;EXIT

/* Action 5 -Talk to
ACTION: 5;IFOBJ 2;MOBJ;SAY Hello!;RESP R,2,Hello!;DSET 2,+1,+2,+3,+4;EXIT
ACTION: 5;IFOBJ 4;IFRF 2=1;IFRF 1=1;MOBJ;DSET 1,+1,+3,+4,+5,+6,+7,+2;EXIT
ACTION: 5;IFOBJ 4;MOBJ;DSET 1,+1,-3,+4,+5,+6,+7,+2;EXIT

/* Action 8 - Look at
ACTION: 8;IFOBJ 3;IFNOTCARR 3;MOBJ;SAY A rather thin leg.|I wonder if he can stand on his single foot.;EXIT
ACTION: 8;IFOBJ 2;MOBJ;SAY Some kind of childish graffiti.;EXIT
ACTION: 8;IFOBJ ROBJ1;SAY Just a window.;EXIT

/* Action 9 - Attack
ACTION: 9;IFOBJ 2;SAY Attacking the wall? Come on, that doesn't make any sense.;EXIT
ACTION: 9;IFOBJ 4;SAY No way, he looks much stronger than me.|No way, he's too big!;EXIT

/* End of room file
