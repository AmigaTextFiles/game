/* Home Sweet Home
/*
/* 1.room
/*
/* RF 1=0,2 upper drawer closed | =1 open
/* RF 2=0,2 lower drawer closed | =1 open

/* UPDATE: scroll_frames;normal_frames
/*
UPDATE: 1;1

/* SECTION: section number
/*
SECTION: 1

/* BACKDROP: filename
/*
BACKDROP: 1BG.IFF

/* START_POS: sposno;image;x;y;camera;floor
/*
START_POS: 1;//SIMG11;45;150;M;1
START_POS: 2;SIMG11;160;150;M;1

/* FLOOR: floorno;x1;y1;x2;y2;floormap/.../floormap
/*
FLOOR: 1;32;132;246;169;1-1/2-2
FLOOR: 2;130;161;304;168;1-1/2-2

/* EXIT: no;x1;y1;x2;y2;epointx;epointy;description
/*
EXIT: 1;11;70;37;148;40;150;door

/* CLPART: filename
/*
CLPART: 1FG.IFF

/* ROOMIMGS: no_of_imgs;startrimg;grabx;graby;width;height;xoffset;hotsp
/*
/*   See the BOBS: statement in the graal.main file. The difference is
/*   that ROOMIMGS: define "room" images instead of "global" images - that
/*   is, the image numbers given here must be prefixed with "RIMG" when
/*   referred to in other statements and commands.
/*
/* 1=window  2=stove
ROOMIMGS: 1;1;3;2;50;40;0;0
ROOMIMGS: 1;2;139;2;56;61;0;-100
/* 3=topf mit mushroom 4=topf mit water 5=topf leer
ROOMIMGS: 3;3;59;3;23;22;24;0
/* 6/7=upper drawer closed/open
ROOMIMGS: 2;6;1;80;46;18;47;0
/* 8/9=lower drawer closed/open
ROOMIMGS: 2;8;0;99;47;18;47;0
/* 10=chest of drawers
ROOMIMGS: 1;10;103;80;63;42;0;-100

/* ANIM: bobno;image;anim_ch;amalsequence;putx;puty

/* ROOMOBJ: roomobjno,object_definition.... (see graal.main documentation)
ROOMOBJ: 1;stove;1;VIS;2;RIMG2;275;-13;-30;151;//SIMG33; ;NPICK; ;8; ;HIGH; ;a;this;it
ROOMOBJ: 2;window;1;VIS;3;RIMG1;174;91;22;44;SIMG33; ;NPICK; ;8; ;MID; ;a;this;it
ROOMOBJ: 3;drawer;1;VIS;4;RIMG6;73;113;11;21;SIMG11; ;NPICK; ;8; ;MID; ;a;this;it
ROOMOBJ: 4;drawer;1;VIS;5;RIMG8;73;129;11;5;SIMG11; ;NPICK; ;8; ;LOW; ;a;this;it
ROOMOBJ: 5;chest of\drawers;1;VIS;6;RIMG10;80;-12;4;146;SIMG11; ;NPICK; ;8; ; ; ;a;this;it

/* DACT: condition;...;action;...
/*
/*   These lines contain room actions performed directly as you enter the
/*   room.
DACT: TRACK 1.mod,0,NOFILTER
DACT: IFOF 6,1=0;IFNOTCARR 6;SHOW 6,273,100,RIMG5
DACT: IFOF 6,1=1;IFNOTCARR 6;SHOW 6,273,100,RIMG4
DACT: IFOF 6,1=2;IFNOTCARR 6;SHOW 6,273,100,RIMG3
DACT: IFRF 1=1;SHOW ROBJ3,73,113,RIMG7
DACT: IFRF 2=1;SHOW ROBJ4,73,129,RIMG9
DACT: LIGHTS ON;EXIT

/* LINE: dlgno;lineno;line;reuseline;conditions
/*
/*   Specifies a line of dialogue
/*
/* LACT: dlgno;lineno;action;...;action
/*
/*   Specifies a line executed when the corresponding LINE
/*   has been selected by the player. LACT:s belonging to a
/*   certain LINE: must ALWAYS be placed directly below the
/*   corresponding LINE: statement as shown here.

/* Room actions follow below ***
/*
/* ACTION: verbno;condition;...;command;...

/*
/* Action 0 - Exit
/* ===============
/*
ACTION: 0;IFOBJ 1;MEXIT;GOTO 2,1
/* Action 1 - Give
/*
/* Action 2- Pick up
ACTION: 2;IFOBJ 6;IFOF 6,1=1;SAY I can't pick it up. It's full of hot water.;EXIT
ACTION: 2;IFOBJ 6;IFOF 6,1=2;SAY I can't pick it up. It's full of hot mushroom brew.;EXIT
/*
/* Action 3 - Use
ACTION: 3;IFOBJ 6;IFOBJ2 ROBJ1;IFCARR 6;IFOF 6,1=0;MOBJ ROBJ1;HANDLE HIGH;PUT 6,U,1;SHOW 6,273,100,RIMG5;W 12;HANDLE -1;EXIT
ACTION: 3;IFOBJ 6;IFOBJ2 ROBJ1;IFCARR 6;IFOF 6,1=1;MOBJ ROBJ1;HANDLE HIGH;PUT 6,U,1;SHOW 6,273,100,RIMG4;W 12;HANDLE -1;EXIT
/*
/* Action 4 - Open
ACTION: 4;IFOBJ ROBJ2;SAY There is enough fresh air in the room.;EXIT
ACTION: 4;IFOBJ ROBJ3;IFRF 1=0;SETRF 1=1;MOBJ;HANDLE;SHOW ROBJ3,73,113,RIMG7;W 12;HANDLE -1;SAY There is a knife in this drawer. I'll take it.;HANDLE;PICK 17;W 12;HANDLE -1;EXIT
ACTION: 4;IFOBJ ROBJ4;IFRF 2=0;SETRF 2=1;MOBJ;HANDLE;SHOW ROBJ4,73,129,RIMG9;W 12;HANDLE -1;SAY This drawer is empty.;EXIT
ACTION: 4;IFOBJ ROBJ3;IFRF 1=1;MOBJ;SAY It's already open.;EXIT
ACTION: 4;IFOBJ ROBJ4;IFRF 2=1;MOBJ;SAY It's already open.;EXIT
ACTION: 4;IFOBJ ROBJ3;IFRF 1=2;SETRF 1=1;MOBJ;HANDLE;SHOW ROBJ3,73,113,RIMG7;W 12;HANDLE -1;EXIT
ACTION: 4;IFOBJ ROBJ4;IFRF 2=2;SETRF 2=1;MOBJ;HANDLE;SHOW ROBJ4,73,129,RIMG9;W 12;HANDLE -1;EXIT
/*
/* Action 5 - Talk to
/*
/* Action 6 - Consume
ACTION: 6;IFOBJ 6;IFOF 6,1=2;MOBJ 6;HANDLE HIGH;W 12;HANDLE -1;CIMG //SIMG19;W 6;CIMG //SIMG11;W 24;SAY Mmmmm! Tasty!;THINK I'm getting somewhat tired. I think I'll sleep for a while...;GOTO 10,1
/*
/* Action 7 - Close
ACTION: 7;IFOBJ ROBJ2;SAY The window is already closed.;EXIT
ACTION: 7;IFOBJ ROBJ3;IFRF 1=1;SETRF 1=2;MOBJ;HANDLE;SHOW ROBJ3,73,113,RIMG6;W 12;HANDLE -1;EXIT
ACTION: 7;IFOBJ ROBJ4;IFRF 2=1;SETRF 2=2;MOBJ;HANDLE;SHOW ROBJ4,73,129,RIMG8;W 12;HANDLE -1;EXIT
ACTION: 7;IFOBJ ROBJ3;IFRF 1=0;MOBJ;SAY It's already closed.;EXIT
ACTION: 7;IFOBJ ROBJ4;IFRF 2=0;MOBJ;SAY It's already closed.;EXIT
ACTION: 7;IFOBJ ROBJ3;IFRF 1=2;MOBJ;SAY It's already closed.;EXIT
ACTION: 7;IFOBJ ROBJ4;IFRF 2=2;MOBJ;SAY It's already closed.;EXIT
/*
/* Action 8 - Look at
ACTION: 8;IFOBJ ROBJ2;MOBJ;SAY Nice weather out there.;EXIT
/*
/* Action 9 - Attack
/*
/* End of room file
