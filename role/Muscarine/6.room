/* King Cranium
/* Room script for GRAAL
/*
/* RF 1 = 0 init | = 1 star caught

/* UPDATE: scroll_frames[;normal_frames]
/*
UPDATE: 6;1

/* 3D: max;mid;min;adjspeed (This statement is optional)
/*
/* SECTION: SAME|section_no
/*
SECTION: 2

/* BACKDROP: file
/*
BACKDROP: 6BG.iff

/* START_POS: startpos_no;image;x;y;L|M|R;floor_no
/*
START_POS: 1;//SIMG11;21;169;M;1

/* FLOOR: floor_no;x1;y1;x2;y2;floormaps
/*
FLOOR: 1;0;157;186;169;1-1/2-2
FLOOR: 2;66;139;90;169;1-1/2-2

/* PATH: path_no;floor1;floor2;px1;py1;px2;py2;[{more_points}] (This statement is optional)
/*
/* EXIT: exit_no;x1;y1;x2;y2;x;y;description (This statement is optional)
/*
EXIT: 1;0;0;5;169;4;178;exit

/* CLPART: file (This statement is optional)
/*
CLPART: 6FG.iff

/* ROOMIMGS: number;start_bob;x1;y1;w;h;x-offset;hotspot (This statement is optional)
/*
/* 1=King Cranium
ROOMIMGS: 1;1;1;90;85;59;0;-10
/* 2-4=Eye Anim
ROOMIMGS: 3;2;4;1;24;20;25; 
/* 5-7=Mouth Anim
ROOMIMGS: 3;5;1;26;85;59;85;-10
/* 8=Star
ROOMIMGS: 1;8;85;7;8;8;0; 
/* 9=Startrap full, 10=Startrap empty
ROOMIMGS: 2;9;104;1;15;21;15; 
/* 11=hole
ROOMIMGS: 1;11;105;97;49;46;0;-50

/* STATIC: bob;image;x;y (This statement is optional)
/*
/* ANIM: bob;image;anim_ch;anim|ptrn;x;y (This statement is optional)
/*
ANIM: 10;RIMG2;2;A 0,(RIMG2,80)(RIMG3,6)(RIMG4,6)(RIMG3,6)(RIMG2,300)(RIMG3,6)(RIMG4,6)(RIMG3,6)(RIMG2,240)(RIMG3,6)(RIMG4,6)(RIMG3,6);183;91

/* ROOMOBJ: obj;name;room;VIS|NVIS;bob;image;x;y;cx;cy;cimage;prep;PICK|NPICK;anim_ch;verb;icon;LOW|MID|HIGH;types;w1;w2;w3 (This statement is optional)
/*
ROOMOBJ: 1;hole;6;VIS;11;RIMG11;76;16;-5;124;SIMG31; ;NPICK; ;8; ;HIGH; ;a;this;it

/* DACT: {cond|comm}
/*
DACT: TRACK 6.mod,0,NOFILTER
DACT: IFNOTCARR 9;IFRF 1=0;SHOW 9,260,53,RIMG8;SETOF 1=0;DOAFTER 7-10,9,1
DACT: IFRF 1=1;IFNOTCARR 10;SHOW 10,68,76,RIMG10
DACT: LIGHTS ON;EXIT

/* LINE: dlg;line_no;sentence1;sentence2| ;{cond} (This statement is optional)
/*
/* LACT: dlg;line_no;{cond|comm} (This statement is optional)
/*
LINE: 4;1;At your service, my king!; ; 
LACT: 4;1;RESP R,4,Welcome, my little creep!;DSET 4,-1,+2,+8,+9,+10;EXIT

LINE: 4;2;Are there any quests to do?; ;IFOF 8,1=0
LACT: 4;2;RESP R,4,I do indeed have a little mission for you.
LACT: 4;2;DSET 4,-1,N2,+3;EXIT

LINE: 4;3;Tell me more about that mission!; ; 
LACT: 4;3;RESP R,4,Ok, the mission is to destroy the Universe. That is all!
LACT: 4;3;DSET 4,-1,N3,+4;EXIT

LINE: 4;4;Is it really necessary to destroy the Universe?;Why did you say is it necessary to destroy the\Universe?; 
LACT: 4;4;IFOF 8,2=0;RESP R,4,Yes, it is!;SETOF 8,2=1
LACT: 4;4;RESP R,4,There are far too many computer games out there where the player has to save the galaxy, or the earth, or his family or something.
LACT: 4;4;RESP R,4,Therefore you have to destroy the entire Universe in this game to keep up the COSMIC BALANCE!
LACT: 4;4;DSET 4,-1,+5,+6;EXIT

LINE: 4;5;Ok, I'll do it! I'll destroy the Universe.; ;IFOF 8,1=0
LACT: 4;5;RESP R,4,Fine! Here, have a little gift. It will help you on your way.
LACT: 4;5;DSET 4,+7,N6,N5;EDLG
LACT: 4;5;CMOVE 185,165,P;HANDLE LOW;W 12;PICK 14;HANDLE -1;SAY Thank you and goodbye!
LACT: 4;5;SETOF 8,1=1;RESP R,4,May the farce be with you!;EXIT

LINE: 4;6;Perhaps I'll destroy the Universe later.; ;IFOF 8,1=0
LACT: 4;6;RESP R,4,Ok, come back when you are ready for this mission.
LACT: 4;6;RESP R,4,You'll get a little gift from me then.;DSET 4,-1,-6;EXIT

LINE: 4;7;Do you really think I can destroy the Universe\with a golden knife?; ;IFOF 8,1=1
LACT: 4;7;RESP R,4,Of course not, but it will help you on your way.
LACT: 4;7;RESP R,4,Sorry, but the knife is all I can give you.;DSET 4,-1,N7;EXIT

LINE: 4;8;What do you know about the star nilende?; ;IFOF 9,2=1;IFNOTCARR 9
LACT: 4;8;RESP R,4,I would be very pleased if I could get rid of the nasty thing.;DSET 4,-1,-8;EXIT

LINE: 4;9;I've caught the star nilende!; ;IFCARR 9
LACT: 4;9;RESP R,4,Well done, my little creep!;DSET 4,-1,N9,N8;EXIT

LINE: 4;10;Goodbye, my king.; ; 
LACT: 4;10;RESP R,4,May the farce be with you!;EDLG;EXIT

/* ACTION: verb_no;{cond|comm} (This statement is optional)

ACTION: -1;IFOBJ 9;IFOF 1=0;IFOF 10,1=0;SHOW 9,68,80,RIMG8;SETOF 1=1;DOAFTER 10,9,1;EXIT
ACTION: -1;IFOBJ 9;IFOF 1=0;IFOF 10,1=1;SHOW 9,68,76,RIMG9;SETOF 1=2;SETRF 1=1;CANCEL 1;EXIT
ACTION: -1;IFOBJ 9;IFOF 1=1;SHOW 9,260,53,RIMG8;SETOF 1=0;DOAFTER 20-30,9,1;EXIT

ACTION: 0;IFOBJ 1;CANCEL 1;MEXIT;GOTO 4,4

ACTION: 1;IFOBJ 9;SETOF 9,2=1

/* Action 2 - Pick Up
ACTION: 2;IFOBJ 9;SETOF 9,2=1
ACTION: 2;IFOBJ 9;IFOF 1=0;SAY I can't reach it there.;EXIT
ACTION: 2;IFOBJ 9;IFOF 1=1;IFOF 3<3;ADDOF 3,1;MOBJ;HANDLE;SHOW 9,260,53,RIMG8;SETOF 1=0;DOAFTER 10-20,9,1;W 12;HANDLE -1;SAY It's gone.;EXIT
ACTION: 2;IFOBJ 9;IFOF 1=1;IFOF 3=3;ADDOF 3,1;MOBJ;HANDLE;SHOW 9,260,53,RIMG8;SETOF 1=0;DOAFTER 10-20,9,1;W 12;HANDLE -1;SAY It's gone.;CIMG SIMG11;W 12;CIMG SIMG21;SAY I don't think I can catch it this way.;EXIT
ACTION: 2;IFOBJ 9;IFOF 1=1;IFOF 3>3;SAY Sorry, but I don't think I can catch it this way.;EXIT
ACTION: 2;IFOBJ 9;IFOF 1=2;MOBJ;HANDLE;W 12;PICK 9;PICK 10;HANDLE -1;EXIT

/* Use
ACTION: 3;IFOBJ 9;SETOF 9,2=1
ACTION: 3;IFOBJ 10;IFOBJ2 ROBJ1;IFOF 9,1=1;SETOF 10,1=1;SHOW 9,260,53,RIMG8;MOBJ ROBJ1;HANDLE ROBJ1;SHOW 10,68,76,RIMG10;W 12;HANDLE -1;SETOF 9,1=0;DOAFTER 10-15,9,1;EXIT
ACTION: 3;IFOBJ 10;IFOBJ2 ROBJ1;IFOF 9,1=0;MOBJ ROBJ1;HANDLE ROBJ1;SHOW 10,68,76,RIMG10;W 12;HANDLE -1;SETOF 10,1=1;EXIT

ACTION: 4;IFOBJ 9;SETOF 9,2=1

ACTION: 5;IFOBJ 8;MOBJ;DSET 4,+1;EXIT

ACTION: 6;IFOBJ 9;SETOF 9,2=1

ACTION: 7;IFOBJ 9;SETOF 9,2=1

ACTION: 8;IFOBJ 9;SETOF 9,2=1;SAY The star nilende. An excellent light source.;EXIT

ACTION: 9;IFOBJ 9;SETOF 9,2=1

/* end
