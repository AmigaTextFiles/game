/* Professor Crckbrn
/* Room script for GRAAL
/*

/* UPDATE: scroll_frames[;normal_frames]
/*
UPDATE: 6;1

/* 3D: max;mid;min;adjspeed (This statement is optional)
/*
/* SECTION: SAME|section_no
/*
SECTION: 2

/* BACKDROP: file
/*
BACKDROP: 7BG.iff

/* START_POS: startpos_no;image;x;y;L|M|R;floor_no
/*
START_POS: 1;//SIMG11;36;96;L;1

/* FLOOR: floor_no;x1;y1;x2;y2;floormaps
/*
FLOOR: 1;18;87;136;169;1-1/2-2
FLOOR: 2;61;165;224;169;1-1/2-2

EXIT: 1;10;5;28;86;32;93;door

CLPART: 7FG.iff
/* 1=rat
ROOMIMGS: 1;1;0;0;57;49;0;-66
/* 2-4=Crckbrn
ROOMIMGS: 3;2;0;53;92;72;94;0
/* 5-8=grip arm
ROOMIMGS: 4;5;60;0;44;39;44;0

/* ROOMIMGS: number;start_bob;x1;y1;w;h;x-offset;hotspot (This statement is optional)
/*
/* STATIC: bob;image;x;y (This statement is optional)
/*
/* ANIM: bob;image;anim_ch;anim|ptrn;x;y (This statement is optional)
/*
/* ROOMOBJ: obj;name;room;VIS|NVIS;bob;image;x;y;cx;cy;cimage;prep;PICK|NPICK;anim_ch;verb;icon;LOW|MID|HIGH;types;w1;w2;w3 (This statement is optional)
/*
/* DACT: {cond|comm}
/*
DACT: TRACK 7.mod,0,NOFILTER
DACT: LIGHTS ON;EXIT

/* LINE: dlg;line_no;sentence1;sentence2| ;{cond} (This statement is optional)
/*
/* LACT: dlg;line_no;{cond|comm} (This statement is optional)
/*
LINE: 5;1;My name is Erica.; ; 
LACT: 5;1;RESP R,5,Hello Erica!;DSET 5,N1;EXIT

LINE: 5;2;My mission is to destroy the Universe.; ;IFOF 8,1=1
LACT: 5;2;RESP R,5,Fine! That has to be done to keep up the cosmic balance.
LACT: 5;2;RESP R,5,Hope you have success.;DSET 5,N2,+3;EXIT

LINE: 5;3;Could you help me to destroy the Universe?; ;IFOF 8,1=1
LACT: 5;3;RESP R,5,No time! Sorry!;DSET 5,N3;EXIT

LINE: 5;4;What do you know about the star nilende?; ;IFOF 9,2=1
LACT: 5;4;RESP R,5,It likes dark places. You can catch it with a star trap.;DSET 5,N4,+5;EXIT

LINE: 5;5;Where I can get a star trap?;Where did you say I can get a star trap?;IFOF 9,2=1;IFOF 10,2=0
LACT: 5;5;SETOF 3,2=1;RESP R,5,I'll build you one.;RESP R,5,But I need some human body parts at first.
LACT: 5;5;DSET 5;EXIT

LINE: 5;6;Could you give me a gravity converter, please?; ;IFOF 15,1=1;IFNOTCARR 11
LACT: 5;6;SETOF 12,3=1;RESP R,5,Bring me a green glowing mutant rat.;RESP R,5,Then I'll give you a gravity converter.
LACT: 5,6;DSET 5;EXIT

LINE: 5;7;Could I have this lab rat, please?; ;IFOF 12,2=0
LACT: 5;7;RESP R,5,Ok, take it. I don't need it any more.;DSET 5,N7;EXIT

LINE: 5;8;Goodbye!; ; 
LACT: 5;8;RESP R,5,Goodbye!;EDLG;EXIT

LINE: 5;9;Can you make this cauldron heat-resistable?; ;IFCARR 45;IFOF 45,1=0
LACT: 5;9;RESP R,5,Let me take a look at it!;HANDLE MID;CUTSCENE 1,H;W 24;HANDLE -1
LACT: 5;9;RESP R,5,Ok, I put a special varnish on it.;RESP R,5,This varnish will make the cauldron heat-resistable when it comes into contact with blood.
LACT: 5;9;HANDLE MID;CUTSCENE 1,H;SETOF 45,1=4;PICK 45;W 24;HANDLE -1;SAY Thank you!
LACT: 5;9;RESP R,5,Ok, and don't forget to put it into blood first.
LACT: 5;9;DSET 5,-9;EXIT

/* ACTION: verb_no;{cond|comm} (This statement is optional)
/*
ACTION: 0;IFOBJ 1;MEXIT;GOTO 4,5

/* Action 1 - Give
/*
ACTION: 1;IFOBJ 3;IFOBJ2 13;IFOF 3,2=1;MOBJ 13
ACTION: =;SAY Here you go, have a leg.;SAY Could you build a star trap for me now?;HANDLE MID;CUTSCENE 1,H;PUT 3,U, ;HANDLE -1
ACTION: =;RESP R,5,Ok, a leg. That should work.;RESP R,5,One moment please...;W 24;RESP R,5,...and here it comes: one star trap!
ACTION: =;HANDLE MID;CUTSCENE 1,H;PICK 10;SETOF 10,2=1;HANDLE -1;EXIT
ACTION: 1;IFOBJ 3;IFOBJ2 13;SAY I'm not sure if he really needs it.;EXIT
ACTION: 1;IFOBJ 12;IFOBJ2 13;IFOF 12,1=1;IFOF 12,3=1;MOBJ 13;HANDLE MID;CUTSCENE 1,H;PUT 12,U, ;HANDLE -1
ACTION: =;RESP R,5,Ok, thank you.;RESP R,5,And here is your gravity converter.
ACTION: =;HANDLE MID;CUTSCENE 1,H;PICK 11;HANDLE -1;EXIT
ACTION: 1;IFOBJ 12;IFOBJ2 13;IFOF 12,1=1;SAY He hasn't asked me for that.;EXIT
ACTION: 1;IFOBJ 12;IFOBJ2 13;IFOF 12,1=0;IFOF 12,3=0;MOBJ 13;RESP R,5,Thank you, but I don't need it.;EXIT
ACTION: 1;IFOBJ 12;IFOBJ2 13;IFOF 12,1=0;IFOF 12,3=1;SAY He said he needs a green glowing mutant rat.;SAY This is a normal lab rat.;EXIT

/* Action 2 - Pick up
/*
ACTION: 2;IFOBJ 12;MOBJ;HANDLE;W 12;PICK;HANDLE -1;SETOF 12,2=1;EXIT

/* Action 3 - Use
/*
/* Action 4 - Open
/*
/* Action 5 - Talk to
/*
ACTION: 5;IFOBJ 13;MOBJ;DSET 5,+1,+2,+4,+6,+7,+8,+9;EXIT

/* Action 6 - Consume
/*
/* Action 7 - Close
/*
/* Action 8 - Look at
/*
/* Action 9 - Attack
/*
/* End of room file
