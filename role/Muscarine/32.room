/* Outside Petshop
/* Room script for GRAAL
/*
/* RF 1=0 dustbin closed  =1 open
/* RF 2=1 boots picked up

/* UPDATE: scroll_frames[;normal_frames]
/*
UPDATE: 6;1

/* 3D: max;mid;min;adjspeed (This statement is optional)
/*
/* SECTION: SAME|section_no
/*
SECTION: 2

/* BACKDROP: file
BACKDROP: 32BG.iff

/* START_POS: startpos_no;image;x;y;L|M|R;floor_no
START_POS: 1;SIMG31;171;168; ;1
START_POS: 2;SIMG21;174;135; ;1

/* FLOOR: floor_no;x1;y1;x2;y2;floormaps
FLOOR: 1;70;128;226;169;1-1/2-2
FLOOR: 2;70;142;302;169;1-1/2-2

/* PATH: path_no;floor1;floor2;px1;py1;px2;py2;[{more_points}] (This statement is optional)
/*
/* EXIT: exit_no;x1;y1;x2;y2;x;y;description (This statement is optional)
EXIT: 1;70;155;226;169;171;171;east
EXIT: 2;150;56;196;125;174;133;pet shop

/* CLPART: file (This statement is optional)
CLPART: 32FG.iff

/* ROOMIMGS: number;start_bob;x1;y1;w;h;x-offset;hotspot (This statement is optional)
/* 1=special offer  2=pinguin board  3,4=dustbin closed/open 5=white fur
ROOMIMGS: 1;1;0;0;48;39;0; 
ROOMIMGS: 1;2;48;0;54;39;0; 
ROOMIMGS: 2;3;169;0;32;39;64;16/-40
ROOMIMGS: 1;5;209;3;16;8;0; 

/* STATIC: bob;image;x;y (This statement is optional)
/*
/* ANIM: bob;image;anim_ch;anim|ptrn;x;y (This statement is optional)
/*
/* ROOMOBJ: obj;name;room;VIS|NVIS;bob;image;x;y;cx;cy;cimage;prep;PICK|NPICK;anim_ch;verb;icon;LOW|MID|HIGH;types;w1;w2;w3 (This statement is optional)
ROOMOBJ: 1;board;32;VIS;1;RIMG1;41;113;40;54;SIMG11; ;NPICK; ;8; ; ; ;a;this;it
ROOMOBJ: 2;board;32;VIS;2;RIMG2;242;85;-3;77;SIMG31; ;NPICK; ;8; ; ; ;a;this;it
ROOMOBJ: 3;dustbin;32;VIS;3;RIMG3;82;45;0;85;SIMG31; ;NPICK; ;8; ;MID; ;a;this;it

/* DACT: {cond|comm}
/*
DACT: IFRF 1=0;HIDE 37
DACT: IFRF 1=1;SHOW ROBJ3,82,45,RIMG4
DACT: LIGHTS ON;EXIT

/* LINE: dlg;line_no;sentence1;sentence2| ;{cond} (This statement is optional)
/* LACT: dlg;line_no;{cond|comm} (This statement is optional)
/*
/* ACTION: verb_no;{cond|comm} (This statement is optional)
/*
/* Action 0 - Exit
ACTION: 0;IFOBJ 1;MEXIT;GOTO 29,4
ACTION: 0;IFOBJ 2;MEXIT;GOTO 33,1

/* Action 1 - Give
/*
/* Action 2- Pick up
ACTION: 2;IFOBJ 37;MOBJ;HANDLE;NAME 37,sealskin\boots,a,this,it;PICK;W 12;HANDLE -1;SETRF 2=1;EXIT

/* Action 3 - Use
/*
/* Action 4 - Open
ACTION: 4;IFOBJ ROBJ3;IFRF 1=1;MOBJ;SAY It's already open.;EXIT
ACTION: 4;IFOBJ ROBJ3;IFRF 1=0;IFRF 2=0;MOBJ;HANDLE;SHOW 37,81,88,RIMG5;SHOW ROBJ3,82,45,RIMG4;W 12;HANDLE -1;SETRF 1=1;EXIT
ACTION: 4;IFOBJ ROBJ3;IFRF 1=0;IFRF 2=1;MOBJ;HANDLE;SHOW ROBJ3,82,45,RIMG4;W 12;HANDLE -1;SETRF 1=1;EXIT

/* Action 5 - Talk to
/*
/* Action 6 - Consume
/*
/* Action 7 - Close
ACTION: 7;IFOBJ ROBJ3;IFRF 1=0;MOBJ;SAY It's already closed.;EXIT
ACTION: 7;IFOBJ ROBJ3;IFRF 1=1;IFRF 2=0;MOBJ;HANDLE;SHOW ROBJ3,82,45,RIMG3;HIDE 37;W 12;SETRF 1=0;HANDLE -1;EXIT
ACTION: 7;IFOBJ ROBJ3;IFRF 1=1;IFRF 2=1;MOBJ;HANDLE;SHOW ROBJ3,82,45,RIMG3;W 12;SETRF 1=0;HANDLE -1;EXIT

/* Action 8 - Look at
ACTION: 8;IFOBJ 37;IFNOTCARR 37;MOBJ;CIMG SIMG32;W 50;CIMG SIMG31;SAY I don't know what it is. But it doesn't move.;EXIT
ACTION: 8;IFOBJ ROBJ1;MOBJ;SAY   Special Offer Today: \norwegian blue (parrot)\baby seal (without fur);EXIT
ACTION: 8;IFOBJ ROBJ2;MOBJ;SAY We buy and sell penguins.;SETRF 30,2=1;EXIT

/* Action 9 - Attack
/*
/*
/* end of room file
