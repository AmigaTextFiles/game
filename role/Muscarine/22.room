/* Onius Ant
/* Room script for GRAAL
/*
/* RF 1=1 sthg. bright quest solved
/* RF 2=1 sthg. dark quest solved
/* RF 3=1 sthg. hot quest solved
/* RF 4=1 green snow quest solved
/* RF 5=1 all solved

/* UPDATE: scroll_frames[;normal_frames]
/*
UPDATE: 6;1

/* 3D: max;mid;min;adjspeed (This statement is optional)
/*
/* SECTION: SAME|section_no
/*
SECTION: 2

/* BACKDROP: file
/*
BACKDROP: 22BG.iff

/* START_POS: startpos_no;image;x;y;L|M|R;floor_no
/*
START_POS: 1;SIMG11;285;137; ;1

/* FLOOR: floor_no;x1;y1;x2;y2;floormaps
/*
FLOOR: 1;209;118;303;169;1-1/2-2
FLOOR: 2;9;166;317;169;1-1/2-2

/* PATH: path_no;floor1;floor2;px1;py1;px2;py2;[{more_points}] (This statement is optional)
/*
/* EXIT: exit_no;x1;y1;x2;y2;x;y;description (This statement is optional)
/*
EXIT: 1;289;37;312;136;286;137;exit

/* CLPART: file (This statement is optional)
/*
CLPART: 22FG.iff

/* ROOMIMGS: number;start_bob;x1;y1;w;h;x-offset;hotspot (This statement is optional)
/* 1,7=Onius lying 2=lying shadow
ROOMIMGS: 1;1;0;148;151;58;0;0
ROOMIMGS: 1;2;0;207;151;18;0;-63
/* 3-6=Onius talk
ROOMIMGS: 4;3;0;0;65;146;65;48/2
ROOMIMGS: 1;7;152;148;151;58;0;0

/* STATIC: bob;image;x;y (This statement is optional)
/*
STATIC: 2;RIMG2;123;82

/* ANIM: bob;image;anim_ch;anim|ptrn;x;y (This statement is optional)
/*
/* ROOMOBJ: obj;name;room;VIS|NVIS;bob;image;x;y;cx;cy;cimage;prep;PICK|NPICK;anim_ch;verb;icon;LOW|MID|HIGH;types;w1;w2;w3 (This statement is optional)
/*
/* DACT: {cond|comm}
/*
DACT: TRACK 21.mod,0,NOFILTER
DACT: LIGHTS ON;EXIT

/* LINE: dlg;line_no;sentence1;sentence2| ;{cond} (This statement is optional)
/* LACT: dlg;line_no;{cond|comm} (This statement is optional)
/*
LINE: 11;1;I wonder if you could help me destroy\the Universe?; ; 
LACT: 11;1;RESP S,11,I can't help you to destroy the Universe, sorry.
LACT: 11;1;RESP S,11,But I can help you to reach the Plane of Gods.
LACT: 11;1;SAY And there I'll find a way to destroy the Universe?
LACT: 11;1;RESP S,11,Yes, but it's a very dangerous journey to the Plane of Gods.
LACT: 11;1;RESP S,11,You need four things to reach the Plane of Gods.
LACT: 11;1;RESP S,11,Once you have these four things you have to pass through the dimension of slime and ooze.
LACT: 11;1;DSET 11,N1,+2,+3;EXIT

LINE: 11;2;What do I need to reach the Plane of Gods?; ; 
LACT: 11;2;RESP S,11,You need four things: something very bright, something very dark, something very hot and...
LACT: 11;2;SAY And something very cold?!;RESP S,11,No, some green snow!;SAY Green snow?;RESP S,11,Yes, some real green snow.
LACT: 11;2;RESP S,11,Bring me these things and I'll help you get to the Plane of Gods.
LACT: 11;2;SETOF 44,1=1;SETRF 12,1=1;DSET 11,N2,-4;EXIT

LINE: 11;3;What is the dimension of slime and ooze?; ; 
LACT: 11;3;RESP S,11,It's a very dangerous place. If you find no way out you have to stay there forever.
LACT: 11;3;RESP S,11,But it's the only way to reach the plane of gods. You have to go through the dimension of slime and ooze.
LACT: 11;3;DSET 11,N3;EXIT

LINE: 11;4;What else do I need to reach the Plane of Gods?; ;IFRF 5=0;IFOF 44,1=1|2
LACT: 11;4;IFRF 1=0;RESP S,11,Something very bright.
LACT: 11;4;IFRF 2=0;RESP S,11,Something very dark.
LACT: 11;4;IFRF 3=0;RESP S,11,Something very hot.
LACT: 11;4;IFRF 4=0;RESP S,11,Some green snow.
LACT: 11;4;DSET 11,-4;EXIT

LINE: 11;5;See you later.; ; 
LACT: 11;5;RESP S,11,Goodbye.;EDLG;BOBON 2,123,82,RIMG2;SHOW 26,123,144,PTRN 1;EXIT

LINE: 11;6;Yes, I'm ready.; ;IFRF 5=1
LACT: 11;6;RESP S,11,Then you'll go. But remember that you have to pass through the dimension of slime and ooze first.
LACT: 11;6;SAY Can you give me some hints how I get through the slime and ooze dimension?
LACT: 11;6;RESP S,11,No, I'm sorry. You have to find your way alone. Goodbye Erica!
LACT: 11;6;SAY Goodbye.;EDLG;GOTO 39,1

LINE: 11;7;No, wait a moment.; ; 
LACT: 11;7;RESP S,11,Ok, come back when you are ready.;DSET 11,R1,N7,N6,+8;EDLG;BOBON 2,123,82,RIMG2;SHOW 26,123,144,PTRN 1;EXIT

LINE: 11;8;I am now ready to travel to the Plane of Gods.; ;IFRF 5=1
LACT: 11;8;RESP S,11,Then you shall go. But remember that you have to pass through the dimension of slime and ooze first.
LACT: 11;8;SAY Can you give me some hints how I get through the slime and ooze dimension?
LACT: 11;8;RESP S,11,No, I'm sorry. You have to find your way alone. Goodbye Erica!
LACT: 11;8;SAY Goodbye!;EDLG;GOTO 39,1

/* ACTION: verb_no;{cond|comm} (This statement is optional)
/*
/* Action 0 - Exit
ACTION: 0;IFOBJ 1;MEXIT;GOTO 21,2

/* Action 1 - Give
ACTION: 1;IFOBJ 44;IFOBJ2 26;MOBJ 26;BOBOFF 2;SHOW 26,182,20,RIMG3
ACTION: =;SAY I have this very bright sunray here.;HANDLE HIGH;SETRF 1=1
ACTION: =;RESP S,11,Ok, that is something bright.;PUT 44,U,0;HANDLE -1
ACTION: 1;IFOBJ 25;IFOBJ2 26;MOBJ 26;BOBOFF 2;SHOW 26,182,20,RIMG3
ACTION: =;SAY I have this very dark shadow here.;HANDLE HIGH;SETRF 2=1
ACTION: =;RESP S,11,Ok, that is something dark.;PUT 25,U,0;HANDLE -1
ACTION: 1;IFOBJ 45;IFOBJ2 26;IFOF 45,1=3;MOBJ 26;BOBOFF 2;SHOW 26,182,20,RIMG3
ACTION: =;SAY I have some molten lava here.;HANDLE HIGH;SETRF 3=1
ACTION: =;RESP S,11,Ok, that is something hot.;PUT 45,U,0;HANDLE -1
ACTION: 1;IFOBJ 32;IFOBJ2 26;MOBJ 26;BOBOFF 2;SHOW 26,182,20,RIMG3
ACTION: =;SAY I have some green snow here.;HANDLE HIGH
ACTION: =;PUT 32,U,21;W 12;HANDLE -1
ACTION: 1;IFOBJ 32;IFOBJ2 26;IFRF 21,1=1;IFRF 21,5=1;SETRF 4=1;RESP S,11,Ok, this is real green snow.
ACTION: 1;IFOBJ 32;IFOBJ2 26;IFRF 21,5=0;RESP S,11,I asked you for real green snow. This is not real green snow, it's just a mixture of yellow and blue snow.
ACTION: =;RESP S,11,Now go and bring me some real green snow. Don't try to cheat!
ACTION: =;RESP S,11,You have to learn to avoid roundabout ways. Just take what you need when you see it.;SETRF 21,1=1;BOBON 2,123,82,RIMG2;SHOW 26,123,144,PTRN 1;EXIT
ACTION: 1;IFOBJ 25|32|44|45;IFOBJ2 26;IFRF 1=1;IFRF 2=1;IFRF 3=1;IFRF 4=1;SETRF 5=1;RESP S,11,You have now brought me all four items. Are you ready to travel to the Plane of Gods now?;DSET 11,S1,+6,+7;EXIT
ACTION: 1;IFOBJ 25|32|44;IFOBJ2 26;RESP S,11,Now go and bring me the remaining items.;BOBON 2,123,82,RIMG2;SHOW 26,123,144,PTRN 1;EXIT
ACTION: 1;IFOBJ 45;IFOBJ2 26;IFOF 45,1=3;RESP S,11,Now go and bring me the remaining items.;BOBON 2,123,82,RIMG2;SHOW 26,123,144,PTRN 1;EXIT

/* Action 2- Pick up
/*
/* Action 3 - Use
/*
/* Action 4 - Open
/*
/* Action 5 - Talk to
ACTION: 5;IFOBJ 26;MOBJ;BOBOFF 2;SHOW 26,182,20,RIMG3;RESP S,11,Hello Erica.;SAY Hello.;DSET 11,+1,+4,+5,+6;EXIT

/* Action 6 - Consume
/*
/* Action 7 - Close
/*
/* Action 8 - Look at
/*
/* Action 9 - Attack
/*
/*
/* end of room file
