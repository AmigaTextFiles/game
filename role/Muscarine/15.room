/* Hotel reception
/* Room script for GRAAL
/*

/* UPDATE: scroll_frames[;normal_frames]
/*
UPDATE: 6;1

/* 3D: max;mid;min;adjspeed (This statement is optional)
/*
/* SECTION: SAME|section_no
/*
SECTION: 2

/* BACKDROP: file
/*
BACKDROP: 15BG.iff

/* START_POS: startpos_no;image;x;y;L|M|R;floor_no
/*
START_POS: 1;SIMG23;71;106;M;1
START_POS: 2;SIMG13;169;51;M;3   /* room 101
START_POS: 3;SIMG23;124;51;M;3   /* room 102
START_POS: 4;SIMG23;59;51;M;3    /* room 103
START_POS: 5;//SIMG13;1;169;M;2  /* prison escape

/* FLOOR: floor_no;x1;y1;x2;y2;floormaps
/*
FLOOR: 1;43;96;82;169;1-1/2-2/3-2
FLOOR: 2;25;165;292;169;1-1/2-2/3-P1
FLOOR: 3;25;48;184;54;1-P1/2-P1/3-3

/* PATH: path_no;floor1;floor2;px1;py1;px2;py2;[{more_points}] (This statement is optional)
/*
PATH: 1;3;2;182;54;233;110;262;167

/* EXIT: exit_no;x1;y1;x2;y2;x;y;description (This statement is optional)
/*
EXIT: 1;55;63;85;93;71;106;exit
EXIT: 2;183;5;206;45;183;51;room 101
EXIT: 3;108;5;137;42;124;51;room 102
EXIT: 4;42;3;73;42;59;51;room 103

/* CLPART: file (This statement is optional)
/*
CLPART: 15FG.iff

/* ROOMIMGS: number;start_bob;x1;y1;w;h;x-offset;hotspot (This statement is optional)
/*
/* 1=gelaender
ROOMIMGS: 1;1;0;0;169;23;0;50
/* 2=mushroom pic 3=general pic
ROOMIMGS: 1;2;0;29;42;35;0; 
ROOMIMGS: 1;3;44;30;47;44;0; 
/* 4-6=doorman
ROOMIMGS: 3;4;181;0;38;60;38; 

/* STATIC: bob;image;x;y (This statement is optional)
/*
STATIC: 1;RIMG1;87;84

/* ANIM: bob;image;anim_ch;anim|ptrn;x;y (This statement is optional)
/*
/* ROOMOBJ: obj;name;room;VIS|NVIS;bob;image;x;y;cx;cy;cimage;prep;PICK|NPICK;anim_ch;verb;icon;LOW|MID|HIGH;types;w1;w2;w3 (This statement is optional)
/*
ROOMOBJ: 1;picture;15;VIS;2;RIMG2;260;17;-9;152;SIMG31; ;NPICK; ;8; ; ; ;a;this;it
ROOMOBJ: 2;picture;15;VIS;3;RIMG3;297;55;-28;114;SIMG31; ;NPICK; ;8; ; ; ;a;this;it

/* DACT: {cond|comm}
/*
DACT: TRACK 15.mod,0,NOFILTER
DACT: IFSPOS 5;CLPART 14FG.iff;IMGS 1,RIMG11,107,102,43,59,0,0;IMGS 5,RIMG12,151,135,22,26,22,0
DACT: IFSPOS 5;CIMG //RIMG14;LIGHTS ON
DACT: IFSPOS 5;OMOVE 18,27,169,3, ,WAIT,A 0,(//RIMG13,6)(//RIMG14,6)(//RIMG15,6)(//RIMG16,6)(//RIMG15,6)(//RIMG14,6)
DACT: IFSPOS 5;CIMG //RIMG12;W 12;CIMG //RIMG11;W 12;CIMG //SIMG13;EXIT
DACT: LIGHTS ON;EXIT

/* LINE: dlg;line_no;sentence1;sentence2| ;{cond} (This statement is optional)
/*
/* LACT: dlg;line_no;{cond|comm} (This statement is optional)
/*
LINE: 9;1;I'd like a room, please.; ; 
LACT: 9;1;RESP R,9,Room 103 is free. Do you want it?;DSET 9,S1,+2,+3;EXIT

LINE: 9;2;Yes, I'll take it.; ; 
LACT: 9;2;RESP R,9,Ok, it's yours.
LACT: 9;2;W 50;CIMG //SIMG11;W 12;CIMG //SIMG13;W 100;CIMG //SIMG11;W 12;CIMG //SIMG13
LACT: 9;2;DSET 9,R1,N1,N2,N3,+4;EXIT

LINE: 9;3;No, thank you.; ; 
LACT: 9;3;RESP R,9,I'm sorry, but all our other rooms are occupied.
LACT: 9;3;DSET 9,R1,-1,-2,-3;EXIT

LINE: 9;4;Could I have the key for my room?; ; 
LACT: 9;4;RESP R,9,Oh, you don't need a key for our rooms.
LACT: 9;4;RESP R,9,There is no need to lock the doors. We believe that our guests have nothing to hide.
LACT: 9;4;DSET 9,N4;EXIT

LINE: 9;5;Isn't it impossible for there to be rooms behind\the doors in the front wall?; ; 
LACT: 9;5;RESP R,9,It IS possible, because all our rooms are only two-dimensional.
LACT: 9;5;RESP R,9,But we have installed dimension expanders in every room so they look like normal three-dimensional rooms.
LACT: 9;5;RESP R,9,The problem is that we can't generate a fourth dimension. This is why the dimension of time does not exist in our rooms.
LACT: 9;5;DSET 9,N5;EXIT

LINE: 9;6;See you later.; ; 
LACT: 9;6;RESP R,9,Goodbye.;EDLG;EXIT

/* ACTION: verb_no;{cond|comm} (This statement is optional)
/*
/* Action 0 - Exit
ACTION: 0;IFOBJ 1;MEXIT;GOTO 23,1
ACTION: 0;IFOBJ 2;MEXIT;GOTO 16,1
ACTION: 0;IFOBJ 3;MEXIT;GOTO 17,1
ACTION: 0;IFOBJ 4;MEXIT;GOTO 18,1

/* Action 1 - Give
/*
/* Action 2- Pick up
/*
/* Action 3 - Use
/*
/* Action 4 - Open
/*
/* Action 5 - Talk to
ACTION: 5;IFOBJ 21;MOBJ;SAY Hello!;RESP R,9,Welcome in our little hotel. What can I do for you?;DSET 9,+1,+5,+6;EXIT

/* Action 6 - Consume
/*
/* Action 7 - Close
/*
/* Action 8 - Look at
ACTION: 8;IFOBJ ROBJ1;MOBJ;W 12;LIGHTS OFF;COMAREA OFF;TITLE 13BG.iff,FADE;W 1150;TITLE END;COMAREA ON;LIGHTS ON;SAY I have a slight feeling of deja vu.;EXIT
ACTION: 8;IFOBJ ROBJ2;MOBJ;W 12;LIGHTS OFF;COMAREA OFF;TITLE RedDngr.iff,FADE;W 1150;TITLE END;COMAREA ON;LIGHTS ON;SAY Strange picture.;EXIT

/* Action 9 - Attack
/*
/*
/* end of room file
