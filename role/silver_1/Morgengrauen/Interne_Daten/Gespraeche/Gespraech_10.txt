
& Gespraech mit einem Magier in Karte 115 (Magiergilde in Asgendorn)

#Vor dir sitzt ein in graue Roben gekleideter Magier.

§anfang
  INPUT name beruf alter ende Ding Dinge bibliothek
  #Dazu kann ich nichts sagen
  GOTO anfang


§ende
  END


§name
  #Ich bin Rasluf Sanderson, der Gildenälteste.
  GOTO anfang


§beruf
  #Ich bin der Gildenälteste.
  GOTO anfang


§alter
  #Ich habe viele, viele Winter erlebt. Zu alt, um manche [Dinge]
  #selbst zu erledigen.
  GOTO anfang



§bibliothek
    #Oh ja, die grosse Bibliothek! Ein Hort des Wissens!
    #Lag nördlich von unserer schönen Stadt - aber vor einigen Jahren haben
    #die Goblins sie verwüstet. (Schluchz) Die schönen Bücher!!!
    GOTO anfang


§Ding
§Dinge
  IFSET 61 gesetzt61
    #Es wäre nett, wenn Ihr einige Dinge für mich erledigen würdet.
    SET 61
  §gesetzt61

    &&&&&&&&&&&&&& Auftragsteil &&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

    &&&&&&&&& Auftrag 1
    & Hat die Gruppe den Auftrag schon erhalten?
    IFSET 62 schonerhalten1
      & Nein... Also den Auftrag geben
      #Ich suche schon lange nach einem alten Zauberbuch, daß einen mächtigen
      #Heilzauber enthalten soll. Meine letzten Nachforschungen deuten darauf hin,
      #daß es in den alten Gewölben unter der Stadt verschollen ist.
      #Sucht es bitte, und bringt es mir.
      INKEY
      SET 62

    §schonerhalten1
      & Ja...

      & Wurde das Buch schon abgegeben?
      IFSET 63 erledigt1
        & Nein, es wurde noch nicht abgegeben
        #Habt Ihr das Buch?
        GIVEITEM Zauberbuch habennicht1
        #Sehr schön - Ihr habt eure Sache gut gemacht!
        SET 63
        EP 30
        MONEY 300
        GOTO erledigt1

        §habennicht1
        #Schade... Ihr habt es nicht?!
        GOTO anfang

      §erledigt1


    &&&&&&&&& Auftrag 2
    & Hat die Gruppe den Auftrag schon erhalten?
    IFSET 64 schonerhalten2
      & Nein... Also den Auftrag geben
      #Da Ihr mir schon einmal einen Dienst erwiesen habt...
      #Könntet Ihr mir eine Silberwurzel besorgen? Ich benötige sie für ein
      #Ritual - und würde euch gut entlohnen.
      #Hier, nehmt dies als Vorschuss...
      INKEY
      MONEY 50
      SET 64

    §schonerhalten2
      & Ja...

      & Wurde die Wurzel schon abgegeben?
      IFSET 65 erledigt2
        & Nein, es wurde noch nicht abgegeben
        #Habt Ihr die Silberwurzel?
        GIVEITEM Silberwurzel habennicht2
        #Sehr schön - Ihr habt eure Sache gut gemacht!
        SET 65
        EP 30
        MONEY 500
        GOTO erledigt2

        §habennicht2
        #Schade... Ihr habt sie nicht?!
        GOTO anfang

      §erledigt2

    &&&&&&&&& Auftrag 3
    & Hat die Gruppe den Auftrag schon erhalten (und damit erledigt) ?
    IFSET 69 erledigt3
      & Nein... Also den Auftrag geben
      #Eine persönliche Frage: Ich habe da einen Lehrling, der etwas von der
      #Welt sehen möchte... Er ist nicht ganz unbegabt - sozusagen noch
      #ausbaufähig, wenn ihr versteht, was ich meine...
      #Wäret ihr einverstanden, wenn er euch begleitet?
      YN darfmit
      GOTO erledigt3

      §darfmit
        FOLLOW 10 ladefehler
        SET 69
        GOTO erledigt3

      §ladefehler
      #=== Fehler beim Laden des Charakters ===

    §erledigt3

&&&& Hier kann man weitere Aufträge einbauen

      #Ich habe zur Zeit leider keine weiteren Aufträge.
      GOTO anfang




