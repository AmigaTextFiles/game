
& Gespraech mit einem Magier in Karte 115

& Hier kann man gegen Bezahlung Zauber lernen

#Vor dir sitzt ein Mann in den Vierzigern, der die Robe eines Magiers trägt.

§anfang
  #Was kann ich für Euch tun?
  INPUT name beruf alter ende unterrichte unterricht
  #Dazu kann ich nichts sagen.
  GOTO anfang

§ende
  #Lebt wohl!
  END

§name
  #Gordo Gordon - Zweiter Lehrmeister dieser Akademie.
  #Ich [unterrichte] die Anfänger.
  GOTO anfang

§beruf
  #Meine Aufgabe ist es, die Schülerinnen und Schüler dieses Instituts
  #in die niederen Weihen einzuführen. Allerdings erteile ich auch
  #Privat[unterricht].
  GOTO anfang

§alter
  #Was geht euch das an?
  GOTO anfang


&=========== Beginn der Unterrichtssektion


§unterricht
§unterrichte
  #Falls Ihr an Privatunterricht interessiert seid - hier mein Angebot:

  #[Sonnenschein]    100 Taler            [Schwächung]      250 Taler
  #[Anregen]         200 Taler            [Aufwecken]       100 Taler
  INPUT sonnenschein schwächung anregen aufwecken
  GOTO anfang

§zuweniggeld
  #Nun, dann kommt wieder, wenn Ihr genug Geld habt.
  GOTO anfang

§sonnenschein
  GIVEMONEY 100 zuweniggeld
  #Nun, gebt gut acht: Zunächst ist es wichtig, die Astralkonjugation
  #der Elementarlinien...
  SPELL 1
  INKEY
  GOTO anfang


§schwächung
  GIVEMONEY 250 zuweniggeld
  #Nun, gebt gut acht: Der Corpus-Komponente dieses Zaubers kommt wesentliche
  #Bedeutung zu, besonders wenn man bedenkt, daß...
  SPELL 17
  INKEY
  GOTO anfang


§anregen
  GIVEMONEY 200 zuweniggeld
  #Nun, gebt gut acht: Der Rückkopplungseffekt dieses Zaubers ist wirklich
  #einzigartig - schon alleine deshalb erfordert es...
  SPELL 49
  INKEY
  GOTO anfang


§aufwecken
  GIVEMONEY 100 zuweniggeld
  #Nun, gebt gut acht: Dieser Standardzauber, der auch schon in Barsubals
  #erstem Kompendium beschrieben ist, hat...
  SPELL 23
  INKEY
  GOTO anfang

