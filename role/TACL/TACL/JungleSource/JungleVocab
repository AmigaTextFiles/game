*
* Sample T.A.C.L. Adventure Game source code © 1989 by Kevin Kelm
* 33 points possible in this file!
VOCAB

ACTION WAIT
 GO THISROOM
ENDACT

ACTION CLIMB, CLIMB WALLS, CLIMB UP, CLIMB UP WALLS, CLIMB WALL, CLIMB UP WALL
 IF PLAYER IN Shifting_Room THEN
  IF WallState > 2 THEN
   IF WallState < 5 THEN
    T Wedging yourself between the walls, you manage to climb ten feet!
    ADD Height 1
    GO THISROOM
    SCORE 5          * x2 = 10 points!
   ELSE
    T You don't need to anymore!
   ENDIF
  ELSE
   T You can't reach them!
  ENDIF
 ELSE
  T I do not understand.
 ENDIF
ENDACT

ACTION READ WALLS, READ WRITING, READ WALL, READ SLAB, LOOK WALL, LOOK WALLS
 IF PLAYER IN Sacred_Chamber THEN
  IF ReadCount = 0 THEN
   T You walk to the northwest corner and start to read the walls, which are
   T each divided into two slabs for a total of eight in the room.
   T
   T You gaze fixedly at the first slab.  Of the characters you can pick
   T out, you translate:
   T     ~iThis wall is Mayan,
   T     that wall is Your-an.~n

  ELSIF ReadCount = 1 THEN
   T The second slab reads:
   T     ~iT.A.C.L. is great,
   T     T.A.C.L. is good,
   T     Be the first in ~bYOUR ~n~i neighborhood!~n
   T              ~r -- BURMA SHAVE ~n

  ELSIF ReadCount = 2 THEN
   T Peering at the third slab, you decipher:
   T     ~iStay away from Alpha Centauri
   T     restaurants-- they're only two stars. ~n

  ELSIF ReadCount = 3 THEN
   T The fourth slab reads:
   T     ~iThere once was a man from Nantucket...~n
   T The rest of this slab is unreadable.

  ELSIF ReadCount = 4 THEN
   T The fifth one says:
   T     ~iChico was here.~n
   T Say! There's a draft coming from around this slab!

  ELSIF ReadCount = 5 THEN
   T The sixth slab says:
   T     ~iPizzaro is an <EXPLETIVE DELETED>!!!~n

  ELSIF ReadCount = 6 THEN
   T On the seventh slab you find:
   T     ~iAbandon ye all gold who enter these walls!~n

  ELSIF ReadCount = 7 THEN
   T The eighth and final slab reads:
   T     ~iSome reading room, eh? ~n
  ENDIF

  IF FirstRound = 1 THEN
   SCORE 1                      *    x8 = 8 points total!
  ENDIF

  ADD ReadCount 1
  IF ReadCount = 8 THEN
   EQU ReadCount 0
   EQU FirstRound 0
  ENDIF
 ENDIF
ENDACT

ACTION PUSH WALL, PUSH SLAB, OPEN WALL, OPEN SLAB
 IF PLAYER IN Sacred_Chamber THEN
  IF ReadCount = 5 THEN
   IF SlabPushed = 0 THEN
    T You lean hard on the wall and it rotates aside to reveal a passage
    T to the south!
    DIRECTIONS Sacred_Chamber N S
    LINK Sacred_Chamber S Hall_of_the_Priests
    EQU SlabPushed 1
    SCORE 5
    IF PLAYER HAS Idol THEN
     IMAGE Sacred_Chamber Sacred4
     SHOW Sacred4
    ELSE
     IMAGE Sacred_Chamber Sacred3
     SHOW Sacred3
    ENDIF
   ELSE
    T Now that it's open, the slab won't budge.
   ENDIF
  ELSE
   T Nothing happens.
  ENDIF
 ELSIF InCaverns = 1 THEN
  T Nothing happens.
 ELSE
  T I don't ~isee ~nany walls here, do you?
 ENDIF
ENDACT

ACTION YES, YUP, SURE, YAH, YEAH, YEP
 T Talking to yourself?
ENDACT

ACTION NO, NOPE, NO WAY, JUST SAY NO
 T You can talk to yourself, just as long as you don't answer yourself.
ENDACT

ACTION DIG, DIG ROCKS
 IF PLAYER IN Break_Down_Rocks THEN
  T You try to pry loose some of the sharp rocks in your way, and in so
  T doing, you bring the tunnel down on yourself.
  CALL BuyTheFarm

 ELSIF PLAYER IN Rubble_Crawl THEN
  T You scoop some of the wedged rocks out of the crawlway, but make little
  T progress.

 ELSIF PLAYER IN Cobble_Crawl THEN
  IF Cobble_Crawl NOT Dug THEN
   T You remove some of the stones wedged together at the north end of the
   T burrow.  You begin to feel a warm draft and dig frantically.  Finally,
   T a beam of daylight stabs through the darkness!  After a bit more
   T digging, you finally make a hole large enough to fit through.  Blue sky
   T awaits you through that hole!
   DIRECTIONS Cobble_Crawl SE N
   LINK Cobble_Crawl N Ledge
   SET Cobble_Crawl Dug
   IMAGE Cobble_Crawl Rock_Crawl_2
   SHOW Rock_Crawl_2
   SCORE 10
  ENDIF
 ELSE
  T In a fit of good judgement, you decide not to.
 ENDIF
ENDACT

ACTION DIE, END, EXIT
 T If you insist!
 CALL BuyTheFarm
ENDACT

ACTION YELL, SHOUT, SCREAM, SHRIEK, BELLOW, WAIL, CRY
 T AAAAAAAAAAAAAAAAAAAAAAAARRRRRGGGGGGGGGHHHHHHHHHH!!!
 T
 T Feel better?
ENDACT

ENDVOCAB
