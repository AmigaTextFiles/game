*
* Sample T.A.C.L. Aventure Game source code © 1989 by Kevin Kelm
*
ROOM Dark_Death   * if player is here, he's done for

CODE
 T You stumble in the darkness and break your neck.
 CALL BuyTheFarm
ENDCODE
ENDROOM  * what could be simpler?!? :)
*
*
*
ROOM Cavern_Entrance

DEFAULT
 N Dark_Death
 S Dark_Death
 W Small_Meadow
 U Small_Meadow
ENDDEFAULT

CODE
 IF InCaverns = 0 THEN
  T You pause inside the entrance to let your eyes adjust to the dusky
  T grey light.
 EQU InCaverns 1
 ENDIF
 IF Cavern_Entrance NOT VISITED THEN
  T You are standing in the entrance of a natural cavern.  To your east
  T and up is the warm light of day, which grows dimmer as it creeps down
  T the wet walls of the cave.
  T Tunnels lead off to the north and to the south.
  IMAGE Cavern_Entrance Entrance
  SHOW Entrance
 ELSE
  T Cavern Entrance.
 ENDIF
 IF CanSee = 0 THEN
  T It is too dark to see into the side tunnels-- be very careful!
  LINK Cavern_Entrance N Dark_Death
  LINK Cavern_Entrance S Dark_Death
 ELSE
  LINK Cavern_Entrance N Rock_Slide
  LINK Cavern_Entrance S Stair_Well
 ENDIF
ENDCODE

ENDROOM
*
*
*
ROOM Rock_Slide

CODE
 T You step into the narrow, winding tunnel and squeeze between the walls.
 T After about ten meters, you disturb the rocks in the east wall and cause
 T a rockslide, which kills you instantly.
 CALL BuyTheFarm
ENDCODE

ENDROOM
*
*
*
ROOM Stair_Well

DEFAULT
 N Cavern_Entrance
 D Spiral_Stairs
ENDDEFAULT

CODE
 IF CanSee = 1 THEN
  IF Stair_Well NOT VISITED THEN
   T You are in a small, cold room.  In its floor is a neatly carved spiral
   T staircase, leading down!  A cold draft whispers from the hole.  You
   T hear from it a dim roaring sound.
   IMAGE Stair_Well Top_of_Stairs
   SHOW Top_of_Stairs
  ELSE
   T Top of spiral stairs.
  ENDIF
 ELSE
  T Unable to see anything, you stumble into some kind hole in the
  T floor and break you leg.  You expire after several hours of delerium
  T at the bottom of the hole.
  CALL BuyTheFarm
 ENDIF
ENDCODE

ENDROOM
*
*
*
ROOM Spiral_Stairs

DEFAULT
 U Stair_Well
 D Cavernous_Hall
ENDDEFAULT

CODE
 IF Spiral_Stairs NOT VISITED THEN
  T Your boots scuff the slimy grit on the solid stone steps as you walk.
  T Below you is a dark passage, and the stairs wind upward above your head.
  IMAGE Spiral_Stairs Stairs
  SHOW Stairs
 ELSE
  T Spiral staircase.
 ENDIF
ENDCODE

ENDROOM
*
*
*
ROOM Cavernous_Hall

DEFAULT
 U Spiral_Stairs
 E Dark_Passage
 S Small_Room
ENDDEFAULT

CODE
 IF Cavernous_Hall NOT VISITED THEN
  T You are in a very large and long chamber with a smooth floor and rough,
  T sloping walls and ceiling.  There is a small tunnel in the east wall and
  T another at the other end of the room in the south wall.
  IMAGE Cavernous_Hall Big_Hall
  SHOW Big_Hall
 ELSE
  T Cavernous Hall.
 ENDIF
ENDCODE

ENDROOM
*
*
*
ROOM Dark_Passage

DEFAULT
 W Cavernous_Hall
 E Broken_Floor_Hall
ENDDEFAULT

CODE
 IF Dark_Passage NOT VISITED THEN
  T You are in a 3-meter-wide, east-west running passage.
  IMAGE Dark_Passage Std_Tunnel
  SHOW Std_Tunnel
 ELSE
  T Dark Passage.
 ENDIF
ENDCODE

ENDROOM
*
*
*
ROOM Broken_Floor_Hall

CODE
 T As you walk along, the floor gives way and you plummet into a pit of
 T spikes!!!  You have died!
 CALL BuyTheFarm
ENDCODE

ENDROOM
*
*
*
ROOM Small_Room

DEFAULT
 N Cavernous_Hall
 S Dead_End
ENDDEFAULT

CODE
 T You are in a small room with an exit to the north.
 IF DoorState < 3 THEN
  T There is also an exit to the south.
  IF DoorState = 0 THEN
   T A large stone wall is slowly covering the south exit!
   IMAGE Small_Room SRoom_Door1
   SHOW SRoom_Door1

  ELSIF DoorState = 1 THEN
   T The exit to the south is almost closed!
   IMAGE Small_Room SRoom_Door2
   SHOW SRoom_Door2

  ELSIF DoorState = 2 THEN
   T The stone block slams shut over the southern exit!  The booming echo
   T returns from the cavernous hall behind you as it dies.
   DIRECTIONS Small_Room N
   IMAGE Small_Room SRoom_Door3
   SHOW SRoom_Door3
  ENDIF

 ELSIF DoorState = 5 THEN
  T You hear a rumbling sound the west wall of the room slides
  T down to reveal an western passage!
  DIRECTIONS Small_Room N W
  LINK Small_Room W Western_Passage
  IMAGE Small_Room SRoom_Door4
  SHOW SRoom_Door4

 ELSIF DoorState > 5 THEN
  T There is also an exit to the west.
 ENDIF

 ADD DoorState 1
ENDCODE

ENDROOM
*
*
*
ROOM Dead_End

CODE
 T You safely dive under the falling stone just as it slams shut behind
 T you.  After resting for a moment, you dust yourself off and stand up
 T for a look around.
 T
 T You discover that you are trapped in a small room of solid stone along
 T with the skeletons of adventurers who made the same decision.  After
 T trying desperately to get out, you sit down beside the remains of
 T Professor Pendelbrot, who disappeared several years ago.  You discover
 T that he is no longer an engaging conversationalist.  After a few days
 T you dehydrate, die and wait for the ~inext ~nunlucky adventurer.
 CALL BuyTheFarm
ENDCODE

ENDROOM
*
*
*
ROOM Western_Passage

DEFAULT
 E Small_Room
 W Shifting_Room
ENDDEFAULT

CODE
 IF Western_Passage NOT VISITED THEN
  T You are in an east-west passage of average width and height.
  IF Lever IS Pulled THEN
   IMAGE Western_Passage Switch_Hall2
   SHOW Switch_Hall2
  ELSE
   IMAGE Western_Passage Switch_Hall1
   SHOW Switch_Hall1
  ENDIF
 ELSE
  T Eastern Passage.
 ENDIF
ENDCODE

ENDROOM
*
*
*
ROOM Shifting_Room

DEFAULT
 E Western_Passage
ENDDEFAULT

CODE
 IF WallState = 0 THEN
  T You step into a room that is littered with crushed bones and other
  T debris.
  T
  T The walls begin to close in on you!
  IMAGE Shifting_Room Crunch1
  SHOW Crunch1

 ELSIF WallState = 1 THEN
  T The walls continue to close in.  They are about 8 feet apart now!
  IMAGE Shifting_Room Crunch2
  SHOW Crunch2

 ELSIF WallState = 2 THEN
  T Still moving, the walls can now be touched!
  IMAGE Shifting_Room Crunch3
  SHOW Crunch3

 ELSIF WallState = 3 THEN
  T The walls are almost shoulder's width apart now!
  IMAGE Shifting_Room Crunch4
  SHOW Crunch4

 ELSIF WallState = 4 THEN
  IF Height < 2 THEN
   T The walls slam shut with finality, punctuating your life with an
   T exclamation point.
   CALL BuyTheFarm
  ELSE
   T It seems that only the bottom 20 feet of the walls were moving; the
   T walls slam shut just as you reach the top.  You are standing on the
   T tops of the lower walls in a small room.  Glancing behind, you notice
   T that your exit has been sealed off, but there is a passage to the west.
   DIRECTIONS Shifting_Room W
   LINK Shifting_Room W Ornate_Passage
   IMAGE Shifting_Room Crunch5
   DIRECTIONS Western_Passage E
   SHOW Crunch5
  ENDIF

 ELSIF WallState > 4 THEN
  IF Shifting_Room NOT VISITED THEN
   T You are in a small room with a seam down the middle of the floor.
   T There is an exit to the west.
  ELSE
   T Top of the Shifting Room.
  ENDIF
 ENDIF

 ADD WallState 1
ENDCODE

ENDROOM
*
*
*
ROOM Ornate_Passage

DEFAULT
 S Sacred_Chamber
 E Shifting_Room
ENDDEFAULT

CODE
 IF Ornate_Passage NOT VISITED THEN
  T You find yourself in a short passage with ornate stone carvings and
  T colorful paintings on the walls and ceiling.  The passage goes east and
  T turns south.
  IMAGE Ornate_Passage Ornate
  SHOW Ornate
 ELSE
  T Ornate Passage.
 ENDIF
ENDCODE

ENDROOM
*
*
*
ROOM Sacred_Chamber

ATTRIB
 FirstVisited Y
ENDATTRIB

DEFAULT
 N Ornate_Passage
ENDDEFAULT

CODE
 IF Sacred_Chamber IS FirstVisited THEN
  IMAGE Sacred_Chamber Sacred1
  UNSET Sacred_Chamber FirstVisited
 ENDIF

 IF Sacred_Chamber NOT VISITED THEN
  T You are in a large, cold chamber.  The walls are covered with
  T Mayan writing and there is an altar in the center of the room.
  T There is an exit to the north.
  IF SlabPushed = 1 THEN
   T To the south is an open secret doorway.
   IF Idol IN Sacred_Chamber THEN
    SHOW Sacred3
   ELSE
    SHOW Sacred4
   ENDIF
  ELSE
   IF Idol IN Sacred_Chamber THEN
    SHOW Sacred1
   ELSE
    SHOW Sacred2
   ENDIF
  ENDIF
 ELSE
  T Sacred Chamber.
 ENDIF
ENDCODE

ENDROOM
*
*
*
