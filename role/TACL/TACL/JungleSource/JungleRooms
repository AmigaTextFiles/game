*
* Sample T.A.C.L. Adventure Game source code © 1989 by Kevin Kelm
* 10 points possible this file!
ROOM Float_Plane

ATTRIB
 FirstTurn Y
ENDATTRIB

DEFAULT
 E River_Clearing
ENDDEFAULT

CODE
 IF PLAYER HAS Idol THEN
  T You climb back into your float plane, stow your gear, place the idol on
  T the co-pilot's seat, and start the plane.  After a few sputters, the
  T engine roars to life and carries you and your prize to the glory and
  T fame that awaits you back in civilization.
  T
  T   ~iCONGRATULATIONS, ADVENTURER! ~n
  T  You have successfully completed a T.A.C.L. adventure game!
  T  Would you like the power to ~bmake your own~n adventure games with
  T  T.A.C.L.?  If so, contact:
  T
  T  Micro Momentum, Inc.
  T  P.O. Box 372
  T  Washington Depot, CT 06794
  T  1 (800) 448-7421
  T
  T  We might be interested in marketing ~iYOUR ~nT.A.C.L. adventure games!
  SCORE 10
  SHOW VictoryScreen       * Yeeaahh!
  CALL WeHaveAWinner

 ELSIF Float_Plane IS FirstTurn THEN
  UNSET Float_Plane FirstTurn
  T
  T
  T
  T
  T      Welcome to "Jungle," a sample T.A.C.L. Adventure Game which
  T demonstrates many, but by no means all, of The Adventure Contruction
  T Language's capabilities.  This game was put together in less than 4
  T day's worth of work; the adventure was planned for several hours, I
  T actually ~iwrote ~nit in only 1 day, and spent the next three days
  T putting together the graphics.  (No wisecracks!  I never said I was
  T a good artist!  :^)
  T
  T      Just imagine what a ~iREAL ~nadventure guru could do!  We hope you
  T enjoy (~band solve!~n) the game.  If you have trouble, you may refer to
  T the included T.A.C.L. source code that created this adventure game.
  T
  T                  ~r  HAVE FUN!  ~n
  T
  SHOW WelcomeScreen             * "SHOW" displays an image NOW
  T
  T
     * I think there is a "PRESS RETURN" right about HERE to pause!
  T
  T
  T You guide the single-engine float plane in for a landing on the
  T swift waters of a river in the middle of a dense jungle.  The plane
  T glides into the east shore and comes to a rest on the bank.  After
  T an arduous journey and years of planning, you have finally found your
  T way to the legendary ruins of an ancient Mayan temple.  The mere thought
  T of the golden idol sends chills down your spine.
  IMAGE Float_Plane In_Plane     * "IMAGE" tells which image to use if...
  SHOW In_Plane                  * ...the player clicks in image window
 ELSIF Float_Plane NOT VISITED THEN
  T You are sitting comfortably in your single-engine float plane on a
  T winding river in the middle of a dense, gloomy jungle.
  SHOW In_Plane

 ELSE
  T Your float plane.
 ENDIF
 T

ENDCODE

ENDROOM
*
*
*
ROOM River_Clearing

DEFAULT
 N  Lost_In_Jungle
 NE Lost_In_Jungle
 E  Jungle_Path
 SE Lost_In_Jungle
 S  Lost_In_Jungle
 SW River
 W  Float_Plane
 NW River
ENDDEFAULT

CODE
 IF River_Clearing NOT VISITED THEN
  T You are standing in a small, grassy clearing between the river to the
  T east and a jungle forest which surrounds you to the north, east and
  T south.  Your float plane is on the river to the west.
  IMAGE River_Clearing Jungle
  SHOW Jungle
 ELSE
  T River Clearing.
 ENDIF
ENDCODE

ENDROOM
*
*
*
ROOM Lost_In_Jungle     * once you get here, you aren't getting out!

DEFAULT
 N  Lost_In_Jungle
 E  Lost_In_Jungle
 S  Lost_In_Jungle
 W  Lost_In_Jungle
 SE Lost_In_Jungle
 NW Lost_In_Jungle
 SW Lost_In_Jungle
 NE Lost_In_Jungle
ENDDEFAULT

CODE
 T You are lost in the dense foliage of the jungle.
 IMAGE Lost_In_Jungle Jungle

 RANDOM Temp 15
 IF Temp = 0 THEN
  T You are killed and eaten by a jungle cat!
  CALL BuyTheFarm
 ELSIF Temp = 1 THEN
  T You are sucked dry by 10,000 mosquitos!
  CALL BuyTheFarm
 ELSIF Temp = 2 THEN
  T You slip into a bog of quicksand and suffocate!
  CALL BuyTheFarm
 ELSIF Temp = 3 THEN
  T The biggest snake you've ever seen gets friendly with you.  ~iTOO ~n
  T friendly.  It hugged you to death!
  CALL BuyTheFarm
 ELSIF Temp = 4 THEN
  T Some friendly natives ask you to dinner.  You accept, only to find out
  T later that they are ~inot ~nvery friendly and that really they are
  T having you ~ifor ~ndinner!
  CALL BuyTheFarm
 ELSE
  T Who knows what forms of death may lurk here?!?
 ENDIF
 T
ENDCODE

ENDROOM
*
*
*
ROOM River     * this is the end of the line!

CODE
 T You foolishly wade into the river.  Remember all those gruesome stories
 T about how fast pirahnas eat?  They weren't exaggerating!  Before you can
 T make it back to the shore or dive for your plane, they reduce you to
 T hamburger.  You distinctly hear small, satisified burps as you slip
 T into oblivion.
 CALL BuyTheFarm
ENDCODE

ENDROOM
*
*
*
ROOM Jungle_Path

DEFAULT
 N  Dark_Jungle
 NE Lost_In_Jungle
 E  Lost_In_Jungle
 SE Lost_In_Jungle
 S  Lost_In_Jungle
 SW Lost_In_Jungle
 W  River_Clearing
 NW Lost_In_Jungle
 U  Forest_Canopy
ENDDEFAULT

CODE
 IF Jungle_Path NOT VISITED THEN
  T You've managed to find a faint path into the greenery.  You are
  T surrounded in all directions by a seemingly impenetrable jungle.
  IMAGE Jungle_Path Jungle_Tree
  SHOW Jungle_Tree
 ELSE
  T Jungle Path.
 ENDIF
 PLACEOBJ Big_Tree Jungle_Path  * so the player can see tree here
ENDCODE

ENDROOM
*
*
*
ROOM Forest_Canopy

DEFAULT
 D Jungle_Path
ENDDEFAULT

CODE
 IF Forest_Canopy NOT VISITED THEN
  T You find yourself about 40 feet above the ground on a large and solid-
  T looking limb.  There is a bit more light up here than on the forest
  T floor.
  IMAGE Forest_Canopy In_Tree
  SHOW In_Tree
 ELSE
  T Forest Canopy.
 ENDIF
 PLACEOBJ Big_Tree Forest_Canopy  * so same object "seen" in both rooms
ENDCODE

ENDROOM
*
*
*
ROOM Dark_Jungle

DEFAULT
 N  Lost_In_Jungle
 NE Lost_In_Jungle
 E  Small_Meadow
 SE Lost_In_Jungle
 S  Jungle_Path
 SW Lost_In_Jungle
 W  Lost_In_Jungle
 NW Lost_In_Jungle
ENDDEFAULT

CODE
 IF Dark_Jungle NOT VISITED THEN
  T You are deep in this gloomy jungle.  There is very little light, save
  T that filtering through the thick canopy about you.  There is a hush
  T about this area; the normal jungle sounds have ceased.
  IMAGE Dark_Jungle Jungle
  SHOW Jungle
 ELSE
  T Dark Jungle.
 ENDIF
ENDCODE

ENDROOM
*
*
*
ROOM Small_Meadow

DEFAULT
 N  Lost_In_Jungle
 NE Lost_In_Jungle
 E  Cavern_Entrance
 SE Lost_In_Jungle
 S  Lost_In_Jungle
 SW Lost_In_Jungle
 W  Dark_Jungle
 NW Lost_In_Jungle
 D  Cavern_Entrance
ENDDEFAULT

CODE
 IF InCaverns = 1 THEN
  T You step blinking into the daylight as your eyes adjust to the
  T brightness.
 EQU InCaverns 0
 ENDIF
 IF Small_Meadow NOT VISITED THEN
  T You are in a small meadow that lies between the black, mossy rocks of a
  T cliff face to the east and the surrounding jungle.  At the base of the
  T cliff is the mouth of a cavern.  Cold, stale air eminates from it.
  IMAGE Small_Meadow SMeadow
  SHOW SMeadow
 ELSE
  T Small Meadow.
 ENDIF
ENDCODE

ENDROOM
*
*
*
ROOM Ledge

DEFAULT
 S Cobble_Crawl
 D Falling
ENDDEFAULT

CODE
 IF InCaverns = 1 THEN
  T You emerge blinking into the daylight!
  EQU InCaverns 0
 ENDIF

 IF Ledge NOT VISITED THEN
  T You are on a mossy, north-facing ledge about 50 feet above the jungle
  T floor.  You spot your float plane near the clearing almost directly
  T below and to the north!
  IMAGE Ledge Cliff_Ledge1
  IF Vine IS Tied THEN
   SHOW Cliff_Ledge2
  ELSE
   SHOW Cliff_Ledge1
  ENDIF
 ELSE
  T Ledge.
 ENDIF
ENDCODE

ENDROOM
*
*
*
ROOM Falling

CODE
 T You step over the edge into thin air.  You then fall ~iout ~nof thin air
 T and ~ionto ~n a rather hard bit of ground.
 CALL BuyTheFarm
ENDCODE

ENDROOM
*
*
*
ROOM River_Path

DEFAULT
 N River_Clearing
 NE Lost_In_Jungle
 E Lost_In_Jungle
 SE Lost_In_Jungle
 SW River
 W River
 NW River
ENDDEFAULT

CODE
 T You are on a small path that leads to the north along the river.
 IMAGE River_Path Along_River
 SHOW Along_River
ENDCODE

ENDROOM
*
*
*
ROOM NoWhere  * not really a jungle room, but it has to be somewhere
              * ... or does it??? :-)  ... This is a "stash" room.
CODE
ENDCODE
ENDROOM
*
* --- END OF FILE
*
