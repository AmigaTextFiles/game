/* Constantinople
/*
/* =========================================================================
/*
/*                          Section 1 - Constantinople
/*
/* =========================================================================
/*


CLPART: Olaf_Extras.iff

/* SECTIONIMGS: no_of_imgs;start_img;grabx;graby;width;height;xoffset;hotsp
/*
/*   See the IMGS: statement in the graal.main file. The difference is
/*   that SECTIONIMGS: define "section" images instead of "global" images
/*   - that is, the image numbers given here must be prefixed with "SIMG"
/*   when referred to in other statements and commands.
/*
/* 1-2 playing flute
SECTIONIMGS: 2;1;34;2;31;47;32;0
/* 3 reading guide
SECTIONIMGS: 2;3;2;50;31;47;32;0
/* 5 reading dictionary
SECTIONIMGS: 1;5;98;2;31;47;0;0

CLPART: Olaf_Symbols.iff
/* SOBJ10-14 flute, copper coin, cup, rubber band, gold
SECTIONIMGS: 5;10;0;83;51;21;50;0


/* SECTIONOBJ: sectionobjno,object_definition....
/*             (see graal.main documentation)
/*
/*   The object number specified here must later be prefixed by "ROBJ" when
/*   referred to in other statements and commands.
/*
SECTIONOBJ: 1;exotic\flute;0;0;0;0;0;0;0;0;0; ;-1;0;8;SIMG10;MID;WD;an;this;the
SECTIONOBJ: 2;copper coin;0;0;0;99;0;0;0;0;0; ;-1;0;8;SIMG11;MID;TD;a;this;the
SECTIONOBJ: 3;cup;1;-1;58;RIMG18;69;222;6;-106;11; ;-1;0;8;SIMG12;MID;CD;a;this;the
SECTIONOBJ: 4;rubber band;0;0;0;0;0;0;0;0;0; ;-1;0;8;SIMG13;MID;WD;a;this;the
SECTIONOBJ: 5;20 gold\pieces;0;0;0;0;0;0;0;0;0; ;-1;0;8;SIMG14;MID;WD; ;these;the


/* Action 1 - Give
/* ===============
/*
/*
/* Action 3 - Use
/* ==============
/*
ACTION: 3;IFOBJ SOBJ3;MOBJ;SAY I'm not sure I want\more of this stuff!;W 40;SAY It's almost lethal, you know.;EXIT
ACTION: 3;IFOBJ SOBJ2;SAY I don't see anything to spend\it on right here.;EXIT
ACTION: 3;IFOBJ 21;IFNOTCARR;MOBJ;HANDLE;PICK;SOUND 1;W 25;HANDLE -1
ACTION: 3;IFOBJ 21;IFOBJ2 SOBJ1;IFOF 1=0;SAY I say - using the feather to\clean the flute worked splendidly!;OBJ1 SOBJ1;SETOF 1=1;EXIT
ACTION: 3;IFOBJ 21;IFOBJ2 SOBJ1;IFOF 1=1;SAY It's already as clean as can be.;EXIT
ACTION: 3;IFOBJ 24;IFOBJ2 SOBJ3;SAY What? You mean you want me to\stir the mead with the knife??\I'm shaken!;EXIT
ACTION: 3;IFOBJ 24;IFOBJ2 SOBJ1;SAY It is not small enough\- I can't get the blade\into the opening.;EXIT
ACTION: 3;IFOBJ 27;VERB 4;REDO
ACTION: 3;IFOBJ 30;IFOBJ2 SOBJ1;CIMG SIMG5;W 50;SAY How convenient - the strange text on the\flute is explained in this book!;SAY Hmm...;SAY Apparently, the text on the flute reads:\"Play all notes twice to change the weather.";CIMG 11
ACTION: 3;IFOBJ 30;IFOBJ2 SOBJ1;OBJ1 SOBJ1;SAY Well, it seems it's either\a genuine, magical flute...;SAY ...or tourist junk.;SETOF 2=1;EXIT
ACTION: 3;IFOBJ SOBJ1;IFOF 1=0;OMOVE 0, , ,1, , ,A 0,(SIMG1,12)(SIMG2,24)(SIMG1,24)(SIMG2,12);W 150;CIMG 11;SAY I can't get a tone.\It seems something inside\is clogging the flute.;SAY Perhaps I'd better get\something to clean it out with.;EXIT
ACTION: 3;IFOBJ SOBJ1;IFOF 1=1;IFOF 2=0;CUTSCENE 10,S;EXIT
ACTION: 3;IFOBJ SOBJ1;IFOF 1=1;IFOF 2=1;IFOF 3=0;IFROOM 1;SAY I think I'd better be outside\beneath the open sky to try that.;EXIT
ACTION: 3;IFOBJ SOBJ1;IFOF 1=1;IFOF 2=1;IFOF 3=0;CUTSCENE 11,F;SETRF 2,6=1;SETRF 3,6=1;SETRF 4,6=1
/* Since conditions are not allowed in cutscene files, the weather change
/* sequences for rooms 2 and 3 stay here: However, the CUTSCENE 11,F command
/* above leaves the cutscene indicator on screen. It is removed again
/* after CUTSCENE 12,S which follows after the fade commands below!
ACTION: 3;IFOBJ SOBJ1;IFOF 1=1;IFOF 2=1;IFOF 3=0;IFROOM 2;FADE 23,30,$678,WAIT
ACTION: 3;IFOBJ SOBJ1;IFOF 1=1;IFOF 2=1;IFOF 3=0;IFROOM 3;FADE 29,10,$678,STACK;FADE 31,10,$555,STACK;FADE 24,10,$555,STACK;FADE 25,10,$666,STACK;FADE 26,10,$666,STACK;FADE 27,10,$777,STACK;FADE 28,30,$777,WAIT
ACTION: 3;IFOBJ SOBJ1;IFOF 1=1;IFOF 2=1;IFOF 3=0;CUTSCENE 12,S;SETOF 3=1;EXIT
ACTION: 3;IFOBJ SOBJ1;IFOF 3=1;SAY I think it has served its purpose.\I am not that good a musician.;EXIT
/*
/* Action 4 - Open
/* ===============
/*
ACTION: 4;IFOBJ 27;IFOF 1=0;SAY There's a book inside!;ICON 43;NAME guide book;SOUND 1;SAY I'll keep the wrapping paper\and the strange, elastic string\that was around the parcel, too.;GET 28,N;GET SOBJ4,U;SOUND 1;SETOF 1=1;EXIT
ACTION: 4;IFOBJ 27;CIMG 11;OMOVE 0, , ,1, ,WAIT,A 0,(SIMG3,250)(SIMG4,50)
ACTION: 4;IFOBJ 27;IFOF 2=0;THINK This book seems to contain a\lot of useful tips.;THINK Let's see...;THINK Ah, here:;SETOF 2=1
ACTION: 4;IFOBJ 27;THINK "CONSTANTINOPLE. If you are stranded in\Constantinople, get out as quickly as\possible. It is a boring place."
ACTION: 4;IFOBJ 27;THINK "Sea transportation in Constantinople is scarce\and generally unreliable. Your best bet\is to find a tough, reliable camel of some\sort, and then travel East towards Gurgan,\where better transportation may be found."
ACTION: 4;IFOBJ 27;IFRF 2,4=0;SETRF 2,4=1
ACTION: 4;IFOBJ 27;CIMG 11;EXIT
/*
/* Action 8 - Look at
/* ==================
/*
ACTION: 8;IFOBJ SOBJ1;SAY There are some carvings that look like\text in a foreign language along the\side of the flute.;SETRF 4,3=1;EXIT
ACTION: 8;IFOBJ SOBJ2;SAY I can't buy much with this,\that's for sure.;EXIT
ACTION: 8;IFOBJ SOBJ3;SAY It contains mead of an\appallingly bad quality!;EXIT
ACTION: 8;IFOBJ SOBJ4;SAY It is a thin, elastic string\made of some material that\is unknown to me.;EXIT
ACTION: 8;IFOBJ SOBJ5;SAY This should make it\possible to get out\of here in some\way or other!;EXIT
/*
/* END of section file
