/* Mini-demo section
/* Section script for GRAAL
/*

/* ========================================================================
/*
/*                                Dialogues
/*
/* ========================================================================

/* This is the dialogue called whenever Olaf tries to talk to Thorfinn.
/*
LINE: 3;1;I use the same dialogue in both rooms.; ; 
LACT: 3;1;RESP S,2,You don't say!;SAY I do, I do!;RESP S,2,OK then.;DSET 3;EXIT
LINE: 3;2;Bye now!; ; 
LACT: 3;2;RESP S,2,Bye bye!;EDLG;EXIT

/* ========================================================================
/*
/*                                 Actions
/*
/* ========================================================================

/*
/* -2 - Resume actions
/*
/* Before jumping back to the index, the current status of the mini-demo
/* is always saved - see ACTION: 5;... below.
/*
/* Because modules do not resume playing automatically when loading a
/* saved position, you have to put the restart of the music in a -2
/* statement like this!
/*
ACTION: -2;TRACK Herald2_1.mod,125,FILTER

/*
/* 1 - Give
/* --------
ACTION: 1;IFCHAR 2;IFOBJ2 21;SAY Why should I give something to myself?;EXIT
ACTION: 1;IFCHAR 1;IFOBJ2 20;SAY Why should I give something to myself?;EXIT
/*
/* Thorfinn gives something to Olaf
/*
ACTION: 1;IFCHAR 2;IFOBJ2 20;MOBJ 20;SAY Here you are!;HANDLE MID;SHOW 20, , ,14;W 25;SHOW 20, , ,33;W 25;HANDLE -1;REMOVE OBJ1,U,I1;SHOW 20, , ,14
/*
/* Olaf gives something to Thorfinn
/*
ACTION: 1;IFCHAR 1;IFOBJ2 21;MOBJ 21;SAY Here you are!;HANDLE MID;SHOW 21, , ,//44;W 25;SHOW 21, , ,//63;W 25;HANDLE -1;REMOVE OBJ1,U,I2;SHOW 21, , ,//44
/*
/* Now check if it's the "magic" item F. When given to the other character, 
/* This character will follow the controlled character until control is
/* switched next time!
/*
ACTION: 1;IFOBJ 27; ;IFOBJ2 20;FOLLOW 1,6,-2,10,2,75
ACTION: =;IFOBJ2 21;FOLLOW 2,6,-2,10,2,75
ACTION: 1;IFOBJ2 20|21;EXIT
/*
/* Not much other use for GIVE in this demo
/*
ACTION: 1;SAY That sounds rather stupid.;EXIT

/*
/* 2 - Pick up
/* -----------
/*
ACTION: 2;IFPICK;MOBJ;HANDLE LOW;W 25;PICK;SOUND 1;HANDLE -1;EXIT

/*
/* 5 - Index
/*
ACTION: 5;MARK 1;INVENTORY 3,U;GOTO 1,1

/*
/* 6 - Quit
/*
ACTION: 6;PROMPT 2,Do you really wish to quit? (y/n)
ACTION: 6;IFVAR 2=y|Y|yes|Yes|YES;QUIT
ACTION: 6;EXIT

/*
/* 7 - Switch to Olaf
/* ------------------
/*
ACTION: 7;INVENTORY 1,U;SWITCH 1;EXIT

/*
/* 8 - Switch to Thorfinn
/* ----------------------
/*
ACTION: 8;INVENTORY 2,U;SWITCH 2;EXIT

/*
/* ? - Kill all other commands below!
/*
/*
ACTION: ?;SAY Hmm... I wouldn't recommend it.;EXIT
