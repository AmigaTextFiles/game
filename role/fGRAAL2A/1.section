/* Constantinople
/*
/* =========================================================================
/*
/*                          Section 1 - Constantinople
/*
/* =========================================================================
/*


CLPART: Olaf_Extras.iff

/* SECTIONBOBS: no_of_imgs;startrbob;grabx;graby;width;height;xoffset;hotsp
/*
/*   See the BOBS: statement in the graal.main file. The difference is
/*   that SECTIONBOBS: define "section" images instead of "global" images
/*   - that is, the image numbers given here must be prefixed with "SBOB"
/*   when referred to in other statements and commands.
/*
/* 1-2 playing flute
SECTIONBOBS: 2;1;34;2;31;47;32;0
/* 3 reading guide
SECTIONBOBS: 2;3;2;50;31;47;32;0
/* 5 reading dictionary
SECTIONBOBS: 1;5;98;2;31;47;0;0

CLPART: Olaf_Symbols.iff
/* SOBJ10-14 flute, copper coin, cup, rubber band, gold
SECTIONBOBS: 5;10;0;83;51;21;50;0


/* SECTIONOBJ: sectionobjno,object_definition....
/*             (see graal.main documentation)
/*
/*   The object number specified here must later be prefixed by "ROBJ" when
/*   referred to in other statements and commands.
/*
SECTIONOBJ: 1;flute\exotique;0;0;0;0;0;0;0;0;0; ;-1;0;8;SBOB10;MID;WD;une;cette;la
SECTIONOBJ: 2;pièce;0;0;0;99;0;0;0;0;0; ;-1;0;8;SBOB11;MID;TD;une;cette;la
SECTIONOBJ: 3;tasse;1;-1;58;RBOB18;69;222;6;-106;11; ;-1;0;8;SBOB12;MID;CD;une;cette;la
SECTIONOBJ: 4;bande de caoutchou;0;0;0;0;0;0;0;0;0; ;-1;0;8;SBOB13;MID;WD;une;cette;ca
SECTIONOBJ: 5;20 pièces\d'or;0;0;0;0;0;0;0;0;0; ;-1;0;8;SBOB14;MID;WD; ;ces;ca


/* Action 1 - Give
/* ===============
/*
/*
/* Action 3 - Use
/* ==============
/*
ACTION: 3;IFOBJ SOBJ3;MOBJ;SAY Je ne suis pas sur\d'en vouloir plus!;W 40;SAY C'est assez mortel, vous savez.;EXIT
ACTION: 3;IFOBJ SOBJ2;SAY Je ne vois rien\a y faire ici.;EXIT
ACTION: 3;IFOBJ 21;IFNOTCARR;MOBJ;HANDLE;PICK;SOUND 1;W 25;HANDLE -1
ACTION: 3;IFOBJ 21;IFOBJ2 SOBJ1;IFOF 1=0;SAY Utiliser la plume pour nettoyer\la flute a marché trés bien!;OBJ1 SOBJ1;SETOF 1=1;EXIT
ACTION: 3;IFOBJ 21;IFOBJ2 SOBJ1;IFOF 1=1;SAY Elle est suffisament propre.;EXIT
ACTION: 3;IFOBJ 24;IFOBJ2 SOBJ3;SAY Hein? vous voulez que\je remue l'hydromel avec le couteau??\I'm shaken!;EXIT
ACTION: 3;IFOBJ 24;IFOBJ2 SOBJ1;SAY C'est trop petit\- je ne peut entrer la lame\dans l'ouverture.;EXIT
ACTION: 3;IFOBJ 27;VERB 4;REDO
ACTION: 3;IFOBJ 30;IFOBJ2 SOBJ1;CBOB SBOB5;W 50;SAY C'est genial - l'étrange texte sur la\flute est expliqué dans ce livre!;SAY Hmm...;SAY Apparement il y a ecrit sur la flute:\"Jouez chaque note deux fois pour changer le temps.";CBOB 11
ACTION: 3;IFOBJ 30;IFOBJ2 SOBJ1;OBJ1 SOBJ1;SAY Bien, il semble que ce soit soit\une super flute magique...;SAY ...ou de la connerie à touriste.;SETOF 2=1;EXIT
ACTION: 3;IFOBJ SOBJ1;IFOF 1=0;OMOVE 0, , ,1, , ,A 0,(SBOB1,12)(SBOB2,24)(SBOB1,24)(SBOB2,12);W 150;CBOB 11;SAY Je en peux sortir un son.\Il semble y avoir quelque chose dedans\qui bouche la flute.;SAY Je devrais peut etre\la nettoyer
ACTION: 3;IFOBJ SOBJ1;IFOF 1=1;IFOF 2=0;CUTSCENE 10,S;EXIT
ACTION: 3;IFOBJ SOBJ1;IFOF 1=1;IFOF 2=1;IFOF 3=0;IFROOM 1;SAY Je pense que je devrais plutot etre dehors\sous le soleil pour essayer ca.;EXIT
ACTION: 3;IFOBJ SOBJ1;IFOF 1=1;IFOF 2=1;IFOF 3=0;CUTSCENE 11,F;SETRF 2,6=1;SETRF 3,6=1;SETRF 4,6=1
/* Since conditions are not allowed in cutscene files, the weather change
/* sequences for rooms 2 and 3 stay here: However, the CUTSCENE 11,F command
/* above leaves the cutscene indicator on screen. It is removed again
/* after CUTSCENE 12,S which follows after the fade commands below!
ACTION: 3;IFOBJ SOBJ1;IFOF 1=1;IFOF 2=1;IFOF 3=0;IFROOM 2;FADE 23,30,$678,WAIT
ACTION: 3;IFOBJ SOBJ1;IFOF 1=1;IFOF 2=1;IFOF 3=0;IFROOM 3;FADE 29,10,$678,STACK;FADE 31,10,$555,STACK;FADE 24,10,$555,STACK;FADE 25,10,$666,STACK;FADE 26,10,$666,STACK;FADE 27,10,$777,STACK;FADE 28,30,$777,WAIT
ACTION: 3;IFOBJ SOBJ1;IFOF 1=1;IFOF 2=1;IFOF 3=0;CUTSCENE 12,S;SETOF 3=1;EXIT
ACTION: 3;IFOBJ SOBJ1;IFOF 3=1;SAY Je pense que c'est bon.\je ne suis pas un bon musicien.;EXIT
/*
/* Action 4 - Open
/* ===============
/*
ACTION: 4;IFOBJ 27;IFOF 1=0;SAY Il y a un livre dedans!;ICON 43;NAME guide;SOUND 1;SAY Je vais garder le papier\et l'etrange élastique\qui étaient autour du paquet, aussi.;GET 28,N;GET SOBJ4,U;SOUND 1;SETOF 1=1;EXIT
ACTION: 4;IFOBJ 27;CBOB 11;OMOVE 0, , ,1, ,WAIT,A 0,(SBOB3,250)(SBOB4,50)
ACTION: 4;IFOBJ 27;IFOF 2=0;THINK Ce livre semble contenir\plein de trucs utiles.;THINK voyons ça...;THINK Ah, ici:;SETOF 2=1
ACTION: 4;IFOBJ 27;THINK "CONSTANTINOPLE. Si vous etes coincés à\Constantinople, partez aussi vite que\possible. On s'y ennuie beaucoup."
ACTION: 4;IFOBJ 27;THINK "Peu de transports maritimes à Constantinople et generalement peu fiables. Il vous faut trouver un bon, et costaud chameau pour ensuite, aller vers l'Est jusqu'à Gurgan, où l'on trouve de meilleurs transports."
ACTION: 4;IFOBJ 27;IFRF 2,4=0;SETRF 2,4=1
ACTION: 4;IFOBJ 27;CBOB 11;EXIT
/*
/* Action 8 - Look at
/* ==================
/*
ACTION: 8;IFOBJ SOBJ1;SAY Il y a des gravures ressemblant à un\texte étranger sur le\coté de la flute.;SETRF 4,3=1;EXIT
ACTION: 8;IFOBJ SOBJ2;SAY Je ne peux pas m'acheter beaucoup de chose\avec ça, ca c'est sur!.;EXIT
ACTION: 8;IFOBJ SOBJ3;SAY Ca contient de l'hydromel\d'apparement mauvaise qualité!!;EXIT
ACTION: 8;IFOBJ SOBJ4;SAY C'est un fin élastique\fait dans un materiau\qui m'est inconnu.;EXIT
ACTION: 8;IFOBJ SOBJ5;SAY Il devrait etre\possible de sortir\d'ici d'une facon\ou d'une autre!;EXIT
/*
/* END of section file
