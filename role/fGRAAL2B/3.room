/* Port de Constantinople
/*
/*                                   3.room
/*
/*                          Le port dans Byzance
/*
/* RF usage: 1=seagull status, 2=number of times spoken to captain
/*           3=captain has received gift, 6=weather, 4=shop sign status
/*           5=dictionary/captain combo. 7=Which shop sign is actually out?

SECTION: 1

UPDATE: 6;2

BACKDROP: 3BG.IFF

START_POS: 1;//13;455;104;R;1
START_POS: 2;13;70;76;L;1

FLOOR: 1;81;76;120;120;1-1/2-2/3-2/4-4
FLOOR: 2;0;90;480;120;1-1/2-2/3-3/4-4
FLOOR: 3;180;76;480;120;1-2/2-2/3-3/4-2
FLOOR: 4;0;82;120;120;1-1/2-2/3-2/4-4

EXIT: 1;450;50;480;120;472;99;la\ville
EXIT: 2;60;37;68;74;64;76;remise\délabrée

CLPART: 3FG.IFF

/* 1 is ship
ROOMBOBS: 1;1;0;0;193;73;0;0
/* 2 is a fishing net
ROOMBOBS: 1;2;194;0;38;24;0;0
/* 3 is some reflections in the water
/* ROOMBOBS: 3;4;75;168;85;0
/* 4-8 is captain walking (4 is standing sideways)
ROOMBOBS: 5;4;5;93;15;36;16;0
/* 9-10 is captain bending down doing something
ROOMBOBS: 2;9;4;132;30;22;30;0
/* 11-16 is captain loosing hat
ROOMBOBS: 6;11;64;132;28;22;30;0
/* 17 is gangplank
ROOMBOBS: 1;17;196;30;31;16;0;0
/* 18 is beam and sail
ROOMBOBS: 1;18;238;0;68;62;0;72
/* 19-21 are arabic "sentences"
ROOMBOBS: 1;19;5;159;56;20;0;0
ROOMBOBS: 1;20;60;157;61;21;0;0
ROOMBOBS: 1;21;125;157;51;21;0;0
/* 22-23 is captain talking
ROOMBOBS: 2;22;85;93;15;36;16;0
/* 24 is captain facing forward.
ROOMBOBS: 1;24;116;93;16;36;0;0
/* 25 is horizon
ROOMBOBS: 1;25;3;183;171;9;0;-30
/* 26-27 are shop signs
ROOMBOBS: 1;26;197;54;36;15;0;0
ROOMBOBS: 1;27;197;69;23;15;0;0
/* 28 Captain stretching out hand.
ROOMBOBS: 1;28;132;93;16;36;0;0
/* 29 Feather
ROOMBOBS: 1;29;3;193;9;7;0;0
/* 30 Captain securing hat
ROOMBOBS: 1;30;148;93;16;36;0;0
/* 31-33 Olaf using net
ROOMBOBS: 3;31;15;194;22;38;22;0
/* 34-35 Seagull sitting down
ROOMBOBS: 2;34;84;196;13;9;15;0
/* 36-38; Seagull in flight
ROOMBOBS: 3;36;115;196;18;8;20;0
/* 39-42 Olaf waving knife
ROOMBOBS: 4;39;173;192;33;41;31;0
/* 43-44 Captain hitting
ROOMBOBS: 2;43;165;93;15;36;16;0
/* 45-46 Sales signs
ROOMBOBS: 2;45;237;67;19;28;20;0
/* 52 Olaf talks!
ROOMBOBS: 1;52;184;159;77;20;0;0
/* 53 Cargo 1
ROOMBOBS: 1;53;257;112;32;25;0;0
/* 54 Cargo 2
ROOMBOBS: 1;54;88;210;81;27;0;0


CLPART: Olaf_Extras.IFF

/* 47-51 Olaf getting hit
ROOMBOBS: 5;47;130;2;31;47;32;0


ROOMOBJ: 1;bateau;3;-1;59;RBOB1;327;71;80;5;//14; ;0;0;8;0;MID;WD;un;ce;ca
ROOMOBJ: 2;horizon;3;-1;56;RBOB25;162;2;-45;40;12; ;0;0;8;0;HIGH;-;un;ce;ca
ROOMOBJ: 3;panneau;3;-1;55;RBOB26;25;32;0;30;12; ;0;0;8;0;HIGH;WD;un;ce;ca
ROOMOBJ: 4;panneau;3;-1;54;RBOB27;84;44;00;30;12; ;0;0;8;0;HIGH;WD;un;ce;ca
ROOMOBJ: 5;mouette;3;VIS;52;A 0,(RBOB35,100)(RBOB34,50)(RBOB35,200)(RBOB34,25);152;61;0;40;12; ;0;8;8;0;MID;AV;une;cette;elle
ROOMOBJ: 6;panneau\ventes;3;0;51;RBOB45;84;74;0;15;12; ;0;0;8;0;MID;WD;un;ce;ca


STATIC: 40;RBOB17;309;73
STATIC: 41;RBOB18;316;72
STATIC: 37;RBOB53;285;121
STATIC: 38;RBOB54;196;122



/* This room uses the command area
/*
DACT: SETRF 40,2=0
/*
/* Display proper sales sign. See also DACTs in 2.room!
DACT: IFRF 7=1;SHOW ROBJ6,84,74,DEF
DACT: IFRF 7=2;SHOW ROBJ6,84,74,RBOB46
/* Add Rajah question to bartender dialogue
DACT: OBJ1 5;SETOF 2=1;OBJ1
/* Captain's hat problem status
DACT: IFRF 3=1;SHOW 15,244,58,PTRN 2
/* Show seagull on pollard
DACT: IFRF 1=0;SHOW ROBJ5,152,61,DEF
/* Show seagull on roof, make more floor space available
DACT: IFRF 1=1;SHOW ROBJ5,50,27,DEF;FLOOR 1,61,76,480,120,1-1/2-2;FLOOR 2,0,82,480,120,1-1/2-2;NFLOOR 2
/* If Byzantium puzzle is solved, the ship and captain is gone!
DACT: IFRF 2,4=4;HIDE ROBJ1;BOBOFF 40;BOBOFF 41;HIDE 15
/* Check the weather
DACT: IFRF 6=1;COLOUR DLY,29,$678;COLOUR DLY,31,$555;COLOUR DLY,24,$555;COLOUR DLY,25,$666;COLOUR DLY,26,$666;COLOUR DLY,27,$777;COLOUR DLY,28,$777
/* MUSIC!
DACT: TRACK seagulls.track,125,FILTER
/* Restore command area if we came back from shop
DACT: IFSPOS 2;COMAREA ON
/*
DACT: LIGHTS ON

/* Captain dialogue
LINE: 3;1;Oh, oubliez ça! Attendez que je trouve un dictionnaire, d'accord?;Il va falloir essayer autre chose.; 
LACT: 3;1;BOBON 42,330,44,RBOB21;RESP S,3,          ;BOBOFF 42;CUTSCENE 3,H;SETOF 1=1
LACT: 3;1;SETRF 4,3=1;EDLG
/*
LINE: 3;2;Pareil pour toi!; ; 
LACT: 3;2;BOBON 42,330,44,RBOB20;RESP S,3,          ;SHOW 15,330,84,RBOB4;BOBOFF 42
LACT: 3;2;ADDOF 2;IFOF 2=3;DSET 3,+1,N2;EXIT
LACT: 3;2;DSET 3,N2
/*
LINE: 3;3;Joli bateau que vous avez là.; ; 
LACT: 3;3;BOBON 42,330,44,RBOB21;RESP S,3,          ;SHOW 15,330,84,RBOB4;BOBOFF 42
LACT: 3;3;ADDOF 2;IFOF 2=3;DSET 3,+1,N3;EXIT
LACT: 3;3;DSET 3,N3
/*
LINE: 3;4;Ce bateau est-il à louer?; ; 
LACT: 3;4;BOBON 42,330,44,RBOB19;RESP S,3,          ;SHOW 15,330,84,RBOB4;BOBOFF 42
LACT: 3;4;ADDOF 2;IFOF 2=3;DSET 3,+1,N4;EXIT
LACT: 3;4;DSET 3,N4
/*
LINE: 3;5;Avez-vous un bon truc contre les mouettes?; ; 
LACT: 3;5;BOBON 42,330,44,RBOB19;RESP S,3,          ;SHOW 15,330,84,RBOB4;BOBOFF 42
LACT: 3;5;ADDOF 2;IFOF 2=3;DSET 3,+1,N5;EXIT
LACT: 3;5;DSET 3,N5


/*
/* Action 0 - Exit
/* ===============
/*
ACTION: 0;IFOBJ 1;MEXIT;GOTO 2,2
ACTION: 0;IFOBJ 2;MEXIT;COMAREA OFF;GOTO 4,1
/*
/* Action 1 - Give
/* ===============
/*
ACTION: 1;IFOBJ 14;IFOBJ2 15;SAY Je suis presque sur que\ce n'est pas un bateau de peche;EXIT
ACTION: 1;IFOBJ2 ROBJ5;SAY Si je voulais quelque chose à moi, il y a longtemps que je l'aurais;EXIT
ACTION: 1;IFOBJ SOBJ4;IFOBJ2 15;SAY Ca ne peut pas marcher!;SAY Ho, trés bien, allons y...;CUTSCENE 2,F;CUTSCENE 7,F;CUTSCENE 8,S;SETRF 3=1;EXIT
ACTION: 1;IFOBJ2 15;IFRF 3=1;SAY Ca sufit maintenant.;SAY Je ne fais pas dans la charité.;EXIT
/* Another try to get feather from beneath seagull here. Handle like USE.
ACTION: 1;IFOBJ 21;IFRF 1=0;VERB 3;REDO
/* Trying out a few good ideas for gifts to the captain below.
ACTION: 1;IFOBJ 21;IFOBJ2 15;SAY Oui, peut etre qu'il pourra mettre la plume dans son chapeau!
ACTION: 1;IFOBJ 30;IFOBJ2 15;SAY OK. avec ces problemes linguistiques, ca vaut le coup!
ACTION: 1;IFOBJ 21|30;IFOBJ2 15;CUTSCENE 2,F;CUTSCENE 9,F;CUTSCENE 3,S;CBOB 11;SAY Ca ne m'aide pas vraiment...;EXIT
/*
/* Action 2- Pick up
/* =================
/*
ACTION: 2;IFOBJ 14;IFOF 1=0;MOBJ;HANDLE;W 25;HANDLE -1;SAY Je ne peux pas - Le filet est accroché à ces crochets!;EXIT
ACTION: 2;IFOBJ 21;IFRF 1=0;SAY Pas tant que cette mouette est assise là!;EXIT
ACTION: 2;IFOBJ ROBJ5;SAY Quoi, comme ca? vous n'avez pas vu ce bec??;EXIT
/*
/* Action 3 - Use
/* ==============
/*
ACTION: 3;IFOBJ ROBJ1;IFOF 15,1=0;SAY Je pense que quelqu'un serait trés énervé si je faisait ça! - surtout sans demander!;EXIT
ACTION: 3;IFOBJ ROBJ1;IFOF 15,1=1;SAY Je ne crois pas avoir persuadé le capitaine. Et vous?;EXIT
ACTION: 3;IFOBJ 14;IFOBJ2 15;SAY Peut-etre si j'étais plus fort... et un peu moins désperé.;EXIT
ACTION: 3;IFOBJ 14;IFOBJ2 ROBJ5;IFRF 1=0;IFOF 1=0;VERB 2;OBJ2 0;REDO
ACTION: 3;IFOBJ 14;IFOBJ2 ROBJ5;IFRF 1=0;IFOF 1=1;IFNOTCARR;MOBJ;HANDLE;PICK;SOUND 1;HANDLE -1
ACTION: 3;IFOBJ 14;IFOBJ2 ROBJ5;IFRF 1=0;IFOF 1=1;CUTSCENE 4,S;EXIT
ACTION: 3;IFOBJ 14;IFOBJ2 ROBJ5;IFRF 1=1;SAY Il est hors d'atteinte.;EXIT
ACTION: 3;IFOBJ 21;IFRF 1=0;SAY Je dois mettre mes mains dessus d'abord.;EXIT
ACTION: 3;IFOBJ 24;IFRF 1=0;IFOBJ2 ROBJ5;CUTSCENE 6,S;EXIT
ACTION: 3;IFOBJ2 ROBJ5;SAY Je ne jetterai rien là dessus qui pourrait me servir plus tard. L'eau est trop proche.;EXIT
ACTION: 3;IFOBJ ROBJ5;SAY Je ne peux pas mettre mes mains dessus. Et si je le pouvais, que suggerez vous? du ragout de mouette?;EXIT
ACTION: 3;IFOBJ 24;IFOBJ2 14;IFOF2 1=0;MOBJ 14;HANDLE;W 30;HANDLE -1;SETOF 14,1=1;SAY Voilà - je crois que c'est fichu, maintenant.;EXIT
ACTION: 3;IFOBJ 24;IFOBJ2 14;IFOF2 1=0;SAY Je ne pense pas que le #OBJ1 soit le bon outil pour ça.;EXIT
ACTION: 3;IFOBJ 30;IFOBJ2 15;VERB 5;OBJ1 15;REDO
/*
/* Action 4 - Open
/* ===============
/*
/*
/* Action 5 - Talk to
/* ==================
/*
ACTION: 5;IFOBJ 15;IFRF 3=1;SAY Non, j'en ai eu assez!;EXIT
ACTION: 5;IFOBJ 15;IFRF 5=1;SAY NON, plus maintenant!;EXIT
ACTION: 5;IFOBJ 15;IFCARR 30;CUTSCENE 13,S;SETRF 5=1;EXIT
ACTION: 5;IFOBJ 15;CUTSCENE 2,H;BOBON 42,330,44,RBOB19;RESP S,3,            ;SHOW 15,330,84,RBOB4;BOBOFF 42
ACTION: 5;IFOBJ 15;IFOF 2=0;DSET 3,+2,+3,+4,+5;EXIT
ACTION: 5;IFOBJ 15;DSET 3,+1;EXIT
ACTION: 5;IFOBJ ROBJ5;SAY Je ne pense pas que l'on soit en bon terme!;EXIT
/*
/* Action 6 - Push
/* ===============
/*
ACTION: 6;IFOBJ ROBJ1;SAY Il est trés gardé.;EXIT
/*
/* Action 7 - Close
/* ================
/*
/*
/* Action 8 - Look at
/* ==================
/*
ACTION: 8;MOBJ
ACTION: 8;IFOBJ ROBJ1;SAY C'est un bateau qui me serait trés utile.;W 50;SAY Mais ce n'est pas le MIEN.;EXIT
ACTION: 8;IFOBJ ROBJ2;SAY Le Nord... là où je vis.;SAY Mais c'est à un bout de chemin à travers les océans...;SAY ...et encore plus loin pour la Suede!;EXIT
ACTION: 8;IFOBJ ROBJ3;SAY Ca dit: "Chez Ali - Appats pour poisson et magasin hors-taxe".;EXIT
ACTION: 8;IFOBJ ROBJ4;SAY Ca dit: "Tout pour le voyageur intelligent"!;EXIT
ACTION: 8;IFOBJ ROBJ6;SAY Hmm... On dirait qu'il se passe des trucs speciaux dans ce magasin!;EXIT
ACTION: 8;IFOBJ 15;SAY On dirait qu'il jette un oeil sur tout.;MOBJ;SAY Et il a un joli chapeau.;EXIT
ACTION: 8;IFOBJ 21;IFRF 1=0;SAY Je ne peux pas examiner ca tant que ce monstre de mouette est assis là!;EXIT
ACTION: 8;IFOBJ ROBJ5;IFRF 1=0;SAY Je ne m'approcherait pas de cette mouette sans une sorte d'arme...;SAY ...il me semble vraiment trés vicieux!;EXIT
ACTION: 8;IFOBJ ROBJ5;IFRF 1=1;SAY Il semble bien...de loin.;EXIT
/*
/* Action 9 - Pull
/* ===============
/*
ACTION: 9;IFOBJ 14;IFOF 1=0;MOBJ 14;HANDLE;W 30;HANDLE -1;SAY Ca ne change rien. Il est toujours accroché au crochets.;EXIT
ACTION: 9;IFOBJ 14;VERB 2;REDO
/* End of room file.
