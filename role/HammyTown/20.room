/*
/* Room script for GRAAL
/*

/* UPDATE: scroll_frames[;normal_frames]
/*
UPDATE: 6;1

/* SECTION: section_no
/*
SECTION: 1

/* BG_IFF: file
/*
BG_IFF: 20BG.IFF

/* START_POS: startpos_no;image;x;y;L|M|R;floor_no
/*
START_POS: 1;14;10;78;L;1

/* FLOOR: floor_no;x1;y1;x2;y2;floormaps
/*
FLOOR: 1;0;59;285;119;1-1

/* PATH: path_no;floor1;floor2;px1;py1;px2;py2;{more_points} (This statement is optional)

/* EXIT: exit_no;x1;y1;x2;y2;x;y;description (This statement is optional)
EXIT: 1;0;53;21;119;8;96;Outside

/* CLPART: file (This statement is optional)
CLPART: 20FG.IFF

/* ROOMBOBS: number;start_bob;x1;y1;w;h;x-offset;hotspot (This statement is optional)

/* 1-3 is E.M.Cloud
ROOMBOBS: 3;1;8;5;32;37;33;0
/* 4 is the Az-Bulb
ROOMBOBS: 1;4;109;28;7;11;0; 
/* 5 is the Transmitter
ROOMBOBS: 1;5;117;16;7;25;0;0
/* 6 is the Teleporter
ROOMBOBS: 1;6;127;27;7;14;0;0
/* 7 is the Duplicator
ROOMBOBS: 1;7;137;13;29;28;0;0


/* STATIC: bob;image;x;y (This statement is optional)

/* ANIM: bob;image;anim_ch;anim;x;y (This statement is optional)

/* ROOMOBJ: obj;name;room;VIS|NVIS;bob;image;x;y;cx;cy;cimage;prep;PICK|NPICK;anim_ch;verb;icon;LOW|MID|HIGH;types;w1;w2;w3 (This statement is optional)
ROOMOBJ: 1;Az-Duplicator;20;VIS;54;RBOB7;111;58;-20;6;14; ;NPICK; ;8; ;MID; ;a;this;it

/* DACT: {cond|comm}
/*
DACT: LIGHTS ON;EXIT

/* LINE: dlg;line_no;sentence1;sentence2| ;{cond} (This statement is optional)

/* LACT: dlg;line_no;{cond|comm} (This statement is optional)

LINE: 5;1;I'll come back later.;I'll come back later then.; 
LACT: 5;1;RESP R,5,Okey dokey, there, see you!;EDLG;EXIT

LINE: 5;2;Who are you?;Who did you say you were again?; 
LACT: 5;2;RESP R,5,I am E.M.Cloud, the owner of Cloud Electronics.;NAME E.M.Cloud;DSET 5,+1

LINE: 5;3;So, what do you do here?; ; 
LACT: 5;3;RESP R,5,We sell goodies from Aztec Electronics' catalogue.
LACT: 5;3;RESP R,5,Nice goods they are too.;DSET 5,+4,N3

LINE: 5;4;What is so special about Aztec products?; ; 
LACT: 5;4;RESP R,5,Well, two things, really.;RESP R,5,First of all, they are great value for money!
LACT: 5;4;RESP R,5,And secondly, they do things that no other company has been able to do.;RESP R,5,They even manage to do things that are theoretically impossible!
LACT: 5;4;DSET 5,+5,+6,N4

LINE: 5;5;How come Aztec products are such great value for money?; ; 
LACT: 5;5;RESP R,5,Well, the main reason is that they cost absolutely nothing to produce!;RESP R,5,Aztec have made a machine which manufactures parts from thin air and assembles them.
LACT: 5;5;RESP R,5,It's really neat.;DSET 5,+7,N5

LINE: 5;6;What interesting things can Aztec products do?; ; 
LACT: 5;6;RESP R,5,Well, for a start, there is the Aztec physical copier!;RESP R,5,Can make copies of any object.
LACT: 5;6;RESP R,5,Aztec Teleporters are also useful, especially as they are so portable.
LACT: 5;6;DSET 5,+8,N6

LINE: 5;7;How much do you charge for Aztec products?; ; 
LACT: 5;7;RESP R,5,Well, Aztec products are good value for money even at the RRP, they are normally less than £20.
LACT: 5;7;RESP R,5,However, we are not out to make money.;RESP R,5,The RRP is, of course, completely voluntary.;RESP R,5,We just want to bring a bit of magic into people's lives!
LACT: 5;7;RESP R,5,So there is no charge for any of our goods.;SAY Oh, nice one!;RESP R,5,Simply help yourself to anything you want.;RESP R,5,We can just replace the item by using the Aztec Physical Copier over there.
LACT: 5;7;SETRF 2=1;DSET 5,+8,N7

LINE: 5;8;Tell me about these products.;Tell me more about your products; 
LACT: 5;8;RESP R,5,Well, which product do you want to know about?
LACT: 5;8;DSET 5,S1,+9,+10,+11,+12,+13

LINE: 5;9;Tell me about the Az-Light Bulb!;Tell me about the Az-Light Bulb again!; 
LACT: 5;9;RESP R,5,The Aztec light bulb is a great leap forward in lighting technology.;RESP R,5,It acts like a normal light bulb, but it never breaks down!
LACT: 5;9;RESP R,5,Also, it consumes ZERO current!;SAY Hang on, zero current? What makes it light up then?;RESP R,5,Ah, it uses the energy of the surrounding air to light the lamp.;RESP R,5,Only Aztec makes this possible!
LACT: 5;9;SAY That really is neat.;DSET 5,-9

LINE: 5;10;Tell me about the Az-Transmitter!;Tell me about the Az-Transmitter again!; 
LACT: 5;10;RESP R,5,Well, the Aztec transmitter is a neat device.;RESP R,5,It lets you transmit a signal on ANY frequency you like, AM, FM, CW, Sideband, whatever!
LACT: 5;10;RESP R,5,Also, it can transmit up to 1Kw! Off batteries!;SAY One killaaa-watt?!;RESP R,5,Yeah,it uses the same principle as the Aztec light bulb.
LACT: 5;10;DSET 5,-10

LINE: 5;11;Tell me about the Az-Teleporter!;Tell me about the Az-Teleporter again!; 
LACT: 5;11;RESP R,5,This innocent small looking device can actually teleport objects to another location!;RESP R,5,However, we haven't got full control of the destination yet, the designers are working on that.
LACT: 5;11;DSET 5,-11

LINE: 5;12;Tell me about the Az-Duplicator!;Tell me about the Az-Duplicator again!; 
LACT: 5;12;RESP R,5,This lovely device is what we use to duplicate our stock- The Aztec Physical Copier.;RESP R,5,This is still quite a big box, however...;RESP R,5,...unlike the Az-Teleporter, the object to be duplicated has to be inside the duplicator.
LACT: 5;12;RESP R,5,A portable version is likely in the future, but the current model is perfectly adequete for our needs.
LACT: 5;12;DSET 5,-12

LINE: 5;13;I think I've heard enough.;I've heard enough, thanks.; 
LACT: 5;13;RESP R,5,Oh, okay.
LACT: 5;13;DSET 5,R1


/* ACTION: verb_no;{cond|comm} (This statement is optional)

/* Action 0 - Exit
ACTION: 0;IFOBJ 1;MEXIT;GOTO 19,3

/* Action 1 - Give
ACTION: 1;IFOBJ2 5;RESP R,5,Oh, thanks, but I don't really need it.;EXIT

/* Action 2 - Pick Up
ACTION: 2;IFRF 2=0;IFOBJ 25|26|27;SAY I'd better not take anything without talking to him first!;VERB 5;OBJ1 5;REDO

/* Action 3 - Use

ACTION: 3;IFOBJ 25;IFOBJ2 21;IFRF 2=0;SAY I'd better not, it's not mine.;EXIT
ACTION: 3;IFOBJ 25;IFOBJ2 21;IFNOTCARR;SAY I would have to pick it up first!;EXIT

ACTION: 3;IFOBJ 28;IFOBJ2 ROBJ1;IFOF 1=0;MOBJ ROBJ1;SAY Right, here goes.;HANDLE ROBJ1;W 50;HANDLE -1;SAY Wow, it worked!;SAY I think I'll duplicate a few, just for fun.;HANDLE ROBJ1;W 50;HANDLE -1
ACTION: 3;IFOBJ 28;IFOBJ2 ROBJ1;IFOF 1=0;NAME Nine Golden Hamsters;ICON 62;SAY That sure is a clever machine;SETOF 1=1;EXIT
ACTION: 3;IFOBJ 28;IFOBJ2 ROBJ1;SAY I think I've got enough of them.;EXIT

ACTION: 3;IFOBJ 31;IFOBJ2 ROBJ1;IFOF 1=1;SAY I think I've got enough sunflower seeds for anything anyway!;EXIT
ACTION: 3;IFOBJ 31;IFOBJ2 ROBJ1;MOBJ ROBJ1;SAY Okay, let's try this!;HANDLE ROBJ1;W 50;HANDLE -1;SAY Wow, loads of sunflower seeds!
ACTION: 3;IFOBJ 31;IFOBJ2 ROBJ1;NAME Loadsa Sunflower Seeds;ICON 65;SETOF 1=1;EXIT

ACTION: 3;IFTYPE X;IFOBJ2 ROBJ1;SAY Why would I want to smash up such a nice piece of equipment?;EXIT
ACTION: 3;IFOBJ2 ROBJ1;SAY I don't really think that is worth duplicating.;EXIT
ACTION: 3;IFOBJ ROBJ1;SAY I need something to duplicate.;EXIT

/* Action 5 - Talk To

ACTION: 5;IFOBJ 5;IFOF 1=0;SETRF 1=1;MOBJ;SAY Hello.;RESP R,5,Okay dokey there!;DSET 5,+1,+2,+3;EXIT
ACTION: 5;IFOBJ 5;IFOF 1=1;MOBJ;SAY Hello again.;RESP R,5,Oh, alright?;DSET 5;EXIT

/* Action 8 - Look At

ACTION: 8;MOBJ
ACTION: 8;IFOBJ 5;SAY Hmm, funny looking bloke.;EXIT
ACTION: 8;IFOBJ ROBJ1;SAY It's the Aztec Physical Copier!;EXIT

/* No Comment! Well, it is, actually...
