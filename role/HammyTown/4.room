/*                     This is a room file for GRAAL
/*
/*                                 4.room
/* RF 1=0 first visit
/* RF 2=1 when crap tape has been played in Onkshewa!

/* UPDATE: scroll_frames;normal_frames
/*
UPDATE: 3;1

/* SECTION: section number
SECTION: 1

/* BG_IFF: filename
/*
BG_IFF: 4BG.IFF

/* START_POS: sposno;bobimage;x;y;camera;floor
/*
START_POS: 1;13;30;84;L;1

/*FLOOR: floorno;x1;y1;x2;y2;floormap/.../floormap
/*
FLOOR: 1;0;82;319;118;1-1

/* EXIT: no;x1;y1;x2;y2;epointx;epointy;description
/*
EXIT: 1;17;38;43;81;31;82;street

/* CLPART: filename
/*
CLPART: 3FG.IFF

/* ROOMBOBS: no_of_imgs;startrbob;grabx;graby;width;height;xoffset;hotsp
/*
/* 1-7 is the shop owner
ROOMBOBS: 7;1;95;8;17;43;17;0
/* 8 is the onkshewa system
ROOMBOBS: 1;8;170;61;59;29;0;0
/* 9 is the CD.
ROOMBOBS: 1;9;234;68;27;23;18;0

/* STATIC: bobno;LBOBimage;putx;puty

/* ANIM: bobno;bobimg;amalchannel;amalsequence;putx;puty

/* ROOMOBJ: roomobjno,object_definition.... (see graal.main documentation)
/*
ROOMOBJ: 1;Onkshewa System;4;VIS;51;RBOB8;202;57;-12;25;11; ;NPICK; ;8; ;HIGH;TD;a; ; 

/* DACT: condition;...;action;...
/*
/* Light up scene
DACT: TRACK NO;LIGHTS ON
DACT: IFRF 1=0;SAY This shop looks pretty dull.;SETRF 1=1;EXIT

/* LINE: dlgno;lineno;line;reuseline;conditions
/*
/*   Specifies a line of dialogue
/*
/* LACT: dlgno;lineno;action;...;action
/*
/* 2 is shopkeeper dialogue
/*
LINE: 2;1;I think I'll just look round.;Nothing. Forget it.; 
LACT: 2;1;IFOF 1=0;RESP R,2,Okay. Talk to me again if you see anything you like.;SETOF 1=1;EDLG;EXIT
LACT: 2;1;IFOF 1=1;RESP R,2,Okay. Happy bargain hunting!;EDLG;EXIT
/*
LINE: 2;2;There aren't many goods in this shop.; ;IFOF 10,1=0
LACT: 2;2;RESP R,2,Well, we've sold most of our stuff in the sale!;RESP R,2,All we have left is the Onkshewa system and one CD.;RESP R,2,Perhaps I should get some more stock soon.;DSET 2,N2,+1
/*
LINE: 2;3;Why are you blue?; ; 
LACT: 2;3;RESP R,2,Because I'm an alien!;RESP R,2,Honestly!;RESP R,2,Well, that or it's cold in here.
LACT: 2;3;DSET 2,N3,+1,+5
/*
LINE: 2;4;Let's talk about your merchandise.;Let's talk about some of your merchandise again.; 
LACT: 2;4;RESP R,2,Well, what are you interested in?;DSET 2,S1,+6,+7,+8,+9,+12

LINE: 2;5;Why does your head disappear sometimes?; ; 
LACT: 2;5;RESP R,2,No real reason.;RESP R,2,Except that it would be a shame to waste those animation frames!
LACT: 2;5;DSET 2,N5;EXIT
/*
LINE: 2;6;How much is that Onkshewa system?;How much was that Onkshewa system again?; 
LACT: 2;6;RESP R,2,That Onkshewa system...;RESP R,2,It's a bargain at £40!
LACT: 2;6;SETOF 9,2=1;DSET 2,S2,+10,+11

LINE: 2;7;That Onkshewa system is really crap!;Still not sold that crap Onkshewa system?;IFNOTCARR 13
LACT: 2;7;RESP R,2,No it's not crap!;RESP R,2,It plays music, doesn't it?;SAY Yes, but what about Sound Quality?
LACT: 2;7;RESP R,2,What does that matter as long as you can hear the music?;SAY Oh dear.;SAY It matters, alright?!;RESP R,2,Well, I like it, anyway.;DSET 1,-7

LINE: 2;8;What's that CD?;What was that CD again?;IFNOTCARR 10
LACT: 2;8;RESP R,2,It's "Turbo Guitar", a drowned-by-guitar track by Jools.;RESP R,2,£2 to you.;RESP R,2,If I can sell all of this stuff, I can close up and go home.;DSET 2,-8,+12

LINE: 2;9;Actually, I don't want to talk about merchandise.;Actually, I don't want to discuss merchandise any more.; 
LACT: 2;9;RESP R,2,Okay. Let's not.;DSET 2,R1

LINE: 2;10;I haven't got that much money on me at the moment.;I still haven't got that much money.; 
LACT: 2;10;RESP R,2,Sorry, you can't have it then.;DSET 2,R2

LINE: 2;11;Fourty quid? What a rip off!;Forty quid? What a rip off!; 
LACT: 2;11;SAY I wouldn't even give you forty pence for it!;RESP R,2,Keep your hair on!;DSET 2,R2

LINE: 2;12;I'd like to buy that CD, I've got the money!; ;IFCARR 13
LACT: 2;12;RESP R,2,Sure thing.;HANDLE;W 25;HANDLE -1;W 5;REMOVE 13,N,0
LACT: 2;12;RESP R,2,Okay, now it's yours.
LACT: 2;12;SETOF 10,1=1;DSET 2,-12


/* Room actions follow below ***
/*
/* ACTION: verbno;condition;...;command;...

/*
/* Action 0 - Exit
/* ===============
/*
ACTION: 0;IFOBJ 1;MEXIT;GOTO 2,3
/*
/* Action 1 - Give
/* ===============
/*

ACTION: 1;IFOBJ 13;IFOBJ2 2;SAY I'll talk to him.;OBJ1 2;VERB 5;REDO;EXIT
/*
/* Action 2- Pick up
/* =================
/*

ACTION: 2;IFOBJ 10;IFCARR 13;SAY I'd better talk to him about it first.;OBJ1 2;VERB 5;REDO;EXIT
ACTION: 2;IFOBJ ROBJ1;SAY I haven't got enough cash to buy it.;SAY Not that I'd want to buy it anyway.;EXIT
ACTION: 2;IFOBJ 10;IFOF 1=0;SAY I haven't got any money to buy it with!;EXIT
/*
/* Action 3 - Use
/* ==============
/*
ACTION: 3;IFOBJ ROBJ1;MOBJ;W 5;TRACK MUSIC.03.MOD,LOOP,NOFILTER;SAY Yuk! That sounds AWFUL! I'll turn it off.;W 30;TRACK NO;EXIT
ACTION: 3;IFOBJ 10;IFOBJ2 ROBJ1;SAY This dodgy Onkshewa system won't accept CDs;SAY Only muffley tapes.;EXIT

ACTION: 3;IFOBJ 29;IFOBJ2 ROBJ1;MOBJ ROBJ1;TRACK MUSIC.08.MOD,LOOP,FILTER;REMOVE 29,U,0;SAY Urgh!;RESP R,2,Can you turn that horrible racket off?;SAY Pardon?;RESP R,2,I said, can you turn that horrible racket off?;SAY Can't hear you. I'll come over.
ACTION: 3;IFOBJ 29;IFOBJ2 ROBJ1;MOBJ 2;SAY Now, what did you say?;RESP R,2,I said, can you turn that row off, please?;SAY I thought you liked really low quality stuff!;RESP R,2,Well, even I have my limits!;SAY Why can't you turn it off yourself?
ACTION: 3;IFOBJ 29;IFOBJ2 ROBJ1;RESP R,2,Because I'm glued to the floor with the cold. You'll have to turn it off yourself.;SAY What will you give me if I do?;RESP R,2,I'll give you these sunflower seeds.
ACTION: 3;IFOBJ 29;IFOBJ2 ROBJ1;SAY Okay then.;HANDLE ROBJ1;GET 31,U;W 25;HANDLE -1;MOBJ ROBJ1;SAY I can't stop it. You'll have to wait until the tape runs out!;RESP R,2,Oh no!;SETRF 2=1;EXIT

/*
/* Action 4 - Open
/* ===============
/*
ACTION: 4;IFOBJ 10;SAY Yeah, the CD is inside the case.;EXIT
ACTION: 4;IFOBJ ROBJ1;SAY I can't open the tape deck: A tape is stuck in it!;EXIT

/* Action 5 - Talk to
/* ==================
/*
ACTION: 5;IFOBJ 2;IFOF 1=0;MOBJ;SAY Hello.;RESP R,2,Can I help you?;DSET 2,+2,+3,+4;EXIT
ACTION: 5;IFOBJ 2;IFOF 1=1;MOBJ;SAY Hello again.;RESP R,2,Seen anything you fancy?;DSET 2;EXIT
/*
/* Action 6 - Push
/* ===============
/*
ACTION: 6;IFOBJ ROBJ1;VERB 3;REDO;EXIT
/*
/* Action 7 - Close
/* ================
/*
/* Action 8 - Look at
/* ==================
/*
ACTION: 8;MOBJ
ACTION: 8;IFOBJ ROBJ1;SAY It's an Onkshewa system.;SAY Yuk!;EXIT
ACTION: 8;IFOBJ 10;SAY It says "Turbo Guitar" on it.;EXIT
/*
/* Action 9 - Pull
/* ===============
/*
/* End of room file. Some comments must be here, otherwise  the last command
/* line is missed out!
