/*
/* Room script for GRAAL
/*
/* "Room" 17 - MOD bit!

/* UPDATE: scroll_frames[;normal_frames]
/*
UPDATE: 2;1

/* SECTION: section_no
/*
SECTION: 1

/* BG_IFF: file
/*
BG_IFF: 17BG.iff

/* START_POS: startpos_no;image;x;y;L|M|R;floor_no
/*
START_POS: 1;14;5;96;L;1
START_POS: 2;11;624;112;R;1

/* FLOOR: floor_no;x1;y1;x2;y2;floormaps
/*
FLOOR: 1;0;57;639;119;1-1

/* PATH: path_no;floor1;floor2;px1;py1;px2;py2;{more_points} (This statement is optional)

/* EXIT: exit_no;x1;y1;x2;y2;x;y;description (This statement is optional)
EXIT: 1;0;46;36;119;7;99;Outside Airfield
EXIT: 2;575;40;639;119;623;106;Next Bit

/* CLPART: file (This statement is optional)
CLPART: 17FG.iff

/* ROOMBOBS: number;start_bob;x1;y1;w;h;x-offset;hotspot (This statement is optional)
/* 1-3 is MOD bloke (1&2 normal, 1&3 Talking)
ROOMBOBS: 3;1;19;16;26;50;27; 
/* 4-7 is dodgy dog, (4-6 normal 4-7 "talking")
ROOMBOBS: 4;4;112;43;29;16;30; 
/* 8 is plant
ROOMBOBS: 1;8;238;48;10;11;0; 
/* 9 is extra MOD bloke frame stuck on with sellotape ;)
ROOMBOBS: 1;9;255;16;25;50;0; 

/* STATIC: bob;image;x;y (This statement is optional)

/* ANIM: bob;image;anim_ch;anim;x;y (This statement is optional)

/* ROOMOBJ: obj;name;room;VIS|NVIS;bob;image;x;y;cx;cy;cimage;prep;PICK|NPICK;anim_ch;verb;icon;LOW|MID|HIGH;types;w1;w2;w3 (This statement is optional)
ROOMOBJ: 1;Plant;17;VIS;55;RBOB8;398;79;-7;2;14; ;NPICK; ;8; ;LOW; ;a;this ;it

/* DACT: {cond|comm}
/*
DACT: IFOF 26,1=1;HIDE 7;HIDE 8
DACT: LIGHTS ON
DACT: IFOF 26,1=1;IFRF 2=0;SAY I wonder if that got rid of him?;SAY Let's see.;MOBJ ROBJ1;SAY YES! It worked!;SETRF 2=1
DACT: IFOF 26,1=1;EXIT

/* Now, if the DACTs get this far, the MOD bloke is still there.

DACT: IFRF 1=1;SAY Well, here goes. He might've gone somewhere else.;MOBJ ROBJ1;RESP R,8,Grrrrr!;RESP R,7,I thought I told you to stay away!;SAY Erm...
DACT: IFRF 1=1;RESP R,7,Get out!;RESP R,7,Otherwise this dog will bite you.;RESP R,7,Remember, these dogs break bones!;RESP R,8,Aruff!!;SAY Yikes!;SETRF 1=2;GOTO 15,3

DACT: SAY Well, it looks deserted.;MOBJ ROBJ1;THINK Oops...;RESP R,8,Arroof! Grrrrr!;RESP R,7,Stay where you are!;SAY Err, okay...;W 50;RESP R,7,This is MOD Property!;RESP R,7,Can't you read the sign?;SAY Er...
DACT: RESP R,7,What are you doing here?;SAY Um...;SETRF 1=1;DSET 7,+1,+2,+3,+5


/* LINE: dlg;line_no;sentence1;sentence2| ;{cond} (This statement is optional)

LINE: 7;1;I was just going for a quick walk.; ; 
LACT: 7;1;RESP R,7,Well, why walk on MOD property?;RESP R,7,There are pleanty of other good walks, like the one by the Marconi monument.
LACT: 7;1;DSET 7,N1,N3,N5,+4

LINE: 7;2;I didn't see the MOD sign.; ; 
LACT: 7;2;RESP R,7,Didn't see it?!;RESP R,7,It's massive!;SAY Oh yes. Whoops.
LACT: 7;2;DSET 7,N1,N2,N3,N5,+4,+6

LINE: 7;3;It's none of your f'king business!; ; 
LACT: 7;3;RESP R,7,It is my business when it is on MOD property!;RESP R,7,It's this dog's business too.;RESP R,7,It's his business to eat anybody that tries to outrun him, as well!;RESP R,8,Arrrrruff!!;SAY -GULP-
LACT: 7;3;DSET 7,N1,N3,N5,+4

LINE: 7;4;I'm sorry. I won't do it again.; ; 
LACT: 7;4;RESP R,7,I hope not!;RESP R,7,You could be shot or anything.;RESP R,7,If that happened on civilian land, your family would be eligible for compensation.;RESP R,7,But not if you were trespassing on MOD land!
LACT: 7;4;W 50;RESP R,7,If this dog bit you, it would be considered YOUR fault, because you shouldn't be trespassing.;RESP R,8,Ruff!;RESP R,7,...and these dogs break bones!;W 100;RESP R,7,Rex, you're wandering!;RESP R,8,(whimper!);W 100
LACT: 7;4;RESP R,7,Okay, you can go now.
LACT: 7;4;EDLG;GOTO 15,3;EXIT

LINE: 7;5;I was looking for a killer hamster.; ; 
LACT: 7;5;RESP R,7,Killer hamster?!;RESP R,7,We have no killer hamsters here!;RESP R,7,Try again.;DSET 7,N5

LINE: 7;6;What a nice dog.; ; 
LACT: 7;6;RESP R,7,Oh, you think so?;MOBJ 8;SAY Nice doggie!;RESP R,8,GRRRRRRRR!;MOBJ 7;SAY Yikes!;DSET 7,N6


/* LACT: dlg;line_no;{cond|comm} (This statement is optional)

/* ACTION: verb_no;{cond|comm} (This statement is optional)

ACTION: 0;IFOBJ 1;MEXIT;GOTO 15,3
ACTION: 0;IFOBJ 2;MEXIT;GOTO 23,1

ACTION: 8;IFOBJ ROBJ1;SAY It's just a useless bit of grass.;EXIT
