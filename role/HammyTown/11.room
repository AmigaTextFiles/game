/*  This is a room file for GRAAL
/*
/*  11.room - House 2
/*  RF 1=0 first visit
/*  RF 2=1 radio taken from safe.

UPDATE: 3;1

SECTION: 1

/* BG_IFF: filename
/*
/*   This is the backdrop file for the room
/*
BG_IFF: 9BG.IFF

/* START_POS: sposno;bobimage;x;y;camera;floor
/*
/*   (This statement must follow after the BG_IFF: statement.)
/*
/* 1->Village, 2->Garden
START_POS: 1;13;6;104;L;1
START_POS: 2;11;256;77;M;1

FLOOR: 1;0;72;319;120;1-1

/* EXIT: no;x1;y1;x2;y2;epointx;epointy;description
/*
EXIT: 1;0;57;21;120;6;109;Village
EXIT: 2;242;16;271;70;255;76;Garden

/* CLPART: filename
/*
CLPART: 10FG.IFF

/* ROOMBOBS: no_of_imgs;startrbob;grabx;graby;width;height;xoffset;hotsp
/*
/* 1 is the Amiga Advertising Pic!
ROOMBOBS: 1;1;177;1;36;52;20;0
/* 2 is the s8fe.
ROOMBOBS: 1;2;230;21;31;32;0; 

/* ROOMOBJ: roomobjno,object_definition.... (see graal.main documentation)
/*
ROOMOBJ: 1;Amiga Advert;11;VIS;51;RBOB1;215;66;-30;3;14; ;NPICK; ;8; ;MID; ;a;it; 
ROOMOBJ: 2;Safe;11;VIS;52;RBOB2;147;69;-22;6;14; ;NPICK; ;8; ;MID;T;a;this;it


/* DACT: condition;...;action;...
/*
/* Light up scene
DACT: TRACK MUSIC.05.MOD,LOOP,NOFILTER;LIGHTS ON
DACT: IFRF 1=0;SAY Funny, this room looks exactly like the one in the other house.;SAY Either this is a council house...;SAY ...or the programmer couldn't be bothered to draw another screen!;SETRF 1=1;EXIT

/* Room actions follow below ***
/*
/* ACTION: verbno;condition;...;command;...

/*
/* Action 0 - Exit
/* ===============
/*
ACTION: 0;IFOBJ 1;MEXIT;GOTO 8,2
ACTION: 0;IFOBJ 2;MEXIT;GOTO 22,1;EXIT

/* Action 1 - Give
/* ===============
/*
/* Action 2- Pick up
/* =================
/*

ACTION: 2;IFOBJ ROBJ1;SAY I don't really want to carry that around with me!;EXIT
ACTION: 2;IFOBJ ROBJ2;SAY Yeah, right, just rip it out of the wall!;EXIT

/*
/* Action 3 - Use
/* ==============
/*

ACTION: 3;IFOBJ ROBJ1;MOBJ;SAY It's a neat picture, but I don't think it will be useful for anything.;EXIT
ACTION: 3;IFOBJ 17;IFOBJ2 ROBJ2;IFRF 2=0;SAY I doubt it will work, but OK...;MOBJ ROBJ2;HANDLE ROBJ2;W 25;HANDLE -1;SAY Well, that was a total waste of time.;EXIT
ACTION: 3;IFTYPE X;IFOBJ2 ROBJ2;IFRF 2=0;SAY There is no way that is going to get it open!;EXIT

/* This gets the radio out of the safe.
ACTION: 3;IFOBJ 32;IFOBJ2 ROBJ2;IFRF 2=0;SAY Okay, let's see if it fits!;MOBJ ROBJ2;HANDLE ROBJ2;W 50;HANDLE -1;SAY Well, it opened. There is a strange looking radio in here.;SAY I'll take it.;SETRF 2=1;REMOVE 32,N,0;GET 33,U;EXIT

/*
/* Action 4 - Open
/* ===============
/*
ACTION: 4;IFOBJ ROBJ2;IFRF 2=0;MOBJ;SAY It won't open, it is locked.
ACTION: 4;IFOBJ ROBJ2;IFRF 2=0;IFCARR 32;SAY Here is a little hint for you.;SAY Firstly, you are carrying a key.;SAY Secondly, the safe has a lock on the front of it.;SAY Finally, using one on the other should produce the desired result.
ACTION: 4;IFOBJ ROBJ2;IFRF 2=0;EXIT
ACTION: 4;IFOBJ ROBJ2;MOBJ;SAY It's empty.;EXIT



/* Action 5 - Talk to
/* ==================
/*

/*
/* Action 6 - Push
/* ===============
/*
ACTION: 6;IFOBJ ROBJ2;IFRF 2=0;MOBJ;HANDLE;W 25;HANDLE -1;SAY You aren't going to open it just by doing that!;EXIT

/*
/* Action 7 - Close
/* ================


/*
/* Action 8 - Look at
/* ==================
/*
ACTION: 8;MOBJ
ACTION: 8;IFOBJ ROBJ1;SAY "To be this good will take Sega Ages.";SAY It's the last time Commodore did any advertising.;SAY 1993, wasn't it?;EXIT
ACTION: 8;IFOBJ ROBJ2;IFRF 2=0;SAY It's a safe. It's locked.;EXIT
ACTION: 8;IFOBJ ROBJ2;IFRF 2=1;SAY It's a safe. It's unlocked, but it's empty.;EXIT


/*
/* Action 9 - Pull
/* ===============
/*
ACTION: 9;IFOBJ ROBJ1|ROBJ2;VERB 6;REDO

/* End of room file. Some comments must be here!
