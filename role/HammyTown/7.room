/*                     This is a room file for GRAAL
/*
/*                                 7.room
/*                                Kitchen.
/* Room flags, 1=0 first visit, 2=1 Whisky added to soup

/* UPDATE: scroll_frames;normal_frames
/*
/*
UPDATE: 3;1

/* SECTION: section number
/*
SECTION: 1

/* BG_IFF: filename
/*
BG_IFF: 7BG.IFF

/* START_POS: sposno;bobimage;x;y;camera;floor
/* 1->Floating Restaurant
START_POS: 1;11;167;118;L;1

/* FLOOR: floorno;x1;y1;x2;y2;floormap/.../floormap
/*
FLOOR: 1;20;75;300;120;1-1

/* EXIT: no;x1;y1;x2;y2;epointx;epointy;description
/*
EXIT: 1;0;105;319;120;160;119;Floating Restaurant

/* CLPART: filename
/*
CLPART: 3FG.IFF

/* ROOMBOBS: no_of_imgs;startrbob;grabx;graby;width;height;xoffset;hotsp
/*
/* 1 is the cooker.
ROOMBOBS: 1;1;243;206;25;33;20;0
/* 2 is the saucepan.
ROOMBOBS: 1;2;254;194;16;7;0;0
/* 3 is the lamp.
ROOMBOBS: 1;3;279;210;9;23;18;0
/* 4 is the fork.
ROOMBOBS: 1;4;296;209;8;27;0;0

/* STATIC: bobno;LBOBimage;putx;puty

/* ROOMOBJ: roomobjno,object_definition.... (see graal.main documentation)
/*
ROOMOBJ: 1;Cooker;7;VIS;50;RBOB1;103;75;19;2;$800E; ;NPICK; ;8; ;MID;T;a;it; 
ROOMOBJ: 2;Saucepan;7;VIS;51;RBOB2;107;43;12;29;$800E; ;NPICK; ;8; ;HIGH;T;a;it; 


/* DACT: condition;...;action;...
/*
/*   These lines contain room actions performed directly as you enter the
/*   room.
/*
/*
/* Light up scene
DACT: LIGHTS ON
DACT: IFRF 1=0;SAY Interesting Kitchen.;SETRF 1=1;EXIT

/* LINE: dlgno;lineno;line;reuseline;conditions
/*
/*   Specifies a line of dialogue
/*
/* LACT: dlgno;lineno;action;...;action
/*



/* Room actions follow below ***
/*
/* ACTION: verbno;condition;...;command;...

/*
/* Action 0 - Exit
/* ===============
/*
ACTION: 0;IFOBJ 1;MEXIT;GOTO 6,2;EXIT

/*
/* Action 1 - Give
/* ===============
/*
/*
/* Action 2- Pick up
/* =================
/*

ACTION: 2;IFOBJ ROBJ2;MOBJ;SAY Why would I want to pick up this disgusting mess?;EXIT


/*
/* Action 3 - Use
/* ==============
/*

ACTION: 3;IFOBJ ROBJ1;MOBJ;SAY I'm not a real chef, remember!;SAY I just nicked the certificate out of that bloke's house.;EXIT

/*Info on room and object flags (essential for understanding the next 8 lines)
/*RF    2=0 when no whisky in stew,  RF 2=1 when whisky is in stew.
/*OF 11,1=0 when glass empty,  =1 when full of whisky, =2 when full of stew.
/*OF  4,2=0 before given stew, =1 after given stew to Jack.

ACTION: 3;IFOBJ 11;IFOBJ2 ROBJ2;MOBJ ROBJ2

ACTION: 3;IFOBJ 11;IFOBJ2 ROBJ2;IFOF 4,2=1;SAY I think I've done enough mucking about with this stew.;EXIT

ACTION: 3;IFOBJ 11;IFOBJ2 ROBJ2;IFOF 1=2;SAY I've already got a glass full of the stuff!;SAY Believe me, that is more than enough!;EXIT

ACTION: 3;IFOBJ 11;IFOBJ2 ROBJ2;IFRF 2=0;IFOF 1=0;SAY Why would I want some of that horrible stuff?;EXIT

ACTION: 3;IFOBJ 11;IFOBJ2 ROBJ2;IFOF 1=1;SAY Okay, let's try and make this horrible stew taste a bit nicer!
ACTION: 3;IFOBJ 11;IFOBJ2 ROBJ2;IFOF 1=1;SETRF 2=1;HANDLE ROBJ2;W 25;HANDLE -1;SAY Job done!;NAME Empty Glass;ICON 43;SETOF 1=0;EXIT

ACTION: 3;IFOBJ 11;IFOBJ2 ROBJ2;IFRF 2=1;IFOF 1=0;SAY Right, let's take some of this whisky flavoured stew.;HANDLE;W 25;HANDLE -1
ACTION: 3;IFOBJ 11;IFOBJ2 ROBJ2;IFRF 2=1;IFOF 1=0;SETOF 1=2;NAME Whisky & Cockroach stew;ICON 44;EXIT


/*
/* Action 4 - Open
/* ===============
/*

ACTION: 4;IFOBJ ROBJ1;SAY I don't really want to open the cooker.;SAY I dread to think what's inside!;EXIT



/* Action 5 - Talk to
/* ==================
/*

/*
/* Action 6 - Push
/* ===============
/*

ACTION: 6;IFOBJ ROBJ1;MOBJ;HANDLE;W 50;HANDLE -1;SAY It doesn't move.;EXIT
/*
/* Action 7 - Close
/* ================

ACTION: 7;IFOBJ ROBJ1;SAY The door is already shut.;EXIT


/*
/* Action 8 - Look at
/* ==================
/*
ACTION: 8;MOBJ
ACTION: 8;IFOBJ ROBJ1;SAY It's an old and very dodgy looking cooker.;EXIT
ACTION: 8;IFOBJ ROBJ2;IFRF 2=0;SAY This looks like Jack's cockroach stew on the boil.;SAY I think I'm going to puke.;EXIT
ACTION: 8;IFOBJ ROBJ2;SAY Hmm. Whisky flavour cockroach stew.;EXIT

/*
/* Action 9 - Pull
/* ===============
/*
ACTION: 9;IFOBJ ROBJ1;VERB 6;REDO
ACTION: 9;MOBJ

/*
/* End of room file. Some comments must be here, otherwise  the last command
/* line is missed out!
