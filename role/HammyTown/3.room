/*                     This is a room file for GRAAL
/*
/*                                 3.room
/* RF 1=0 first visit


/* UPDATE: scroll_frames;normal_frames
/*
UPDATE: 3;1

/* SECTION: section number
SECTION: 1

/* BG_IFF: filename
BG_IFF: 3BG.IFF

/* START_POS: sposno;bobimage;x;y;camera;floor
/*
START_POS: 1;13;7;103;L;1


/* FLOOR: floorno;x1;y1;x2;y2;floormap/.../floormap
FLOOR: 1;1;83;318;116;1-1


/* EXIT: no;x1;y1;x2;y2;epointx;epointy;description
EXIT: 1;0;49;32;121;1;105;Street

/* CLPART: filename
/*
CLPART: 3FG.IFF

/* ROOMBOBS: no_of_imgs;startrbob;grabx;graby;width;height;xoffset;hotsp

/* 1 is the optic
ROOMBOBS: 1;1;63;2;13;39;0;0
/* 2-4 is the bartender animation
ROOMBOBS: 3;2;14;6;15;20;15;0

/* STATIC: bobno;LBOBimage;putx;puty

/* ANIM: bobno;bobimg;amalchannel;amalsequence;putx;puty

/* ROOMOBJ: roomobjno,object_definition.... (see graal.main documentation)
/*
ROOMOBJ: 1;Optic;3;VIS;51;RBOB1;302;53;-9;29;13; ;NPICK; ;8; ;HIGH; ;an;the; 

/* DACT: condition;...;action;...
/*
/*   These lines contain room actions performed directly as you enter the
/*   room.
/* Light up scene
DACT: TRACK NO;LIGHTS ON
DACT: IFRF 1=0;SAY Look at that dodgy bloke over there!;SAY Looks like he's got his head on upside-down!;SETRF 1=1;EXIT

/* LINE: dlgno;lineno;line;reuseline;conditions
/*
/*   Specifies a line of dialogue
/*
/* LACT: dlgno;lineno;action;...;action
/*
/* 1 is bartender dialogue
/*
LINE: 1;1;Cheers then mate, I'll see you later.;Forget it.; 
LACT: 1;1;IFOF 1=0;RESP R,1,Okey dokey, see you.;SETOF 1=1;EDLG;EXIT
LACT: 1;1;IFOF 1=1;RESP R,1,Forgotten.;EDLG;EXIT
/*
LINE: 1;2;I just crashed my car!; ; 
LACT: 1;2;RESP R,1,Oh. well done.;RESP R,1,I suppose you hit that tree?;SAY Yeah, that's right.;SAY How did you know that?!;RESP R,1,You're not the first one to hit it, you know.;RESP R,1,In fact, we've complained about it before. ;DSET 1,N2,+1
/*
LINE: 1;3;Where am I?;Where did you say I was?; ; 
LACT: 1;3;RESP R,1,This is the Ivy bar.;RESP R,1,Except that we are closed for the minute.;RESP R,1,What can I do for you?
LACT: 1;3;DSET 1,+1,+5,+6,+8
/*
LINE: 1;4;Why is that optic on your left so big?; ; 
LACT: 1;4;RESP R,1,Because our customers like LAAAARGE measures!;RESP R,1,Either that or the programmer of this game can't draw properly to save his life.
LACT: 1;4;DSET 1,N4

/*
LINE: 1;5;Why is it called the Ivy bar?; ; 
LACT: 1;5;RESP R,1,Well, it's called the Ivy bar because...;RESP R,1,...It's got Ivy round the door.
LACT: 1;5;RESP R,1,What a bloody stupid question.;SAY Alright, calm down!;RESP R,1,Sorry.;DSET 1,N5;EXIT
/*
LINE: 1;6;What is this village called?;Tell me about the village again.; 
LACT: 1;6;RESP R,1,It's called Hammy Town.;RESP R,1,That's because there is a huge killer hamster on the loose.;RESP R,1,It has been terrorising this town for the past five years.
LACT: 1;6;RESP R,1,It has killed before and it will kill again!
LACT: 1;6;CBOB 11;W 12;SAY Gulp!;W 24;CBOB 14
LACT: 1;6;DSET 1,+7
/*
LINE: 1;7;Killer Hamster? You must think I'm stupid.; ; 
LACT: 1;7;RESP R,1,You are wrong to mock, my friend.;RESP R,1,Jools Henn wrote this game.;RESP R,1,Therefore anything is possible.
LACT: 1;7;RESP R,1,Dodgy graphics, wierd sound, but most of all:;RESP R,1,KILLER HAMSTERS!
LACT: 1;7;RESP R,1,It's making our lives a misery.;DSET 1,N7
/*
LINE: 1;8;It's a bit dark and dingy in here, isn't it?; ;IFCARR 21
LACT: 1;8;RESP R,1,Yes, it is a bit.;RESP R,1,That's because I've got no electricity supply here.;DSET 1,N8

/* Room actions follow below ***
/*
/* ACTION: verbno;condition;...;command;...

/*
/* Action 0 - Exit
/* ===============
/*
ACTION: 0;IFOBJ 1;MEXIT;GOTO 2,2;EXIT
/*
/* Action 1 - Give
/* ===============
/*

ACTION: 1;IFOBJ 21;IFOBJ2 1;IFOF 3=0;MOBJ 1;SAY How about this lamp to brighten the place up?;HANDLE 1;W 25;HANDLE -1;RESP R,1,It's no good to me- I haven't got any electricity!;HANDLE 1;W 25;HANDLE -1;EXIT

ACTION: 1;IFOBJ 21;IFOBJ2 1;MOBJ 1;SAY Look at this, barman!;HANDLE 1;W 25;HANDLE -1;RESP R,1,Wow, that is neat! A lamp that lights by itself!;RESP R,1,Here, I'll exchange it for a golden hamster!
ACTION: 1;IFOBJ 21;IFOBJ2 1;SAY Well...;SAY Um...;SAY Yeah, alright then.;HANDLE 1;W 25;HANDLE -1;REMOVE 21,N,0;GET 28,U;EXIT

ACTION: 1;IFOBJ 28;IFOBJ2 1;SAY Don't be silly- I got it off him in the first place.;EXIT

ACTION: 1;IFOBJ 13;IFOBJ2 1;MOBJ 1;SAY A pint and a packet of crisps please, barman!;RESP R,1,I'm sorry, the bar is closed.;EXIT
ACTION: 1;IFOBJ 11;IFOBJ2 1;IFOF 1=0;MOBJ 1;SAY This isn't one of yours, is it?;RESP R,1,No, I'm afraid not.;EXIT
ACTION: 1;IFOBJ 11;IFOBJ2 1;IFOF 1=1;SAY No thanks!;SAY I don't want him to know that I've stolen some of his whisky!;EXIT

/*
/* Action 2- Pick up
/* =================
/*
/*
/* Action 3 - Use
/* ==============
/*
ACTION: 3;IFOBJ ROBJ1;SAY No thanks, I don't want to spill valuable whisky on the floor!;EXIT
ACTION: 3;IFOBJ 11;IFOBJ2 ROBJ1;MOBJ ROBJ1;SAY Here goes...;HANDLE ROBJ1;W 50;HANDLE -1;SAY Job Done! The dozy bartender didn't even notice.
ACTION: 3;IFOBJ 11;IFOBJ2 ROBJ1;ICON 11,44;OBJ1 11;NAME Full Glass;SETOF 1=1;EXIT


/* Action 4 - Open
/* ===============
/*
ACTION: 4;IFOBJ ROBJ1;SAY Take the bottle, you mean?;SAY I don't think the dodgy bloke will like that.;EXIT

/* Action 5 - Talk to
/* ==================
/*
ACTION: 5;IFOBJ 1;IFOF 1=0;MOBJ;SAY Hello.;RESP R,1,Hello.;DSET 1,+2,+3,+4;EXIT
ACTION: 5;IFOBJ 1;IFOF 1=1;MOBJ;SAY Hello again.;RESP R,1,Hello again. What can I do for you now?;DSET 1;EXIT
/*
/* Action 6 - Push
/* ===============
/*
ACTION: 6;IFOBJ ROBJ1;VERB 3;REDO;EXIT
/*
/* Action 7 - Close
/* ================

/* Action 8 - Look at
/* ==================
/*
ACTION: 8;MOBJ
ACTION: 8;IFOBJ 1;SAY What a dodgy looking bloke.;RESP R,1,Oy, I heard that!;EXIT
ACTION: 8;IFOBJ ROBJ1;SAY A rather big optic full of lovely whisky.;EXIT
/*
/* Action 9 - Pull
/* ===============
/*
/* End of room file. Some comments must be here, otherwise  the last command
/* line is missed out!
