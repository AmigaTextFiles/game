/*
/* Room script for GRAAL
/*
/* Ivan's Garage.
/* RF 2=1 when mini is gone.
/* RF 3=1 when the crap car hasn't started properly
/* RF 4=1 when it's okay to try to start the crap car
/* RF 5=1 when the mini has been paid for
/* RF 6=1 when the tape has been found in the crap car

/* UPDATE: scroll_frames[;normal_frames]
/*
UPDATE: 3;1

/* SECTION: section_no
/*
SECTION: 1

/* BG_IFF: file
/*
BG_IFF: 21BG.iff

/* START_POS: startpos_no;image;x;y;L|M|R;floor_no
/*
START_POS: 1;$800E;313;104;R;1
START_POS: 2;14;6;106;L;1

/* FLOOR: floor_no;x1;y1;x2;y2;floormaps
/*
FLOOR: 1;0;80;319;119;1-1

/* PATH: path_no;floor1;floor2;px1;py1;px2;py2;{more_points} (This statement is optional)

/* EXIT: exit_no;x1;y1;x2;y2;x;y;description (This statement is optional)

EXIT: 1;301;33;319;119;315;97;Path
EXIT: 2;0;47;15;119;5;103;Path

/* CLPART: file (This statement is optional)
CLPART: 20FG.iff

/* ROOMBOBS: number;start_bob;x1;y1;w;h;x-offset;hotspot (This statement is optional)
/* 1 is Mini, 2 is Escort, 3 is Sign, 4-6 is Ivan
ROOMBOBS: 1;1;111;64;71;44;0;20
ROOMBOBS: 1;2;49;66;51;37;0; 
ROOMBOBS: 1;3;42;107;55;28;0; 
ROOMBOBS: 3;4;108;112;35;41;37; 

/* STATIC: bob;image;x;y (This statement is optional)

/* ANIM: bob;image;anim_ch;anim;x;y (This statement is optional)

/* ROOMOBJ: obj;name;room;VIS|NVIS;bob;image;x;y;cx;cy;cimage;prep;PICK|NPICK;anim_ch;verb;icon;LOW|MID|HIGH;types;w1;w2;w3 (This statement is optional)
ROOMOBJ: 1;Escort;21;VIS;51;RBOB2;56;82;19;1;$800E; ;NPICK; ;8; ;MID;T;a;this;it
ROOMOBJ: 2;Sign;21;VIS;52;RBOB3;225;52;-17;25;11; ;NPICK; ;8; ;HIGH;W;a;this;it

/* DACT: {cond|comm}
/*
DACT: IFRF 2=1;HIDE 30
DACT: LIGHTS ON
DACT: EXIT

/* LINE: dlg;line_no;sentence1;sentence2| ;{cond} (This statement is optional)
/* LACT: dlg;line_no;{cond|comm} (This statement is optional)

LINE: 6;1;I'll talk to you later.;I'll talk to you again later.; 
LACT: 6;1;RESP R,6,Right on, see you later.;EDLG;EXIT

LINE: 6;2;Who are you?; ; 
LACT: 6;2;RESP R,6,I am Ivan, the owner of this garage.;DSET 6,N2

LINE: 6;3;Tell me about the blue car, please.;Tell me about that rustbucket again please.; 
LACT: 6;3;RESP R,6,What do you want to know about it?;DSET 6,S1,+8,+9,+10,+11,+13

LINE: 6;4;Tell me about the mini with the dodgy stripes, please.;Tell me about that mini again please.;IFRF 5=0
LACT: 6;4;RESP R,6,Okay, what do you want to know?;DSET 6,S1,+14,+15

LINE: 6;5;What is the Garage doing on an island, anyway?; ; 
LACT: 6;5;RESP R,6,Well, the land here is cheap.;RESP R,6,There are not many distractions over here.;RESP R,6,And I can wander over to the Cloud Electronics place in my lunch hour.;RESP R,6,Eric is a mate of mine.
LACT: 6;5;DSET 6,N5,+6

LINE: 6;6;Doesn't being on an island deter customers?; ; 
LACT: 6;6;SAY How do they get their cars here if there is no road or ferry?;RESP R,6,Well, I don't know.;RESP R,6,That is probably why we haven't had any customers lately, apart from the Aztec people.
LACT: 6;6;RESP R,6,I did wonder why nobody used this garage.;THINK (What are the people like here?);THINK (This is as bad as Jack and his cockroach stew!);SAY Well, now you know!
LACT: 6;6;DSET 6,N6

LINE: 6;7;Do you think you could repair my car?; ; 
LACT: 6;7;RESP R,6,Probably, where is it?;SAY It's wrapped around a tree by the path into the village.;RESP R,6,Oh, that's your car?;RESP R,6,I saw that on the way to work this morning.
LACT: 6;7;SAY Do you think it is fixable?;RESP R,6,Fixable?;RESP R,6,FIXABLE?!;RESP R,6,Come on, you must be taking the piss!;RESP R,6,All the king's horses and all the king's men couldn't fix THAT car.
LACT: 6;7;DSET 6,N7

/* Blue Car Section, here we go...

LINE: 6;8;Why does it say "To be scrapped" in the window?;Tell me again about the "To be scrapped" sign.;IFOF ROBJ1,1=1;IFRF 3=0
LACT: 6;8;RESP R,6,Erm... That's just there for decoration.;RESP R,6,Honestly!;DSET 6,-8

LINE: 6;9;That car is crap! I'm not buying it!;I still don't want to buy the stupid car.;IFRF 3=1
LACT: 6;9;RESP R,6,Okay, suit yourself.;DSET 6,R1

LINE: 6;10;How much does it cost?;How much did you say it costed again?;IFRF 3=0;IFRF 2=0;IFRF 4=0
LACT: 6;10;RESP R,6,One hundred pounds.;RESP R,6,Or, I will accept one gold hamster. I collect them, you see.;DSET 6,-10,+12

LINE: 6;11;I've got a golden hamster right here.;I've got a golden hamster right here.;IFRF 3=0;IFCARR 28
LACT: 6;11;SAY I do want to take it for a test drive first, though.;RESP R,6,Okay, good luck!;SETRF 4=1;DSET 6,R1

LINE: 6;12;I don't have that sort of money on me.;I still haven't got that much money.;IFNOTCARR 28
LACT: 6;12;RESP R,6,That's a shame.;DSET 6,R1

LINE: 6;13;Actually, I don't really want to talk about the blue car.;Actually, I don't really want to talk about the blue car.; 
LACT: 6;13;RESP R,6,Okay, let's not.;DSET 6,R1

/* Okay, now the Mini!

LINE: 6;14;How much does it cost?;How much did you say it costed again?;IFRF 5=0
LACT: 6;14;RESP R,6,Nine hundred pounds, or nine gold hamsters.;DSET 6,-14,+16

LINE: 6;15;I just happen to have nine gold hamsters right here.; ;IFCARR 28;IFOF 28,1=1
LACT: 6;15;HANDLE;W 25;HANDLE -1;RESP R,6,Aha, brilliant!;RESP R,6,The Mini is now yours.;SAY Nice one!
LACT: 6;15;SETRF 5=1;REMOVE 28,N,0;DSET 6,R1

LINE: 6;16;Sorry, I haven't got the money.;Sorry, I still haven't got the money.;IFOF 28,1=0
LACT: 6;16;RESP R,6,Shame. You would've liked it.;DSET 6,R1


/* ACTION: verb_no;{cond|comm} (This statement is optional)

ACTION: 0;IFOBJ 1;MEXIT;GOTO 19,2
ACTION: 0;IFOBJ 2;MEXIT;GOTO 18,3

ACTION: 1;IFOBJ 19|20;IFOBJ2 6;SAY Would this be any use to you?;RESP R,6,No, I already have enough tools of my own.;EXIT
ACTION: 1;IFOBJ 28;IFOBJ2 6;OBJ1 6;VERB 5;REDO

ACTION: 2;IFOBJ ROBJ2;MOBJ;SAY I can't reach it from here.;SAY Plus, I wouldn't really want to carry a huge sign around with me, anyway.;EXIT
ACTION: 3;IFOBJ2 ROBJ2;MOBJ ROBJ2;SAY My hand isn't long enough!;EXIT

ACTION: 3;IFOBJ 30;IFRF 5=0;SAY I'd better not drive it off, it's not mine!;EXIT
ACTION: 3;IFOBJ 30;IFRF 5=1;SAY Drive it- Where would I drive to?;SAY This is only a small island- there is no road here!;EXIT
ACTION: 3;IFOBJ 19|20;IFOBJ2 30;SAY Why? It's in perfectly good condition.;EXIT

ACTION: 3;IFOBJ ROBJ1;IFRF 4=0;SAY It's not mine, I'd better not.;EXIT
ACTION: 3;IFOBJ ROBJ1;IFRF 3=0;MOBJ;SAY Okay, let's give it a go.;HANDLE;W 50;HANDLE -1;SAY Dammit. The engine won't start;SETRF 3=1;EXIT
ACTION: 3;IFOBJ ROBJ1;SAY I've given up trying with that.;EXIT
ACTION: 3;IFOBJ 19|20;IFOBJ2 ROBJ1;SAY I think it'll take more than that to fix it.;EXIT
ACTION: 3;IFTYPE X;IFOBJ2 ROBJ1;SAY I know it's a crap car, but vandalising it is not going to help!;EXIT

ACTION: 3;IFOBJ 27;IFOBJ2 30;IFRF 5=1;SAY Well, here goes.;HANDLE;W 25;HANDLE -1;SETRF 2=1;HIDE 30;SAY Wow, it worked!;W 25;SAY ...but where did it go?;EXIT
ACTION: 3;IFOBJ 27;IFOBJ2 30|ROBJ1;SAY I don't really want to use the teleporter on a car that isn't mine.;EXIT

ACTION: 3;IFOBJ 29;IFOBJ2 30|ROBJ1;SAY I can't, the tape player in it doesn't work.;EXIT

ACTION: 4;IFOBJ ROBJ1;IFRF 6=0;MOBJ;HANDLE;W 50;HANDLE -1;SAY Hey, there's a tape in here.;SETRF 6=1;GET 29,U;EXIT
ACTION: 4;IFOBJ 30|ROBJ1;MOBJ;HANDLE;W 25;HANDLE -1;SAY The door opens fine. So what?;EXIT

ACTION: 5;IFOBJ 6;IFOF 1=0;SETOF 1=1;MOBJ;DSET 6,+1,+2,+3,+4,+5;EXIT
ACTION: 5;IFOBJ 6;MOBJ;DSET 6;EXIT

ACTION: 8;IFOBJ 30;IFRF 5=1;MOBJ;SAY It's my lovely mini!;EXIT
ACTION: 8;IFOBJ 30;MOBJ;SAY It's a mini with dodgy stripes!;SAY Cool!;EXIT
ACTION: 8;IFOBJ ROBJ1;IFRF 6=0;MOBJ;HANDLE;W 50;HANDLE -1;SAY Hey, there's a tape in here.;SETRF 6=1;GET 29,U;EXIT
ACTION: 8;IFOBJ ROBJ1;MOBJ;SAY This car hasn't been looked after very well.;SAY There's a sign inside saying "To be scrapped".;SETOF 1=1;EXIT
ACTION: 8;IFOBJ ROBJ2;MOBJ;SAY Ivan's Garage. New and used cars, Repairs, etc.;EXIT


/* Plus of course, silly comment.
