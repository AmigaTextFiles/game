/*                     This is a room file for GRAAL
/*
/*                                 12.room
/*                                Toolshed.


/* UPDATE: scroll_frames;normal_frames
/*
/*
UPDATE: 3;1

/* SECTION: section number
/*
/*   The room belongs to section 1, described in the file 1.section.
/*
SECTION: 1

/* BG_IFF: filename
/*
/*   This is the backdrop file for the room
/*
BG_IFF: 12BG.IFF

/* START_POS: sposno;bobimage;x;y;camera;floor
/*
/* 1->Living Room
START_POS: 1;$800E;310;115;L;1

/* FLOOR: floorno;x1;y1;x2;y2;floormap/.../floormap
/*
FLOOR: 1;25;86;319;119;1-1

/* EXIT: no;x1;y1;x2;y2;epointx;epointy;description
/*
EXIT: 1;294;26;319;119;316;109;Living Room

/* CLPART: filename
/*
CLPART: 12FG.IFF

/* ROOMBOBS: no_of_imgs;startrbob;grabx;graby;width;height;xoffset;hotsp
/*
/* 1 is the table.
ROOMBOBS: 1;1;31;20;49;22;20;0
/* 2 is the spade.
ROOMBOBS: 1;2;87;11;16;33;0;0
/* 3 is the saw.
ROOMBOBS: 1;3;107;18;20;24;18;0
/* 4 is the Toolbox
ROOMBOBS: 1;4;136;25;37;18;0;0
/* 5 is the crowbar
ROOMBOBS: 1;5;181;18;19;27;0;0
/* 6 is the socket
ROOMBOBS: 1;6;213;10;12;9;0;0
/* 7 is the voltage control
ROOMBOBS: 1;7;213;23;12;10;0;0
/* 8 is the knife
ROOMBOBS: 1;8;209;37;16;4;0;0

/* STATIC: bobno;LBOBimage;putx;puty
/* ANIM: bobno;bobimg;amalchannel;amalsequence;putx;puty

/* ROOMOBJ: roomobjno,object_definition.... (see graal.main documentation)
/*
/*
ROOMOBJ: 1;Table;12;VIS;50;RBOB1;56;86;24;1;11; ;NPICK; ;8; ;MID;W;a;it; 
ROOMOBJ: 2;Toolbox;12;VIS;51;RBOB4;106;86;21;2;11; ;NPICK; ;8; ;LOW;T;a;it; 
ROOMOBJ: 3;Socket;12;VIS;52;RBOB6;270;68;-13;19;14; ;NPICK; ;8; ;HIGH; ;a;it; 
ROOMOBJ: 4;Voltage Control;12;VIS;53;RBOB7;242;69;-14;19;14; ; ; ;8; ;HIGH; ;a;it; 


/* DACT: condition;...;action;...
/*
/*   These lines contain room actions performed directly as you enter the
/*   room.
/*
/*
/* Light up scene
DACT: LIGHTS ON
DACT: IFRF 1=0;SAY Aha, this seems to be the toolshed!;SETRF 1=1;EXIT

/* LINE: dlgno;lineno;line;reuseline;conditions
/* LACT: dlgno;lineno;action;...;action



/* Room actions follow below ***
/*
/* ACTION: verbno;condition;...;command;...

/*
/* Action 0 - Exit
/* ===============
/*
ACTION: 0;IFOBJ 1;MEXIT;GOTO 10,2;EXIT

/*
/* Action 1 - Give
/* ===============
/*
/* Action 2- Pick up
/* =================
/*

/*
/* Action 3 - Use
/* ==============
/*

ACTION: 3;IFOBJ2 ROBJ4;MOBJ ROBJ4
ACTION: 3;IFOBJ 18;IFOBJ2 ROBJ4;OBJ1 19;REDO
ACTION: 3;IFOBJ 19;IFOBJ2 ROBJ4;IFRF 2=0;SAY It hasn't really got enough leverage.;EXIT
ACTION: 3;IFOBJ 17;IFOBJ2 ROBJ4;IFRF 2=1;SAY I've already opened it.;EXIT
ACTION: 3;IFOBJ 17;IFOBJ2 ROBJ4;SAY Here goes....;HANDLE ROBJ4;SAY Nnnnghhhh...;HANDLE -1;SAY Okay, it's open!;SETRF 2=1;EXIT

ACTION: 3;IFOBJ ROBJ4;IFRF 2=0;SAY I can't turn it, the cover is on too tight.;EXIT
ACTION: 3;IFOBJ ROBJ4;IFRF 3=0;SAY Okay, let's turn up the voltage!;HANDLE;W 25;HANDLE -1;SETRF 3=1;SAY It is now at 500 volts!;EXIT
ACTION: 3;IFOBJ ROBJ4;IFRF 4=0;SAY I've already turned it up.;EXIT
ACTION: 3;IFOBJ ROBJ4;SAY I've had enough fun playing with the electrics.;EXIT

ACTION: 3;IFOBJ ROBJ3;IFRF 4=0;SAY I need something to plug into it.;EXIT
ACTION: 3;IFOBJ ROBJ3;SAY I've already broken the lamp, I don't want to do any more damage.;EXIT


ACTION: 3;IFOBJ 21;IFOBJ2 ROBJ3;IFOF 3=1;SAY The lamp is already lighting by itself somehow, there is no point in plugging it in again.;EXIT

ACTION: 3;IFOBJ 21;IFOBJ2 ROBJ3;IFOF 2=1;IFOF 3=0;MOBJ ROBJ3;SAY Let's try this Aztec bulb, see how well it works;HANDLE ROBJ3;W 25;HANDLE -1;SAY Well, it lights up.;HANDLE ROBJ3;W 25;HANDLE -1
ACTION: 3;IFOBJ 21;IFOBJ2 ROBJ3;IFOF 2=1;IFOF 3=0;SAY That's strange!;SAY Even when the lamp is unplugged, the bulb still lights!;NAME Lamp that is magically lit;SETOF 3=1;EXIT

ACTION: 3;IFOBJ 21;IFOBJ2 ROBJ3;IFRF 3=0;MOBJ ROBJ3;HANDLE;SAY Well, the bulb lights up but nothing else interesting happens.;HANDLE -1;EXIT
ACTION: 3;IFOBJ 21;IFOBJ2 ROBJ3;IFRF 4=1;SAY I've already broken the bulb.;EXIT
ACTION: 3;IFOBJ 21;IFOBJ2 ROBJ3;MOBJ ROBJ3;SAY Now, this should be fun!;HANDLE ROBJ3;SAY The bulb just lit up very bright, then went "Ping", and went off.;HANDLE -1;SAY I think I just busted the bulb.;NAME Lamp with busted bulb;SETRF 4=1;SETOF 1=1;EXIT

ACTION: 3;IFOBJ 18|19;IFOBJ2 ROBJ3;SAY ...and give myself an electric shock?;SAY No thanks!;EXIT


/* Action 4 - Open
/* ===============
/*
ACTION: 4;IFOBJ ROBJ4;IFRF 2=0;VERB 3;REDO
ACTION: 4;IFOBJ ROBJ4;IFRF 2=1;SAY It's Already Open.;EXIT

ACTION: 4;IFOBJ ROBJ2;IFRF 5=0;MOBJ;HANDLE;W 10;SAY Hey, there's a screwdriver and a spanner in here!;HANDLE -1;GET 19,N;GET 20,U;SETRF 5=1;EXIT
ACTION: 4;IFOBJ ROBJ2;SAY It's already open.;EXIT


/* Action 5 - Talk to
/* ==================
/*

/*
/* Action 6 - Push
/* ===============
/*
/*
/* Action 7 - Close
/* ================

ACTION: 7;IFOBJ ROBJ4;IFRF 2=1;SAY There's no point in doing that.;SAY Besides, the cover broke when I used the crowbar.;EXIT


/*
/* Action 8 - Look at
/* ==================
/*
ACTION: 8;MOBJ
ACTION: 8;IFOBJ ROBJ1;SAY It's your average table, really.;EXIT
ACTION: 8;IFOBJ ROBJ2;IFRF 5=0;SAY An interesting looking toolbox!;SAY Wonder what's inside?;EXIT
ACTION: 8;IFOBJ ROBJ2;SAY It's empty now.;EXIT
ACTION: 8;IFOBJ ROBJ3;SAY Just a normal three pin socket.;EXIT
ACTION: 8;IFOBJ ROBJ4;IFRF 2=0;SAY It's a voltage control, but the front cover is stuck on.;EXIT
ACTION: 8;IFOBJ ROBJ4;IFRF 3=0;SAY Voltage control currently set to 230 volts.;EXIT
ACTION: 8;IFOBJ ROBJ4;IFRF 3=1;SAY Voltage control currently set to 500 volts! Heh heh.;EXIT


/*
/* Action 9 - Pull
/* ===============
/*
ACTION: 9;IFOBJ ROBJ1;VERB 6;REDO
/*
/* End of room file. Some comments must be here, otherwise  the last command
/* line is missed out!
