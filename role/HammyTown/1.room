/*                     This is a room file for GRAAL
/*
/*                                 1.room
/* RF 1=0 first visit


/* UPDATE: scroll_frames;normal_frames
/*
UPDATE: 3;1

/* SECTION: section number
/*
/*   The room belongs to section 1, described in the file 1.section.
/*
SECTION: 1

/* BG_IFF: filename
BG_IFF: 1BG.IFF

/* START_POS: sposno;bobimage;x;y;camera;floor
START_POS: 1;13;189;75;L;1
START_POS: 2;11;11;102;M;1

/* FLOOR: floorno;x1;y1;x2;y2;floormap/.../floormap
/*
FLOOR: 1;-1;74;223;102;1-1/2-2
FLOOR: 2;191;38;249;95;1-1/2-2

/* EXIT: no;x1;y1;x2;y2;epointx;epointy;description
/*
/*   This command defines an exit
/*   x1;y1;x2;y2 = exit area
/*   epointx;epointy = point to move character to when exiting
/*   description = cursor description of exit
/*
EXIT: 1;0;64;18;111;1;105;street

/* CLPART: filename
/*
CLPART: 1FG.IFF

/* ROOMBOBS: no_of_imgs;startrbob;grabx;graby;width;height;xoffset;hotsp
/* 1 is the car wreck
ROOMBOBS: 1;1;54;2;44;35;0;0
/* 2 is the picnic bench
ROOMBOBS: 1;2;2;4;49;17;0;0

/* ROOMOBJ: roomobjno,object_definition.... (see graal.main documentation)
/*
/*   The object number specified here must later be prefixed by "ROBJ" when
/*   referred to in other statements and commands.
/*
ROOMOBJ: 1;Car Wreck;1;VIS;51;RBOB1;172;72;24;-3;11; ;NPICK; ;8; ;LOW;TD;a; ; 
ROOMOBJ: 2;Picnic Table;1;VIS;50;RBOB2;80;75;19;0;11; ;NPICK; ;8; ;MID;W;a; ; 

/* DACT: condition;...;action;...
/*
/*   These lines contain room actions performed directly as you enter the
/*   room.
/*
/* Put some items in the inventory from the very start:
DACT: IFRF 1=0;GET 34,U
/*
/* Make sure none of the above happens more than once...
/*DACT: IFRF 1=0;SETRF 1=1
/* That is already done later. (9 lines below?)
/*
/* Light up scene
DACT: TRACK MUSIC.01.MOD,LOOP,NOFILTER;LIGHTS ON
DACT: IFRF 1=0;SAY Now what do I do?;SETRF 1=1;EXIT

/* LINE: dlgno;lineno;line;reuseline;conditions

/* Room actions follow below ***
/*
/* ACTION: verbno;condition;...;command;...

/*
/* Action 0 - Exit
/* ===============
/*
ACTION: 0;IFOBJ 1;MEXIT;GOTO 2,1
/*
/* Action 1 - Give
/* ===============
/*
/*
/* Action 2- Pick up
/* =================
/*
ACTION: 2;IFOBJ ROBJ1;SAY Oh, yeah, right. Carry it to the garage.;SAY What a great idea.;EXIT
ACTION: 2;IFOBJ ROBJ2;SAY Why? This spot is as good as any for a picnic!;EXIT
/*
/* Action 3 - Use
/* ==============
/*
ACTION: 3;IFTYPE X;IFOBJ2 ROBJ1;SAY I don't think that would help me to get anything out of the car...;SAY ...you'd need a disc cutter or something!;EXIT
ACTION: 3;IFOBJ ROBJ1;SAY Use the car?!;SAY Come on!;SAY Look at the state of it!;EXIT
ACTION: 3;IFOBJ ROBJ2;SAY Now is not the time to be having a picnic!;EXIT
/*
/* Action 4 - Open
/* ===============
/*
ACTION: 4;IFOBJ ROBJ1;SAY Everything is crushed: I can't open it!;EXIT

/* Action 5 - Talk to
/* ==================
/*
/*
/* Action 6 - Push
/* ===============
/*
/*
/* Action 7 - Close
/* ================

ACTION: 7;IFOBJ ROBJ1;SAY The Door is already crumpled shut.;EXIT

/* Action 8 - Look at
/* ==================
/*
ACTION: 8;MOBJ
ACTION: 8;IFOBJ ROBJ1;SAY My Poor Car!;SAY It's a total write off!;EXIT
ACTION: 8;IFOBJ ROBJ2;SAY It's a badly drawn wooden picnic table.;EXIT
/*
/* Action 9 - Pull
/* ===============
/*
ACTION: 9;MOBJ
ACTION: 9;IFOBJ ROBJ1;SAY There's no chance of pulling my car off that tree.;EXIT
ACTION: 9;IFOBJ ROBJ2;SAY It's bolted to the floor.


ACTION: 10;TEXT -1,0,9,You have scored #R#0#20# points so far.;EXIT
/*
/* End of room file. Some comments must be here, otherwise  the last command
/* line is missed out!
