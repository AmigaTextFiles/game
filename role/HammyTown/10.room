/*                     This is a room file for GRAAL
/*
/*                                10.room
/* RF 1=0 First Visit
/* RF 2=0 Hi-Fi not in operation.
/* RF 3=0 Squeaky toy not found.
/* RF 4=0 Don't put comment about door being open.

/* UPDATE: scroll_frames;normal_frames
/*
/*
UPDATE: 2;1

/* SECTION: section number
SECTION: 1

/* BG_IFF: filename
BG_IFF: 10BG.IFF

/* START_POS: sposno;bobimage;x;y;camera;floor
/*
/* 1->Hallway, 2->Toolshed
START_POS: 1;13;6;106;L;1
START_POS: 2;11;361;97;R;1

/* FLOOR: floorno;x1;y1;x2;y2;floormap/.../floormap
/*
FLOOR: 1;0;93;399;120;1-1

/* EXIT: no;x1;y1;x2;y2;epointx;epointy;description
/*
EXIT: 1;0;57;36;120;6;109;Hallway
EXIT: 2;347;37;377;92;352;95;Room

/* CLPART: filename
/*
CLPART: 10FG.IFF

/* ROOMBOBS: no_of_imgs;startrbob;grabx;graby;width;height;xoffset;hotsp
/*
/* 1 is the Hammy Electronics CD Player!
ROOMBOBS: 1;1;37;9;76;16;20;0
/* 2 is the the badly drawn sofa.
ROOMBOBS: 1;2;118;9;23;28;0;0

/* STATIC: bobno;LBOBimage;putx;puty
/*

/* ANIM: bobno;bobimg;amalchannel;amalsequence;putx;puty

/* ROOMOBJ: roomobjno,object_definition.... (see graal.main documentation)
/*
ROOMOBJ: 1;CD Player;10;VIS;51;RBOB1;181;64;-16;31;14; ;NPICK; ;8; ;HIGH; ;a;it; 
ROOMOBJ: 2;Sofa;10;VIS;50;RBOB2;327;95;-14;1;14; ;NPICK; ;8; ;LOW; ;a;it; 


/* DACT: condition;...;action;...
/*
/* Light up scene
DACT: IFRF 2=0;TRACK NO
DACT: IFRF 2=1;TRACK MUSIC.04.MOD,LOOP,NOFILTER
DACT: LIGHTS ON
DACT: IFRF 1=0;SAY Wow, nice one!;SAY A Hammy Electronics Hi-Fi System!;SAY At least this guy's taste in Hi-Fi is better than his taste in wallpaper!;SETRF 1=1;EXIT

/* LINE: dlgno;lineno;line;reuseline;conditions
/* LACT: dlgno;lineno;action;...;action



/* Room actions follow below ***
/*
/* ACTION: verbno;condition;...;command;...

/*
/* Action 0 - Exit
/* ===============
/*
ACTION: 0;IFOBJ 1;MEXIT;GOTO 9,2
ACTION: 0;IFOBJ 2;IFRF 2=0;MEXIT;HANDLE;W 50;HANDLE -1;SAY The door seems to be stuck.;EXIT
ACTION: 0;IFOBJ 2;IFRF 2=0;IFCARR 32;SAY The key doesn't help much, either.;EXIT
ACTION: 0;IFOBJ 2;IFRF 4=1;MEXIT;SAY Hey, the bass seems to have dislodged the door!;SETRF 4=0
ACTION: 0;IFOBJ 2;MEXIT;GOTO 12,1;EXIT

/*
/* Action 1 - Give
/* ===============
/*
/*
/* Action 2- Pick up
/* =================
/*
ACTION: 2;IFOBJ ROBJ1;SAY Much as I would love to have it...;SAY I don't feel like carrying a CD player round for the rest of the game.;EXIT
ACTION: 2;IFOBJ ROBJ2;VERB 9;REDO

/*
/* Action 3 - Use
/* ==============
/*

ACTION: 3;IFOBJ ROBJ1;IFRF 2=0;SAY I can't, there is no CD in it.;EXIT
ACTION: 3;IFOBJ ROBJ1;IFRF 2=1;SAY I'm not going to turn it off, I am enjoying the music!;EXIT
ACTION: 3;IFOBJ ROBJ2;SAY No thanks, I don't want to sit down.;SAY I'd rather have fun trying to finish this adventure!;EXIT
ACTION: 3;IFOBJ 10;IFOBJ2 ROBJ1;MOBJ ROBJ1;SETRF 2=1;SAY Okay, let's play some music!;HANDLE;W 50;HANDLE -1;TRACK MUSIC.04.MOD,LOOP,NOFILTER;SETRF 4=1;REMOVE 10,U,0;EXIT

ACTION: 3;IFOBJ 29;IFOBJ2 ROBJ1;SAY Come on! Put the tape in the CD player?!;SAY I don't really want to put it in the tape deck, either. It will probably clog up the tape heads with rust!;EXIT

/* Action 4 - Open
/* ===============
/*
ACTION: 4;IFOBJ ROBJ1;IFRF 2=1;SAY No thanks, I quite like this music!;EXIT
ACTION: 4;IFOBJ ROBJ1;IFNOTCARR 10;SAY No point in that- I have no CD to put in it.;EXIT
ACTION: 4;IFOBJ ROBJ1;SAY Well, why don't you try putting the CD in?;EXIT
ACTION: 4;IFOBJ ROBJ2;VERB 9;REDO


/* Action 5 - Talk to
/* ==================
/*

/*
/* Action 6 - Push
/* ===============
/*
ACTION: 6;IFOBJ ROBJ2;VERB 9;REDO

/*
/* Action 7 - Close
/* ================

ACTION: 7;IFOBJ ROBJ1;SAY The CD drawer is already shut.;EXIT

/*
/* Action 8 - Look at
/* ==================
/*
ACTION: 8;MOBJ
ACTION: 8;IFOBJ ROBJ1;SAY Hammy Electronics CD player.;SAY What a lovely machine!;EXIT
ACTION: 8;IFOBJ ROBJ2;SAY What a dodgy looking sofa.;EXIT
/*
/* Action 9 - Pull
/* ===============
/*
ACTION: 9;MOBJ
ACTION: 9;IFOBJ ROBJ2;IFRF 3=0;SAY Hey, there's a squeaky toy under here!;GET 14,U;SETRF 3=1;EXIT
ACTION: 9;IFOBJ ROBJ2;SAY I've already taken the squeaky toy, there is nothing else there.;EXIT

/* End of room file. Some comments must be here, otherwise  the last command
/* line is missed out!
