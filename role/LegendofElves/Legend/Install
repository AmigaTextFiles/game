; The Legend of the Elves (HD Version 1.5 and above) Installation Script
; (C) 1998/1999 Peter Armann

(complete 0)

(set #vernum (getversion))
(set #ver (/ #vernum 65536))
(set #vb (database "vblank"))

(If (= (cat #vb) 60)
   (abort "You need a PAL Amiga to run \"The Legend of the Elves\".\nSorry!")
)

(message (cat "Thank you for choosing to install \"The Legend of the Elves\".\n\n"
              "This script will guide you through the simple installation procedure.\n\n"  
              "Please make sure that your Amiga has the minimum requirement of 2Mb Chip Ram before proceeding.\n\n"
              "The game will run better with Fast Ram installed.\n"
              "We recommend 2-4Mb Fast Ram for optimum performance."
         )
)

(set #destdir
   (askdir
      (prompt "Please select the directory where the\n\"Legend of the Elves\" directory should be created")
      (help @askdir-help)
      (default "Work:")
   )
)

(If (< (getdiskspace #destdir) 2621440)
   (abort (cat "The drive that you have selected does not have enough free space.\n"
               "\"The Legend of the Elves\" needs just over 2Mb of Hard Disk Space.\n\n"
               "Please restart the Installation process and choose a different drive."
          )
   )
)

(set @default-dest (tackon #destdir "Elves"))
(if (not (exists (@default-dest)))
   (makedir @default-dest (infos))
)

(copyfiles
   (source "c/Elves.info")
   (dest #destdir)
)

(complete 2)

(makedir (tackon @default-dest "savegame"))

(copyfiles
   (source "Title")
   (infos)
   (dest @default-dest)
)

(complete 10)

(copyfiles
   (source "Eric")
   (infos)
   (dest @default-dest)
)

(complete 40)

(copyfiles
   (source "Legend.guide")
   (infos)
   (dest @default-dest)
)

(complete 50)

(copyfiles
   (source "")
   (dest @default-dest)
   (pattern "#?.iff")
)

(complete 55)

(copyfiles
   (source "")
   (dest @default-dest)
   (pattern "C#?")
)

(complete 57)

(copyfiles
   (source "")
   (dest @default-dest)
   (pattern "ODATA#?")
)

(complete 82)

(copyfiles
   (source "")
   (dest @default-dest)
   (pattern "BACKG#?")
)

(complete 97)

(copyfiles
   (source "hdversion")
   (dest @default-dest)
)

(complete 99)

(message (cat "Thank you for installing \"The Legend of the Elves\"\n\n"
              "1. Double click the \"Title\" icon to load the game\n"
              "and see the Introductory Animation first or\n"
              "2. Double click the \"Eric\" icon to load the game\n"
              "without the Introductory Animation\n\n"
              "Please remember to read the User Guide before playing the game\n\n"

              "Register your copy of \"The Legend of the Elves\" for free at\n"
              "http://www.ericelf.freeserve.co.uk/\n"
              "if you didn't download it from the official web site"
        )
)

; The following displays the User Guide according to OS Version
; If the user is running OS3.0 or above, Multiview (in it's default location) loads
; If the user is running an OS below 3.0, AmigaGuide (if it exists) laods

(makeassign "Eric" @default-dest)

(If (= @user-level 1)
   (working "Viewing User Guide...")
)

(If (not (exists "ENV:fromguide"))
   (   
      (If (< #ver 39)
         (If (exists "SYS:Utilities/AmigaGuide" (noreq))
            (run "SYS:Utilities/AmigaGuide Eric:Legend.guide"
               (prompt "Do you want to read the User Guide now?\n")
               (help (cat "The Installer will display the User Guide using AmigaGuide if you select \"Proceed\".\n"
                          "We recommend that you read the User Guide before running the game."
                     )
               )
               (confirm)
               (safe)
            )
         ) 
      )

      (If (> #ver 38)
         (If (exists "SYS:Utilities/Multiview" (noreq))
            (run "SYS:Utilities/Multiview Eric:Legend.guide"
               (prompt "Do you want to read the User Guide now?\n")
               (help (cat "The Installer will display the User Guide using Multiview if you select \"Proceed\".\n"
                          "We recommend that you read the User Guide before running the game."
                     )
               )
               (confirm)
               (safe)
            )
         )
      )
   )
)

(complete 100)
(exit)
