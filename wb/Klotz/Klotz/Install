; $VER: Install_Klotz
; ---------------------------------------------------------------------------

(procedure english (
    (set #version-error "This program requires OS 2.04 or better.")

    (set #installing "Installing ")
    (set #docs "documents")
    (set #icon "icon")
    (set #sfx  "sound effects")

    (set #welcome ( cat "This script will install the Klotz game on your system. "
			"If the lowlevel.library is present in the LIBS: directory"
			" a special version will be installed, if not you'll get "
			"the direct-hardware-access-version [ won't run on a Draco :-]\n"
			"\n  __      "
			"\n //_)     "
			"\n//__) WARE"
		)
    )
    (set #destdir-prompt "Please select the directory where you want Klotz installed.\n(No directory will be created.)")
    (set #askkeys-prompt "Do you want to use the keyboard-control?" )
    (set #asksfx-prompt "Do you want to have sound effects?" )
    (set #morekeys-prompt "Which key-control do you want to use?" )
    (set #opt0 "default cursor-keys" )
    (set #opt1 "number-block control" )
    (set #opt2 "function-keys" )
    (set #opt3 "space-drop mode" )
    (set #opt4 "left-hand mode" )

    (set #nohelp "No help available.")
    (set #destdir-help "The Klotz executable will be copied in this directory.\n")
    (set #askkeys-help "Klotz allows you to control the game by joystick/joypad or by keyboard. The latter is disabled by default. Select 'Yes' to enable it.")
    (set #morekey-help (cat "These are the key-settings:                   \n\n"
			    " Mode|default|numblock|F-keys|spacedrop|lefty|\n"
			    " Key +-------+--------+------+---------+-----+\n"
			    "left | left  |    4   |  F1  | left    |  a  |\n"
			    "right| right |    6   |  F10 | right   |  s  |\n"
			    "down | down  |    2   | space| space   |  z  |\n"
			    "rot L| space |    0   |  F2  | down    | TAB |\n"
			    "rot R| delete|    5   |  F9  |  up     |space|\n"
			    "-----+-------+--------+------+---------+-----+"
		    )
    )
    (set #asksfx-help (cat ("The sound effects are all located under the \"sfx\" directory. "
			    "These are 8SVX files. You can easily create your own sfx.")))

    (set #yes "Yes")
    (set #no  "No")
  )
)
; ---------------------------------------------------------------------------
(procedure deutsch (
    (set #version-error "Dieses Programm benötigt OS 2.04 oder besser.")

    (set #installing "Installiere ")
    (set #docs "Dokumente")
    (set #icon "Piktogramm")
    (set #sfx "Sound Effekte")

    (set #welcome ( cat "Diese Skript installiert das Spiel \"Klotz\"  auf Ihrem System. "
			"Gibt es die \"lowlevel.library\" im LIBS: Verzeichnis, so"
			" wird eine Spezialversion installiert, wenn nicht, erhalten Sie "
			"die Version, die direkt auf die Hardware zugreift.\n"
			"\n  __      "
			"\n //_)     "
			"\n//__) WARE"
		)
    )
    (set #destdir-prompt "Bitte wählen Sie das Verzeichnis,in dem Sie Klotz installieren möchten.\n(Es wird kein Verzeichnis erstellt.)")
    (set #askkeys-prompt "Möchten Sie die Tastatur-Steuerung benutzen?" )
    (set #asksfx-prompt "Möchten Sie Sound Effekte installieren?" )
    (set #morekeys-prompt "Welche Steuerungsmethode möchten Sie nutzen?" )
    (set #opt0 "Cursor-Tasten" )
    (set #opt1 "Nummernblock" )
    (set #opt2 "Funktionstasten" )
    (set #opt3 "Leertaste für Fall" )
    (set #opt4 "Linkshänder-Tasten" )

    (set #nohelp "Keine Hilfe vorhanden.")
    (set #destdir-help "Das Klotz-Programm wird in dieses Verzeichnis kopiert.\n")
    (set #askkeys-help "Klotz erlaubt Ihnen die Kontrolle via Joystick/Joypad oder Tastatur. Das letztere ist normalerweise ausgeschaltet. Wählen Sie 'Ja', um es zu aktivieren .")
    (set #morekey-help (cat "Folgende Steuerungen sind voreingestellt:        \n\n"
			    " Modus|Standard|Numblock|F-Tasten|LeerFall|Links|\n"
			    " Taste+--------+--------+--------+--------+-----+\n"
			    "links | left   |    4   |  F1    | left   |  a  |\n"
			    "rechts| right  |    6   |  F10   | right  |  s  |\n"
			    "runter| down   |    2   | space  | space  |  z  |\n"
			    "dreh L| space  |    0   |  F2    | down   | TAB |\n"
			    "dreh R| delete |    5   |  F9    |  up    |space|\n"
			    "------+--------+--------+--------+--------+-----+"
		    )
    )
    (set #asksfx-help (cat ("Die Sound Effekte befinden sich im \"sfx\" Verzeichnis. "
			    "Es sind 8SVX Dateien, so daß man leicht eigene Effekte verwenden kann.")))
    (set #yes "Ja")
    (set #no  "Nein")
  )
)
; ---------------------------------------------------------------------------
(complete 0)
; check for OS 2.0
(if (< (/ (getversion "exec.library" (resident)) 65536) 37)
    (abort #version-error)
)
; ---------------------------------------------------------------------------
; this is somewhat 'safer' than the @language line
(if (= ( getenv "language") "deutsch" ) (deutsch) (english) )
;(if ( = @language "deutsch" ) (deutsch) (english) )

(message #welcome)
(welcome)
(complete 0)

(set @default-dest
    (askdir
	(prompt #destdir-prompt)
	(help (cat #destdir-help @askdir-help))
	(default @default-dest)
    )
)

(if (exists "LIBS:lowlevel.library")
    (copyfiles
	(source "Klotz")
	(dest @default-dest)
	(prompt #installing "Klotz")
	(help #nohelp)
    )
    (copyfiles
	(source "Klotz_noLL")
	(dest @default-dest)
	(prompt #installing "Klotz")
	(newname "Klotz")
	(help #nohelp)
    )
)
(complete 50)

(copyfiles
    (source "Klotz.guide")
    (dest @default-dest)
    (help #nohelp)
    (prompt #installing #docs)
    (infos)
)
(complete 65)

(copyfiles
    (source "Klotz.info")
    (dest @default-dest)
    (help #nohelp)
    (prompt #installing #icon)
)
(complete 75)

(set usekeys (askbool (prompt #askkeys-prompt)
		      (help #askkeys-help)
		      (choices #yes #no)
	     )
)

(if usekeys
    (
	(set keynum (askchoice (prompt #morekeys-prompt)
			    (help #morekey-help)
			    (choices #opt0 #opt1 #opt2 #opt3 #opt4 )
		    )
	)
	(tooltype (settooltype "USEKEYS" "")
		(settooltype "KEYS" (select keynum "4f,4e,4d,40,46,19"
						   "2d,2f,1e,2e,0f,19"
						   "50,59,40,51,58,19"
						   "4f,4e,40,4d,4c,19"
						   "20,21,31,42,40,19"
				    )
		)
		(dest (tackon @default-dest "Klotz") )
	)
    )
)
(complete 85)

(set usesfx (askbool (prompt #asksfx-prompt)
		     (help #asksfx-help)
		     (choices #yes #no)
	    )
)
(if usesfx ((message "bla") (copyfiles (source "sfx")
		      (dest (tackon @default-dest "sfx") )
		      (all)
		      (help #no-help)
		      (prompt #installing #sfx)
	   ))
	   ((message blubb) (tooltype (settooltype "NOSOUND" "")
		     (dest (tackon @default-dest "Klotz"))
	   ))
)
(complete 100)
