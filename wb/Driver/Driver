Skip Runned

Lab Loop
Set CS `Echo FIRST 2 $CS`
IF "x" eq "$CS"
Set Rand `Eval $Rand + 13 * $Rand + 9699691 mod 342210277`
Set C `Eval $Rand mod 4 + 4 mod 4 + 3`
Set CS `Echo LEN $C "xxxxxxxxxxxxxxx"`
Set D `Eval $Rand mod 3 + 3 mod 3 - 1`
EndIF

Set X `Eval $X+$D`
IF 10 gt $X VAL
Set X 10
Set D 1
Else
IF $X gt 44 VAL
Set X 44
Set D -1
EndIF
EndIF

Set MyD `GetENV RAM:Auto-Direction`
Set MyX `Eval $MyX+$MyD`
Echo NOLINE FIRST 1 LEN $X "                                             "
Set Col `Echo FIRST 1 LEN 2 $His`
Set His `Echo FIRST 3 $His$X`
Set Pom "          `Echo LEN $Col "                                             HHH    HHH"`"
Echo "HHH    HHH*e[2;${MyX}HO*e[20;1H"
Set road `eval $road + 1`
IF NOT EXISTS RAM:Auto-Brk
IF NOT "`Echo FIRST $MyX "$Pom"`" gt "H"
Skip Loop BACK
EndIF
EndIF

Echo "*e[1;${MyX}H*e[2D==O==*e[4D*e[B/|\"
Wait
Echo "*ec*e[6;16HWell, you did $road meters."
Set rating "Braindead !!"
IF $road gt 25 VAL
Set rating "Poor boy !"
EndIF
IF $road gt 100 VAL
Set rating "Well !"
EndIF
IF $road gt 300 VAL
Set rating "Great ! Try on faster computer !"
EndIF

IF EXISTS RAM:AUTO-Brk
RequestChoice "The results ..." "$rating*nYou got $road points." "Quit now"
Else
IF "1" eq "`RequestChoice "The results ..." "$rating*nYou got $road points." "Drive again !" "Quit this shit"`"
Skip Start2
EndIF
EndIF

Echo >RAM:Auto-Break "Driver"
UnsetENV RAM:AUTO-LOG
UnsetENV RAM:Auto-Brk
UnsetENV RAM:Auto-Control
Resident Eval REMOVE
EndCLI

Lab Runned
FailAt 21
UnsetENV RAM:Auto-Break
UnsetENV RAM:Auto-Brk
Set NUM `GetENV RAM:AUTO-LOG`
IF "$NUM" eq "1"
Skip Init
Else
IF "$NUM" eq "0"
SetENV RAM:AUTO-LOG 1
Skip Start
Else
Lab Init
SetENV RAM:AUTO-LOG 0
Which >NIL: Driver
IF "$RC" eq "0"
Echo >RAM:Auto-Direction 0
Set t1 "IF >NIL: ***$Pom eq q"
Set t2 "Echo >RAM:Auto-Brk Driver"
Set t3 "EndCLI"
Set t4 "EndIF"
Set t7 RAM:Auto-Slave
Echo >$t7 "FailAt 21"
Echo >>$t7 "ChangeTaskPri 3"
Echo >>$t7 "Echo *"*ecDrive with <ENTER>**nQuit with q<ENTER>*""
Echo >>$t7 "LAB Loop"
Echo >>$t7 "Echo *" | |   *" NOLINE"
Echo >>$t7 "Set >NIL: Pom ?"
Echo >>$t7 "Echo >RAM:Auto-Direction 1"
Echo >>$t7 "$t1*n$t2*n$t3*n$t4*n"
Echo >>$t7 "Echo *"  \ \  *" NOLINE"
Echo >>$t7 "Set >NIL: Pom ?"
Echo >>$t7 "Echo >RAM:Auto-Direction 0"
Echo >>$t7 "$t1*n$t2*n$t3*n$t4*n"
Echo >>$t7 "Echo *"   | | *" NOLINE"
Echo >>$t7 "Set >NIL: Pom ?"
Echo >>$t7 "Echo >RAM:Auto-Direction -1"
Echo >>$t7 "$t1*n$t2*n$t3*n$t4*n"
Echo >>$t7 "Echo *"  / /  *" NOLINE"
Echo >>$t7 "Set >NIL: Pom ?"
Echo >>$t7 "Echo >RAM:Auto-Direction 0"
Echo >>$t7 "$t1*n$t2*n$t3*n$t4*n"
Echo >>$t7 "Skip Loop BACK"
NewShell FROM "`Which Driver`" CON:20/50/448/164//NOSIZE
NewShell FROM $t7  "CON:300/0/240/30/Control/NOSIZE"
Else
UnsetENV RAM:AUTO-LOG
Echo "I can not locate myself !*nPlace me somewhere in the path and set me the 's' flag !"
EndIF
Quit
EndIF
EndIF
Lab Start
RequestChoice >NIL: "The Driver" "DRIVER - Pure Shell !*n------------------------*nUse the <RETURN> key in the control*nwindow to alternate the direction*nof your O'car !*n*nMade by Harry '97*nPublic domain*nEMail: pmar5303@menza.mff.cuni.cz" "I Will"
Lab Start2
SetFont topaz SIZE 8
echo "*e[1;1H*e[J*e[4x*e[2y" NOLINE
echo "*e[>3m*e[31;43m*E[0;0H*E[J" NOLINE
Set MyX 32
Set X 27
Set Rand 79797
Set C 10
Set CS "xxxxxxxxxx"
Set D 1
Resident C:Eval PURE
Set His "272727272727272727272727272727272727"
Set t1 "                           HHH    HHH"
Set road 0
Echo "$t1*n$t1*n$t1*n$t1*n$t1"
Echo "$t1*n$t1*n$t1*n$t1*n$t1"
Echo "$t1*n$t1*n$t1*n$t1*n$t1"
Echo "$t1*n$t1*n$t1*n$t1*n$t1"
Skip Loop BACK

;$VER: Driver game 1.0 (20.2.97)
