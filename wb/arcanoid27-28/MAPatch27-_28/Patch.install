; MiniArcanoid Installer script

(procedure P_PATCHFILE #old #new #patch
  (
    (set #err
      (run ("patch \"%s\" \"%s\" \"%s\" QUIET" #old #new #patch)) 
    )
    (if (<> #err 0)
      (abort
        (select (- #err 10)
          "Not enough memory"
          "Read error"
          "Wrong version of patch command\nor something strange has happened"
          "*** BREAK ***"
          ("Couldn't write file \"%s\"" #new)
          ("Wrong \"%s\" file format" #patch)
          ("Wrong \"%s\" file size.\nProbably it's incorrect version." #old)
          ("Wrong \"%s\" file checksum.\nProbably the file has been altered." #old)
        )
      )
    )
  )
)

(welcome)

(set #destdir
	(askdir
		(prompt "Select where are your MiniArcanoid files")
		(help @askdir-help)
		(default @default-dest)
	)
)

(set #destexe (tackon #destdir "MiniArcanoid"))
(set #destguide (tackon #destdir "MiniArcanoid.guide"))

(working "Installing patches...")
(if
  (not (exists #destexe))
  (abort "MiniArcanoid executable not found!")
)
(P_PATCHFILE #destexe #destexe "MiniArcanoid.pch")

(if
  (not (exists #destguide))
  (abort "MiniArcanoid guide file not found!")
)
(P_PATCHFILE #destguide #destguide "MiniArcanoid.guide.pch")
(exit)
