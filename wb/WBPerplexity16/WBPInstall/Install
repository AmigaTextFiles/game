; WBPerplexity v1.6 Installer Script
; $Ver: WBP-Ins1.6 (16/04/2000)
; Written by Matthew Briggs
; Copyright © Engine Nine Design

; .-------------------.
; | Set text messages |
; `-------------------'

(set @default-dest "Games:")

(set #welcome
   (cat "\nWelcome to Workbench Perplexity!\n"
        "\nYou are about to install WBPerplexity v1.6, one of"
        "\nthe best workbench games available for the Amiga!"
        "\nThis is no average fifteen game and features three"
        "\ndifferent modes of play (3x3, 4x4 & 5x5), sounds,"
        "\nanimated puzzles, datatype support, image scaling\nand much much more!"
        "\n\nIf you need assistance during this installation,"
        "\nsimply click the \"Help\" gadget."
        "\n\nClick Proceed to continue..."))

(set #terms
   (cat "\nTerms and conditions\n"
        "\nEngine Nine Design will not accept any responsibility"
        "\nfor any disruption, damage and/or loss to your data"
        "\nor computer system that may occur whilst using this"
        "\nsoftware. This software is classed as shareware and"
        "\nyou may use it for a limited period of 30 days for"
        "\nevaluation purposes; After which you are asked to"
        "\neither stop using it or register by sending a small"
        "\nfee of £5.00 to the author. Please read the"
        "\nadditional documentation for further details."
        "\n\nClick Proceed if you agree with these terms."))

(set #welcome-message
   (cat "\nWBPerplexity ©1999-2000 Engine Nine Design\nhttp://www.engine9design.co.uk"))

(set #install-which
   (cat "\nWhich WBPerplexity components do"
        "\nyou wish to install?\n"))

(set #install-langs
   (cat "\nWould you like to install any of the following catalogs?"
        "\n(English language is built in)\n"))

(set #install-icons
   (cat "\nWhich icon set would you like"
        "\nto use for WBPerplexity?\n"))

(set #installto
   (cat "\nWhere would you like to install WBPerplexity?"
        "\nA directory named WBPerplexity will be created.\n"))

(set #exit-opts
   (cat "\nWould you like to run WBPerplexity"
        "\nand/or view the documentation now?\n"))

(set #installfonts
   (cat "\nWhere would you like to copy"
        "\nthe WBPerplexity fonts to?\n"))

(set #installplay16
   (cat "\nWhere would you like to install"
        "\nthe Play16 Sound Player to?\n"))

(set #exit-message
   (cat "\nInstallation complete!\n"
        "\nThank you for installing WBPerplexity."
        "\nWe hope you enjoy this game! Please"
        "\ndon't forget to visit us online at:"
        "\n\nhttp://www.engine9design.co.uk\n"
        "\nAdditional puzzle sets for WBPerplexity"
        "\ncan be found on the Engine Nine Design"
        "\nweb site and from aminet."))

; .--------------------------.
; | Display welcome messages |
; `--------------------------'

(complete 0)
(message #welcome (all))
(message #terms (all))
(welcome #welcome-message)

; .---------------------------.
; | Select install components |
; `---------------------------'

(complete 0)
(set InstallOpt
      (askoptions (prompt #install-which)
                  (help @askoptions-help)
                  (choices "WBPerplexity" "Guides" "Puzzle sets" "Sounds" "Sample Games" "Fonts" "Extras" "Play16 Sound Player")
                  (default 127)
      )
)

; .----------------------------.
; | Select language components |
; `----------------------------'

(set LangOpt
  (askoptions (prompt #install-langs)
                (help @askoptions-help)
                (choices "Català" "Deutsch" "Español" "Français" "Italiano" "Nederlands" "Norsk" "Polski")
                  (default 0)
      )
)

; .------------------------.
; | Select Icon components |
; `------------------------'

(set IconOpt
  (askchoice
    (prompt #install-icons)
    (help @askoptions-help)
    (choices "New Icons" "Magic WB")
    (default 0)
  )
)

; .----------------------------------.
; | Select Location + Make Directory |
; `----------------------------------'

((set destdir
   (askdir
      (prompt #installto)
         (help @askdir-help)
         (default "Ram:")
      )
   )
)

  (set destdir (tackon destdir "WBPerplexity"))
  (set destpuzzles (tackon destdir "Puzzles"))
  (set destsounds (tackon destdir "Sounds"))
  (set destgames (tackon destdir "Games"))
  (set destextras (tackon destdir "Extras"))
  (set destcats (tackon destdir "Catalogs"))
  (set destcatsinfo (tackon destdir "Catalogs.info"))
  (set destinfo (tackon destdir "/"))
  (set destwbp (tackon destdir "WBP"))
  (set destguide (tackon destdir "Guide"))


  (makedir destdir
  )
(if (= IconOpt 0)
  (copyfiles
    (source "InstallFiles/NewIcons.info")
    (dest destinfo)
    (newname "WBPerplexity.info")
  )
)

(if (= IconOpt 1)
  (copyfiles
    (source "InstallFiles/MagicWB.info")
    (dest destinfo)
    (newname "WBPerplexity.info")
  )
)

(complete 10)

; .----------------------.
; | Install WBPerplexity |
; `----------------------'

(if (BITAND InstallOpt 1)
  (copyfiles
    (source "WBP")
    (dest destdir)
  )
)

(if (AND(BITAND InstallOpt 1) (= IconOpt 0))
  (copyfiles
   (source "InstallFiles/NewIcons/WBP.info")
   (dest destdir)
   (infos)
  )
)

(if (AND(BITAND InstallOpt 1) (= IconOpt 1))
  (copyfiles
   (source "InstallFiles/MagicWB/WBP.info")
   (dest destdir)
   (infos)
  )
)

(complete 20)

; .----------------.
; | Install Guides |
; `----------------'

(if (BITAND InstallOpt 2)
  (copyfiles
    (source "Readme")
    (dest destdir)
  )
)

(if (BITAND InstallOpt 2)
  (copyfiles
    (source "Guide")
    (dest destdir)
  )
)

(if (AND(BITAND InstallOpt 2) (= IconOpt 0))
  (copyfiles
   (source "InstallFiles/NewIcons/Guide.info")
   (dest destdir)
   (infos)
  )
)

(if (AND(BITAND InstallOpt 2) (= IconOpt 1))
  (copyfiles
   (source "InstallFiles/MagicWB/Guide.info")
   (dest destdir)
   (infos)
  )
)

(complete 30)

; .---------------------.
; | Install Puzzle Sets |
; `---------------------'

(if (BITAND InstallOpt 4)
  (makedir destpuzzles
  )
)

(if (AND(BITAND InstallOpt 4) (= IconOpt 0))
  (copyfiles
   (source "InstallFiles/NewIcons/Puzzles.info")
   (dest destdir)
   (infos)
  )
)

(if (AND(BITAND InstallOpt 4) (= IconOpt 1))
  (copyfiles
   (source "InstallFiles/MagicWB/Puzzles.info")
   (dest destdir)
   (infos)
  )
)

(if (BITAND InstallOpt 4)
  (copyfiles
   (source "Puzzles/")
   (dest destpuzzles)
   (all)
   (infos)
  )
)

(complete 40)

; .----------------.
; | Install Sounds |
; `----------------'

(if (BITAND InstallOpt 8)
  (makedir destsounds
  )
)

(if (AND(BITAND InstallOpt 8) (= IconOpt 0))
  (copyfiles
   (source "InstallFiles/NewIcons/Sounds.info")
   (dest destdir)
   (infos)
  )
)

(if (AND(BITAND InstallOpt 8) (= IconOpt 1))
  (copyfiles
   (source "InstallFiles/MagicWB/Sounds.info")
   (dest destdir)
   (infos)
  )
)

(if (BITAND InstallOpt 8)
  (copyfiles
   (source "Sounds/")
   (dest destsounds)
   (all)
   (infos)
  )
)

(complete 50)

; .---------------.
; | Install Games |
; `---------------'

(if (BITAND InstallOpt 16)
  (makedir destgames
  )
)

(if (AND(BITAND InstallOpt 16) (= IconOpt 0))
  (copyfiles
   (source "InstallFiles/NewIcons/Games.info")
   (dest destdir)
   (infos)
  )
)

(if (AND(BITAND InstallOpt 16) (= IconOpt 1))
  (copyfiles
   (source "InstallFiles/MagicWB/Games.info")
   (dest destdir)
   (infos)
  )
)

(if (BITAND InstallOpt 16)
  (copyfiles
   (source "Games/")
   (dest destgames)
   (all)
   (infos)
  )
)

(complete 60)

; .---------------.
; | Install Fonts |
; `---------------'

(if (BITAND InstallOpt 32)
((set destfonts
   (askdir
      (prompt #installfonts)
         (help @askdir-help)
         (default "Sys:Fonts")
      )
   )
)
)

(if (BITAND InstallOpt 32)
  (copyfiles
   (source "fonts/")
   (dest destfonts)
   (all)
  )
)

(complete 70)

; .----------------.
; | Install Extras |
; `----------------'

(if (BITAND InstallOpt 64)
  (makedir destextras
  )
)

(if (AND(BITAND InstallOpt 64) (= IconOpt 0))
  (copyfiles
   (source "InstallFiles/NewIcons/Extras.info")
   (dest destdir)
   (infos)
  )
)

(if (AND(BITAND InstallOpt 64) (= IconOpt 1))
  (copyfiles
   (source "InstallFiles/MagicWB/Extras.info")
   (dest destdir)
   (infos)
  )
)

(if (BITAND InstallOpt 64)
  (copyfiles
   (source "Extras/")
   (dest destextras)
   (all)
   (infos)
  )
)

(complete 80)

; .----------------.
; | Install Play16 |
; `----------------'

(if (BITAND InstallOpt 128)
((set destplay16
   (askdir
      (prompt #installplay16)
         (help @askdir-help)
         (default "Sys:c")
      )
   )
)
)

(if (BITAND InstallOpt 128)
  (copyfiles
   (source "InstallFiles/Play16/Play16")
   (dest destplay16)
   (infos)
  )
)

; .------------------------------.
; | Install Additional Languages |
; `------------------------------'

; ---> Català
(if (BITAND LangOpt 1)
  (if (< (exists ("Sys:Locale/Catalogs/català")) 2) (makedir "Sys:Locale/Catalogs/català"))
)
(if (BITAND LangOpt 1)
 (copyfiles
  (source "Catalogs/català/WBPerplexity.catalog")
  (dest "Locale:Catalogs/català")
  (infos)
 )
)

; ---> German
(if (BITAND LangOpt 2)
  (if (< (exists ("Sys:Locale/Catalogs/deutsch")) 2) (makedir "Sys:Locale/Catalogs/deutsch"))
)
(if (BITAND LangOpt 2)
 (copyfiles
  (source "Catalogs/deutsch/WBPerplexity.catalog")
  (dest "Locale:Catalogs/deutsch")
  (infos)
 )
)

; ---> Spanish
(if (BITAND LangOpt 4)
  (if (< (exists ("Sys:Locale/Catalogs/español")) 2) (makedir "Sys:Locale/Catalogs/español"))
)
(if (BITAND LangOpt 4)
 (copyfiles
  (source "Catalogs/español/WBPerplexity.catalog")
  (dest "Locale:Catalogs/español")
  (infos)
 )
)

; ---> French
(if (BITAND LangOpt 8)
  (if (< (exists ("Sys:Locale/Catalogs/français")) 2) (makedir "Sys:Locale/Catalogs/français"))
)
(if (BITAND LangOpt 8)
 (copyfiles
  (source "Catalogs/français/WBPerplexity.catalog")
  (dest "Locale:Catalogs/français")
  (infos)
 )
)

; ---> Italian
(if (BITAND LangOpt 16)
  (if (< (exists ("Sys:Locale/Catalogs/italiano")) 2) (makedir "Sys:Locale/Catalogs/italiano"))
)
(if (BITAND LangOpt 16)
 (copyfiles
  (source "Catalogs/Italiano/WBPerplexity.catalog")
  (dest "Locale:Catalogs/italiano")
  (infos)
 )
)

; ---> Dutch
(if (BITAND LangOpt 32)
  (if (< (exists ("Sys:Locale/Catalogs/nederlands")) 2) (makedir "Sys:Locale/Catalogs/nederlands"))
)
(if (BITAND LangOpt 32)
 (copyfiles
  (source "Catalogs/nederlands/WBPerplexity.catalog")
  (dest "Locale:Catalogs/nederlands")
  (infos)
 )
)

; ---> Norwegian
(if (BITAND LangOpt 64)
  (if (< (exists ("Sys:Locale/Catalogs/norsk")) 2) (makedir "Sys:Locale/Catalogs/norsk"))
)
(if (BITAND LangOpt 64)
 (copyfiles
  (source "Catalogs/norsk/WBPerplexity.catalog")
  (dest "Locale:Catalogs/norsk")
  (infos)
 )
)

; ---> Polish
(if (BITAND LangOpt 128)
  (if (< (exists ("Sys:Locale/Catalogs/polski")) 2) (makedir "Sys:Locale/Catalogs/polski"))
)
(if (BITAND LangOpt 128)
 (copyfiles
  (source "Catalogs/polski/WBPerplexity.catalog")
  (dest "Locale:Catalogs/polski")
  (infos)
 )
)

(complete 90)

; .---------------------------.
; | Copy Keyfile (If Present) |
; `---------------------------'

(if (= (exists ("WBPerplexity.key")) 2) (copyfiles
    (source "WBPerplexity.key")
    (dest "Sys:L")
))

; .-----------------------------.
; | Add assign to user start-up |
; `-----------------------------'

(set #startup-change
   (cat "\nWe recommend using an assign to locate WBPerplexity."
        "\nDo you wish to add the following line to your"
        "\nS:user-startup file?"
        "\n\nAssign WBPerplexity: "destdir""))

((startup "WBPerplexity"
  (prompt #startup-change)
  (command "if exists "destdir"\n  Assign WBPerplexity: "destdir"\nendif")
  (help @startup-help)
))

; .----------.
; | Finalize |
; `----------'

(complete 100)

(if (BITAND InstallOpt 1)
  (set FinishOpt
    (askoptions (prompt #exit-opts)
      (help @askoptions-help)
      (choices "Run WBPerplexity" "View Guide")
      (default 3)
    )
  )
)

(if (BITAND FinishOpt 1)
  (run "run <NIL: >NIL: " destwbp)
)

(if (BITAND FinishOpt 2)
  (run "run <NIL: >NIL: Multiview " destguide)
)

(message #exit-message (all))
(exit (quiet))

