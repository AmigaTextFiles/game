G4C


;xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
;  $VER: AmiKniffel v1.17 by Nightshade (23.03.2002)
;xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx


WINBIG -1 -1 420 220 "AmiKniffel"
WinType 11010000
winfont thinpaz.font 8 000
winbackground image pat1 0
Screen Workbench
varpath "AmiKnPrefsPix"


xOnLoad
	prog = AmiKniffel
	vers = 1.17
	date = 23.03.2002
	name = Nightshade
	mail = thenightshade@freenet.de

	loadimage $$this.path\gfx/startpic startpic *

	guiload AmiKnLaden

	guiopen AmiKnLaden

	setscreentitle AmiKnLaden "Bitte warten, lade $prog ..."

	setattr AmiKnLaden/1 title "2/1/shadow/center/below/Lade Bilder..."

	redraw AmiKnLaden 

	bild1 = $$this.path\gfx/MarbleDark
	bild2 = $$this.path\gfx/TissueDark
	bild3 = $$this.path\gfx/MarbleBright

	update AmiKnLaden 1 10

	delay 20

	würfel = $$this.path\gfx/würfel_1/

	vor_bild1 = $bild1
	vor_bild2 = $bild2
	vor_bild3 = $bild3
	vor_würfel = $würfel

	update AmiKnLaden 1 40

	delay 20

	ifexists file S:AmiKniffel.pix
		guiload S:AmiKniffel.pix
	endif

	update AmiKnLaden 1 50

	delay 20

	gosub #this lade_bilder

	update AmiKnLaden 1 60

	delay 20

	gosub #this lade_bilder1

	update AmiKnLaden 1 100

	delay 20

	setattr AmiKnLaden/1 title "2/1/shadow/center/below/Lade Töne..."

	redraw AmiKnLaden 

	update AmiKnLaden 1 0

	delay 20

	sfx1 = $$this.path\Samp/Click1
	sfx2 = $$this.path\Samp/Click2
	sfx3 = $$this.path\Samp/Click3
	startsnd = $$this.path\Samp/On

	update AmiKnLaden 1 10

	delay 20

	vor_sfx1 = $sfx1
	vor_sfx2 = $sfx2
	vor_sfx3 = $sfx3

	vor_startsnd = $startsnd

	update AmiKnLaden 1 40

	delay 20

	ifexists file S:AmiKniffel.sounds
		guiload S:AmiKniffel.sounds
	endif

	update AmiKnLaden 1 50

	delay 20

	gosub #this lade_sounds

	update AmiKnLaden 1 100

	delay 20

	setattr AmiKnLaden/1 title "2/1/shadow/center/below/Lade Guis..."

	redraw AmiKnLaden

	update AmiKnLaden 1 0

	guiload AmiKnName
	guiload AmiKnHiScores
	guiload AmiKnPrefsPix
	guiload AmiKnViewBG
	guiload AmiKnViewDices
	guiload AmiKnPrefsSnd
	guiload AmiKnAbout

	update AmiKnLaden 1 60

	delay 20

	ifexists file ~S:AmiKniffel.HiScores
		gosub AmiKnHiScores clear
	else
		gosub AmiKnHiScores load
	endif

	update AmiKnLaden 1 100

	delay 20

	setattr AmiKnLaden/1 title "2/1/shadow/center/below/Öffne AmiKniffel..."

	redraw AmiKnLaden

	delay 10

	guiclose AmiKnLaden

	GuiOpen #This

	gosub #this set_default

	gosub #this start

	playsound startsnd


xonopen
	set margins 5/0/5
	set tbmargins 5/2

	setscreentitle #this "$prog v$vers ($date) - ©2002 by $name ($mail)"

	setwintitle #this "$prog v$vers - ©2002 $name"

	if $listopen = yes
		guiopen AmiKnList
	endif


xonclose
	ifexists window AmiKnList
		guiclose AmiKnList
		listopen = yes
	else
		listopen = no
	endif

	ifexists window ~AmiKnPrefsPix
	andifexists window ~AmiKnPrefsSnd
	and $ende != 1
		setgad #this 99 on
	endif


xonquit
	set buffer 1000

	guiclose #this

	update AmiKnLaden 1 0

	guiopen AmiKnLaden

	setscreentitle AmiKnLaden "Bitte warten, schliesse $prog ..."

	setattr AmiKnLaden/1 title "2/1/shadow/center/below/Gebe Bilder frei..."

	redraw AmiKnLaden

	update AmiKnLaden 1 10

	delay 40

	freeimage pat1
	freeimage pat2
	freeimage pat3

	update AmiKnLaden 1 50

	delay 40

	freeimage 1
	freeimage 2
	freeimage 3
	freeimage 4
	freeimage 5
	freeimage 6

	update AmiKnLaden 1 100

	delay 40

	setattr AmiKnLaden/1 title "2/1/shadow/center/below/Gebe Töne frei..."

	redraw AmiKnLaden

	update AmiKnLaden 1 10

	delay 40

	freesound sfx1
	freesound sfx2
	freesound sfx3

	update AmiKnLaden 1 80

	delay 40

	freesound startsnd

	update AmiKnLaden 1 100

	delay 40

	setattr AmiKnLaden/1 title "2/1/shadow/center/below/Guis beenden..."

	redraw AmiKnLaden

	update AmiKnLaden 1 0

	delay 40

	guiquit AmiKnName
	guiquit AmiKnHiScores

	update AmiKnLaden 1 20

	delay 40

	guiquit AmiKnPrefsPix
	guiquit AmiKnViewBG
	guiquit AmiKnViewDices

	update AmiKnLaden 1 60

	delay 40

	guiquit AmiKnPrefsSnd

	update AmiKnLaden 1 100

	setattr AmiKnLaden/1 title "2/1/shadow/center/below/Sichere Bestenliste..."

	redraw AmiKnLaden

	delay 40

	update AmiKnLaden 1 10

	copy T:AmiKn.temp S:AmiKniffel.HiScores

	update AmiKnLaden 1 50

	delete T:AmiKn.temp
	delete ENV:.amikninfo

	update AmiKnLaden 1 100

	setattr AmiKnLaden/1 title "2/1/shadow/center/below/Auf Wiedersehen!"

	redraw AmiKnLaden

	delay 40

	guiquit AmiKnLaden


xonreload
	guiwindow #this front
	guiwindow #this on


xroutine invert_dices
	if $game != 1
	or $dice != 1
	or $hide != 1
		flash
		stop
	endif

	playsound sfx1

	if $w1 = 0
		w1 = 1
		update #this 11 Nö
	else
		w1 = 0
		update #this 11 Ja
	endif

	if $w2 = 0
		w2 = 1
		update #this 12 Nö
	else
		w2 = 0
		update #this 12 Ja
	endif

	if $w3 = 0
		w3 = 1
		update #this 13 Nö
	else
		w3 = 0
		update #this 13 Ja
	endif

	if $w4 = 0
		w4 = 1
		update #this 14 Nö
	else
		w4 = 0
		update #this 14 Ja
	endif

	if $w5 = 0
		w5 = 1
		update #this 15 Nö
	else
		w5 = 0
		update #this 15 Ja
	endif

xroutine info
	guiwindow #this wait

	setvar info "1er: $s1\n2er: $s2\n3er: $s3\n4er: $s4\n5er: $s5\n6er: $s6\n"
	if $bc = 0
		bonus = 0
		summe = $o
	elseif $bc = 1
		bonus = 35
		summe = $($o - $bonus)
	endif
	appvar info "============================\nOben: $summe\n"
	appvar info "============================\nBonus: $bonus\n"
	appvar info "============================\nGesamter oberer Teil: $o\n"
	appvar info "----------------------------\n"
	appvar info "Dreierpasch: $dpasch\nViererpasch: $vpasch\n"
	appvar info "Full-House: $fh\nKleine Straße: $ks\nGroße Straße: $gs\n"
	appvar info "Kniffel: $kn\nChance: $chn\n============================\n"
	appvar info "Gesamter unterer Teil: $u\n============================\n"
	appvar info "Punkte insgesamt: $punkte"
	.amikninfo = $info
	delvar info

	lvuse #this 50

	if $lv.total > 0
		lvclear
	endif

	lvchange env:.amikninfo

	lvgo first
	update #this 50 0

	partredraw #this 0 0 0 0

	guiwindow #this resume


XLISTVIEW 217 12 190 196 "" "" "" 0 txt
	gadid 50


xonkey #R
	if $game != 1
	or $dice != 1
	or $hide != 1
		stop
	endif

	if $$rawkey.code = 80
	or $$rawkey.code = 85
		gosub #this set_w1
	elseif $$rawkey.code = 81
	or $$rawkey.code = 86
		gosub #this set_w2
	elseif $$rawkey.code = 82
	or $$rawkey.code = 87
		gosub #this set_w3
	elseif $$rawkey.code = 83
	or $$rawkey.code = 88
		gosub #this set_w4
	elseif $$rawkey.code = 84
	or $$rawkey.code = 89
		gosub #this set_w5
	else
		stop
	endif


xappicon 0 0 :AmiKniffel (AmiKniffel) "" off
gadid 99
	guiopen #this
	setgad #this 99 off


xroutine lade_sounds
	loadsound $sfx1 sfx1
	loadsound $sfx2 sfx2
	loadsound $sfx3 sfx3
	loadsound $startsnd startsnd

xroutine lade_bilder
	loadimage $bild1 pat1 *
	loadimage $bild2 pat2 *
	loadimage $bild3 pat3 *

xroutine lade_bilder1
	loadimage $würfel\1 1 *
	loadimage $würfel\2 2 *
	loadimage $würfel\3 3 *
	loadimage $würfel\4 4 *
	loadimage $würfel\5 5 *
	loadimage $würfel\6 6 *

xroutine set_default
	delvar set
	delvar oldset

	summe = 0

	w1 = 0
	w2 = 0
	w3 = 0
	w4 = 0
	w5 = 0

	s1 = 0
	s2 = 0
	s3 = 0
	s4 = 0
	s5 = 0
	s6 = 0

	bonus = 0

	dpasch = 0
	vpasch = 0

	fh = 0
	ks = 0
	gs = 0
	kn = 0

	chn = 0

	o = 0
	u = 0

	bc = 0
	oldbc = 0

	counter = 0
	cnt = 0
	oldcnt = 0

	punkte = 0
	game = 1
	undo = 0
	id = 0

	gosub #this reset1

	setgad #this 20 hide
	setgad #this 10 on
	setgad #this 10 show
	setgad #this 21/25 hide

	partredraw #this 0 0 0 0


IMAGE 31 10 ""
	gadid 1

IMAGE 61 10 ""
	gadid 2

IMAGE 91 10 ""
	gadid 3

IMAGE 121 10 ""
	gadid 4

IMAGE 151 10 ""
	gadid 5


BOX -1 -1 0 0 out button
BOX 27 6 155 73 out button
BOX 5 97 200 2 in button
BOX 210 5 205 210 out icondrop


XBUTTON 27 79 155 15 "Los geht's..."
gadid 20
gadkey #32
	if $game = 1
		stop
	endif

	gosub #this neu


XBUTTON 27 79 155 15 "Würfeln ..."
gadid 10
gadkey #13
	if $game != 1
		stop
	endif

	gosub #this wuerfeln


xroutine wuerfeln
	update #this 30 "Bitte warten!"

	delvar undo
	dice = 1

	if $cnt < $dicecounter
		if $hide != 1
			setgad #this 100/112 on
			hide = 1
		endif

		if $cnt = 0
			gosub #this reset
			setgad #this 21/25 show
		endif
		
		gosub #this dice

		++cnt
	else
		gosub #this dice

		setgad #this 10 off
		setgad #this 21/25 hide

		dice = 0
	endif

	update #this 30 "Bitte wählen..."

	partredraw #this 0 0 0 0

xroutine reset
	delvar gadid

	w1 = 0
	w2 = 0
	w3 = 0
	w4 = 0
	w5 = 0

	update #this 11 Ja
	update #this 12 Ja
	update #this 13 Ja
	update #this 14 Ja
	update #this 15 Ja

xroutine reset1
	changeimage #this 1 -1 -1 1
	changeimage #this 2 -1 -1 2
	changeimage #this 3 -1 -1 3
	changeimage #this 4 -1 -1 4
	changeimage #this 5 -1 -1 5

	update #this 11 " -"
	update #this 12 " -"
	update #this 13 " -"
	update #this 14 " -"
	update #this 15 " -"

	setgad #this 100/113 off

	update #this 100 0
	update #this 101 0
	update #this 102 0
	update #this 103 0
	update #this 104 0
	update #this 105 0
	update #this 106 0
	update #this 107 0
	update #this 108 0
	update #this 109 0
	update #this 110 0
	update #this 111 0
	update #this 112 0

xroutine dice
	guiwindow #this wait

	dpasch1 = 0
	dpasch2 = 0
	dpasch3 = 0
	dpasch4 = 0
	dpasch5 = 0
	dpasch6 = 0

	vpasch1 = 0
	vpasch2 = 0
	vpasch3 = 0
	vpasch4 = 0
	vpasch5 = 0
	vpasch6 = 0

	d1 = 0
	d2 = 0
	d3 = 0
	d4 = 0
	d5 = 0
	d6 = 0

	f1 = 0
	f2 = 0
	f3 = 0
	f4 = 0
	f5 = 0
	f6 = 0

	v1 = 0
	v2 = 0
	v3 = 0
	v4 = 0
	v5 = 0
	v6 = 0

	c = 0

	while $c < 10
		playsound sfx3

		if $w1 = 0
			1 = $($$rand.5 + 1)
			changeimage #this 1 -1 -1 $1
		endif

		if $w2 = 0
			2 = $($$rand.5 + 1)
			changeimage #this 2 -1 -1 $2
		endif

		if $w3 = 0
			3 = $($$rand.5 + 1)
			changeimage #this 3 -1 -1 $3
		endif

		if $w4 = 0
			4 = $($$rand.5 + 1)
			changeimage #this 4 -1 -1 $4
		endif

		if $w5 = 0
			5 = $($$rand.5 + 1)
			changeimage #this 5 -1 -1 $5
		endif

		delay 5

		++c
	endwhile

	delvar c

	z = ""

	appvar z $1
	appvar z $2
	appvar z $3
	appvar z $4
	appvar z $5
					
	a1 = 0      
	a2 = 0      
	a3 = 0      
	a4 = 0
	a5 = 0
	a6 = 0

	while $z > ""
		cutvar z cut char 1 a
		if $a = 1
			++a1
		endif
		if $a = 2
			++a2
		endif
		if $a = 3
			++a3
		endif
		if $a = 4
			++a4
		endif
		if $a = 5
			++a5
		endif
		if $a = 6
			++a6
		endif
	endwhile

	delvar z
	delvar a

	guiwindow #this resume


TEXT 31 40 27 16 "Ja" 40 BOX
	gadid 11

TEXT 61 40 27 16 "Ja" 40 BOX
	gadid 12

TEXT 91 40 27 16 "Ja" 40 BOX
	gadid 13

TEXT 121 40 27 16 "Ja" 40 BOX
	gadid 14

TEXT 151 40 27 16 "Ja" 40 BOX
	gadid 15


XAREA 31 10 27 27 comp
gadid 21
	gosub #this set_w1

xroutine set_w1
	playsound sfx1
	if $w1 = 0
		w1 = 1
		update #this 11 Nö
	elseif $w1 = 1
		w1 = 0
		update #this 11 Ja
	endif
	

XAREA 61 10 27 27 comp
gadid 22
	gosub #this set_w2

xroutine set_w2
	playsound sfx1
	if $w2 = 0
		w2 = 1
		update #this 12 Nö
	elseif $w2 = 1
		w2 = 0
		update #this 12 Ja
	endif
	

XAREA 91 10 27 27 comp
gadid 23
	gosub #this set_w3

xroutine set_w3
	playsound sfx1
	if $w3 = 0
		w3 = 1
		update #this 13 Nö
	elseif $w3 = 1
		w3 = 0
		update #this 13 Ja
	endif
	

XAREA 121 10 27 27 comp
gadid 24
	gosub #this set_w4

xroutine set_w4
	playsound sfx1
	if $w4 = 0
		w4 = 1
		update #this 14 Nö
	elseif $w4 = 1
		w4 = 0
		update #this 14 Ja
	endif
	

XAREA 151 10 27 27 comp
gadid 25
	gosub #this set_w5

xroutine set_w5
	playsound sfx1
	if $w5 = 0
		w5 = 1
		update #this 15 Nö
	elseif $w5 = 1
		w5 = 0
		update #this 15 Ja
	endif


TEXT 31 59 147 16 "" 100 BOX
	gadid 30


xroutine reset_checkbox id
	if $cnt = 0
		flash
		update #this $id 0
		stop
	endif
	oldcnt = $cnt


XCHECKBOX 70 200 16 16 "_Chance" "ch_chn" "chn" "" off
gadid 100
gadtitle right
	gosub #this reset_checkbox 100
	++counter
	setgad #this 100 off
	gosub #this check $ch_chn

XCHECKBOX 35 103 16 16 "_1er" "ch1" "1" "" off
gadid 101
gadtitle right
	gosub #this reset_checkbox 101
	++counter
	setgad #this 101 off
	gosub #this check $ch1

XCHECKBOX 35 121 16 16 "_2er" "ch2" "2" "" off
gadid 102
gadtitle right
	gosub #this reset_checkbox 102
	++counter
	setgad #this 102 off
	gosub #this check $ch2

XCHECKBOX 85 103 16 16 "_3er" "ch3" "3" "" off
gadid 103
gadtitle right
	gosub #this reset_checkbox 103
	++counter
	setgad #this 103 off
	gosub #this check $ch3


XCHECKBOX 85 121 16 16 "_4er" "ch4" "4" "" off
gadid 104
gadtitle right
	gosub #this reset_checkbox 104
	++counter
	setgad #this 104 off
	gosub #this check $ch4

XCHECKBOX 135 103 16 16 "_5er" "ch5" "5" "" off
gadid 105
gadtitle right
	gosub #this reset_checkbox 105
	++counter
	setgad #this 105 off
	gosub #this check $ch5

XCHECKBOX 135 121 16 16 "_6er" "ch6" "6" "" off
gadid 106
gadtitle right
	gosub #this reset_checkbox 106
	++counter
	setgad #this 106 off
	gosub #this check $ch6

XCHECKBOX 8 143 16 16 "_Dreierpasch" "ch_dp" "dp" "" off
gadid 107
gadtitle right
	gosub #this reset_checkbox 107
	++counter
	setgad #this 107 off
	gosub #this check $ch_dp

XCHECKBOX 8 161 16 16 "_Viererpasch" "ch_vp" "vp" "" off
gadid 108
gadtitle right
	gosub #this reset_checkbox 108
	++counter
	setgad #this 108 off
	gosub #this check $ch_vp
  

XCHECKBOX 8 179 16 16 "_Full-House" "ch_fh" "fh" "" off
gadid 109
gadtitle right
	gosub #this reset_checkbox 109
	++counter
	setgad #this 109 off
	gosub #this check $ch_fh

XCHECKBOX 101 143 16 16 "Kleine _Straße" "ch_ks" "ks" "" off
gadid 110
gadtitle right
	gosub #this reset_checkbox 110
	++counter
	setgad #this 110 off
	gosub #this check $ch_ks

XCHECKBOX 101 161 16 16 "_Große Straße" "ch_gs" "gs" "" off
gadid 111
gadtitle right
	gosub #this reset_checkbox 111
	++counter
	setgad #this 111 off
	gosub #this check $ch_gs

XCHECKBOX 101 179 16 16 "_Kniffel" "ch_k" "kn" "" off
gadid 112
gadtitle right
	gosub #this reset_checkbox 112
	++counter
	setgad #this 112 off
	gosub #this check $ch_k


xroutine check selected
	playsound sfx2

	undo = 0
	p = 0

	if $selected != ""
		if $selected = chn
			chn = $($1 + $2 + $3 + $4 + $5)
			p = $chn
			wintxt = "Chance: $p Punkte"
			u = $($u + $p)
			set = chn
		endif

		if $selected = 1
			s1 = $($a1 * 1)
			if $s1 > 1
				wintxt = "Das sind $s1 Punkte"
			else
				wintxt = "Das ist ja nur 1 Punkt"
			endif
			p = $s1
			o = $($o + $p)
			set = s1
		endif

		if $selected = 2
			s2 = $($a2 * 2)
			wintxt = "Das sind $s2 Punkte"
			p = $s2
			o = $($o + $p)
			set = s2
		endif

		if $selected = 3
			s3 = $($a3 * 3)
			wintxt = "Das sind $s3 Punkte"
			p = $s3
			o = $($o + $p)
			set = s3
		endif

		if $selected = 4
			s4 = $($a4 * 4)
			wintxt = "Das sind $s4 Punkte"
			p = $s4
			o = $($o + $p)
			set = s4
		endif

		if $selected = 5
			s5 = $($a5 * 5)
			wintxt = "Das sind $s5 Punkte"
			p = $s5
			o = $($o + $p)
			set = s5
		endif

		if $selected = 6
			s6 = $($a6 * 6)
			wintxt = "Das sind $s6 Punkte"
			p = $s6
			o = $($o + $p)
			set = s6
		endif

		if $selected = 1
		or $selected = 2
		or $selected = 3
		or $selected = 4
		or $selected = 5
		or $selected = 6
		and $o >= 63
		and $bc != 1
			bonus = 35
			p = $($p + $bonus)
			o = $($o + $bonus)
			bc = 1
		endif

		if $selected = kn
			if $1 = $2
			and $1 = $3
			and $1 = $4
			and $1 = $5
				wintxt = "   !!! Kniffel !!!"
				kn = 50
				p = $kn
				u = $($u + $p)
				set = kn
			endif
		endif

		if $selected = ks
		and $p = 0
			if $a1 > 0
			and $a2 > 0
			and $a3 > 0
			and $a4 > 0
				wintxt = "Kleine Straße (1-4)"
				ks = 30
				p = $ks
				u = $($u + $p)
				set = ks
			endif

			if $p = 0
			and $a2 > 0
			and $a3 > 0
			and $a4 > 0
			and $a5 > 0
				wintxt = "Kleine Straße (2-5)"
				ks = 30
				p = $ks
				u = $($u + $p)
				set = ks
			endif

			if $p = 0
			and $a3 > 0
			and $a4 > 0
			and $a5 > 0
			and $a6 > 0
				wintxt = "Kleine Straße (3-6)"
				ks = 30
				p = $ks
				u = $($u + $p)
				set = ks
			endif
		endif

		if $selected = gs
		and $p = 0
			if $a1 > 0
			and $a2 > 0
			and $a3 > 0
			and $a4 > 0
			and $a5 > 0
				wintxt = "Große Straße (1-5)"
				gs = 40
				p = $gs
				u = $($u + $p)
				set = gs
			endif

			if $p = 0
			and $a2 > 0
			and $a3 > 0
			and $a4 > 0
			and $a5 > 0
			and $a6 > 0
				wintxt = "Große Straße (2-6)"
				gs = 40
				p = $gs
				u = $($u + $p)
				set = gs
			endif
		endif

		if $selected = dp
		and $p = 0
			gosub #this dreier

			if $d1 = 1
			or $d2 = 1
			or $d3 = 1
			or $d4 = 1
			or $d5 = 1
			or $d6 = 1
				wintxt = "Dreierpasch: $dpasch Punkte"
				p = $dpasch
				u = $($u + $p)
				set = dp
			endif
		endif

		if $selected = fh
		and $p = 0
			gosub #this dreier

			if $f1 = 1
			or $f2 = 1
			or $f3 = 1
			or $f4 = 1
			or $f5 = 1
			or $f6 = 1
				wintxt = "   +++ Fullhouse +++"
				fh = 25
				p = $fh
				u = $($u + $p)
				set = fh
			endif
		endif

		if $selected = vp
		and $p = 0
			gosub #this vierer

			if $v1 = 1
			or $v2 = 1
			or $v3 = 1
			or $v4 = 1
			or $v5 = 1
			or $v6 = 1
				wintxt = "Viererpasch: $vpasch Punkte"
				p = $vpasch
				u = $($u + $p)
				set = vp
			endif
		endif
	endif

	oldset = $\$set

	punkte = $($punkte + $p)

	if $p = 0
		set = null
		wintxt = "Das war wohl nix..."
	endif

	update #this 30 $wintxt

	cnt = 0

	setgad #this 10 on
	setgad #this 21/25 hide

	partredraw #this 0 0 0 0

	gosub #this info

	if $counter = 13
		lvuse AmiKnHiScores 1
		lvgo last
			if $punkte > $%Punkte
				guiopen AmiKnName
			else
				guiwindow #this wait

					ezreq "Du hast nur $punkte Punkte erzielt" "Nochmal|Ende" spiel

				guiwindow #this resume

					if $spiel = 1
						gosub #this neu
					else
						gosub #this start
					endif
			endif
		lvgo first
	endif

xroutine dreier
	if $a1 >= 3
		dpasch1 = $(3 * 1)
		if $dpasch1 = 3
			if $selected != fh
				dpasch = $dpasch1
			endif
			d1 = 1
			if $a2 = 2
			or $a3 = 2
			or $a4 = 2
			or $a5 = 2
			or $a6 = 2
				f1 = 1
			else
				f1 = 0
			endif
		else
			d1 = 0
		endif
	endif

	if $a2 >= 3
		dpasch2 = $(3 * 2)
		if $dpasch2 = 6
			if $selected != fh
				dpasch = $dpasch2
			endif
			d2 = 1
			if $a1 = 2
			or $a3 = 2
			or $a4 = 2
			or $a5 = 2
			or $a6 = 2
				f2 = 1
			else
				f2 = 0
			endif
		else
			d2 = 0
		endif
	endif

	if $a3 >= 3
		dpasch3 = $(3 * 3)
		if $dpasch3 = 9
			if $selected != fh
				dpasch = $dpasch3
			endif
			d3 = 1
			if $a1 = 2
			or $a2 = 2
			or $a4 = 2
			or $a5 = 2
			or $a6 = 2
				f3 = 1
			else
				f3 = 0
			endif
		else
			d3 = 0
		endif
	endif

	if $a4 >= 3
		dpasch4 = $(3 * 4)
		if $dpasch4 = 12
			if $selected != fh
				dpasch = $dpasch4
			endif
			d4 = 1
			if $a1 = 2
			or $a2 = 2
			or $a3 = 2
			or $a5 = 2
			or $a6 = 2
				f4 = 1
			else
				f4 = 0
			endif
		else
			d4 = 0
		endif
	endif

	if $a5 >= 3
		dpasch5 = $(3 * 5)
		if $dpasch5 = 15
			if $selected != fh
				dpasch = $dpasch5
			endif
			d5 = 1
			if $a1 = 2
			or $a2 = 2
			or $a3 = 2
			or $a4 = 2
			or $a6 = 2
				f5 = 1
			else
				f5 = 0
			endif
		else
			d5 = 0
		endif
	endif

	if $a6 >= 3
		dpasch6 = $(3 * 6)
		if $dpasch6 = 18
			if $selected != fh
				dpasch = $dpasch6
			endif
			d6 = 1
			if $a1 = 2
			or $a2 = 2
			or $a3 = 2
			or $a4 = 2
			or $a5 = 2
				f6 = 1
			else
				f6 = 0
			endif
		else
			d6 = 0
		endif
	endif

xroutine vierer
	if $a1 >= 4
		vpasch1 = $(4 * 1)
		if $vpasch1 = 4
			vpasch = $vpasch1
			v1 = 1
		else
			v1 = 0
		endif
	endif

	if $a2 >= 4
		vpasch2 = $(4 * 2)
		if $vpasch2 = 8
			vpasch = $vpasch2
			v2 = 1
		else
			v2 = 0
		endif
	endif

	if $a3 >= 4
		vpasch3 = $(4 * 3)
		if $vpasch3 = 12
			vpasch = $vpasch3
			v3 = 1
		else
			v3 = 0
		endif
	endif

	if $a4 >= 4
		vpasch4 = $(4 * 4)
		if $vpasch4 = 16
			vpasch = $vpasch4
			v4 = 1
		else
			v4 = 0
		endif
	endif

	if $a5 >= 4
		vpasch5 = $(4 * 5)
		if $vpasch5 = 20
			vpasch = $vpasch5
			v5 = 1
		else
			v5 = 0
		endif
	endif

	if $a6 >= 4
		vpasch6 = $(4 * 6)
		if $vpasch6 = 24
			vpasch = $vpasch6
			v6 = 1
		else
			v6 = 0
		endif
	endif

xroutine start
	gosub #this set_default
	gosub #this reset
	gosub #this reset1

	gosub #this info

	update #this 30 "Hallo bei $prog\!"

	game = 0
	hide = 0

	setgad #this 20 show
	setgad #this 10 on
	setgad #this 10 hide

	partredraw #this 0 0 0 0

xroutine neu
	guiwindow #this wait
		ezreq "Wie oft willst Du würfeln?\n\n3x = Leicht\n2x = Schwer\n\nBitte wählen:" 3x|2x seldice
	guiwindow #this resume

	if $seldice = 1
		dicecounter = 2
	else
		dicecounter = 1
	endif

	gosub #this set_default
	gosub #this reset1
	gosub #this reset

	gosub #this info

	update #this 30 "Das Spiel beginnt!"

	setgad #this 100/113 on

xroutine help
	guiwindow #this wait
		setvar rtxt "Spielregeln\n\n"
		appvar rtxt "Starte ein neues Spiel, indem du auf den »Los geht's« Button\n"
		appvar rtxt "oder wähle den Menüpunkt »Neu« an. In einem Requester kannst Du\n"
		appvar rtxt "auswählen wie oft gewürfelt werden darf? (2x oder 3x)\n\n"
		appvar rtxt "Klick auf »Würfeln...« Button um die Würfel zu werfen,\n"
		appvar rtxt "Du kannst nach dem ersten, zweiten, oder dritten Wurf auf eines\n"
		appvar rtxt "der Checkboxen anklicken, jenachdem was der Wurf gebracht hat.\n\n"
		appvar rtxt "Das Spiel ist vorbei, wenn Du keines der Checkboxen mehr\n"
		appvar rtxt "anklicken kannst. Hast Du mehr Punkte als der Letzte in der\n"
		appvar rtxt "Bestenliste, öffnet sich ein Fenster in dem Du Deinen Namen\n"
		appvar rtxt "eingeben musst. Drücke die Eingabetaste um Deinen Namen in die\n"
		appvar rtxt "Bestenliste eintragen zu lassen."
		ezreq $rtxt "Oberfläche|Menüs|Ok" rvar
		delvar rtxt
			if $rvar = 1
				delvar rvar
				setvar htxt "Oberfläche I\n\n"
				appvar htxt "Oben sind die fünf Würfel.\n"
				appvar htxt "Klick auf einen Würfel, den Du behalten willst,\n"
				appvar htxt "klick nochmals auf ihn, um den Würfel freizugeben\n"
				appvar htxt "Die Funktionstaste 1-5 oder 6-10 sind die Tasten der\n"
				appvar htxt "jeweiligen Würfel\n\n"
				appvar htxt "Unter den Würfeln ist die Anzeige die Dir zeigt ob\n"
				appvar htxt "der Würfel darüber gewürfelt werden soll\n"
				appvar htxt "(Ja) oder nicht (Nö)\n\n"
				appvar htxt "Darunter ist das Mitteilungstextfeld,\n"
				appvar htxt "hier stehen alle möglich Sachen drin\n\n"
				appvar htxt "Klick auf auf »Los geht's...«, um ein Spiel zu starten\n"
				appvar htxt "Du kannst auch auch die Leertaste dazu benutzen\n\n"
				appvar htxt "Gewürfelt wird mit dem »Würfeln...« Button oder mit\n"
				appvar htxt "der Eingabe-Taste (Enter/Return)"
				ezreq $htxt "Weiter...|Abbrechen" hvar
				delvar htxt
				if $hvar = 1
					delvar hvar
					setvar mtxt "Oberfläche II (A)\n\n"
					appvar mtxt "Bonus: Wenn 1er bis 6er zusammen 63 Punkte,\n"
					appvar mtxt "oder mehr Augen ergeben = 35 Punkte (Taste: B)\n\n"
					appvar mtxt "Chance: Alle Augen werden gezählt (Taste: C)\n\n"
					appvar mtxt "1er: Alle Würfel mit einem Auge zählen\n"
					appvar mtxt "2er: Alle Würfel mit zwei Augen zählen\n"
					appvar mtxt "3er: Alle Würfel mit drei Augen zählen\n"
					appvar mtxt "4er: Alle Würfel mit vier Augen zählen\n"
					appvar mtxt "5er: Alle Würfel mit fünf Augen zählen\n"
					appvar mtxt "6er: Alle Würfel mit sechs Augen zählen\n"
					appvar mtxt "Die Tasten 1-6 auf der Tastatur/Nummern-Karee\n"
					appvar mtxt "Wenn 1er bis 6er zusammen 63 Punkte (oder mehr) ergeben,\n"
					appvar mtxt "dann erhälst Du 35 Bonuspunkte"
					ezreq $mtxt "Mehr...|Schliessen" mvar
					delvar mtxt
					if $mvar = 1
						delvar mvar
						setvar wtxt "Oberfläche II (B)\n\n"
						appvar wtxt "Dreierpasch: Drei Würfel mit gleichen\n"
						appvar wtxt "Anzahl von Augen zählen (Taste: D)\n\n"
						appvar wtxt "Viererpasch: Vier Würfel mit gleichen\n"
						appvar wtxt "Anzahl von Augen zählen (Taste: V)\n\n"
						appvar wtxt "Full-House: Drei und zwei Würfel mit gleichen\n"
						appvar wtxt "gleichen Augen, z.B.: 2x Drei Augen und 3x Sechs Augen\n"
						appvar wtxt "Ein Full-House bringt 25 Punkte (Taste: F)"
						ezreq $wtxt "Noch mehr...|Aufhören" wvar
						delvar wtxt
						if $wvar = 1
							delvar wvar
							setvar nmtxt "Oberfläche II (C)\n\n"
							appvar nmtxt "Kleine Straße: Würfel mit 1-4, 2-5 oder 3-6 Augen\n"
							appvar nmtxt "30 Punkte gibs für eine kleine Strasse (Taste: K)\n\n"
							appvar nmtxt "Große Straße: Würfel mit 1-5 oder 2-6 Augen\n"
							appvar nmtxt "Hierfür 40 Punkte mehr aufs Punkte-Konto (Taste: G)\n\n"
							appvar nmtxt "Kniffel: Alle fünf Würfel mit der selben Augenzahl\n"
							appvar nmtxt "bringen Dir sagenhafte 50 Punkte (Taste: K)"
							ezreq $nmtxt Super ""
							delvar nmtxt
						endif
					endif
				endif
			elseif $rvar = 2
				delvar rvar
				setvar mt "Die Menüs\n\n »AmiKniffel«\n"
				appvar mt "Neu: Neues Spiel anfangen\n"
				appvar mt "Spiel abbrechen: Das laufende Spiel wird abgebrochen\n"
				appvar mt "Bestenliste: Öffnet eine Liste mit den zehn Besten\n"
				appvar mt "Über...: Programminformationen\n"
				appvar mt "Ikonifizieren: Schliesst das Fenster und lässt ein Ikon auf\n"
				appvar mt "der Workbench zurück\n"
				appvar mt "(Doppelklicken um das Fenster wieder zu öffnen)\n"
				appvar mt "Benden: Beendet AmiKniffel :-(\n\n"
				appvar mt "»Extras«\n"
				appvar mt "Würfelauswahl umkehren: Ja => Nö und Nö => Ja\n"
				appvar mt "Zu zurücknehmen: Nimmt den letzten Zug zurück und die Punkte\n\n"
				appvar mt "»Einstellungen«\n"
				appvar mt "Öffnet die Bilder/Töne Einstellungs-Fenster. Nicht erschrecken,\n"
				appvar mt "wenn sich das Fenster von AmiKniffel schliesst, das muß so sein\n"
				appvar mt "(aus technische Gründen)\n\n"
				appvar mt "Hilfe: Zeigt Dir die Erläuterungen in Requestern an"
				ezreq $mt "Andere|Gut zu wissen" anvar
				delvar mt
				if $anvar = 1
					delvar anvar
					setvar antxt "Die Menüs (Bestenliste)\n\n"
					appvar antxt "Laden: Läd die Datei «S:AmiKniffel.HiScores»\n"
					appvar antxt "Normalerweise wird diese schon beim Programmstart\n"
					appvar antxt "geladen bzw erzeugt, wenn diese nicht existiert\n\n"
					appvar antxt "Speichern: Speichert die Liste, wird auch\n"
					appvar antxt "beim beenden des Programms gespeichert\n\n"
					appvar antxt "Löschen: Löscht die Einträge der Liste, sollte man nur\n"
					appvar antxt "verwenden, wenn man es nicht mehr in die Liste schafft\n\n"
					appvar antxt "Schliessen: Schliesst die Liste wieder,\n"
					appvar antxt " du kannst auch auf »Ok« klicken (Taste: O)"
					ezreq $antxt "Alles klar" ""
					delvar antxt
				endif
			endif
	guiwindow #this resume


xroutine undo
	ifexists variable ~undo
		flash
		stop
	endif

	if $game != 0
	and $id != 0
		if $undo = 0
			playsound sfx1

			setgad #this $id on
			update #this $id 0

			if $selected = 1
			or $selected = 2
			or $selected = 3
			or $selected = 4
			or $selected = 5
			or $selected = 6
				if $bc = 1
					bc = 0
					oldbc = 1
				endif
				o = $($o - $p)
			else
				u = $($u - $p)
			endif

			punkte = $($punkte - $p)

			--counter

			undo = 1

			if $oldcnt = 2
				setgad #this 10 off
				setgad #this 21/25 hide
			elseif $oldcnt = 1
			or $oldcnt = 0
				setgad #this 10 on
				setgad #this 21/25 show
			endif

			cnt = $oldcnt

			if $set != null
				$set == $\$set - $oldset
			endif

			update #this 30 "Bitte wählen..."
		elseif $undo = 1
			playsound sfx2

			setgad #this $id off
			update #this $id 1

			if $selected = 1
			or $selected = 2
			or $selected = 3
			or $selected = 4
			or $selected = 5
			or $selected = 6
				if $oldbc = 1
					bc = 1
					oldbc = 0
				endif
				o = $($o + $p)
			else
				u = $($u + $p)
			endif

			punkte = $($punkte + $p)

			++counter

			setgad #this 10 on
			setgad #this 21/25 show

			undo = 0
			cnt = 0

			update #this 30 $wintxt

			if $set != null
				$set == $\$set + $oldset
			endif
		endif

		partredraw #this 0 0 0 0

		gosub #this info
	else
		flash
	endif


xmenu AmiKniffel "Neu" "" "N"
		if $game = 1
			guiwindow #this wait
				ezreq "Neues Spiel beginnen?" "Ja|Nein" g
			guiwindow #this resume
		endif
		if $g != 0
			gosub #this neu
		endif

xmenu AmiKniffel "Spiel abbrechen" "" "B"
		if $game = 1
			guiwindow #this wait
				ezreq "Spiel abbrechen" "Ja|Nein" g
			guiwindow #this resume
			if $g != 0
				gosub #this start
			endif
		else
			flash
			guiwindow #this wait
				ezreq "Es läuft zur Zeit kein Spiel." "Ehrlich?" ""
			guiwindow #this resume
		endif

xmenu AmiKniffel barlabel "" ""

xmenu AmiKniffel "Bestenliste" "" "H"
		guiopen AmiKnHiScores

xmenu AmiKniffel barlabel "" ""

xmenu AmiKniffel "Über..." "" "A"
		guiopen AmiKnAbout

xmenu AmiKniffel barlabel "" ""

xmenu AmiKniffel "Ikonifizieren" "" "I"
		guiclose #this

xmenu AmiKniffel "Beenden" "" "Q"
		guiwindow #this wait
			ezreq "Macht Dir AmiKniffel keinen Spaß mehr?" "Ich muss weg|Weiter spielen" ende
			if $ende = 1
				guiquit #this
			endif
		guiwindow #this resume


xmenu Extras "Würfelauswahl umkehren" "" "-"
		gosub #this invert_dices            

xmenu Extras barlabel "" ""

xmenu Extras "Zug zurücknehmen" "" "U"
		gosub #this undo


xmenu Einstellungen "Bilder" "" "P"
		if $game != 1
			guiopen AmiKnPrefsPix
		else
			flash
			guiwindow #this wait
				ezreq "Nicht während ein Spiel läuft..." "Is gut" ""
			guiwindow #this resume
		endif

xmenu Einstellungen "Töne" "" "S"
		if $game != 1
			guiopen AmiKnPrefsSnd
		else
			flash
			guiwindow #this wait
				ezreq "Nicht während ein Spiel läuft..." "Is gut" ""
			guiwindow #this resume
		endif


xmenu ? "Hilfe..." "" "?"
		gosub #this help



;xxxxxxxxxxxxxxxxxxxxxxxxx
	newfile AmiKnHiScores
;xxxxxxxxxxxxxxxxxxxxxxxxx

WINBIG -1 -1 196 150 "Bestenliste"
WinType 11010000
winfont thinpaz.font 8 000
varpath "AmiKniffel/AmiKnName"
winbackground image pat2 0
screen Workbench


xonopen
	set margins 5/0/5
	set tbmargins 5/2

	guiwindow AmiKniffel wait

	newname = ""

	lvuse #this 1


xonclose
	lvuse #this 1
	lvgo first

	guiwindow AmiKniffel resume


xonkey #13
	guiclose #this


xtextbox 16 4 170 14 "" "Die Besten von AmiKniffel:"
	attr tbstyle 1/1/outline/left
	attr tbox 2/1/-1/none


box -1 -1 0 0 out icondrop
box -1 110 172 2 in ridge
square -1 119 51 18 3 nofill


XLISTVIEW -1 20 178 84 "" "hiscore" "" 0 txt
	attr title "2/3/outline/center/above/Die Besten von AmiKniffel:"
	gadid 1


xbutton -1 120 50 17 _Ok
	gosub #this make close


xroutine make it
	lvuse #this 1
	update #this 1 0
	lvgo first
	if $it = delete
		guiwindow #this wait
			if $%Name S= "< - - - Leer - - - >"
				flash
				ezreq "Keine Namen in Bestenliste..." "Soso" ""
			else
				setvar deltxt "Sollen die die Einträge aus der\n"
				appvar deltxt "Bestenliste gelöscht werden?"
				ezreq $deltxt Löschen|Abbrechen delhi
				if $delhi = 1
					gosub #this clear
					delvar delhi
					delvar deltxt
				endif
			endif
		guiwindow #this resume
	elseif $it = close
		guiclose #this
	elseif $it = save
		guiwindow #this wait
			if $%Name S= "< - - - Leer - - - >"
				flash
				ezreq "Bestenliste enthält keine Namen..." "Schade" ""
			else
				ezreq "Bestenliste speichern?" Ja|Nein savehi
				if $savehi = 1
					copy T:AmiKn.temp S:AmiKniffel.HiScores
					guiclose #this
					delvar savehi
				endif
			endif
		guiwindow #this resume
	elseif $it = load
		guiwindow #this wait
			ifexists file ~S:AmiKniffel.HiScores
				flash
				ezreq "S:AmiKniffel.HiScores existiert nicht!" "Aha" ""
			else
				ezreq "Alte Liste laden?" Ja|Nein load
				if $load = 1
					gosub #this load
				endif
			endif
		guiwindow #this resume
	endif

xroutine load
	lvuse #this 1
	lvchange S:AmiKniffel.HiScores
	gosub #this list_style
	lvsave T:AmiKn.temp
	partredraw #this 0 0 0 0

xroutine clear
	lvuse #this 1
	lvclear
	cli 'echo >T:AmiKn.temp "GCDB*N0*N3*N%Rang 2 N*N%Name 20 S*N%Punkte 3 N"'
	lvchange T:AmiKn.temp
	lvgo first
	count = 10
	while $count > 0
		lvadd ""
		if $count < 10
			cutvar count copy char -1 mycnt
			setvar cnt 0
			appvar cnt $mycnt
			count = $cnt
			delvar mycnt
			delvar cnt
		endif
		%Rang = $count
		%Name = "< - - - Leer - - - >"
		%Punkte = 0
		 --count
		lvgo next
	endwhile
	lvsort %Rang
	lvsave T:AmiKn.temp
	gosub #this list_style
	partredraw #this 0 0 0 0

xroutine setup
	lvuse #this 1
	lvgo last
	%Name = $newname
	%Punkte = $punkte
	lvgo first
	lvsort %Punkte
	count = 10
	while $count > 0
		if $count < 10
			cutvar count copy char -1 mycnt
			setvar cnt 0
			appvar cnt $mycnt
			count = $cnt
			delvar mycnt
			delvar cnt
		endif
		%Rang = $count
		--count
		lvgo next
	endwhile
	lvsort %Rang
	lvsave T:AmiKn.temp
	gosub #this list_style
	partredraw #this 0 0 0 0

xroutine list_style
	setattr #this/1 flstyle %Rang/2013
	setattr #this/1 flstyle %Name/2013
	setattr #this/1 flstyle %Punkte/2013


xmenu Liste Laden "" R
		gosub #this make load

xmenu Liste Speichen "" S
		gosub #this make save

xmenu Liste barlabel "" ""

xmenu Liste Löschen "" D
		gosub #this make delete

xmenu Liste barlabel "" ""

xmenu Liste Schliessen "" C
		gosub #this make close



;xxxxxxxxxxxxxxxxxxxxx
	newfile AmiKnName
;xxxxxxxxxxxxxxxxxxxxx

winbig -1 -1 200 100 "Neuer Eintrag"
wintype 00000000
winfont thinpaz.font 8 000
varpath "AmiKnHiScores/AmiKniffel"
winbackground image pat3 0
screen Workbench


xonopen
	guiwindow AmiKniffel wait
	set tbmargins 5/4
	update #this 2 "\nHerzlichen Glückwunsch!\n\nDu hast $punkte Punkte erreicht."
	redraw #this
	gosub #this setcur


xonactive
	gosub #this setcur


xroutine setcur
	if $newname = ""
		update #this 1 $oldname
	endif
	setattr #this/1 tipos 22
	setgad #this 1 on


xonclose
	guiwindow AmiKniffel resume
	gosub AmiKniffel start
	guiopen AmiKnHiScores


xtextbox -1 -1 190 90 "" ""
	attr tbstyle 2/1/Shadow/Center
	attr tbox 2/1/-1/button/in
		gadid 2


xtextin -1 74 180 16 "" newname "" 21
gadid 1
	if $newname > " "
		oldname = $newname
		gosub AmiKnHiScores setup
		guiclose #this
	else
		flash
		gosub #this setcur
	endif
	attr title "2/1/shadow/center/above/Gib Deinen Namen ein:"



;xxxxxxxxxxxxxxxxxxxxxxxx
	newfile AmiKnPrefsPix
;xxxxxxxxxxxxxxxxxxxxxxxx

WINBIG -1 -1 300 200 "Einstellungen: Bilder"
wintype 11110000
winfont thinpaz.font 8 000
varpath "AmiKniffel"
winbackground image pat3 0
screen Workbench

xonopen
	set margins 5/0/5
	set tbmargins 5/2
	guiclose AmiKniffel
	gosub #this update


xonclose
	guiopen AmiKniffel


xroutine update
	update #this 1 $bild1
	update #this 2 $bild2
	update #this 3 $bild3
	update #this 4 $würfel
	altbild1 = $bild1
	altbild2 = $bild2
	altbild3 = $bild3
	altwürfel = $würfel
	neubild1 = $altbild1
	neubild2 = $altbild2
	neubild3 = $altbild3
	neuwürfel = $altwürfel

xroutine req_bild bild_e nr
	guiwindow #this wait
		reqbild = ""
		reqfile -1 -1 300 200 "Wähle ein Bild:" load reqbild #$bild_e
			if $reqbild != ""
				neubild$nr = $reqbild
				update #this $nr $neubild$nr
				pic = $neubild$nr
				altbild$nr = $pic
			endif
	guiwindow #this resume

xroutine in_bild datei id
	if $datei != ""
		ifexists file $datei
			neubild$id = $in_bild$id
			pic = $neubild$id
			altbild$id = $pic
		else
			flash
			guiwindow #this wait
				ezreq "Datei ist nicht auffindbar:\n$datei" "So'n Pech" ""
				update #this $id $altbild$id
			guiwindow #this resume
		endif
	else
		update #this $id $altbild$id
	endif

xroutine req_würfel würfel_e gadid
	guiwindow #this wait
		reqwürfel = ""
		reqfile -1 -1 300 200 "Wähle einen Würfel:" dir reqwürfel #$würfel_e
			if $reqwürfel != ""
				neuwürfel = $reqwürfel
				altwürfel = $neuwürfel
			endif
			update #this $gadid $neuwürfel
	guiwindow #this resume

xroutine in_würfel meinwürfel gadid
	guiwindow #this wait
		if $meinwürfel != ""
			ifexists dir $meinwürfel
				neuwürfel = $meinwürfel
				cutvar neuwürfel copy char -1 sep
				if $sep != /
					appvar neuwürfel "/"
				endif
				path = $neuwürfel
				appvar path name
				ifexists file $path
					if $sep != /
						update #this $gadid $neuwürfel
					endif
					altwürfel = $neuwürfel
				else
					flash
					ezreq "$neuwürfel\nenthält keine Würfel-Bilder." "Sowas" ""
					update #this $gadid $altwürfel
				endif
			else
				flash
				ezreq "Konnte Würfel nicht laden:\n$meinwürfel" "So'n Pech" ""
				update #this $gadid $altwürfel
			endif
		else
			update #this $gadid $altwürfel
		endif
	guiwindow #this resume


box -1 -1 0 0 out button
box -1 98 268 2 in ridge
box -1 140 290 55 in icondrop


xtextbox 74 4 170 14 "" "AmiKniffel Einstellungen"
	attr tbstyle 1/1/outline/left
	attr tbox 2/1/-1/none

xtextbox -1 20 290 115 "" ""
	attr tbox 2/1/-1/button/in
	attr title "2/3/outline/center/above/AmiKniffel Einstellungen"


XTEXTIN 75 30 170 16 "H-Bild 1:" "in_bild1" "" 256
gosub #this in_bild $in_bild1 1 
gadid 1

XTEXTIN 75 50 170 16 "H-Bild 2:" "in_bild2" "" 256
gosub #this in_bild $in_bild2 2 
gadid 2

XTEXTIN 75 70 170 16 "H-Bild 3:" "in_bild3" "" 256
gosub #this in_bild $in_bild3 3 
gadid 3

XTEXTIN 75 110 170 16 "Würfel:" "in_würfel" "" 256
gosub #this in_würfel $in_würfel 4
gadid 4                           


xbutton 248 30 18 15 "<"
gosub #this req_bild $in_bild1 1
gadkey 1

xbutton 248 50 18 15 "<"
gosub #this req_bild $in_bild2 2
gadkey 2

xbutton 248 70 18 15 "<"
gosub #this req_bild $in_bild3 3
gadkey 3

xbutton 248 110 18 15 "<"
gosub #this req_würfel $in_würfel 4
gadkey w


xbutton 268 30 18 15 "T"
gosub #this test_bild $neubild1

xbutton 268 50 18 15 "T"
gosub #this test_bild $neubild2

xbutton 268 70 18 15 "T"
gosub #this test_bild $neubild3

xbutton 268 110 18 15 "T"
gosub #this test_würfel $neuwürfel


xroutine test_bild pic
	guiwindow #this wait

	extract pic file title

	setwintitle AmiKnViewBG $title

	freeimage vpic

	loadimage $pic vpic *

	changeimage AmiKnViewBG 1 -1 -1 vpic

	guiopen AmiKnViewBG

xroutine test_würfel testwürfel
	guiwindow #this wait

	wname = $testwürfel

	appvar wname "name"

	readvar $wname 0 32 title

	extract title clean title

	setwintitle AmiKnViewDices $title

	freeimage v1
	freeimage v2
	freeimage v3
	freeimage v4
	freeimage v5
	freeimage v6

	loadimage $testwürfel\1 v1 *
	loadimage $testwürfel\2 v2 *
	loadimage $testwürfel\3 v3 *
	loadimage $testwürfel\4 v4 *
	loadimage $testwürfel\5 v5 *
	loadimage $testwürfel\6 v6 *

	changeimage AmiKnViewDices 1 0 0 v1
	changeimage AmiKnViewDices 2 27 0 v2
	changeimage AmiKnViewDices 3 54 0 v3
	changeimage AmiKnViewDices 4 81 0 v4
	changeimage AmiKnViewDices 5 108 0 v5
	changeimage AmiKnViewDices 6 135 0 v6

	redraw AmiKnViewDices

	guiopen AmiKnViewDices


xbutton 20 150 70 15 "_Speichern"
	gosub #this save

xbutton 115 150 70 15 "_Benutzen"
	gosub #this use

xbutton 210 150 70 15 "_Vorgabe"
	gosub #this vorgabe

xbutton 20 170 70 15 "_Laden"
	gosub #this laden

xbutton 115 170 70 15 "_Hilfe"
	gosub #this hilfe

xbutton 210 170 70 15 "_Abbrechen"
	guiclose #this


xroutine save
	guiwindow #this wait
		ezreq "Wirklich speichern?" Ja|Nein sp
			if $sp = 1
				setvar .aksave "G4C\n\nxonload\n"
				appvar .aksave "\nAmiKniffel/bild1 = $neubild1"
				appvar .aksave "\nAmiKniffel/bild2 = $neubild2"
				appvar .aksave "\nAmiKniffel/bild3 = $neubild3"
				appvar .aksave "\nAmiKniffel/würfel = $neuwürfel"
				appvar .aksave "\nguiquit #this"
				copy env:.aksave S:AmiKniffel.pix
				delete env:.aksave
			endif
	guiwindow #this resume
	gosub #this use

xroutine laden
	guiwindow #this wait
		ifexists file S:AmiKniffel.pix
			guiload S:AmiKniffel.pix
			gosub #this update
		else
			flash
			ezreq "S:AmiKniffel.pix existiert nicht!" "Was?" ""
		endif
	guiwindow #this resume

xroutine hilfe
	guiwindow #this wait
		setvar helptext "Einstellungen: Bilder\n\n"
		appvar helptext "H-Bild 1: Hintergrundbild von AmiKniffel\n\n"
		appvar helptext "H-Bild 2: Hintergrundbild von der Bestenliste\n"
		appvar helptext "und dem »Über...«-Fenster\n\n"
		appvar helptext "H-Bild 3: Hintergrundbild der Einstellungsfenster\n"
		appvar helptext "und dem Namen-Eingabefenster\n(Tasten: 1 bis 3)\n\n"
		appvar helptext "Würfel: Das Würfel-Verzeichnis (Taste: W)"
		ezreq $helptext Buttons|Achso hvar
		delvar helptext
		if $hvar = 1
			delvar hvar
			setvar btxt "Buttons:\n\n"
			appvar btxt "Speichern: Einstellungen speichern in S:AmiKniffel.pix\n"
			appvar btxt "(Taste: S)\n\n"
			appvar btxt "Laden: Einstellungen laden (Taste: L)\n\n"
			appvar btxt "Benutzten: Aktuellen Einstellungen übernehmen (Taste: B)\n"
			appvar btxt "Hilfe: Online-Hilfe (Taste: H)\n\n"
			appvar btxt "Vorgabe: Vorprogrammierte Einstellungen zurücksetzen\n"
			appvar btxt "(Taste: V)\n\n"
			appvar btxt "Abbrechen: Schliesst das Einstellungsfenster,\n"
			appvar btxt "und öffnet wieder das Fenster von AmiKniffel;\n"
			appvar btxt "alle Änderungen gehen dann verloren (Taste: A)"
			ezreq $btxt "Weiter|So, so..." bvar
			delvar btxt
			if $bvar = 1
				delvar bvar
				setvar bbtxt "Weitere Buttons\n\n"
				appvar bbtxt "[<] - Dateiauswahl-Fenster öffnen\n"
				appvar bbtxt "[T] - Vorschau"
				ezreq $bbtxt "Donnerwetter" ""
				delvar bbtxt
			endif
		endif
	guiwindow #this resume

xroutine vorgabe
	bild1 = $vor_bild1
	bild2 = $vor_bild2
	bild3 = $vor_bild3
	würfel = $vor_würfel
	gosub #this update

xroutine use
	guiwindow #this wait

	bild1  = $neubild1
	bild2  = $neubild2
	bild3  = $neubild3
	würfel = $neuwürfel

	gosub #this lade_bilder
	gosub #this lade_bilder2

	guiwindow #this resume

	guiclose #this

xroutine lade_bilder
	freeimage pat1
	freeimage pat2
	freeimage pat3

	loadimage $bild1 pat1 *
	loadimage $bild2 pat2 *
	loadimage $bild3 pat3 *

xroutine lade_bilder2
	freeimage 1
	freeimage 2
	freeimage 3
	freeimage 4
	freeimage 5
	freeimage 6

	loadimage $würfel\1 1 *
	loadimage $würfel\2 2 *
	loadimage $würfel\3 3 *
	loadimage $würfel\4 4 *
	loadimage $würfel\5 5 *
	loadimage $würfel\6 6 *

	changeimage AmiKniffel 1 -1 -1 1
	changeimage AmiKniffel 2 -1 -1 2
	changeimage AmiKniffel 3 -1 -1 3
	changeimage AmiKniffel 4 -1 -1 4
	changeimage AmiKniffel 5 -1 -1 5



;xxxxxxxxxxxxxxxxxxxxxxx
	newfile AmiKnViewBG
;xxxxxxxxxxxxxxxxxxxxxxx

WINBIG -1 -1 200 100 ""
wintype 11010011
winfont thinpaz.font 8 000
varpath "AmiKnPrefsPix"
screen Workbench


xonclose
	guiwindow AmiKnPrefsPix resume


xonquit
	freeimage vpic


xonrmb
	guiclose #this


image 0 0 ""
	gadid 1



;xxxxxxxxxxxxxxxxxxxxxxxxxx
	newfile AmiKnViewDices
;xxxxxxxxxxxxxxxxxxxxxxxxxx

WINBIG -1 -1 162 27 "Vorschau"
wintype 11000000
winfont thinpaz.font 8 000
varpath "AmiKnPrefsPix"
screen Workbench


xonclose
	guiwindow AmiKnPrefsPix resume


xonquit
	freeimage v1
	freeimage v2
	freeimage v3
	freeimage v4
	freeimage v5
	freeimage v6


xonrmb
	guiclose #this


image 0 0 ""
	gadid 1

image 27 0 ""
	gadid 2

image 54 0 ""
	gadid 3

image 81 0 ""
	gadid 4

image 108 0 ""
	gadid 5

image 135 0 ""
	gadid 6



;xxxxxxxxxxxxxxxxxxxxxxxxx
	newfile AmiKnPrefsSnd
;xxxxxxxxxxXxxxxxxxxxxxxxx

WINBIG -1 -1 300 200 "Einstellungen: Töne"
wintype 11110000
winfont thinpaz.font 8 000
varpath "AmiKniffel"
winbackground image pat3 0
screen Workbench

xonopen
	set margins 5/0/5
	set tbmargins 5/2

	guiclose AmiKniffel
	gosub #this update


xonclose
	guiopen AmiKniffel


xroutine update
	update #this 1 $sfx1
	update #this 2 $sfx2
	update #this 3 $sfx3
	update #this 4 $startsnd
	altsfx1 = $sfx1
	altsfx2 = $sfx2
	altsfx3 = $sfx3
	altstartsnd = $startsnd
	neusfx1 = $altsfx1
	neusfx2 = $altsfx2
	neusfx3 = $altsfx3
	neustartsnd = $altstartsnd


xroutine req_sfx sfx_e nr
	guiwindow #this wait
		reqsfx = ""
		reqfile -1 -1 300 200 "Wähle einen Ton:" load reqsfx #$sfx_e
			if $reqton != ""
				neusfx$nr = $reqsfx
				update #this $nr $neusfx$nr
				altsfx$nr = $neusfx$nr
			endif
	guiwindow #this resume

xroutine in_sfx datei id
	guiwindow #this wait
		if $datei != ""
			ifexists file $datei
				neusfx$id = $in_sfx$id
				sfx = $neusfx$id
				altsfx$id = $neusfx$id
			else
				flash
				ezreq "Datei ist nicht auffindbar:\n$datei" "So'n Pech" ""
				update #this $id $altsfx$id
			endif
		else
			update #this $id $altsfx$id
		endif
	guiwindow #this resume

xroutine req_start start_e gadid
	guiwindow #this wait
		reqstartsnd = ""
		reqfile -1 -1 300 200 "Wähle einen Start-Ton:" file reqstartsnd #$start_e
			if $reqstartsnd != ""
				neustartsnd = $reqstartsnd
				altstartsnd = $neustartsnd
			endif
			update #this $gadid $neustartsnd
	guiwindow #this resume


xroutine in_start meinstartsnd gadid
	guiwindow #this wait
		if $meinstartsnd != ""
			ifexists file $meinstartsnd
				neustartsnd = $in_start
				altstartsnd = $neustartsnd
			else
				flash
				ezreq "Konnte Start-Ton nicht laden:\n$meinstartsnd" "So'n Pech" ""
				update #this $gadid $altstartsnd
			endif
		else
			update #this $gadid $altstartsnd
		endif
	guiwindow #this resume


box -1 -1 0 0 out button
box -1 98 268 2 in ridge
box -1 140 290 55 in icondrop


xtextbox 74 4 170 14 "" "AmiKniffel Einstellungen"
	attr tbstyle 1/1/outline/left
	attr tbox 2/1/-1/none

xtextbox -1 20 290 115 "" ""
	attr tbox 2/1/-1/button/in
	attr title "2/3/outline/center/above/AmiKniffel Einstellungen"


XTEXTIN 75 30 170 16 "Ton Nr.1:" "in_sfx1" "" 256
gosub #this in_sfx $in_sfx1 1
gadid 1

XTEXTIN 75 50 170 16 "Ton Nr.2:" "in_sfx2" "" 256
gosub #this in_sfx $in_sfx2 2
gadid 2

XTEXTIN 75 70 170 16 "Ton Nr.3:" "in_sfx3" "" 256
gosub #this in_sfx $in_sfx3 3
gadid 3

XTEXTIN 75 110 170 16 "St. Ton:" "in_start" "" 256
gosub #this in_start $in_start 4
gadid 4                           


xbutton 248 30 18 15 "<"
gosub #this req_sfx $in_sfx1 1
gadkey 1

xbutton 248 50 18 15 "<"
gosub #this req_sfx $in_sfx2 2
gadkey 2

xbutton 248 70 18 15 "<"
gosub #this req_sfx $in_sfx3 3
gadkey 3

xbutton 248 110 18 15 "<"
gosub #this req_start $in_start 4
gadkey 4


xbutton 268 30 18 15 "T"
gosub #this test_sfx $neusfx1

xbutton 268 50 18 15 "T"
gosub #this test_sfx $neusfx2

xbutton 268 70 18 15 "T"
gosub #this test_sfx $neusfx3

xbutton 268 110 18 15 "T"
gosub #this test_sfx $neustartsnd


xroutine test_sfx sfx
	guiwindow #this wait
		loadsound $sfx testsfx
		playsound testsfx
		ezreq "\n$sfx\n" "Stopp" ""
		freesound testsfx
	guiwindow #this resume


xbutton 20 150 70 15 "_Speichern"
	gosub #this save

xbutton 115 150 70 15 "_Benutzen"
	gosub #this use

xbutton 210 150 70 15 "_Vorgabe"
	gosub #this vorgabe

xbutton 20 170 70 15 "_Laden"
	gosub #this laden

xbutton 115 170 70 15 "_Hilfe"
	gosub #this hilfe

xbutton 210 170 70 15 "_Abbrechen"
	guiclose #this


xroutine save
	guiwindow #this wait
		ezreq "Wirklich speichern?" Ja|Nein sp
			if $sp = 1
				setvar .aksave "G4C\n\nxonload\n"
				appvar .aksave "\nAmiKniffel/sfx1 = $neusfx1"
				appvar .aksave "\nAmiKniffel/sfx2 = $neusfx2"
				appvar .aksave "\nAmiKniffel/sfx3 = $neusfx3"
				appvar .aksave "\nAmiKniffel/startsnd = $neustartsnd"
				appvar .aksave "\nguiquit #this"
				copy env:.aksave S:AmiKniffel.sounds
				delete env:.aksave
			endif
	guiwindow #this resume
	gosub #this use

xroutine laden
	guiwindow #this wait
		ifexists file S:AmiKniffel.sounds
			guiload S:AmiKniffel.sounds
			gosub #this update
		else
			flash
			ezreq "S:AmiKniffel.sounds existiert nicht!" "Was?" ""
		endif
	guiwindow #this resume

xroutine hilfe
	guiwindow #this wait
		setvar helptext "Einstellungen: Töne\n\n"
		appvar helptext "Ton Nr.1: Ton fürs anklicken der Würfel\n\n"
		appvar helptext "Ton Nr.2: Ton fürs würfeln\n\n"
		appvar helptext "Ton Nr.3: Ton fürs die Checkboxes\n(Tasten: 1 bis 3)\n\n"
		appvar helptext "St. Ton: Ton der beim Öffnen abgespielt wird (Taste: 4)"
		ezreq $helptext Buttons|Achso hvar
		delvar helptext
		if $hvar = 1
			delvar hvar
			setvar btxt "Buttons:\n\n"
			appvar btxt "Speichern: Einstellungen speichern in S:AmiKniffel.sounds\n"
			appvar btxt "(Taste: S)\n\n"
			appvar btxt "Laden: Einstellungen laden (Taste: L)\n\n"
			appvar btxt "Benutzten: Aktuellen Einstellungen übernehmen (Taste: B)\n"
			appvar btxt "Hilfe: Online-Hilfe (Taste: H)\n\n"
			appvar btxt "Vorgabe: Vorprogrammierte Einstellungen zurücksetzen\n"
			appvar btxt "(Taste: V)\n\n"
			appvar btxt "Abbrechen: Schliesst das Einstellungsfenster,\n"
			appvar btxt "und öffnet wieder das Fenster von AmiKniffel;\n"
			appvar btxt "alle Änderungen gehen dann verloren (Taste: A)"
			ezreq $btxt "Weiter|So, so..." bvar
			delvar btxt
			if $bvar = 1
				delvar bvar
				setvar bbtxt "Weitere Buttons\n\n"
				appvar bbtxt "[<] - Dateiauswahl-Fenster öffnen\n"
				appvar bbtxt "[T] - Vorschau"
				ezreq $bbtxt "Donnerwetter" ""
				delvar bbtxt
			endif
		endif
	guiwindow #this resume

xroutine vorgabe
	sfx1 = $vor_sfx1
	sfx2 = $vor_sfx2
	sfx3 = $vor_sfx3
	startsnd = $vor_startsnd
	gosub #this update

xroutine use
	guiwindow #this wait

	sfx1 = $neusfx1
	sfx2 = $neusfx2
	sfx3 = $neusfx3
	startsnd = $neustartsnd

	gosub #this lade_sfx

	guiwindow #this resume

	guiclose #this

xroutine lade_sfx
	freesound sfx1
	freesound sfx2
	freesound sfx3
	freesound startsnd

	loadsound $sfx1 sfx1
	loadsound $sfx2 sfx2
	loadsound $sfx3 sfx3
	loadsound $startsnd startsnd



;xxxxxxxxxxxxxxxxxxxxxx
	newfile AmiKnLaden
;xxxxxxxxxxxxxxxxxxxxxx

winbig -1 -1 184 162 ""
wintype 00001000
winfont thinpaz.font 8 000
screen Workbench


xonopen
	set buffer 4000
	set margins 5/0/4

	guiwindow #this wait
	setpointer #this hide


xonclose
	guiwindow #this resume
	setpointer #this default


xonquit
	freeimage startpic
	guiquit AmiKnAbout


xonactive
	setpointer #this hide


xoninactive
	setpointer #this default


box -1 -1 0 0 out icondrop
box -1 -1 174 152 in ridge

box -1 118 160 2 out ridge


image 12 12 startpic


gauge -1 125 150 14 in button 3 0 0
	gadid 1



;xxxxxxxxxxxxxxxxxxxxxx
	newfile AmiKnAbout
;xxxxxxxxxxxxxxxxxxxxxx

WINBIG -1 -1 250 250 ""
wintype 00001000
winfont thinpaz.font 8 000
winbackground image pat2 0
screen Workbench
varpath "AmiKniffel"


xonopen
	guiwindow AmiKniffel wait

	set tbmargins 5/6

	setvar abtxt "$prog v$vers ($date)\n\n"
	appvar abtxt "Geschrieben in Gui4Cli 3.8.5\n\n"
	appvar abtxt "EmailWare ©2002 bei $name\n"
	appvar abtxt "($mail)\n\n"
	appvar abtxt "Alle Rechte vorbehalten!"

	update #this 1 $abtxt


xonclose
	guiwindow AmiKniffel resume
	delvar abtxt


xonquit
	freeimage pat2
	freeimage startpic


BOX -1 -1 0 0 out icondrop
BOX -1 -1 240 240 in ridge


IMAGE -1 20 startpic


XTEXTBOX -1 130 200 85 "" ""
	attr tbstyle 2/1/shadow/center
	attr tbox 2/1/0/button/in
		gadid 1


XBUTTON -1 222 40 16 _OK
	guiclose #this
