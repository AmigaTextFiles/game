;HomerGotchi!

; James L Boyd
; What a mess!
; Heh heh!

; Set Bitmaps to 11 in Compiler Options
; Set Blitzlibs:amigalibs.res in Compiler Options

; Compile from where you found it, in the unpacked
; HomerGotchi archive drawer!

; Assign PROGDIR: to the HomerGotchi drawer (only needed
; when compiling).

Statement UnlockWindow{win.l,lock.l}
  win=Peek.l(Addr Window(win))
  *Exec.Library=Peek.l(4)
  If *Exec\lib_Version=>39
    Delay_ 5
    Dim tag.TagItem(0)
    tag(0)\ti_Tag=#TAG_END
    SetWindowPointerA_ win,&tag(0)
  EndIf
  EndRequest_ lock,win
  FreeMem_ lock,SizeOf .Requester
End Statement

Function.l LockWindow {win.l}
  lock.l=AllocMem_(SizeOf .Requester,1)
  If lock
    win=Peek.l(Addr Window(win))
    InitRequester_(lock)
    If Request_(lock,win)
      *Exec.Library=Peek.l(4)
      If *Exec\lib_Version=>39
        Dim tag.TagItem(1)
        tag(0)\ti_Tag=#WA_BusyPointer,-1
        tag(1)\ti_Tag=#TAG_END
        SetWindowPointerA_ win,&tag(0)
      EndIf
    Else
      FreeMem_ lock,SizeOf .Requester
      lock=0
    EndIf
  EndIf
  Function Return lock
End Function

Function.l Exist {f$}

lock.l=Lock_(&f$,#ACCESS_READ)

If lock

DEFTYPE .FileInfoBlock fib

If Examine_(lock, fib) <> 0

  If fib\fib_DirEntryType < 0
    size=Peek.l(&fib\fib_Size)  ; file
  Else size=-1                  ; drawer
  EndIf

  Else size=-2 ; failed to examine file! rare occurence!
EndIf

UnLock_ lock
Else size=0 ; failed to lock file (doesn't exist basically)...
EndIf

Function Return size
End Function

Function.w colours{planes}
cols.w=2^planes
  Function Return cols
End Function

WBStartup

;remapping by Curt Esser    camge@ix.netcom.com
;
;ie the hard stuff ;)

WBenchToFront_         ;make sure WB is showing
WbToScreen 0           ;grab the wb screen
NoCli                  ;don't need that!

*scr.Screen=Peek.l(Addr Screen(0))
height.w=*scr\Height
width.w=*scr\Width
*fnt.TextAttr=*scr.Screen\Font           ; Font of the screen
fsize.b=(*fnt.TextAttr\ta_YSize)
fname$=Peek$(*fnt.TextAttr\ta_Name)

LoadFont 0,fname$,fsize

foodpath$="PROGDIR:Food/"
commentpath$="PROGDIR:Comments/"
smpmissing.b=0

comms:
Data$ "belch","foodinhere","gottago","outta","stupid"

; ^  |  just for sample existence check
; |  v

Restore comms

For a.b=1 To 5
Read cmnt$

If Exist {commentpath$+cmnt$+".iff"}=0
  r$="Can't find all of Homer's 'Comments' samples!||They should be in the Comments drawer!"
  Request"HomerGotchi",r$,"Better Re-install...":Pop For:smpmissing+1:Goto quit
EndIf

Next a

Restore samples
For a=1 To 5
Read f$

If Exist {foodpath$+f$}=0
  r$="Can't find all of Homer's 'Food' samples!||They should be in the Food drawer!"
  Request"HomerGotchi",r$,"Better Re-install...":Pop For:smpmissing+1:Goto quit
EndIf

Next a

sampleno.b=7

samples:
Data$ "Beer","Criminal","Cheese","Cookies","Ham","Burger","Donut"

;first put the workbench palette into palette #0

planes=WBDepth                ;find the number of colours available
cols.w=colours{planes}
InitPalette 0,cols            ;set up palette 0 to catch wb colours

If CheckAGA                   ; AGA - use AGA commands (duh...)

 For i=0 To cols-1
   AGAPalRGB 0,i,AGARed(i),AGAGreen(i),AGABlue(i)
 Next

Else                          ;no AGA, use ECS colour commands

 For i=0 To cols-1
   PalRGB 0,i,Red(i),Green(i),Blue(i)
 Next

EndIf

If GetIconObject("progdir:homergotchi")

  facing$=FindToolValue ("FACING")
  If LCase$(facing$)="left" Then otherway.b=1

  If FindToolType("LEFT")Then wx=Val(FindToolValue("LEFT"))
  If FindToolType("TOP") Then wy=Val(FindToolValue("TOP"))

  FreeIconObject

EndIf

DecodeShapes 0,10,?picload

DecodePalette 1,?lpal

tbarh.b=fsize+*scr\WBorTop+1

fw.w=width*.50
fx.w=(width/2)-(fw/2)
fy.w=(height/2)-(tbarh/2)
fh.w=tbarh

If Window (0,fx,fy,fw,fh,$1002,"Processing...",1,2)=0 Then Request "","Weird failure!","@#*?!":End

suc.l=PICreateRequest("HomerGotchi : rescaling images...",0,10,1)

For no.b=0 To 10

If suc Then dum.l=PIUpdateRequest (no)

If otherway=1 Then XFlip no ; face other way...

sd.w= Peek.w(Addr Shape(no)+4)   ;get the shape's depth
bplanes = planes
If sd>bplanes Then bplanes=sd ;if > WBdepth, set palette to shape's depth
shpcols=colours{sd}

x=2                           ;scale for laced or non-laced bench
y=2
If height<400 Then y=1

Scale no,x,y

Handle no,0,0                  ;make sure the handle is correct
                              ;and put it on a bitmap of screen-depth
                              ;for the colour-remapping

BitMap no,ShapeWidth(no),ShapeHeight(no),bplanes
Blit no,0,0

PaletteInfo 1                 ;use the shape's palette

For i = 1 To shpcols-1        ;remap the shape to wb screen
                              ;note that colour 0 is skipped
                              ;you can start with 0 to include it
  If CheckAGA
    match=FindColor(0,AGAPalRed(i),AGAPalGreen(i),AGAPalBlue(i))
  Else
    match=FindColor(0,PalRed(i),PalGreen(i),PalBlue(i))
  EndIf
  ReMap i,match
Next


Next no

If suc Then PIEndRequest

Free Window 0

sx=ShapeWidth(0):sy=ShapeHeight(0)

fsize=fsize+*scr\WBorTop+1 ; fsize now = title bar height

AddIDCMP $10

If wx+sx+30>width Then wx=0
If wy+sy+fsize>height Then wy=fsize

.win

; if window fails, try it at 0,0...

GTMenuTitle 0,0,"HomerGotchi"
GTMenuItem  0,0,0,0,"About","A"
GTMenuItem  0,0,0,1,"Flip","F"
GTMenuItem  0,0,0,2,"Quit","Q"

If Window (0,wx,wy,sx+30,sy+fsize+6,$2|$8|$1000,"",1,0)=0
  If Window (0,0,0,sx+30,sy+fsize+5,$2|$8|$1000,"",1,0)=0
    Request "","Massive window opening failure!","ABORT"
  EndIf
EndIf

*win.Window=Peek.l(Addr Window (0))

WWidth.w=*win\Width
WHeight.w=*win\Height
WX.w=*win\LeftEdge
WY.w=*win\TopEdge


GTSetMenu 0

BitMaptoWindow 0,0,0,0,15,fsize+3,sx,sy

Free Palette 1

If AddAppWindow (0)=0 Then Request "Error","Couldn't create appwindow","OK":End

ResetTimer

patience=100:t.l=Ticks:timup.l=t+(Rnd (60*patience))+500:blnk.l=t

WTitle Str$(patience)

  .loop

  Repeat

    WaitTOF_

    If Ticks>blnk+Rnd(10000)
    WaitTOF_:BitMaptoWindow 9,0,0,0,15,fsize+3,sx,sy
    blnk=Timer
    Delay_ 10:BitMaptoWindow 0,0,0,0,15,fsize+3,sx,sy
    EndIf

    If Ticks>timup
      patience-10:
      WaitTOF_:BitMaptoWindow 10,0,0,0,15,fsize+3,sx,sy
      Restore samples:For s=1 To 1+Int(Rnd(sampleno)):Read food$:Next s
      LoadSound 0,foodpath$+food$
      Sound 0,10:Free Sound 0
      Delay_150:WTitle Str$(patience)
      WaitTOF_:BitMaptoWindow 0,0,0,0,15,fsize+3,sx,sy

      If patience<0
        WTitle "0"
        LoadSound 0,commentpath$+"outta.iff"
        Delay_50:Sound 0,10:Free Sound 0:Delay_70
        lock.l=LockWindow{0}
        Request "HomerGotchi","Homer seems to have lost faith in you...||He's off to Moe's...","See ya,Homer!"
        UnlockWindow {0,lock}
        stdate$="0"
        mn=0
        hr=0
        Goto quit
      EndIf

      ResetTimer:t=Ticks:timup=t+(Rnd (60*patience))+500

    EndIf

    e.l=Event

    If e=$200 Then Goto quit

    If e=$10
    wmx=SMouseX:wmy=SMouseY
    WX=*win\LeftEdge:WY=*win\TopEdge
    If wmx>WX AND wmx<WX+WWidth AND wmy>WY AND wmy<WY+WHeight
      WaitTOF_:BitMaptoWindow 1,0,0,0,15,fsize+3,sx,sy
    If foodin=0 Then LoadSound 0,commentpath$+"Foodinhere.iff":Sound 0,10:foodin=1:Free Sound 0
    Else WaitTOF_:BitMaptoWindow 0,0,0,0,15,fsize+3,sx,sy
    EndIf
    EndIf

    If e=$100
      Select ItemHit
        Case 0
          r$="This is HomerGotchi!||By James L Boyd (PD) 1998/9|jamesboyd@all-hail.freeserve.co.uk||Feed Homer or he'll leave!"
          lock.l=LockWindow{0}
          Request "HomerGotchi",r$,"Oh,right..."
          UnlockWindow {0,lock}
        Case 1

For no.b=0 To 10

Free BitMap no

XFlip no ; face other way...

If otherway=0 Then otherway=1 Else otherway=0

sd.w= Peek.w(Addr Shape(no)+4)   ;get the shape's depth
bplanes = planes
If sd>bplanes Then bplanes=sd ;if > WBdepth, set palette to shape's depth
shpcols=colours{sd}

;x=2                           ;scale for laced or non-laced bench
;y=2
;If height<400 Then y=1
;Scale no,x,y
;Handle no,0,0                  ;make sure the handle is correct
                              ;and put it on a bitmap of screen-depth
                              ;for the colour-remapping

BitMap no,ShapeWidth(no),ShapeHeight(no),bplanes
Blit no,0,0

DecodePalette 1,?lpal

PaletteInfo 1                 ;use the shape's palette

For i = 1 To shpcols-1        ;remap the shape to wb screen
                              ;note that colour 0 is skipped
                              ;you can start with 0 to include it
  If CheckAGA
    match=FindColor(0,AGAPalRed(i),AGAPalGreen(i),AGAPalBlue(i))
  Else
    match=FindColor(0,PalRed(i),PalGreen(i),PalBlue(i))
  EndIf

  ReMap i,match

Next i

Next no

        Case 2
        Goto quit
      End Select
    EndIf

    If e=$400
    a$=Inkey$:If food$="" Then food$="nothing"
    r$="Homer asked for "+LCase$(food$)+"...||Ya cheat!"
      If RawKey=$5f
        lock.l=LockWindow{0}
        Request"HomerGotchi",r$,"Cheating helps us win..."
        UnlockWindow{0,lock}
      EndIf
    EndIf

  Until AppEvent

  For dropno.w=1 To AppNumFiles

    WaitTOF_
    BitMaptoWindow 1,0,0,0,15,fsize+3,sx,sy

    i$=AppFile(dropno)

    For char=Len(i$)-1 To 1 Step -1
      If Mid$(i$,char,1)="/" OR Mid$(i$,char,1)=":"
        fil1$=Right$(i$,Len(i$)-char)
        Pop For
        Goto exitfor
      EndIf
    Next char

.exitfor

    If LCase$(fil1$)=LCase$(food$)

    LoadSound 0,commentpath$+"belch.iff"

    For fr=2 To 8

        Delay_ 6

    BitMaptoWindow fr,0,0,0,15,fsize+3,sx,sy

    Next fr

    WaitTOF_:BitMaptoWindow 1,0,0,0,15,fsize+3,sx,sy
    Sound 0,10:Free Sound 0:Delay_ 30
    patience+10+Int(10*Rnd):If patience>100 Then patience=100
    WTitle Str$(patience):food$=""

    Else
    For fr=2 To 8

        Delay_ 6

    BitMaptoWindow fr,0,0,0,15,fsize+3,sx,sy

    Next fr

    BitMaptoWindow 2,0,0,0,15,fsize+3,sx,sy
    LoadSound 0,commentpath$+"Stupid.iff"
    Sound 0,10:Free Sound 0
    patience-10
    Delay_ 150
    WTitle Str$(patience)
    BitMaptoWindow 1,0,0,0,15,fsize+3,sx,sy

    EndIf

  WaitTOF_
    BitMaptoWindow 0,0,0,0,15,fsize+3,sx,sy

  Next dropno

Goto loop

.quit

If smpmissing=0

LoadSound 0,commentpath$+"gottago.iff"

Sound 0,10:Free Sound 0

Delay_ 75

WaitTOF_
    BitMaptoWindow 0,0,0,0,15,fsize+3,sx,sy

Delay_25

If GetIconObject("progdir:homergotchi")

  If FindToolType("LEFT")
    WX=*win\LeftEdge
    If wx<>WX
    SetToolValue "LEFT",Str$(WX)
    EndIf
  EndIf

  If FindToolType("TOP")
    WY=*win\TopEdge
    If wy<>WY
    SetToolValue "TOP",Str$(WY)
    EndIf
  EndIf

  If FindToolType("FACING")
    If otherway=1
      SetToolValue "FACING","LEFT"
    Else SetToolValue "FACING","RIGHT"
    EndIf
  EndIf

PutIconObject ("progdir:HomerGotchi")

FreeIconObject

EndIf

EndIf

DelAppWindow (0)
End

.picload

IncBin "stuff:temp/homergotchi/saveshapes"

.lpal

IncBin "stuff:temp/homergotchi/homerspalette"
