; $VER: Installer script 1.0 for DEU (13.04.98) © Sitsofe Wheeler

(set #deu-docs
     (cat "\nWhere would you like to install the DEU documentation?"))

(set #deu-demo "\nInstall demo wads?")

(set #deu-nodemo
     (cat "\nDue to space constraints, the demo wads and tutorial are in a"
          " seperate archive. This archive is available from"
          " <http://www.aber.ac.uk/~img4>."))

(set #deu-demo-help
     (cat "\nDEU comes with some demomstration wads which you can look at"
          " to help you understand how particular things are built. These"
          " wads total about 600K in size."))

(set #deu-dir
     (cat "Where do you wish to install DEU? "
          "The directory 'DEU' will be created if it is not there. If a"
          " copy of DEU is found it will be updated."))

(set #deu-fpu "Which version of DEU do you wish to install?")

(set #deu-fpu-help
     (cat "\nIf your computer has an FPU (Floating Point Unit) then"
          " installing the FPU version should yeild a slight speed increase."
          " If you are not sure whether your computer has an FPU then install"
          " the 68000 version."))

(set #deu-ini-update
     (cat "\nDo you want to replace the existing DEU.ini?"))

(set #deu-nofpu
     (cat "\nThe FPU version of DEU was not found in the archive. The 68000"
          " version will be installed instead."))

(set #deu-tutor"\nInstall tutorial?")

(set #deu-tutor-help
     (cat "\nThe tutorial is a sort of step-by-step instruction booklet to"
          " creating a level from scratch. There is a documentation file along"
          " with wads which the documentation refers to. It also contains"
          " useful information on texture alignment."))

(set #deu-update
     (cat "\nA copy of DEU has been found. Would you like to update it?"))

(set #deu-noupdate "\nDEU was not updated.")

(set #deu-wad
     (cat "\nPlease select a main wad."))

(set #deu-wad-help
     (cat "\nA main wad is a wad Doom uses to load all the original"
          " textures etc. It can be recognised by checking if the first few"
          " characters of the file are 'IWAD' (without the quotes).\n\n"
          "The following are the most common main wads:\n"
          " doom1.wad\n"
          " doom.wad\n"
          " doom2.wad\n"
          " doomu.wad\n\n"
          "It is worth noting that if you are using the shareware nain wad"
          " (doom1.wad) you will be unable to save any wads you make with"
          " DEU."))

(set #deu-nowad1
     (cat "\nSorry, DEU will not work without a main wad.\n\nInstallation"
          " Aborted."))

(set #deu-wad-warning2
     (cat "\nYou will not be able to save your wads because you are using"
          " the shareware wad. If you would like to save your wads BUY Doom."))

(set #deu-screen
     (cat "\nPlease select a screenmode."))

(set #deu-screen-help
     (cat "\nIf you are not sure which screenmode you want to use take a look"
          " screenmode prefs. If the screenmode you would like to use is not"
          " available, alter the deu.ini when the installation is finished."
          " The selectable screen sizes are all 640x480."))

(set #deu-finished
     (cat "\nInstallation successful.\n\n"
          "The latest version of DEU can be found at"
          " http://www.aber.ac.uk/~img4"
          "\n\nHave a nice Doom!"))

(set #config1
     (cat "#\n"
        "#  Default DEU config file\n#\n"
        "# Everything has been commented out, so this file does nothing.\n"
        "# But you can customize it to suit your needs.\n\n"
        "# Name of the main wad file.\n"))

(set #config2
     (cat "# Name of the patch wads you want to load.\n"
        "#file = raphael.wad\n"
        "#file = brendon.wad\n\n"
        "# Set this if you want debugging on by default.\n"
        "#debug = true\n\n"
        "# Set this if you want to swap the mouse buttons by default.\n"
        "#swapbuttons = true\n\n"
        "# Use this to control the the noises.\n"
        "#quiet = true\n"
        "#quieter = true\n\n"
        "# Set this if you think you are a brain.\n"
        "#expert = true\n\n"
        "# Change the Screen mode (Amiga). Must be either decimal or 8 character hex.\n"))

(set #config3
     (cat "screenwidth = 640\n"
        "screenheight = 480\n\n"
        "# Set this if you have an old mouse driver and you don't see the cursor.\n"
        "#fakecursor = true\n\n"
        "# Use this to hide the info bar (displayed by default).\n"
        "#infobar = false\n\n"
        "# Set this if you don't want to unselect all previously selected objects when\n"
        "# you use the selection box (Shift-MouseButton1, Shift-M).\n"
        "#addselbox = true\n\n"
        "# Uncomment this if you don't like to have the object 0 selected when you\n"
        "# switch from one editing mode to another.\n"
        "#select0 = false\n\n"
        "# If you are an expert in Binary Space Partitioning algorithms and you want\n"
        "# to tweak the Nodes builder, then you can use the following parameter.\n"
        "#splitfactor = 8\n\n"
        "# Default textures and preferences.  You may want to change those values...\n"
        "#walltexture = BROWNHUG\n"
        "#uppertexture = REDWALL1\n"
        "#lowertexture = COMPBLUE\n"
        "#ceiltexture = F_SKY1\n"
        "#floortexture = FLAT20\n"
        "#ceilheight = 256\n"
        "#floorheight = 0\n\n"
        "# This has something to do with the funny message displayed when DEU starts...\n"
        "#reminder1 = false\n"))

(set doom-def-dir (getenv "DOOMHOME"))

(complete 0)

(set deu-wad
        (askfile
                (default (tackon doom-def-dir "doom.wad"))
                (prompt  #deu-wad)
                (help    #deu-wad-help)
        )
)

(if (<> (exists deu-wad) 1)
        (abort #deu-nowad1)
)

;**************************************************
;Warn user if they do not have a registered wad...
;**************************************************
(if (< (getsize deu-wad) 5000000)
        (message #deu-wad-warning2)
)


(set dest_dir
        (askdir
                (default doom-def-dir)
                (prompt  #deu-dir)
                (help    @askdir-help)
        )
)

;**********************************************
;Ask user whether they wish to install FPU DEU
;**********************************************
(set deu-ver
        (askchoice
                (choices "68000" "68000+FPU")
                (default 0)
                (prompt  #deu-fpu)
                (help    #deu-fpu-help)
        )
)

(if (= (exists ("DEU_FPU")) 0)
    (if (= deu-ver 1)
         (
         (message (#deu-nofpu))
         (set deu-ver 0)
         )
    )
)

;**************************************
;Check if deu directory already exists
;**************************************
(set deu-update 2)
(if (exists (tackon dest_dir "DEU"))
        (set deu-update 
               (askbool (prompt #deu-update)
                        (help #deu-update)
               )
        )
        (makedir (tackon dest_dir "DEU"))
)

(if (= 0 deu-update)
        (exit #deu-noupdate (quiet))
)

(if (<> 1 (exists(tackon dest_dir "DEU")))
    (set dest_dir (tackon dest_dir "DEU"))
)

(copyfiles  (source "/deu")
            (dest dest_dir)
            (pattern "(deu.guide|deu.txt|readme.1st|wad_auth.txt)")
            (files)
            (infos)
)

(complete 25)

(select deu-ver
        (copyfiles      (source "DEU")
                        (dest dest_dir)
        )
        (copyfiles      (source "DEU_FPU")
                        (newname "DEU")
                        (dest dest_dir)
        )
)

(copyfiles  (source "DEU_go") (dest dest_dir))
(copyfiles  (source "deumwb.info") (newname "DEU_go.info") (dest dest_dir))

(complete 50)

;***************************************************************
;Check to see whether the archive contains demo and tutor files
;***************************************************************
(if (exists("demo"))
(
(set deu-demo
        (askbool
        (prompt   #deu-demo)
        (help     #deu-demo-help)
        )
)

(if deu-demo
        (copyfiles  (source "demo")
                    (dest (tackon dest_dir "demo"))
                    (all)
        )
)

(complete 70)

(set deu-tutor
        (askbool
        (prompt   #deu-tutor)
        (help     #deu-tutor-help)
        )
)

(if deu-tutor
        (copyfiles  (source "tutor")
                    (dest (tackon dest_dir "tutor"))
                    (all)
        )
)
)
(message #deu-nodemo)
)

(complete 80)

;*********************************************************
;Does the user which to replace an existing deu.ini file?
;*********************************************************
(set deu-new-ini 1)
(if (= 1 deu-update)
        (set deu-new-ini
        (askbool (prompt #deu-ini-update)
                 (help   #deu-ini-update)
        ))
)
(if (= 0 deu-new-ini)
        (
        (complete 100)
        (exit #deu-finished (quiet))
        )
)

(set deu-screen
        (askchoice
                (choices "PAL:High Res Laced" "NTSC:High Res Laced" "Cybervision64")
                (default 0)
                (prompt  #deu-screen)
                (help    #deu-screen-help)
        )
)

(set #configA (cat "main = "deu-wad"\n\n"))

(select deu-screen
        (set #configB (cat "# PAL: High Res Laced\nscreenmode = 0x00029004\n\n"))
        (set #configB (cat "# NTSC: High Res Laced\nscreenmode = 0x00019004\n\n"))
        (set #configB (cat "# Cybervision64\nscreenmode = 0x40120001\n\n"))
)

;******************
;Make deu.ini file
;******************
(textfile
        (prompt  #deu-screen)
        (dest    (tackon dest_dir "deu.ini"))
        (append  #config1 #configA #config2 #configB #config3)
)

(complete 100)

(exit #deu-finished (quiet))
