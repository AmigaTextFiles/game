G4C

WINBIG -1 -1 320 256 ""
WinType 00001000
winbackground solid 1 0

xOnLoad
	setgad #this 1 hide
	setgad #this 11 hide
	setgad #this 111 hide
	setgad #this 1111 hide

	setgad #this 10 hide
	setgad #this 20 hide

	cockpit = off

	setgad #this 5 hide

	makescreen destroyer 320/256/3/0x21000/topaz.font/8 "Destroyer Screen"

	setscreen #this destroyer

	movescreen #destroyer 1000 0

	GuiOpen #This

	setpointer #this hide

	loadsound $$this.path\sfx/ptweew.snd      laser
	loadsound $$this.path\sfx/explosion1.smp  explo
	loadsound $$this.path\sfx/fling.snd       abort
	loadsound $$this.path\sfx/orchquick.snd   hit
	loadsound $$this.path\sfx/scratch.snd     pause
	loadsound $$this.path\sfx/waterglass.snd  glass
	loadsound $$this.path\sfx/indigo.snd      timeup

	loadimage $$this.path\gfx/back       back    noremap
	loadimage $$this.path\gfx/pointer    pointer noremap
	loadimage $$this.path\gfx/asteroid   ast     noremap
	loadimage $$this.path\gfx/ufo        ufo     noremap
	loadimage $$this.path\gfx/fighter    fighter noremap
	loadimage $$this.path\gfx/battlestar batstar noremap
	loadimage $$this.path\gfx/xwing      xwing   noremap
	loadimage $$this.path\gfx/jet        jet     noremap
	loadimage $$this.path\gfx/mine       mine    noremap
	loadimage $$this.path\gfx/time+      time+   noremap
	loadimage $$this.path\gfx/time-      time-   noremap
	loadimage $$this.path\gfx/pts+       pts+    noremap
	loadimage $$this.path\gfx/pts-       pts-    noremap
	loadimage $$this.path\gfx/randbonus  rbonus  noremap
	loadimage $$this.path\gfx/explosion  expl    noremap
	loadimage $$this.path\gfx/explosion1 expl1   noremap
	loadimage $$this.path\gfx/explosion2 expl2   noremap
	loadimage $$this.path\gfx/explosion3 expl3   noremap
	loadimage $$this.path\gfx/explosion4 expl4   noremap
	loadimage $$this.path\gfx/explosion5 expl5   noremap
	loadimage $$this.path\gfx/explosion6 expl6   noremap
	loadimage $$this.path\gfx/explosion7 expl7   noremap

	palette set back destroyer

	setgad #this 2 hide
	setgad #this 10 show

	maxpts     = 99999
	timepts    = 100
	astpts     = 150
	minepts    = 200
	ufopts     = 250
	fighterpts = 300
	batstarpts = 350
	xwingpts   = 400
	jetpts     = 450

	hiscore = 0
	score = 0
	time = 0
	p = 0

	changeimage #this 100 0 0 back

	playsound hit

	setgad #this 5 show

	setvar title "\n\nA Game by NIGHTSHADE\nin\nGui4Cli 3.8.5\n\n\n"
	appvar title "Email: thenightshade@freenet.de\n\n\n"
	appvar title "-< Press >-\n\n<RMB> to start a new Game\n<LMB> to Fire\n\n"
	appvar title "<P> for Pause\n<Q> to abort the Game\n"
	appvar title "<C> to change Cockpit-View\n\n"
	appvar title "<CTRL+C> to Quit"

	update #this 5 $title
	update #this 4 "Destroyer (0)"

	partredraw #this 0 0 0 0


xOnClose
	setpointer #this default

	freeimage back
	freeimage pointer
	freeimage ast
	freeimage ufo
	freeimage fighter
	freeimage batstar
	freeimage xwing
	freeimage jet
	freeimage mine
	freeimage time+
	freeimage time-
	freeimage pts+
	freeimage pts-
	freeimage rbonus
	freeimage expl
	freeimage expl1
	freeimage expl2
	freeimage expl3
	freeimage expl4
	freeimage expl5
	freeimage expl6
	freeimage expl7

	freesound laser
	freesound explo
	freesound abort
	freesound hit
	freesound pause
	freesound glass
	freesound timeup

	killscreen destroyer

	ezreq "Program interrupted" "Cancel" ""

	GuiQuit #This


xonclick
	gosub #this shoot

xondoubleclick
	gosub #this shoot


xroutine shoot
	if $time > 0
	and $score < $maxpts
	and $game = 1
	and $p != 1
		if $$mouse.wx >= 0
		and $$mouse.wy >= 0
		and $$mouse.wy <= 229
			gosub #this laser
		endif
	endif

xroutine laser
	playsound laser

	color = $$mouse.color

	setgad #this 1 show
	setgad #this 11 show
	setgad #this 111 show
	setgad #this 1111 show

	changegad #this 1    0 229 $$mouse.x $$mouse.y ""
	changegad #this 11   319 229 $$mouse.x $$mouse.y ""
	changegad #this 111  0 0 $$mouse.x $$mouse.y ""
	changegad #this 1111 319 0 $$mouse.x $$mouse.y ""

	partredraw #this 0 0 0 0

	changegad #this 1    0 229 0 229 ""
	changegad #this 11   319 229 319 229 ""
	changegad #this 111  0 0 0 0 ""
	changegad #this 1111 319 0 319 0 ""

	setgad #this 1 hide
	setgad #this 11 hide
	setgad #this 111 hide
	setgad #this 1111 hide

	ifexists variable nx
	andifexists variable ny
		info image $obj

		if $hit = 0
		and $$mouse.x <= $($nx + $$image.w)
		and $$mouse.x >= $nx
		and $$mouse.y <= $($ny + $$image.h)
		and $$mouse.y >= $ny
		and $color != 1
			playsound explo

			if $obj = rbonus
				randbonus = $$rand.3

				if $randbonus = 0
					obj = time+
				elseif $randbonus = 1
					obj = time-
				elseif $randbonus = 2
					obj = pts+
				elseif $randbonus = 3
					obj = pts-
				endif

				exp = $obj
			else
				delvar randbonus
			endif

			if $obj = ast
				score = $($score + $astpts)
				exp = expl1
				++astc
			elseif $obj = mine
				score = $($score + $minepts)
				exp = expl7
				++minec
			elseif $obj = ufo
				score = $($score + $ufopts)
				exp = expl
				++ufoc
			elseif $obj = fighter
				score = $($score + $fighterpts)
				exp = expl2
				++fighterc
			elseif $obj = batstar
				score = $($score + $batstarpts)
				exp = expl4
				++batstarc
			elseif $obj = xwing
				score = $($score + $xwingpts)
				exp = expl6
				++xwingc
			elseif $obj = jet
				score = $($score + $jetpts)
				exp = expl5
				++jetc
			elseif $obj = time+
				score = $($score + $timepts)
				if $time <= 90
					time = $($time + 10)
				else
					time = 100
				endif
				ifexists variable ~randbonus
					exp = expl3
				endif
			elseif $obj = time-
				if $score >= $timepts
					score = $($score - $timepts)
				else
					score = 0
				endif
				if $time < 10
					time = 0
				else
					time = $($time - 10)
				endif
				ifexists variable ~randbonus
					exp = expl3
				endif
			elseif $obj = pts+
				score = $($score + 500)
				ifexists variable ~randbonus
					exp = expl3
				endif
			elseif $obj = pts-
				if $score >= 500
					score = $($score - 500)
				else
					score = 0
				endif
				ifexists variable ~randbonus
					exp = expl3
				endif
			endif
			if $score >= $maxpts
				score = $maxpts
			endif
			changeimage #this 2 $nx $ny $exp
			hit = 1
		endif
	endif

	update #this 4 "Score: $score"
	update #this 10 $time

	partredraw #this 0 0 0 0


xonkey #32
	if $game = 1
	and $time = 0
	or $score = $maxpts
		gosub #this game 0
	endif


xonkey p
	if $game = 0
	or $time = 0
	or $score = $maxpts
		stop
	endif

	if $p != 1
		playsound pause
		setpointer #this hide
		setgad #this 3 off
		update #this 4 "+++ PAUSED +++"
		p = 1
	else
		setpointer #this #pointer
		setgad #this 3 on
		update #this 4 "Score: $score"
		p = 0
	endif

	partredraw #this 0 0 0 0


xonkey c
	if $game != 1
	or $p = 1
	or $time = 0
	or $score = $maxpts
		stop
	endif

	if $cockpit = off
		playsound explo
		setgad #this 20 show
		cockpit = on
	else
		playsound pause
		setgad #this 20 hide
		cockpit = off
	endif

	partredraw #this 0 0 0 0


xonkey q
	if $game = 1
	and $time != 0
	and $score != $maxpts
		setpointer #this hide
		gosub #this game 0
	endif


xonrmb
	if $game != 1
		gosub #this game 1
	endif


xroutine game st
	if $p = 1
	and $game = 1
		stop
	endif

	if $st != 0
		playsound abort
		setpointer #this #pointer
		setgad #this 20 show
		setgad #this 3 on
		setgad #this 5 hide
		played = 0
		astc = 0
		minec = 0
		ufoc = 0
		fighterc = 0
		batstarc = 0
		xwingc = 0
		jetc = 0
		score = 0
		cockpit = on
		game = 1
		text = "Score: $score"
	else
		playsound glass
		delvar nx
		delvar ny
		delvar astc
		delvar ufoc
		delvar fighterc
		update #this 5 $title
		setgad #this 2 hide
		setgad #this 3 off
		setgad #this 5 show
		setgad #this 20 hide
		text = "Destroyer ($hiscore)"
		score = 0
		cockpit = off
		game = 0
	endif

	time = 100
	update #this 10 $time

	update #this 4 $text
	partredraw #this 0 0 0 0


image 0 0 ""
gadid 100

image 0 0 ""
gadid 2


LINE 0 229 0 229 6
gadid 1
LINE 319 229 319 229 6
gadid 11
LINE 0 0 0 0 6
gadid 111
LINE 319 0 319 0 6
gadid 1111


square 60 60 199 109 3 nofill
gadid 20
line 0 0 60 60 3
gadid 20
line 319 0 259 60 3
gadid 20
line 0 229 60 169 3
gadid 20
line 319 229 259 169 3
gadid 20


xtimer repeat 00:00:01 off
gadid 3
	if $time > 0
	and $score < $maxpts
		--time
		++played
		++score

		update #this 4 "Score: $score"
		update #this 10 $time

		if $time >= 1
		and $score <= $maxpts
			gosub #this runtimer
		else
			gosub #this stoptimer
		endif
	else
		gosub #this stoptimer
	endif

	partredraw #this 0 0 0 0
	

xroutine runtimer
	hit = 0

	randobj = $$rand.50

	if $randobj < 5
		obj = ast
	elseif $randobj >= 5
	and $randobj < 10
		obj = ufo
	elseif $randobj >= 10
	and $randobj < 15
		obj = fighter
	elseif $randobj >= 15
	and $randobj < 20
		obj = batstar
	elseif $randobj >= 20
	and $randobj < 25
		obj = xwing
	elseif $randobj >= 25
	and $randobj < 30
		obj = jet
	elseif $randobj >= 30
	and $randobj < 35
		obj = mine
	elseif $randobj >= 35
	and $randobj < 40
		obj = pts+
	elseif $randobj >= 40
	and $randobj < 45
		obj = pts-
	elseif $randobj >= 45
	and $randobj < 47
		obj = time+
	elseif $randobj >= 47
	and $randobj < 49
		obj = time-
	elseif $randobj = 50
		obj = rbonus
	endif

	info image $obj

	w = $(320 - $$image.w)
	h = $(220 - $$image.h)

	nx = $$rand.$w
	ny = $$rand.$h

	setgad #this 2 show

	changeimage #this 2 $nx $ny $obj

xroutine stoptimer
	playsound timeup

	setgad #this 2 hide
	setgad #this 3 off
	setgad #this 5 show
	setgad #this 20 hide

	if $score < $maxpts
		t = $($timepts / 10)
		a = $($astpts / 10)
		m = $($minepts / 10)
		u = $($ufopts / 10)
		f = $($fighterpts / 10)
		b = $($batstarpts / 10)
		x = $($xwingpts / 10)
		j = $($jetpts / 10)

		plays    = $($played * $t)
		asts     = $($astc * $a)
		mines    = $($minec * $m)
		ufos     = $($ufoc * $u)
		fighters = $($fighterc * $f)
		batstars = $($batstarc * $b)
		xwings   = $($xwingc * $x)
		jets     = $($jetc * $j)
		bonus    = $($play + $asts + $mines + $ufos + $fighters + $batstars + $xwings + $jets)

		setvar pts "\n\nYour Score: $score\n\n"
		appvar pts "Playtime: $played secs x $t = $plays\n\n"
		appvar pts "Asteroid: $astc x $a = $asts\n"
		appvar pts "Mine: $minec x $m = $mines\n"
		appvar pts "UFO: $ufoc x $u = $ufos\n"
		appvar pts "Fighter: $fighterc x $f = $fighters\n"
		appvar pts "Battlestar: $batstarc x $b = $batstars\n"
		appvar pts "X-Wing: $xwingc x $x = $xwings\n"
		appvar pts "Jet: $jetc x $j = $jets\n\n"
		appvar pts "Total Bonus: $bonus\n\n\n"

		if $score < $maxpts
			appvar pts "\n\n"
			score = $($score + $bonus)
		else
			appvar pts "!!! Top Score !!!\n\n\n"
			score = $maxpts
		endif

		appvar pts "Press <SPACE> for Title"

		update #this 5 $pts
	else
		setvar endtxt "\n\n\n\n\n\n\n\nCongratulations!\n\n"
		appvar endtxt "You have reaching the\nmaximum of Points\n\n"
		appvar endtxt "You are really the Best!\n\n\n\n"
		appvar endtxt "Press <SPACE> for Title"
		update #this 5 $endtxt
		score = $maxpts
	endif
	
	if $score > $hiscore
		hiscore = $score
	endif

	if $time = 0
		update #this 4 ">>> $score <<<"
	elseif $score = $maxpts
		update #this 4 "*** $score ***"
	endif

	setpointer #this hide


gauge 3 233 314 20 out ridge 7 4 100
	gadid 10


XTEXTBOX 0 233 314 20 "" ""
	attr tbstyle 2/3/shadow/center
	attr tbox 2/1/-1/none
		gadfont andrea.font 16 000
		gadid 4


XTEXTBOX 0 0 319 230 "" ""
	attr tbstyle 6/5/outline/center
	attr tbox 0/0/-1/none
		gadfont topaz.font 8 000
		gadid 5
