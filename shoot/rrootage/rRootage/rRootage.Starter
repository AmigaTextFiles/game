; rRootage.Starter
;
; This script is not opensource.
;
; $VER: rRootage Starter 1.0 (2005/09/24) Nicolas Sallin <henes@biclodon.com>


failat 21

set debug echo >nil:
set haschanged 0

; default config
set cfgwidth    640
set cfgheight   480
set cfgwindow   0
set cfgnosound  0
set cfgreverse  0
set cfgnowait   0
set cfgaccframe 0

lab start
$debug "start"

set choice `requestchoice "rRootage Starter" "rRootage Starter v1.0 (2005/09/24)" "Start|Edit preferences|Visit website|Quit"`

lab pouet
$debug pouet

; parse config
set tmp ""
set tmp `search nonum rRootage.config width`
set cfgwidth `echo first=10 "$tmp"`

set tmp ""
set tmp `search nonum rRootage.config height`
set cfgheight `echo first=10 "$tmp"`

set tmp ""
set tmp `search nonum rRootage.config window`
set cfgwindow `echo first=10 "$tmp"`

set tmp ""
set tmp `search nonum rRootage.config nosound`
set cfgnosound `echo first=10 "$tmp"`

set tmp ""
set tmp `search nonum rRootage.config reverse`
set cfgreverse `echo first=10 "$tmp"`

set tmp ""
set tmp `search nonum rRootage.config nowait`
set cfgnowait `echo first=10 "$tmp"`

set tmp ""
set tmp `search nonum rRootage.config accframe`
set cfgaccframe `echo first=10 "$tmp"`


; build command line arguments
set args -width $cfgwidth -height $cfgheight
if $cfgwindow not eq 0
	set args $args -window
endif
if $cfgnosound not eq 0
	set args $args -nosound
endif
if $cfgreverse not eq 0
	set args $args -reverse
endif
if $cfgnowait not eq 0
	set args $args -nowait
endif
if $cfgaccframe not eq 0
	set args $args -accframe
endif

$debug "arguments = $args"


if $choice eq 1
	$debug "run"
	echo Starting rRootage...
	rRootage $args ;>CON:40/30/400/100/rRootage/CLOSE/AUTO/
	echo End
else
if $choice eq 2
	skip prefs
else
if $choice eq 3
	openurl http://www.biclodon.com/software/rrootage/
endif
endif
endif

skip exit


; write config
lab writeconfig
$debug writeconfig

if $haschanged not eq 0
	set haschanged 0
	echo >rRootage.config "; AUTOGENERATED FILE... DO NOT EDIT!"
	echo >>rRootage.config "width    $cfgwidth"
	echo >>rRootage.config "height   $cfgheight"
	echo >>rRootage.config "window   $cfgwindow"
	echo >>rRootage.config "nosound  $cfgnosound"
	echo >>rRootage.config "reverse  $cfgreverse"
	echo >>rRootage.config "nowait   $cfgnowait"
	echo >>rRootage.config "accframe $cfgaccframe"
endif

skip back start


; Prefs
lab prefs
$debug "edit prefs"

if $cfgwindow eq 1
	set window window
else
	set window fullscreen
endif
if $cfgnosound eq 1
	set sound " no"
else
	set sound ""
endif
if $cfgreverse eq 1
	set key reversed
else
	set key normal
endif
if $cfgnowait eq 1
	set slowdown " no"
else
	set slowdown ""
endif
if $cfgaccframe eq 1
	set framerate alternative
else
	set framerate normal
endif

set choice `requestchoice "rRootage Preferences" "Currently $cfgwidth x $cfgheight, $window,$sound sound, $key key layout,$slowdown slowdown, $framerate framerate." "Video resolution|Display Mode|Sound|Keys layout|Slowdown|Framerate|Quit prefs"`
if $choice eq 0
	skip back writeconfig
endif
if $choice eq 1
	$debug "edit vidres prefs"
	skip vidres
endif
set haschanged 1
if $choice eq 2
	$debug "edit display mode prefs"
	set choice `requestchoice "rRootage Prefs: Display Mode" "Should the game run in window or fullscreen?" "Window|Fullscreen (default)"`
	if $choice eq 1
		set cfgwindow 1
	else
		set cfgwindow 0
	endif
endif
if $choice eq 3
	$debug "edit sound prefs"
	set choice `requestchoice "rRootage Prefs: Sound" "Should sound be disabled or enabled?" "Disabled|Enabled (default)"`
	if $choice eq 1
		set nosound 1
	else
		set nosound 0
	endif
endif
if $choice eq 4
	$debug "keys layout prefs"
	set choice `requestchoice "rRootage Prefs: Keys Layout" "Should laser and bomb keys be reversed?" "Yes|No (default)"`
	if $choice eq 1
		set cfgreverse 1
	else
		set cfgreverse 0
	endif
endif
if $choice eq 5
	$debug "slowdown prefs"
	set choice `requestchoice "rRootage Prefs: Slowdown" "Should slowdown be disabled or enabled?" "Disabled|Enabled (default)"`
	if $choice eq 1
		set cfgnowait 1
	else
		set cfgnowait 0
	endif
endif
if $choice eq 6
	$debug "framerate prefs"
	set choice `requestchoice "rRootage Prefs: Framerate" "Should the alternative framerate management algorithm be used?" "Yes|No (default)"`
	if $choice eq 1
		set cfgaccframe 1
	else
		set cfgaccframe 0
	endif
endif

skip back prefs


; Edit vidres prefs
lab vidres
$debug "$cfgwidth x $cfgheight"
set choice `requestchoice "rRootage Prefs: display resolution" "The currently selected display resolution is $cfgwidth x $cfgheight." "Select a predefined resolution|Change width|Change height|Done"`
if $choice eq 0
	skip back prefs
endif
if $choice eq 1
	skip selectmode
endif
if $choice eq 2
	set display width
endif
if $choice eq 3
	set display height
endif

skip custommode


; Select a predefined mode
lab selectmode
$debug "select mode"
set choice `requestchoice "rRootage Prefs: resolution selection" "Please select a predefined display resolution." "1600x1200|1280x960|1024x768|800x600|640x480|Cancel"`
if $choice eq 1
	set cfgwidth  1600
	set cfgheight 1200
endif
if $choice eq 2
	set cfgwidth  1280
	set cfgheight 960
endif
if $choice eq 3
	set cfgwidth  1024
	set cfgheight 768
endif
if $choice eq 4
	set cfgwidth  800
	set cfgheight 600
endif
if $choice eq 5
	set cfgwidth  640
	set cfgheight 480
endif
set haschanged 1
skip back vidres


; Change width/height
lab custommode
$debug "edit $display"

if $display eq width
	set size $cfgwidth
else
	set size $cfgheight
endif
set choice `requestchoice "rRootage Prefs: $display setup" "Current display $display is $size." "-100|-10|-1|+1|+10|+100|Done"`
set val +0
if $display eq width
	set min1 430
	set min2 330
	set min3 320
else
	set min1 340
	set min2 250
	set min3 240
endif
if $choice eq 1
	if val $size ge $min1
		set val -100
	endif
endif
if $choice eq 2
	if val $size ge $min2
		set val -10
	endif
endif
if $choice eq 3
	if val $size gt $min3
		set val -1
	endif
endif
if $choice eq 4
	if val $size not ge 4096
		set val +1
	endif
endif
if $choice eq 5
	if val $size not gt 4086
		set val +10
	endif
endif
if $choice eq 6
	if val $size not gt 3996
		set val +100
	endif
endif
if $choice eq 0
	skip back vidres
endif

$debug "$val"
$debug "`eval $size $val`"
set cfg$display `eval $size $val`

skip back custommode


lab exit
$debug "exit"

