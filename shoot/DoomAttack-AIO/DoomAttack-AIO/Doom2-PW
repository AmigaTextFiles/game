;-------------------------------
;Startup script for DoomAttack.
;Game:  Doom II: Hell on Earth
;WAD:   DOOM2.WAD or DOOM2F.WAD
;For use with Patch WAD files.
;-------------------------------


IF NOT EXISTS DoomAttack
Echo "*NError: Could not find DoomAttack!"
SKIP DOOMEND
ENDIF

ASSIGN EXISTS T: >NIL:
IF WARN
MakeDir Ram:T
Assign T: Ram:T
ENDIF

ASSIGN EXISTS Env: >NIL:
IF WARN
MakeDir Ram:Env
Assign Env: Ram:Env
ENDIF

Set MainWAD ""
If Exists DOOM2.WAD
Set MainWAD DOOM2.WAD
EndIf
If Exists DOOM2F.WAD
Set MainWAD DOOM2F.WAD
EndIf

If "$MainWAD" NOT GT ""
Echo "*NError: Could not find DOOM2.WAD or DOOM2F.WAD"
SKIP DOOMEND
EndIf


If NOT Exists Env:D2PWADdir
Echo >Env:D2PWADdir ""
Echo >Env:D2PWADfile ""
 if exists DOOM2-PWADs
 Echo >Env:D2PWADdir DOOM2-PWADs
 endif
EndIf

RequestFile >Env:D2PWADselect DRAWER "$D2PWADdir" FILE "$D2PWADfile" PATTERN #?.WAD TITLE "Select Doom2 Patch WAD file" NOICONS
If Warn
SKIP DOOMEND
EndIf
 If $D2PWADselect NOT GT ""
 Echo "*NNothing was selected!"
 SKIP DOOMEND
 EndIf
If NOT Exists $D2PWADselect
Echo "*N$D2PWADselect does not exist!"
SKIP DOOMEND
EndIf
 If "`Echo $D2PWADselect LEN 1`" EQ "/"
 Echo "*NNothing was selected!"
 SKIP DOOMEND
 EndIf
If "`Echo $D2PWADselect LEN 1`" EQ ":"
Echo "*NNothing was selected!"
SKIP DOOMEND
EndIf
 List >Env:D2PWADdir $D2PWADselect LFORMAT %P
 List >Env:D2PWADfile $D2PWADselect LFORMAT %N


  Echo ""
  IF EXISTS AddBuffDA
  Execute AddBuffDA
  ENDIF


Stack 20000

DoomAttack -wad $MainWAD -file $D2PWADselect


LAB DOOMEND

