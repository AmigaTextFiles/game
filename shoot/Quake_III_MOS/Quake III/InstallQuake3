;
; MorphOS Quake3 installer
; Copyright (C) 2005 Harry Sintonen <sintonen@iki.fi>
;

failat 21

bin/q3freechk
if error
  requestchoice >nil: "MorphOS Quake3 Installer" "You need about 500MB free storage to install to*N*"`cd`*"" "Ok"
  quit 5
endif

requestchoice >env:q3choice "MorphOS Quake3 Installer" "Please insert the Quake III: Arena CD-ROM into CD-Drive*NPress Next when done." "Next|Abort"
if val "$q3choice" eq "0"
  unsetenv q3choice
  quit 5
endif
unsetenv q3choice

echo "Copying files. This will take a moment, please wait..."
copy all Quake3:Quake3/baseq3(%) ""
if warn
  echo "*NSomething went wrong. Could not install Quake3."
else
  protect >nil: all baseq3 rwd
  if not exists baseq3/pak0.pk3
    echo "*NSomething went wrong. Could not install Quake3."
  else
    execute installupdate quiet
    if warn
      echo "*NNOTE: You must launch installupdate to finish the Quake3 installation."
      wait 8
    else
      echo "*NQuake3 installed ok."
    endif
    delete >nil: force bin/q3freechk InstallQuake3 InstallQuake3.info
    delete >nil: force bin
  endif
endif

wait 3
