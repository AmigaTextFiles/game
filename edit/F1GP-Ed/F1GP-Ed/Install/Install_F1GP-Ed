; F1GP-Ed Install Script - Copyright © 1994-2006 Oliver Roberts
;
; $VER: Install_F1GP-Ed 4.31 (29.9.2006)
;
; - German translation by Dirk Kocherscheidt
; - French translation by Daniel Grenson

;****************************************************************************
; Distribution Parameters

(set #frenchdocexists 0)

;****************************************************************************
; English strings
;
(set #str-installtype
	"Choose installation:"
)

(set #str-hd
	"Hard disk installation"
)

(set #str-floppy
	"Floppy disk installation"
)

(set #str-help-installtype (cat
	"F1GP-Ed is best installed to a hard drive.  If you don't have "
	"one, then select floppy disk and this installer will install "
	"F1GP-Ed to floppy disk instead.")
)

(set #str-hdconfirm (cat
	"Make sure you have booted from your hard drive, before "
	"continuing with this installation!\n\nDo you want to continue?")
)

(set #str-floppyconfirm (cat
	"Make sure you have a blank formatted disk ready.  Refer to "
	"the Workbench manual if you are unsure how to format a disk.\n\n"
	"Do you want to continue?")
)

(set #str-diskwarn (cat
	"IMPORTANT: If you are running this installer off a floppy disk, "
	"please make sure the disk is write-protected before proceeding.\n\n"
	"Please be aware that a full installation won't fit on a single DD "
	"floppy disk, so it's best to only choose one language for the "
	"documentation, and only the relevant locale catalog.")
)

(set #str-diskinsertnew
	"Please insert a blank formatted disk in drive DF%ld:"
)

(set #str-diskremove
	"You should now remove this disk and mark it as the %s disk."
)

(set #str-diskrename1
	"main F1GP-Ed"
)

(set #str-diskanydrive (cat
	"From now on you may insert this disk in any drive when requested "
	"(i.e. not just the internal drive).")
)

(set #str-diskinsert
	"Please insert the %s disk in any drive"
)

(set #str-copy
	"Copying files to %s"
)

(set #str-tmpcopy
	"Copying files to temporary directory"
)

(set #str-tmpclean
	"Cleaning temporary directory"
)

(set #str-hdwhere
	"Select where F1GP-Ed should be installed (a drawer called \"F1GP-Ed\" will be created)"
)

(set #str-fileexists (cat
	"Could not create the \"F1GP-Ed\" drawer - make sure there are "
	"no files called \"F1GP-Ed\" in the destination you selected.\n\n\n"
	"Installation Failed!")
)

(set #str-drawerexists (cat
	"The F1GP-Ed drawer already exists.  It will be renamed "
	"to \"F1GP-Ed_OLD\" if you want to keep any of the files "
	"in it, otherwise all files will be deleted/replaced.")
)

(set #str-replacedrawer
	"Replace drawer"
)

(set #str-renamedrawer
	"Rename drawer"
)

(set #str-extract
	"Unpacking files - this may take a while..."
)

(set #str-whichdocs
	"Install documentation for which languages?"
)

(set #str-helplanguage
	"Which language should be used for on-line help?"
)

(set #str-help-helplanguage (cat
	"You should select your preferred language from the list so that "
	"F1GP-Ed knows which version of the documentation to use for "
	"the on-line help feature.  This is done by modifying the "
	"HELPFILE tooltype in the F1GP-Ed program icon.")
)

(set #str-whichcatalogs
	"Install which catalogs? (english is built-in)"
)

(set #str-help-whichcatalogs (cat
	"If you have Workbench 2.1 or higher, F1GP-Ed will be able to "
	"make use of locale catalogs.  This means that the strings in "
	"F1GP-Ed itself can be in a different language.\n\n"
	"Selected catalogs will be installed in the F1GP-Ed drawer.")
)

(set #str-confirmoptional
	"Please select which of these you'd like to install"
)

(set #str-seasondata
	"%s season data"
)

(set #str-soundfiles
	"Replacement sound samples (%s drawer)"
)

(set #str-gfxfiles
	"Replacement cockpit designs (%s drawer)"
)

(set #str-extrafiles
	"Miscellaneous F1GP stuff (%s drawer)"
)

(set #str-update
	"Installing newer version of %s"
)

(set #str-exe
	"F1GP-Ed executable"
)

(set #str-iconfix
	"Setting icon tools and tooltypes"
)

(set #str-floppyextras
	"Copying extra files required for autobooting disk"
)

(set #str-theend (cat
	"\nEnjoy using F1GP-Ed ;-)\n\n"
	"The latest version of F1GP-Ed is always available from the "
	"F1GP-Ed Web Page - %s\n\n%s\n")
)

(set #str-tmpstorage (cat
	"Select an area to use as temporary storage.  A drawer called \"%s\" "
	"will be created there, and will be deleted when installation has "
	"been completed.  The default is usually sufficient.")
)

(set #str-prepdisk
	"Preparing floppy disk"
)

(set #str-bootfloppyend (cat
	"F1GP-Ed disk created.  You should now be able to boot directly "
	"from the disk after this installer has exited.")
)

(set #str-intdrive
	"internal drive"
)

(set #str-selectfloppydrive (cat
	"Select a valid floppy disk drive - this will be the drive that "
	"will be used to initialize your blank disk.  Unless your internal "
        "drive is broken, it's probably best to go along with the default...")
)

;****************************************************************************
; German strings
;
(if (= @language "deutsch") (
	(set #str-installtype
		"Wählen Sie die Art der Installation:"
	)

	(set #str-hd
		"Festplatten Installation"
	)

	(set #str-floppy
		"Floppy Disk Installation"
	)

	(set #str-help-installtype (cat
		"F1GP-Ed wird am besten auf die Festplatte installiert. Wenn "
		"Sie keine haben, dann wählen Sie Floppy Disk und der Installer "
		"wird F1GP-Ed statt dessen auf Diskette installieren.")
	)

	(set #str-hdconfirm (cat
		"Versichern Sie sich, daß Sie von Ihrer Festplatte gebootet haben, "
		"bevor Sie mit dieser Installation fortfahren!\n\nMöchten Sie fortfahren?")
	)

	(set #str-floppyconfirm (cat
		"Denken Sie daran, eine formatierte Leerdiskette bereit zu haben. "
		"Schlagen Sie in Ihrem Workbench Handbuch nach, wenn Sie nicht genau "
		"wissen wie man eine Diskette formatiert.\n\n"
		"Möchten Sie fortfahren?")
	)

	(set #str-diskwarn (cat
		"WICHTIG: Wenn Sie diesen Installer von Diskette laufen lassen, "
		"dann überzeugen Sie sich, daß die Diskette schreibgeschützt ist, "
		"bevor Sie fortfahren.\n\n Bitte denken Sie daran, daß die komplette "
		"Installation nicht auf eine einzige DD Diskette paßt; es ist also am "
		"besten, wenn Sie nur eine Sprache für die Dokumentation und nur den "
		"für Sie relevanten Locale Catalog wählen.")
	)

	(set #str-diskinsertnew
		"Bitte legen Sie eine formatierte Leerdiskette in Laufwerk DF%ld: ein"
	)

	(set #str-diskremove
		"Sie sollten diese Diskette jetzt entfernen und als %s Diskette beschriften."
	)

	(set #str-diskrename1
		"F1GP-Ed Haupt -"
	)

	(set #str-diskanydrive (cat
		"Von jetzt an können Sie die Diskette in jedes beliebige Laufwerk "
		"einlegen (d.h. nicht nur ins interne Laufwerk).")
	)

	(set #str-diskinsert
		"Bitte legen Sie die %s Diskette in ein beliebiges Laufwerk"
	)

	(set #str-copy
		"Kopiere Dateien nach %s"
	)

	(set #str-tmpcopy
		"Kopiere temporär benötigte Dateien in temporäres Verzeichnis"
	)

	(set #str-tmpclean
		"Säubere temporäres Verzeichnis"
	)

	(set #str-hdwhere (cat
		"Wählen Sie, wo F1GP-Ed installiert werden soll (ein Verzeichnis namens "
		"\"F1GP-Ed\" wird dort angelegt)")
	)

	(set #str-fileexists (cat
		"Konnte das Verzeichnis \"F1GP-Ed\" nicht anlegen - überzeugen Sie sich, daß "
		"keine Dateien namens \"F1GP-Ed\" in Ihrem gewählten Ziel sind.\n\n\n"
		"Installation fehlgeschlagen!")
	)

	(set #str-drawerexists (cat
		"Das F1GP-Ed Verzeichnis existiert bereits. Es wird in \"F1GP-Ed_OLD\" "
		"umbenannt, wenn Sie irgendwelche Dateien daraus behalten wollen, "
		"anderenfalls werden alle Dateien gelöscht/ersetzt.")
	)

	(set #str-replacedrawer
		"Ersetze Verzeichnis"
	)

	(set #str-renamedrawer
		"Benenne Verzeichnis um"
	)

	(set #str-extract
		"Entpacke Dateien - das kann eine Weile dauern..."
	)

	(set #str-whichdocs
		"In welchen Sprachen soll die Dokumentation installiert werden?"
	)

	(set #str-helplanguage
		"Welche Sprache soll für die on-line Hilfe benutzt werden?"
	)

	(set #str-help-helplanguage (cat
		"Sie sollten aus dieser Liste Ihre bevorzugte Sprache auswählen, so daß "
		"F1GP-Ed weiß, welche Version der Dokumentation für das on-line Hilfe "
		"Feature benutzt werden soll. Dies wird gemacht, indem das HELPFILE "
		"Tooltype im F1GP-Ed Programm Icon verändert wird.")
	)

	(set #str-whichcatalogs
		"Welche Cataloge sollen installiert werden? (Englisch ist schon eingebaut)"
	)

	(set #str-help-whichcatalogs (cat
		"Wenn Sie Workbench 2.1 oder höher haben, ist F1GP-Ed in der Lage "
		"Locale Catalogs zu benutzen. Das bedeutet, daß die Texte in F1GP-Ed selbst, "
		"in verschiedenen Sprachen sein können.\n\n"
		"Ausgewählte Cataloge werden ins F1GP-Ed Verzeichnis installiert.")
	)

	(set #str-confirmoptional
		"Bitte wählen Sie, was Sie hiervon installieren möchten"
	)

	(set #str-seasondata
		"%s Saison Daten"
	)

	(set #str-soundfiles
		"Ersatz Sound-Samples (%s Verzeichnis)"
	)

	(set #str-gfxfiles
		"Ersatz Cockpit-Designs (%s Verzeichnis)"
	)

	(set #str-extrafiles
		"Verschiedenes F1GP Zeug (%s Verzeichnis)"
	)

	(set #str-update
		"Installiere neue Version der %s"
	)

	(set #str-exe
		"ausführbaren F1GP-Ed Datei"
	)

	(set #str-iconfix
		"Setze Icon Tools und Tooltypes"
	)

	(set #str-floppyextras
		"Kopiere extra Dateien, die für selbst-bootende Diskette benötigt werden"
	)

	(set #str-theend (cat
		"\nViel Spaß beim Benutzen von F1GP-Ed ;-)\n\n"
		"Die neueste Version von F1GP-Ed gibt es immer auf der F1GP-Ed Web Page "
		"- %s\n\n%s\n")
	)
	
	(set #str-tmpstorage (cat
		"Wählen Sie ein Verzeichnis, das als temporäres Verzeichnis benutzt "
		"werden soll. Ein Verzeichnis namens \"%s\" wird dort angelegt und "
		"nach der Installation wieder gelöscht. Die Vorgabe ist normalerweise "
		"ausreichend.")
	)

	(set #str-prepdisk
		"Bereite Diskette vor"
	)

	(set #str-bootfloppyend (cat
		"F1GP-Ed Diskette angelegt. Sie sollten nun direkt von der Diskette "
		"booten können, nachdem der Installer beendet ist.")
	)

	(set #str-intdrive
		"internes Laufwerk"
	)

	(set #str-selectfloppydrive (cat
		"Wählen Sie ein gültiges Diskettenlaufwerk - dieses Laufwerk wird "
		"benutzt, um Ihre Leerdiskette zu initialisieren. Sofern Ihr internes "
		"Laufwerk nicht kaputt ist, ist es wohl am besten, Sie bleiben bei "
		"der Voreinstellung...")
	)
))

;****************************************************************************
; French strings
;
(if (= @language "français") (
	(set #str-installtype
		"Choisissez le type d'installation:"
	)

	(set #str-hd
		"Installation sur disque dur"
	)

	(set #str-floppy
		"Installation sur disquette"
	)

	(set #str-help-installtype (cat
		"Il est préférable d'installer F1GP-Ed sur disque dur. Si vous n'en "
		"possédez pas, sélectionnez alors installation sur disquette, ce "
		"qui sera fait alors par cet installeur.")
	)

	(set #str-hdconfirm (cat
		"Assurez-vous d'avoir lancé votre ordinateur au départ du disque dur, "
		"avant de continuer cette installation!\n\nVoulez-vous continuer?")
	)

	(set #str-floppyconfirm (cat
		"Pensez à avoir une disquette vide et formatée à votre disposition. "
		"Référez-vous au manuel du Workbench si vous ne savez pas comment "
		"formater une disquette.\n\n"
		"Voulez-vous continuer?")
	)

	(set #str-diskwarn (cat
		"IMPORTANT: Si vous lancez cet installeur au départ d'une disquette, "
		"s'il-vous plait, assurez-vous qu'elle est protégée contre l'écriture "
		"avant de continuer.\n\n Soyez conscient que l'installation complète "
		"ne peut tenir sur une simple disquette, il vaut donc mieux ne choisir "
		"qu'une seule langue pour la documentation, et seulement le catalogue "
		"locale utile.")
	)

	(set #str-diskinsertnew
		"Insérez, s'il-vous plait, une disquette vide formatée dans le lecteur DF%ld:"
	)

	(set #str-diskremove
		"Vous devriez retirer cette disquette maintenant et la renommer maintenent comme disquette %s."
	)

	(set #str-diskrename1
		"main F1GP-Ed"
	)

	
	(set #str-diskanydrive (cat
		"A partir d'ici, vous pouvez insérer cette disquette dans un lecteur "
		"lorsque cela est demandé (C'est-à-dire pas seulement dans le lecteur "
		"interne).")
	)

	(set #str-diskinsert
		"Insérez, s'il-vous plait, la disquette %s dans un lecteur de votre choix"
	)

	(set #str-copy
		"Copie des fichiers vers %s"
	)

	(set #str-tmpcopy
		"Copie des fichiers vers un répertoire temporaire"
	)

	(set #str-tmpclean
		"Effacement du répertoire temporaire"
	)

	(set #str-hdwhere (cat
		"Sélectionnez l'endroit où F1GP-Ed devrait être installé (un tiroir appelé "
		"\"F1GP-Ed\" sera créé)")
	)

	(set #str-fileexists (cat
		"Impossible de créer le tiroir \"F1GP-Ed\" - assurez-vous qu'il n'y a pas "
		"de fichier appelé \"F1GP-Ed\" dans la destination que vous avez sélectionnéein.\n\n\n"
		"Echec de l'installation!")
	)

	(set #str-drawerexists (cat
		"Le tiroir F1GP-Ed existe déja. Il sera renommé \"F1GP-Ed_OLD\" "
		"si vous souhaitez conserver un des fichiers qui s'y trouve, autrement "
		"tous les fichiers seront effacés/remplacés.")
	)

	(set #str-replacedrawer
		"Remplacer le tiroir"
	)

	(set #str-renamedrawer
		"Renommer le tiroir"
	)

	(set #str-extract
		"Décompactage des données - cela peut durer un peu..."
	)

	(set #str-whichdocs
		"Dans quelles langues doit être installée la documentation?"
	)

	(set #str-helplanguage
		"Quelle langue devrait être utilisée pour l'aide en ligne?"
	)

	(set #str-help-helplanguage (cat
		"Vous devriez sélectionnez votre langue préférée de la liste de manière "
		"à ce que F1GP-Ed sache quelle version de la documentation utiliser "
		"pour la caractéristique de l'aide en ligne.  Ceci est réalisé par la "
		"modification du type d'outil HELPFILE (Fichier d'aide) de l'icone "
		"du programme F1GP-Ed.")
	)

	(set #str-whichcatalogs
		"Quels catalogues souhaitez-vous installer? (L'anglais est déja incorporé)"
	)

	(set #str-help-whichcatalogs (cat
		"Si vous possédez un Workbench 2.1 ou supérieur, F1GP-Ed sera capable "
		"d'utiliser les catalogues Locale. Cela signifie que les textes de F1GP-Ed "
		"lui-même, peuvent être dans différentes langues.\n\n"
		"Les catalogues sélectionnés seront installés dans le tiroir F1GP-Ed.")
	)

	(set #str-confirmoptional
		"Sélectionnez, s'il-vous plait, ce que vous souhaitez installer"
	)

	(set #str-seasondata
		"%s Données de cette saison"
	)

	(set #str-soundfiles
		"Echantillons de sons de remplacement (%s tiroir)"
	)

	(set #str-gfxfiles
		"Dessins de cockpit de remplacement (%s tiroir)"
	)

	(set #str-extrafiles
		"Diverses choses à propos de/pour F1GP (%s tiroir)"
	)

	(set #str-update
		"Installation de la nouvelle version de %s"
	)

	(set #str-exe
		"Données du programme exécutable F1GP-Ed"
	)

	(set #str-iconfix
		"Pose des icones outils et des types d'outil"
	)

	(set #str-floppyextras
		"Copie des fichiers extras nécessaires pour disquette autoboot"
	)

	(set #str-theend (cat
		"\nPrenez beaucoup de plaisir en utilisant F1GP-Ed ;-)\n\n"
		"La dernière versiob de F1GP-Ed est disponible sur la page Web "
		"- %s\n\n%s\n")
	)
	
	(set #str-tmpstorage (cat
		"Sélectionnez l'endroit utilisé pour conserver momentanément les données. "
		"Un tiroir appelé \"%s\" y sera créé et sera ensuite effacé lorsque "
		"l'installation sera terminée.  Le réglage par défaut est habituellement "
		"suffisant.")
	)

	(set #str-prepdisk
		"Préparez votre disquette"
	)

	(set #str-bootfloppyend (cat
		"Disquette F1GP-Ed créée.  Maintenant vous devriez pouvoir Booter "
		"directement au départ de cette disquette après que cet installeur "
		"ait terminé son activité.")
	)

	(set #str-intdrive
		"lecteur de disquette interne"
	)
))


;****************************************************************************
; Initializations
;
(set #OSVersion (/ (getversion) 65536))
(set #TextTool (if (< #OSVersion 39) "AmigaGuide" "Multiview"))
(set #fromfloppy (exists "C/TestKS13"))
(set #tmpdir "F1GP-Ed_tmp")
(set #packed (exists "F1GP-Ed.lzx"))
(set #floppyassign "F1GP-Ed_newfloppy")
(set #numhelplanguages 3)
(set #numcatalogs 12)

(if (exists "ENV:Language" (noreq))
	(set #envlanguage (getenv "Language"))
	(set #envlanguage @language)
)

(set #n 0)
(set #defcatalog 0)
(while (< #n #numcatalogs)
	(if (= #envlanguage (select #n "ÃeÓtina" "dansk" "deutsch" "español" "français" "greek" "italiano" "nederlands" "norsk" "polski" "suomi" "svenska"))
		(set #defcatalog (shiftleft 1 #n))
	)
	(set #n (+ #n 1))
)

(set #freemem (+ (database "total-mem")))
(set #lowmem (< #freemem 1048576))
(set #lzxopts (if #lowmem ("-bi16 -bo16") ("")))

;****************************************************************************
; Procedures
;
(procedure P_SetTextTool
	(if (exists #TextFile)
		(tooltype
			(prompt #str-iconfix)
			(setdefaulttool #TextTool)
			(dest #TextFile)
		)
	)
)

(procedure P_CleanTmpDir
	(if (exists #tmpdir)
		(
			(working #str-tmpclean)
			(run ("\"%s\" \"%s\" ALL QUIET" #delete (tackon #tmpdir "files/#?")) (safe))
		)
	)
)

(procedure P_CleanTmpDirAll
	(if (exists #tmpdir)
		(
			(P_CleanTmpDir)
			(delete (tackon #tmpdir "F1GP-Ed.lzx") (safe))
			(delete (tackon #tmpdir "UNLZX") (safe))
		)
	)
)

(procedure P_AskForMainDisk
	(if #tofloppy
		(askdisk
			(prompt (#str-diskinsert #str-diskrename1))
			(help @askdisk-help)
			(dest "F1GP-Ed")
		)
	)
)

(procedure P_AskForInstallDisk
	(if (AND #tofloppy #fromfloppy)
		(askdisk
			(prompt (#str-diskinsert "F1GP-Ed_Install"))
			(help @askdisk-help)
			(dest "F1GP-Ed_Install")
		)
	)
)

(procedure P_Extract
	(if #packed
		(
			(if #lowmem (P_AskForInstallDisk))
			(working #str-extract)
			(run ("\"%s\" %s -m -q x \"%s\" %s \"%s\"" #lzx #lzxopts (tackon #srcdir "F1GP-Ed.lzx") #extract (tackon #tmpdir "files/")) (safe))
			(if #lowmem (P_AskForMainDisk))
		)
	)
)

(procedure P_CleanUp
	(if (exists #tmpdir)
		(
			(working #str-tmpclean)
			(run ("\"%s\" \"%s\" ALL QUIET" #delete (tackon #tmpdir "#?")) (safe))
			(delete #tmpdir (safe))
		)
	)
	(makeassign #floppyassign (safe))
)

;****************************************************************************
; Main body of installer (the mess ;)
;

(welcome "")

; ask questions and prepare to install

(set #tofloppy
	(askchoice
		(prompt #str-installtype)
		(default 0)
		(choices #str-hd #str-floppy)
		(help (cat #str-help-installtype "\n\n" @askchoice-help))
	)
)

(if (NOT
	(askbool
		(prompt (if #tofloppy #str-floppyconfirm #str-hdconfirm))
		(default 0)
		(help "")
	))
	(exit (quiet))
)

(onerror (P_CleanUp))

(if (OR #packed #tofloppy)
	(
		(set #tmpstorage
			(if (= 2 @user-level)
				(askdir
					(prompt (#str-tmpstorage #tmpdir))
					(help @askdir-help)
					(disk)
					(default "RAM:")
				)
				"RAM:"
			)
		)
		(set #tmpdir (tackon #tmpstorage #tmpdir))
	)
)

(if #tofloppy
	(		
		(copyfiles
			(prompt #str-tmpcopy)
			(help @copyfiles-help)
			(source "C:Delete")
			(dest #tmpdir)
			(safe)
		)
		(set #delete (tackon #tmpdir "Delete"))
		(if #fromfloppy
			(copyfiles
				(prompt #str-tmpcopy)
				(help @copyfiles-help)
				(source (if (< #OSVersion 36) "C:Install13" "C:Install"))
				(dest #tmpdir)
				(newname "Install")
			)
		)
		(if (AND #fromfloppy (NOT @installer-version))
			(copyfiles
				(prompt #str-tmpcopy)
				(help @copyfiles-help)
				(source "C:Relabel")
				(dest #tmpdir)
				(safe)
			)
		)
		(message #str-diskwarn)
		(until #valid
			(set #drive
				(askchoice
					(prompt #str-selectfloppydrive)
					(help @askchoice-help)
					(choices ("DF0: (%s)" #str-intdrive) "DF1:" "DF2:" "DF3:")
					(default 0)
				)
			)
			(set #valid (exists ("DF%ld:" #drive) (noreq)))
		)
		(message (#str-diskinsertnew #drive))
		(working #str-prepdisk)
		(set #oldname (getassign ("DF%ld" #drive) "d"))
		; old versions of installer didn't recoginize disk param!
		(if @installer-version
			(rename #oldname "F1GP-Ed" (disk) (safe))
			(if #fromfloppy
				(
					(run ("\"%s\" \"%s\" F1GP-Ed" (tackon #tmpdir "Relabel") #oldname) (safe))
					(delete (tackon #tmpdir "Relabel") (safe))
				)
				(run ("C:Relabel \"%s\" F1GP-Ed" #oldname) (safe))
			)
		)
		(if #fromfloppy
			(
				(run ("\"%s\" DF%ld:" (tackon #tmpdir "Install") #drive))
				(delete (tackon #tmpdir "Install"))
			)
			(if (exists "C:Install")
				(run ("C:Install DF%ld:" #drive))
			)
		)
		(makeassign #floppyassign ("DF%ld:" #drive) (safe))
		(run ("\"%s\" %s:#? ALL QUIET" #delete #floppyassign))
		(message (cat (#str-diskremove #str-diskrename1) "  " #str-diskanydrive))
		(set #destdir ("%s:" #floppyassign))
		(set @default-dest #destdir)
	)
; else
	(
		(set #delete "C:Delete")
		(set #destdir
			(askdir
				(prompt #str-hdwhere)
				(help @askdir-help)
				(default @default-dest)
			)
		)
		(set @default-dest (tackon #destdir "F1GP-Ed"))
		(if (= 1 (exists @default-dest))
			(
				(message #str-fileexists)
				(exit (quiet))
			)
		)
		(if (exists @default-dest)
			(if 	(askbool
					(prompt #str-drawerexists)
					(default 0)
					(help "")
					(choices #str-renamedrawer #str-replacedrawer)
				)

				(
					(rename @default-dest (cat @default-dest "_OLD"))
					(rename (cat @default-dest ".info") (cat @default-dest "_OLD.info"))
				)
			)
		)
		(if (NOT (exists @default-dest))
			(makedir @default-dest (infos))
		)
	)
)

(if #fromfloppy
	(
		(set #unpackdir (tackon #tmpdir "files"))
		(set #srcdir (if (AND #tofloppy (NOT #lowmem)) #tmpdir ""))
		(set #diskdir (if #tofloppy #unpackdir ""))
	)
	(
		(set #srcdir "/")
		(set #unpackdir #srcdir)
		(set #diskdir #srcdir)
		(set #frenchdocexists (exists (tackon #srcdir "Docs/F1GP-Ed_français.guide")))
	)
)

(set #n 0)
(set #defhelp 0)
(if (= #envlanguage "deutsch")
	(set #defhelp 1)
)
(if (AND (= #envlanguage "français") #frenchdocexists)
	(set #defhelp 2)
)

(set #whichdocs
	(askoptions
		(prompt #str-whichdocs)
		(help @askoptions-help)
		(if #frenchdocexists (choices "English" "Deutsch" "Français") (choices "English" "Deutsch"))
		(default (shiftleft 1 #defhelp))
	)
)

(set #n 0)
(set #count 0)
(set #single 0)
(while (< #n #numhelplanguages)
	(if (IN #whichdocs #n)
		(
			(set #count (+ #count 1))
			(set #single #n)
		)
	)
	(set #n (+ #n 1))
)

(if (> #count 1)
	(set #help-language
		(askchoice
			(prompt #str-helplanguage)
			(help (cat #str-help-helplanguage "\n\n" @askchoice-help))
			(if #frenchdocexists (choices "English" "Deutsch" "Français") (choices "English" "Deutsch"))
			(default #defhelp)
		)
	)
;else
	(set #help-language #single)
)

(set #helpfile
	("Docs/F1GP-Ed_%s.guide" (select #help-language "english" "deutsch" "français"))
)

(if (>= #OSVersion 36)
	(set #helpfile (cat "PROGDIR:" #helpfile))
)

(if (>= #OSVersion 36)
	(set #whichcatalogs
		(askoptions
			(prompt #str-whichcatalogs)
			(help (cat #str-help-whichcatalogs "\n\n" @askoptions-help))
			(choices "ÃeÓtina" "dansk" "deutsch" "español" "français" "greek"
				 "italiano" "nederlands" "norsk" "polski" "suomi" "svenska")
			(default #defcatalog)
		)
	)
	(set #whichcatalogs 0)
)

(set #opts
	(askoptions
		(prompt #str-confirmoptional)
		(help @askoptions-help)
		(choices (#str-seasondata "2002")
			 (#str-seasondata "2001-1994")
			 (#str-soundfiles "Sound")
			 (#str-gfxfiles "Graphics")
			 (#str-extrafiles "Extras")
		)
		(default 31)
	)
)

(P_AskForInstallDisk)

(set #lzx "C/UNLZX")

(if (AND (AND #fromfloppy #tofloppy) (NOT #lowmem))
	(
		(copyfiles
			(prompt #str-tmpcopy)
			(help @copyfiles-help)
			(source "F1GP-Ed.lzx")
			(dest #tmpdir)
			(safe)
		)
		(copyfiles
			(prompt #str-tmpcopy)
			(help @copyfiles-help)
			(source "C/UNLZX")
			(dest #tmpdir)
			(safe)
		)
		(set #lzx (tackon #tmpdir "UNLZX"))
	)
)

(complete 0)

; install libs

(if #tofloppy
	(set #libdir ("%s:Libs" #floppyassign))
	(set #libdir "LIBS:")
)

(if (< #OSVersion 36)
	(set #extract "Libs/f1gp.l#? Libs/gad#? Libs/reqt#?")
	(set #extract "Libs/f1gp.library Libs/reqtools.library")
)
(P_Extract)

(P_AskForMainDisk)

(copylib
	(prompt (#str-update "f1gp.library"))
	(source (tackon #unpackdir "Libs/f1gp.library"))
	(dest #libdir)
	(help @copylib-help)
	(confirm)
)

(complete 3)

(if (< #OSVersion 36) 
	(	(set #ReqToolsSource "Libs/reqtools.library")
		(copylib
			(prompt (#str-update "gadtools13.library"))
			(source (tackon #unpackdir "Libs/gadtools13.library"))
			(dest #libdir)
			(help @copylib-help)
			(confirm)
		)
	)
	(	(set #ReqToolsSource "Libs/reqtools.library")
		(set #asllib (tackon #libdir "asl.library"))
		(if (exists #asllib)
			(	(set #aslver (/ (getversion #asllib) 65536))
				(if (>= #aslver 38)
					(set #ReqToolsSource "")
				)
			)
		)
	)
)

(if #ReqToolsSource
	(copylib
		(prompt (#str-update "reqtools.library"))
		(source (tackon #unpackdir #ReqToolsSource))
		(dest #libdir)
		(help @copylib-help)
		(newname "reqtools.library")
		(confirm)
	)
)

(P_CleanTmpDir)

(complete 10)

; install F1GP-Ed drawer

(P_AskForMainDisk)

(set #extract "F1GP-Ed F1GP-Ed.info Docs.info")
(P_Extract)

(copylib
	(prompt (#str-update #str-exe))
	(source (tackon #unpackdir "F1GP-Ed"))
	(dest @default-dest)
	(help @copylib-help)
	(infos)
	(confirm)
)

(complete 25)

(copyfiles
	(source (tackon #unpackdir "Docs.info"))
	(dest @default-dest)
	(help @copyfiles-help)
)

(P_CleanTmpDir)

(set #n 0)
(while (< #n #numhelplanguages)
	(if (IN #whichdocs #n)
		(
			(set #docfilename ("Docs/F1GP-Ed_%s.guide" (select #n "english" "deutsch" "français")))
			(set #extract (cat #docfilename "#?"))
			(P_Extract)
			(if (exists (tackon #unpackdir #docfilename))
	                	(copyfiles
					(source (tackon #unpackdir #docfilename))
					(dest (tackon @default-dest "Docs"))
					(help @copyfiles-help)
					(infos)
				)
			)
			(P_CleanTmpDir)
		)
	)
	(set #n (+ #n 1))
)

(complete 40)

(set #extract "History.txt#?")
(P_Extract)

(copyfiles
	(prompt (#str-copy @default-dest))
	(source #unpackdir)
	(choices "History.txt")
	(dest @default-dest)
	(help @copyfiles-help)
	(infos)
	(confirm)
)
(P_CleanTmpDir)

(complete 50)

; copy optional files

(if (IN #opts 0)
	(
		(set #extract "2002.f1gp#?")
		(P_Extract)
		(set #filename "2002.f1gp")
		(copyfiles
			(source (tackon #unpackdir #filename))
			(dest @default-dest)
			(help @copyfiles-help)
			(infos)
		)
		(set #filename (cat (tackon #unpackdir #filename) ".events"))
		(if (exists #filename)
			(copyfiles
				(source #filename)
				(dest @default-dest)
				(help @copyfiles-help)
			)
		)
	)
)

(P_CleanTmpDir)

(complete 55)

; copy optional dirs

(set #n 1)
(while (< #n 5)
	(if (IN #opts #n)
		(
			(set #filename (select (- #n 1) "Datafiles" "Sound" "Graphics" "Extras"))
			(set #extract (cat #filename "/#? " #filename ".info"))
			(P_Extract)
			(copyfiles
				(source (tackon #unpackdir (cat #filename ".info")))
				(dest @default-dest)
				(help @copyfiles-help)
			)
			(copyfiles
				(source (tackon #unpackdir #filename))
				(dest (tackon @default-dest #filename))
				(help @copyfiles-help)
				(all)
				(infos)
			)
			(P_CleanTmpDir)
		)
	)		
	(set #n (+ #n 1))
)

(if (IN #opts 5)
	(tooltype
		(prompt #str-iconfix)
		(setdefaulttool "Installer")
		(dest (tackon @default-dest "Extras/Install_F1GP"))
	)
)

(complete 75)

(if (>= #OSVersion 36)
	(
		(set #newdir (tackon @default-dest "Catalogs"))
		(if (NOT (exists #newdir))
			(makedir #newdir)
		)
		(set #extract "Catalogs/#? Catalogs.info")
		(P_Extract)

		(copyfiles
			(source (tackon #unpackdir "Catalogs.info"))
			(dest @default-dest)
			(help @copyfiles-help)
		)
		(set #dest (tackon @default-dest "Catalogs"))
		(copyfiles
			(prompt (#str-copy #dest))
			(source (tackon #unpackdir "Catalogs/NewCatalog.ct"))
			(dest #dest)
			(help @copyfiles-help)
			(infos)
			(confirm)
		)
	)
)

(complete 77)

(set #n 0)
(while (< #n #numcatalogs)
	(if (IN #whichcatalogs #n)
		(
			(set #filename ("Catalogs/%s/F1GP-Ed.catalog" (select #n "ÃeÓtina" "dansk" "deutsch" "español" "français" "greek" "italiano" "nederlands" "norsk" "polski" "suomi" "svenska")))
			(copyfiles
				(source (tackon #unpackdir #filename))
				(dest (tackon @default-dest (pathonly #filename)))
				(help @copyfiles-help)
			)
		)
	)
	(set #n (+ #n 1))
)

(P_CleanTmpDir)

(complete 82)

(if (AND #fromfloppy #tofloppy)
	(
		(P_AskForInstallDisk)
		(if (exists "F1GP-Ed.key")
			(copyfiles
				(prompt #str-tmpcopy)
				(help @copyfiles-help)
				(source "F1GP-Ed.key")
				(dest #unpackdir)
			)
		)
		(if (exists "Registered.guide")
			(copyfiles
				(prompt #str-tmpcopy)
				(help @copyfiles-help)
				(source "Registered.guide")
				(dest #unpackdir)
				(infos)
			)
		)
		(P_AskForMainDisk)
	)
)

(if (exists (tackon #diskdir "F1GP-Ed.key"))
	(copyfiles
		(prompt (#str-copy @default-dest))
		(source (tackon #diskdir "F1GP-Ed.key"))
		(dest @default-dest)
		(help @copyfiles-help)
		(confirm)
	)
)
(set #dest (tackon @default-dest "Docs"))
(if (exists (tackon #diskdir "Registered.guide"))
	(copyfiles
		(prompt (#str-copy #dest))
		(source (tackon #diskdir "Registered.guide"))
		(dest #dest)
		(help @copyfiles-help)
		(infos)
		(confirm)
	)
)

(if (AND #fromfloppy #tofloppy)
	(P_CleanTmpDir)
)

(complete 87)

(working #str-iconfix)

(tooltype
	(prompt #str-iconfix)
	(settooltype "HELPFILE" #helpfile)
	(dest (tackon @default-dest "F1GP-Ed"))
)

(if (AND #tofloppy #fromfloppy)
	(set #TextTool "F1GP-Ed:C/AmigaGuide")
)

(set #n 0)
(while	(set #TextFile
		(select #n
			"Docs/F1GP-Ed_english.guide"
			"Docs/F1GP-Ed_deutsch.guide"
			"Docs/F1GP-Ed_français.guide"
			"History.txt"
			"Extras/Other_F1GP_Stuff.guide"
                        "Graphics/ReadMe"
                        "Sound/ReadMe"
			""
		)
	)

	(set #TextFile (tackon @default-dest #TextFile))
	(P_SetTextTool)
	(set #n (+ #n 1))
)

(P_CleanTmpDirAll)

(complete 90)

; install files for autoboot disk

(if (AND #tofloppy #fromfloppy)
	(
		(P_AskForInstallDisk)
		(copyfiles
			(prompt #str-tmpcopy)
			(help @copyfiles-help)
			(source "C")
			(dest (tackon #unpackdir "C"))
			(choices "AmigaGuide" "GOWB" "PPLoadSeg" "Type")
		)
		(copyfiles
			(prompt #str-tmpcopy)
			(help @copyfiles-help)
			(source "Libs")
			(dest (tackon #unpackdir "Libs"))
			(choices "amigaguide.library.pp" "powerpacker.library" "iffparse.library.pp")
		)
		(copyfiles
			(prompt #str-tmpcopy)
			(help @copyfiles-help)
			(source "S/disk.startup-sequence")
			(dest (tackon #unpackdir "S"))
			(newname "Startup-Sequence")
		)
		(copyfiles
			(prompt #str-tmpcopy)
			(help @copyfiles-help)
			(source "S/disk.text")
			(dest (tackon #unpackdir "S"))
			(newname "text")
		)
		(copyfiles
			(prompt #str-tmpcopy)
			(help @copyfiles-help)
			(source "system-configuration")
			(dest #unpackdir)
		)
		(copyfiles
			(prompt #str-tmpcopy)
			(help @copyfiles-help)
			(source "S/Disk.info")
			(dest #unpackdir)
		)
			
		(if (< #OSVersion 36)
			(
				(copyfiles
					(prompt #str-tmpcopy)
					(help @copyfiles-help)
					(source "C")
					(dest (tackon #unpackdir "C"))
					(choices "Echo" "Else" "EndIf"
						 "If" "Run")
				)
				(copyfiles
					(prompt #str-tmpcopy)
					(help @copyfiles-help)
					(source "C/Assign13")
					(dest (tackon #unpackdir "C"))
					(newname "Assign")
				)
				(copyfiles
					(prompt #str-tmpcopy)
					(help @copyfiles-help)
					(source "C/SetPatch13")
					(dest (tackon #unpackdir "C"))
					(newname "SetPatch")
				)
				(copyfiles
					(prompt #str-tmpcopy)
					(help @copyfiles-help)
					(source "Libs")
					(dest (tackon #unpackdir "Libs"))
					(choices "icon.library.pp")
				)
				(copyfiles
					(prompt #str-tmpcopy)
					(help @copyfiles-help)
					(source "L")
					(dest (tackon #unpackdir "L"))
					(all)
				)
			)
			(
				(copyfiles
					(prompt #str-tmpcopy)
					(help @copyfiles-help)
					(source "C")
					(dest (tackon #unpackdir "C"))
					(choices "Assign" "SetPatch")
				)
			)
		)
		(P_AskForMainDisk)
		(copyfiles
			(prompt #str-floppyextras)
			(help @copyfiles-help)
			(source #unpackdir)
			(dest @default-dest)
			(all)
		)
		(complete 99)
		(P_AskForInstallDisk)
		(P_CleanTmpDir)
	)
)

(P_CleanUp)

; END

(complete 100)

(exit (cat (if (AND #fromfloppy #tofloppy) #str-bootfloppyend "")
	(#str-theend
	"http://www.amigaf1.co.uk/F1GP-Ed/"
	"Oliver Roberts (oliver@futaura.co.uk)"))
)
