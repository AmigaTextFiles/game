; CivPatcher 2 - install script
;
(set cpu (+ (database "cpu")))

(if     (OR (< (getversion) 39)
	     (< cpu 68020)
	)
	(abort "Sorry, but you need at least Kick/WB 3.0 and a 68020 CPU")
)

(set name
	     (askdir
		(prompt "Where you wish to install to ? (A Drawer will be created)")
		(help @askdir-help "\nA Drawer \"CivPatcher2\" will be created.")
		(default "T:")
	     )
)

(set destname
	(tackon name "CivPatcher2"
	)
)


(makedir destname
	(prompt "")
	(help @makedir-help)
	(infos)
)

(set @default-dest destname)

(set name (tackon destname "catalogs"))

(makedir name
	(prompt "")
	(help @makedir-help)
)

(copyfiles
	(prompt "Wich languages you wish to install ?")
	(help @copyfiles-help)
	(source "catalogs")
	(all)
	(dest name)
	(confirm)
)

(copyfiles
	(prompt "Installing CivPatcher 2")
	(help @copyfiles-help)
	(source "")
	(all)
	(dest destname)
)


(set civin
	(askdir
		(prompt "Where do you save your Civilization games ?")
		(help "The volume you use to save your games. It will be setup as default load dir for the CivPatcher.")
		(default "SYS:")
		(disk)
	)
)

(set civout
	(askdir
		(prompt "Where do you want to save your patched games ?")
		(help "This can be a subdrawer, if you want to save in CivMan mode. Otherwise you must select a volume.")
		(default "RAM:")
		(disk)
	)
)

(set option
	(askbool
		(prompt "Do you wish to activate the CivMan option as default ?")
		(help "You can change it later with a checkbox. It is only for default setting, if you start the Civpatcher")
	)
)

(tooltype
	(prompt "")
	(dest (tackon destname "Civil.2"))
	(settooltype "LoadDrawer" civin)
	(settooltype "SaveDrawer" civout)
	(if option
	      (settooltype "CivMan" "TRUE")
	)
)

(set civname
	(askdir
		(prompt "Where has you Civilization installed ?")
		(help "Needed for the loader-tooltypes, to start Civilization.")
		(default "SYS:")
	)
)

(set extname
	(askdir
		(prompt "Where do you want to extract CivMan-files ?")
		(help "Needed for loader-tooltypes. Allows you to start saved games easily.")
		(default "RAM:")
	)
)

(tooltype
	(prompt "")
	(dest (tackon destname "Loader"))
	(settooltype "GamePath" civname)
	(settooltype "Extract2" extname)
)
