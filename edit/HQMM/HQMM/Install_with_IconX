;install script for HQMM. If you possess the Installer utility, use it
;and do not use this script!

version >NIL: exec.library 37
if warn
	echo "I'm sorry, but you will need at least Release 2 to run HQMM."
	quit
endif

echo "If you have the Commodore Installer utility, please select the"
echo "Install_HQMM icon to install HQMM."
echo ""
echo "Enter the directory where you want HQMM installed to."
echo "(A new directory will be created in this directory): " noline
setenv >NIL: PFDest ?

if not exists $PFDest
	echo Invalid Path: $PFDest
else
	ASK "Ok to install HQMM to $PFDest?"
	if warn
		assign DEST: $PFDest

		echo Creating new directory.
		makedir DEST:HQMM
		copy >NIL: /HQMM.info DEST: clone

		echo Copying executables...
		copy >NIL: HQMM HQMM.info StoryPaper DEST:HQMM clone

		echo Copying guide file...
		copy >NIL: HQMM.guide HQMM.guide.info DEST:HQMM clone

		echo Copying example file...
		copy >NIL: HQMM_example HQMM_example.info DEST:HQMM clone

		echo Copying HQMM font...
		copy >NIL: Fonts all FONTS:

		assign DEST: remove
		echo Installing complete. HQMM can be found in $PFDest/HQMM
	else
		echo Installing aborted.
	endif
endif
