; $VER$: Install script for CivHack v4.01
; By Alex Furmanski
(set help@installpath
    (cat
        "  Choose a location to install CivHack.  A drawer named CivHack "
        "WILL be created for you.\n\n"
        @askdir-help
    )
)
(set help@svpth
    (cat
        "   This is the path that, by default, CivHack will look for your "
        "saved game files.  Saved games may be loaded from antwhere and "
        "this is provided simply for your convenience."
        "\n   This installer forces you to choose the root of a device as "
        "this is where Civlization saves the games.  You may edit the SAVEPATH "
        "tooltype of the executable to set the default path to any location."
    )
)
(set help@font
    (cat
        "   This is the font that CivHack will use for its GUI.  It will "
        "automatically scale the font to 8 pixels, so you need only to "
        "specify the name as '{fontname}.font'."
        "   If it is not already installed on your system, the default font "
        "(ibm.font) will be copied to your Fonts: assign."
    )
)
(set help@docs
    (cat
        "   The documentation comes in two flavours - AmigaGuide and HTML."
        "\nAmigaGuide docs"
        "\n\n   The information is presented in standard AmigaGuide layout.  You "
        "will need a copy of AmigaGuide or Multiview to read the information. "
        "It contains exactly the same information as the HTML docs."
        "\n\nHTML docs"
        "\n   Here, information is in standard HTML code, with some in-line "
        "images of the GUI to aid explanation.  No Javascript of Java code is "
        "used and you will need an application capable of displaying HTML docs "
        "such as a web browser (recommended) or a basic HTML viewer."
    )
)
(set help@libs
    (cat
        "    These libraries are required by CivHack to open its GUI.  You"
        "\ndo not need to install them if you are running Workbench 2 or"
        "\nhigher as they were included with subsequent releases."
    )
)
(message
   "\n\n You are going to install Civ Hack version 4"
   "\n on your harddisk."
   "\n\n Civ Hack (c)1998 Sheepie Software"
   "\n Written by Alex Furmanski"
   "\n Install script by Alex Furmanski"
   (all)
)
(welcome)
(complete 0)
(set dst
    (askdir
        (prompt "Select a place to install CivHack. A directory named CivHack will be created automatically.")
        (help help@installpath)
        (default "")
    )
)
(complete 5)
(set destdir
    (tackon dst "CivHack")
)
(set docdir
    (tackon destdir "Docs")
)
(makedir destdir
    (infos)
)
(complete 10)
(copyfiles
    (source "Civhack")
    (dest destdir)
    (infos)
)
(complete 15)
(copyfiles
    (source "chk.logo")
    (dest destdir)
)
(complete 20)
(copylib
    (source "libs/gadtools.library")
    (dest "Libs:")
    (prompt "Installing libs...")
    (help help@libs)
    (confirm)
)
(complete 25)
(copylib
    (source "libs/asl.library")
    (dest "Libs:")
    (prompt "Installing libs...")
    (help help@libs)
    (confirm)
)
(complete 30)
(set svpth
    (askdir
        (prompt "Where do you usually keep your saved games?")
        (default "")
        (disk)
        (help help@svpth)
    )
)
(complete 40)
(set fnt
    (askstring
        (prompt "What font do you wish to use?  (Include .font extension)")
        (default "ibm.font")
        (help help@font)
    )
)
(complete 50)
(if (not (exists "Fonts:ibm.font"))
    (copyfiles
        (source "fonts")
        (dest "Fonts:")
        (all)
    )
)
(tooltype
    (dest (tackon destdir "Civhack"))
    (settooltype "SAVEPATH" svpth)
    (settooltype "FONT" fnt)
)
(complete 60)
(set copdocs
    (askbool
        (prompt "Do you wish to install documentation?")
        (help "Make a selection")
    )
)
(complete 70)
(if (= copdocs 1)
    ((set docs
        (askoptions
            (prompt "Install..")
            (choices
                "AmigaGuide docs"
                "HTML docs"
            )
            (default 3)
            (help help@docs)
        )
    )
    (complete 80)
    (makedir docdir
        (infos)
    )
    (complete 90)
    (set current 0)
    (while
        (< current 2)
        ((if
            (IN docs current)
    ((copyfiles
        (prompt "Copying the selected documents...")
        (source
            (tackon "docs"
                (select
                    current
                    "CivHack.guide"
                    "HTML"
                        )
                    )
                )
                (dest docdir)
                (infos)
                (all)
            )
                )
            )
            (set current (+ current 1))
        )
    )
)

)
(complete 100)
(exit
   "\n\n Civ Hack and documentation have been successfully installed."
   "\n Civ Hack can be found in " destdir ","
   "\n and the docs are in " docdir "."
   "\n\n If you like Civhack v4, then why not drop me a line."
   "\n All gifts, suggestions and bug reports gratefully received,"
   "\n plus you'll probably make my day."
   "\n\n For more info on contacting me plus the instructions,"
   "\n see the documentation!  Thank you."
   "\n\n                        Alex Furmanski"
   (quiet)
)