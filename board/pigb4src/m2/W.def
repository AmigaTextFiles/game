DEFINITION MODULE W; (* V1.2 12-91 *)

(*$ DEFINE Test:=FALSE *)
(*$ LongAlign:=TRUE StackParms:=TRUE CStrings:=TRUE LargeVars:=FALSE *)
(*$ IF Test *)
  (*$ Volatile:=FALSE StackChk:=TRUE RangeChk:=TRUE OverflowChk:=TRUE
  NilChk:=TRUE EntryClear:=TRUE CaseChk:=TRUE ReturnChk:=TRUE *)
(*$ ELSE *)
  (*$ Volatile:=TRUE StackChk:=FALSE RangeChk:=FALSE OverflowChk:=FALSE
  NilChk:=FALSE EntryClear:=FALSE CaseChk:=FALSE ReturnChk:=FALSE *)
(*$ ENDIF *)


(* PASCAL-agtig READ/WRITE af blandede typer *)

VAR
  Buf:ARRAY[0..255] OF CHAR;
                                                       (* Til Buf : *)
PROCEDURE s(st:ARRAY OF CHAR):CARDINAL;                (* String *)
PROCEDURE sf(st:ARRAY OF CHAR; f:INTEGER):CARDINAL;    (* String,Felt *)
PROCEDURE c(ch:CHAR):CARDINAL;                         (* Char *)
PROCEDURE cf(ch:CHAR; f:INTEGER):CARDINAL;             (* Char,Felt *)
PROCEDURE l(l:LONGINT):CARDINAL;                       (* Longint *)
PROCEDURE lf(l:LONGINT; f:INTEGER):CARDINAL;           (* Longint,Felt *)
PROCEDURE l0(l:LONGINT; f:INTEGER):CARDINAL;           (* Longint,0-felt *)
PROCEDURE lh(l:LONGINT; f:INTEGER):CARDINAL;           (* Longint,Hex-felt *)
PROCEDURE lo(l:LONGINT; f:INTEGER):CARDINAL;           (* Longint,Oct-Felt *)
PROCEDURE lb(l:LONGINT; f:INTEGER):CARDINAL;           (* Longint,Bin-Felt *)
PROCEDURE b(bool:BOOLEAN):CARDINAL;                    (* Boolean *)
PROCEDURE bf(bool:BOOLEAN; f:INTEGER):CARDINAL;        (* Boolean,Felt *)

PROCEDURE WRITE(n:CARDINAL);                          (* Buf -> *)
PROCEDURE WRITELN(n:CARDINAL);                         (* Buf -> *)
PROCEDURE FREEBUF;                                     (* Buf='' *)
PROCEDURE CONCAT(n:CARDINAL);                          (* -> Buf *)

PROCEDURE READs(VAR st:ARRAY OF CHAR);      (* String *)
PROCEDURE READc(VAR n:CARDINAL);            (* Cardinal *)
PROCEDURE READch(VAR n:CARDINAL);           (* Cardinal i Hexadecimal *)
PROCEDURE READco(VAR n:CARDINAL);           (* Cardinal i Octal *)
PROCEDURE READcb(VAR n:CARDINAL);           (* Cardinal i Binær *)
PROCEDURE READl(VAR n:LONGINT);             (* Longint *)
PROCEDURE READlh(VAR n:LONGINT);            (* Longint i Hexadecimal *)
PROCEDURE READlo(VAR n:LONGINT);            (* Longint i Octal *)
PROCEDURE READlb(VAR n:LONGINT);            (* Longint i Binær *)
PROCEDURE READi(VAR n:INTEGER);             (* Integer *)
PROCEDURE READb(VAR bool:BOOLEAN);          (* Boolean (f|t | 0|1) *)

(*   PROCEDURER :
l=LONGINT, c=CHAR, s=String, b=BOOLEAN/Binært, h=Hex, o=Oktal, f=Felt)

     s          udskriver streng til buffer
     sf         udskriver streng f bred til buffer
     c          udskriver char til buffer
     cf         udskriver char i felt f bred til buffer
     l          udskriver tal (INTEGER,CARDINAL,LONGINT) til buffer
     lf         udskriver tal i felt f bred til buffer
     l0         udskriver tal i 0-felt f bred til buffer
     lh         udskriver tal hexadecimalt f bred til buffer
     lo         udskriver tal oktalt f bred til buffer
     lb         udskriver tal binært f bred til buffer
     b          udskriver BOOLEAN (FALSE|TRUE) til buffer
     bf         udskriver BOOLEAN (FALSE|TRUE) i felt f til buffer

     WRITE       udskriver buffer og tømmer den
     WRITELN     udskriver buffer, tømmer den og linieskift
     FREEBUF     tømmer buffer
     CONCAT      lægger til buffer (Buf)
      
     Eks: WRITELN(s('Den er')+lf(34,5)+c('K')+s(' byte'));
     Gir: "Den er   34K byte"
     ( i PASCAL : WRITELN('Den er',34:5,'K',' byte' )

(l=LONGINT, c=CARDINAL, i=INTEGER, s=String, b=BOOLEAN/Binær, h=Hex, o=Oktal)

     READs          string     (indlæs)
     READc          cardinal
     READch         cardinal hex
     READco         cardinal oktal
     READcb         cardinal binær
     READl          longint
     READlh         longint hex
     READlo         longint oktal
     READlb         longint binær
     READi          integer
     READb          boolean

     Eks: READc(count);
*)     
END W.
