DEFINITION MODULE StrSupport; (* V1.0 3-91, V1.1 11-94 *)

(*$ DEFINE Test:=FALSE *)
(*$ LongAlign:=TRUE StackParms:=TRUE CStrings:=TRUE LargeVars:=FALSE *)
(*$ IF Test *)
  (*$ Volatile:=FALSE StackChk:=TRUE RangeChk:=TRUE OverflowChk:=TRUE
  NilChk:=TRUE EntryClear:=TRUE CaseChk:=TRUE ReturnChk:=TRUE *)
(*$ ELSE *)
  (*$ Volatile:=TRUE StackChk:=FALSE RangeChk:=FALSE OverflowChk:=FALSE
  NilChk:=FALSE EntryClear:=FALSE CaseChk:=FALSE ReturnChk:=FALSE *)
(*$ ENDIF *)

TYPE
  STRING=ARRAY[0..80] OF CHAR;

VAR
  ValidStart,ValidStop :CARDINAL;
(* Viser hvor at s1 var i s2 (ignorerer start/slut) F.eks. *ej* i Hejsa=1,3 *)
(* Stop peger på den første efterfølgende så at stop-start=længde. *)

  FromPos       :CARDINAL;
(* Den position i S2 hvor Valid starter. Er normalt altid 0. Den kan dog 
   sættes før kald til Valid, hvorefter den igen er 0. Bruges normalt IKKE. *)

PROCEDURE Valid(S1,S2:ARRAY OF CHAR):BOOLEAN;
  (* Valid returnerer TRUE hvis s1=s2. s1 kan indeholde avancerede wildcards.
     Der er følgende : ?, ¿, *, [ab], (s1,s2,,,sn), ~
   ?        vilkårlig 1 position      ???         tre tegn lange
   ¿        vilkårlig 0-1 position    ??¿¿        to til fire tegn lange
   *        vilkårlig 0-n position    *ef*        indeholdende ef
   [abf]    mængde for 1 position     [abf]*      starter med a,b eller f
   (s1,,sn) mængde for 0-n position   *.(c,asm)   slutter med .c eller .asm
   ~        not efterfølgende         (~*e,tre)   =tre, eller ikke e til sidst
     Bruger 262 bytes + strengene (max 80 tegn hver) på stakken *)

PROCEDURE Eq(S1,S2:ARRAY OF CHAR):BOOLEAN;
  (* Equal:        Er S1 =  S2 ? *)

PROCEDURE Gt(S1,S2:ARRAY OF CHAR):BOOLEAN;
  (* GreaterThan: PT IKKE KORREKT SORTERING AF DK TEGN *)
  (*  Gt(S1,S2)  ; Er S1 >  S2 ? *)
  (* ~Gt(S1,S2)  ; Er S1 <= S2 ? *)
  (*  Gt(S2,S1)  ; Er S1 <  S2 ? *)  
  (* ~Gt(S2,S1)  ; Er S1 >= S2 ? *)

PROCEDURE IntVal(S:ARRAY OF CHAR):INTEGER;
  (* INTEGER Value af S, 0 hvis fejl (illegal eller out-of-range *)

PROCEDURE LongintVal(S:ARRAY OF CHAR):LONGINT;
  (* LONGINT Value af S, 0 hvis fejl (illegal) *)

PROCEDURE CardVal(S:ARRAY OF CHAR):CARDINAL;
  (* CARDINAL Value af S, 0 hvis fejl (illegal eller out-of-range *)

PROCEDURE UpCase(CH:CHAR):CHAR; 
  (* også DK tegn *)

PROCEDURE UpString(VAR Str:ARRAY OF CHAR);
  (* også DK tegn *)

PROCEDURE TrimString(VAR Str:ARRAY OF CHAR; Width:CARDINAL);
  (* Højrestil streng i felt *)

PROCEDURE TrimVal(VAR Str:ARRAY OF CHAR; Value:LONGINT; Width:CARDINAL);
  (* Lav streng med Højrestillet tal udfra Value*)

PROCEDURE in(ch:CHAR; st:ARRAY OF CHAR):BOOLEAN;
(*  char mængde-konstanter i TURBO PASCAL :                                *)
(*  TYPE quitset=set of char                                               *)
(*  IF ch in['a'..'z','æ','ø','å','A'..'Z','Æ','Ø','Å'] THEN ...           *)
(*  IF 'Q' in quitset THEN ...                                             *)
(*                                                                         *)
(*  Modula2 uden in. Indbygget IN kan mængde kun indeholde 16/32 elementer *)
(*  enten :                                                                *)
(*  IF (ch>='a') AND (ch<='z') OR (ch='æ') OR (ch='ø') OR (ch='å') OR      *)
(*     (ch>='A') AND (ch<='Z') OR (ch='Æ') OR (ch='Ø') OR (ch='Å') THEN    *)
(*  eller :                                                                *)
(*  CASE ch OF 'a'..'z','æ','ø','å','A'..'Z','Æ','Ø','Å' :                 *)
(*    ...                                                                  *)
(*  ELSE END;                                                              *)
(*  IF FirstPos(quitset,0,ch)>0 THEN ...                                   *)
(*                                                                         *)
(*  Modula2 med in :                                                       *)
(*  TYPE quitset=ARRAY OF CHAR                                             *)
(*  IF in(ch,'a..zæøåA..ZÆØÅ') THEN ...                                    *)
(*                                                                         *)
(*  IF in('Q',quitset) THEN ...                                            *)
(*                                                                         *)
(***************************************************************************)

END StrSupport.
