/* Add Record (script is called by Primiview) */
/* Author: Arno Hollosi                       */
/* This script is public domain               */
/* TABSIZE = 4                                */

options results
primiview = address()

address value primiview

GetNode stem n.		/* Check if buffer contains file and get node number */

if rc=0 then do
	ginfo.0 = PW;	ginfo.1 = WR;
	ginfo.2 = PB;	ginfo.3 = BR;
	ginfo.4 = KO;	ginfo.5 = HA;	ginfo.6 = RE;
	ginfo.7 = DT;	ginfo.8 = PC;
	ginfo.9 = EV;	ginfo.10 = RO;
	ginfo.11 = SO;	ginfo.12 = GN;

	if n.number~=0 then GotoNode ROOT	/* goto root-node to get gameinfo */

	do i=0 to 12
		GetPropValue ginfo.i
		if rc=0 then value.i = substr(result, 3)
		else value.i = ""
	end i

	ListBuffers stem b.					/* Get filename of active buffer */
	do j=0 to b.bufferlist.count-1
		if substr(b.bufferlist.j, 8, 1) = 'a' then do
			file = substr(b.bufferlist.j, 10)
			leave
		end
	end j

	address 'DB.1'				/* address FIRST copy of DB */
	Add							/* Add new record to database */
	FirstField
	do i=0 to 12
		PutField value.i
		NextField
	end i
	PutField file
end
