Deck "A-Risk.deck"
	Routine "Country"
		If ~E
			Loop CT,Arg1,Arg2
				If G[CT,1]#P
					Let E=1
					ExitLoop
				EndIf
			EndLoop
		EndIf
	EndScript
	Routine "J_2"
		Do "Country",1,9
		Do "Country",42,45
	EndScript
	Routine "J_1"
		Do "Country",1,9
		Do "Country",36,41
	EndScript
	Routine "J_5"
		Do "Country",20,35
	EndScript
	Routine "J_6"
		Do "Country",20,31
		Do "Country",36,41
	EndScript
	Routine "J_7"
		Loop CT,45
			If G[CT,1]=P
				Let WN=WN+1
			EndIf
		EndLoop
		If WN<24
			Let E=1
		EndIf
	EndScript
	Routine "J_10"
		Do "Kill",47
	EndScript
	Routine "J_11"
		Do "Kill",48
	EndScript
	Routine "J_12"
		Do "Kill",49
	EndScript
	Routine "J_13"
		Do "Kill",50
	EndScript
	Routine "J_14"
		Do "Kill",51
	EndScript
	Routine "J_15"
		Do "Kill",52
	EndScript
	Routine "J_16"
		Do "Kill",53
	EndScript
	Routine "Extra"
		Loop CT,Arg1,Arg2
			If G[CT,1]#P
				Let A=Alias("Arg3"),A=0
				ExitLoop
			EndIf
		EndLoop
	EndScript
	Routine "AJob_6"
		Do "Attack",20,31,Arg1
		If EN>31
			Do "Attack",36,41,Arg1
		EndIf
	EndScript
	Routine "AJob_5"
		Do "Attack",20,31,Arg1
		If EN>31
			Do "Attack",32,35,Arg1
		EndIf
	EndScript
	Routine "AJob_2"
		Do "Attack",1,9,Arg1
		If EN>9
			Do "Attack",42,45,Arg1
		EndIf
	EndScript
	Routine "AJob_1"
		Do "Attack",1,9,Arg1
		If EN>9
			Do "Attack",31,36,Arg1
		EndIf
	EndScript
	Routine "AJob_8"
		Do "Attack",32,45,Arg1
		If EN>45
			Do "Attack",1,31,Arg1
		EndIf
	EndScript
	Routine "AJob_7"
		Do "Attack",32,45,Arg1
		If EN>45
			Do "Attack",1,31,Arg1
		EndIf
	EndScript
	Routine "AJob_10"
		Do "Attack",42,45,Arg1
		If EN>45
			Do "Attack",32,41,Arg1
			If EN>41
				Do "Attack",1,31
			EndIf
		EndIf
	EndScript
	Routine "AJob_4"
		Do "Attack",10,16,Arg1
		If EN>16
			Do "Attack",42,45,Arg1
		EndIf
		If EN>45
			Do "Attack",32,41,Arg1
			If EN>41
				Do "Attack",1,9,Arg1
			EndIf
		EndIf
	EndScript
	Routine "AJob_3"
		Do "Attack",10,16,Arg1
		If EN>16
			Do "Attack",32,35,Arg1
		EndIf
		If EN>35
			Do "Attack",42,45,Arg1
		EndIf
		If EN>45
			Do "Attack",36,41,Arg1
			If EN>41
				Do "Attack",1,9
			EndIf
		EndIf
	EndScript
	Routine "RetMess"
		SendToSubDeck "Informer","Quit"
		ShowPalette "Resources/Board.Pic"
		
	EndScript
	Routine "J_8"
		Loop CT,45
			If G[CT,2]>1&G[CT,1]=P
				Let WN=WN+1
			EndIf
		EndLoop
		If WN<18
			Let E=1
		EndIf
	EndScript
	Routine "AEnter"
		SendToSubDeck "Armies","Travel",TX||AM,DV
		IfError
			OpenRequester "Armies","Amiga",Alias("G")
			SendToSubDeck "Armies","Travel",TX||AM,DV
		EndIf
		Let AM=AM-AR,G[DV,2]=G[DV,2]+AR,G[-P,10]=G[-P,10]+AR
		Do "Print",DV
	EndScript
	Routine "J_4"
		Do "Country",10,16
		Do "Country",42,45
		If ~E
			Do "Country",32,35
			If E
				Dispose E
				Do "Country",36,41
			EndIf
			If E
				Dispose E
				Do "Country",1,9
			EndIf
			If E
				Dispose E
				Do "Country",20,31
			EndIf
		EndIf
	EndScript
	Routine "J_3"
		Do "Country",10,16
		Do "Country",32,35
		If ~E
			Do "Country",42,45
			If E
				Dispose E
				Do "Country",36,41
			EndIf
			If E
				Dispose E
				Do "Country",1,9
			EndIf
			If E
				Dispose E
				Do "Country",20,31
			EndIf
		EndIf
	EndScript
	Routine "Quit"
		Quit
	EndScript
	Routine "Print"
		GetTextExtent G[Arg1,2],W,H
		SetPen G[-G[Arg1,1],7]
		InstallClipRegion G[Arg1,3]-W,G[Arg1,4]-H,W*2,H*2,ONLY 
		ClearWindow
		ClearClipRegions
		PrintText G[Arg1,2],G[Arg1,3]-(W%2),G[Arg1,4]-(H%2)
	EndScript
	Routine "Clip"
		Do "Print",AG
		Do "Print",AP
		If AG=1&AP=31|AG=31&AP=1
			CreateBrush "Br",200,180,6
			If AG=1
				ClipBrush G[AG,3]-50,G[AG,4]-75,100,180,"Bh1"
				ClipBrush G[AP,3]-50,G[AP,4]-75,100,180,"Bh2"
			Else
				ClipBrush G[AG,3]-50,G[AG,4]-75,100,180,"Bh2"
				ClipBrush G[AP,3]-50,G[AP,4]-75,100,180,"Bh1"
			EndIf
			RenderTo BUFFER ,"Br"
			ShowBrush "Bh1",0,0
			ShowBrush "Bh2",100,0
			RenderTo WINDOW 
		Else
			ClipBrush (G[AG,3]+G[AP,3])%2-100,(G[AG,4]+G[AP,4])%2-75,200,180,"Br"
		EndIf
		SaveBrush "Br","T:RiskArea.bsh"
		SendToSubDeck "Armies","Show"
		IfError
			OpenRequester "Armies","Dice",Alias("G"),AG,AP,Alias("KI")
		EndIf
		
	EndScript
	Routine "Move"
		Let A1=Arg3,A2=Arg4,A3=Arg5,A4=Arg6,A5=Arg7
		Loop CT,Arg1,Arg2
			If G[CT,1]#P
				ExitScript
			EndIf
		EndLoop
		Loop CT,ArgCount-2
			Let TC=Alias("A"||CT)
			Loop EN,7,NumberOfArrayEntries(G[TC])
				If G[G[TC,EN],2]*1.5>G[TC,2]&G[G[TC,EN],1]#P
					Loop FC,Arg1,Arg2
						If G[FC,2]>1&~Match(FC,A1,A2,A3,A4,A5,A6,A7)
							Let AR=Random(1,G[FC,2]-1),G[FC,2]=G[FC,2]-AR,G[TC,2]=G[TC,2]+AR
							SendToSubDeck "Armies","Travel",AR|||G[FC,5]|||AR,FC; TX||AM,DV
							IfError
								OpenWindow "Armies","Amiga",Alias("G")
								SendToSubDeck "Armies","Travel",AR|||G[FC,5]|||AR,FC; TX||AM,DV
							EndIf
							Do "Print",FC
							SendToSubDeck "Armies","Travel",G[TC,5],TC
							Do "Print",TC
						EndIf
					EndLoop
				EndIf
			EndLoop
		EndLoop
	EndScript
	Routine "Cards"
		Loop CT,5
			If G[-P,CT]#""
				OpenRequester "Armies","Cards",Alias("G"),P,Arg1
				ExitLoop
			EndIf
		EndLoop
	EndScript
	Routine "AExtra"
		Let AM=Alias("Arg1"),TX=Arg2,A3=Arg3,A4=Arg4,A5=Arg5,A6=Arg6,A7=Arg7
		Loop CT,3,ArgCount
			Let A=Alias("A"||CT)
			Loop EN,7,NumberOfArrayEntries(G[A])
				If G[G[A,EN],1]#P&G[A,2]<Random(G[G[A,EN],2]+3,G[G[A,EN],2]*2)
					Let DV=A,AR=Limit(1,AM,Random(Absolute(G[EN,2]*2-G[DV,2]),AM))
					ExitScript
				EndIf
			EndLoop
		EndLoop
		Let A=Alias("A"||Random(3,ArgCount)),DV=A,AR=Limit(1,AM,Random(Absolute(G[EN,2]*2-G[DV,2]),AM))
	EndScript
	Routine "Amiga"
		Let LC=PreviousArrayIndex(G[0],-11)
		While SearchFound
			Let AR=2,TX="2 Extra!",DV=G[0,LC]
			Dispose G[0,LC],AM
			Do "AEnter"
			Let LC=PreviousArrayIndex(G[0],LC)
		EndLoop
		While G[0,-5]
			If G[0,-6]
				Do "AExtra",Alias("G[0,-6]"),"North Amerika:",1,2,5
			ElseIf G[0,-7]
				Do "AExtra",Alias("G[0,-7]"),"Europe:",12,13,15,16
			ElseIf G[0,-8]
				Do "AExtra",Alias("G[0,-8]"),"Asia:",21,24,28,29,31
			ElseIf G[0,-9]
				Do "AExtra",Alias("G[0,-9]"),"South America:",32,33
			ElseIf G[0,-10]
				Do "AExtra",Alias("G[0,-10]"),"Afrika:",36,37,38
			ElseIf G[0,-11]
				Do "AExtra",Alias("G[0,-11]"),"Austalia:",45
			ElseIf G[0,-5]
				Let AM=Alias("G[0,-5]"),TX="Armies:",AB=1,JB=0
				If Random(0,T)
					Do "AJob_"||G[-P,8],1
					IfError
						If G[-P,7]=G[-P,9]
							Do "AJob_7",1
						Else
							Do "AJob_9",1
						EndIf
					EndIf
				Else
					Let JB=Random(1,9)
					Do "AJob_"||JB,1
				EndIf
				While AM
					Do "Attack",Random(1,15),Random(30,45),1,1
					If AM
						Loop DV,1,45
							If G[DV,1]=P
								Let AR=Limit(1,AM,Random(AM%2,AM))
								Do "AEnter"
								ExitLoop
							EndIf
						EndLoop
					EndIf
				EndLoop
				ExitLoop
			EndIf
			Do "AEnter"
		EndLoop
		SendToSubDeck "Armies","Quit"
		IfError
		EndIf
		Dispose AG,AP
		Do "AJob_"||G[-P,8]
		IfError
			If JB
				Do "AJob_"||JB
			Else
				Do "AJob_9"
			EndIf
		EndIf
		If AG&AP
			Do "Clip"
			ExitScript
		ElseIf ~KI
			Loop AG,1,45
				If G[AG,1]=P
					Loop CT,7,NumberOfArrayEntries(G[AG])
						If G[G[AG,CT],1]#P&G[AG,2]>G[G[AG,CT],2]*1.5
							Let AP=G[AG,CT]
							SetRGB AG,255,0,0
							SetRGB AP,0,0,255
							Do "Clip"
							ExitScript
						EndIf
					EndLoop
				EndIf
			EndLoop
		EndIf
		If G[-P,8]=8;18*2
			Let CK1=0,CK2=0
			Loop CT,45
				If G[CT,1]=P
					Let CK1=CK1+1,CK2=CK2+G[CT,2]
				EndIf
			EndLoop
			If CK1>17&CK2>35
				Loop TC,45
					If G[TC,1]=P&G[TC,2]=1
						Loop FC,7,NumberOfArrayEntries(G[TC])
							If G[G[TC,FC],1]=P&G[G[TC,FC],2]>2
								Let AR=Random(1,G[G[TC,FC],2]-2),G[G[TC,FC],2]=G[G[TC,FC],2]-AR,G[TC,2]=G[TC,2]+AR
								SendToSubDeck "Armies","Travel",AR|||G[G[TC,FC],5]|||AR,G[TC,FC]
								IfError
									OpenWindow "Armies","Amiga",Alias("G")
									SendToSubDeck "Armies","Travel",AR|||G[G[TC,FC],5]|||AR,G[TC,FC]
								EndIf
								Do "Print",G[TC,FC]
								SendToSubDeck "Armies","Travel",G[TC,5],TC
								Do "Print",TC
								ExitLoop
							EndIf
						EndLoop
					EndIf
				EndLoop
			EndIf
		EndIf
		If CK1<18|CK2<36
			Do "Move",1,9,1,2,5
			Do "Move",36,41,36,37,38
			Do "Move",10,16,12,13,15,16
			Do "Move",32,35,32,33
			Do "Move",42,45,45
			Do "Move",20,31,21,24,28,29,31
		EndIf
		;G[-P,8]=7;24
		Do "Next"
	EndScript
	Routine "AJob_9"
		If G[-P,7]#G[-P,9]
			Dispose AB
		EndIf
		Do "Attack",Random(1,16),Random(36,45),Arg1
	EndScript
	Routine "Kill"
		If G[-P,7]=Arg1
			Do "J_7",Arg2
		Else
			Loop EN,T
				If G[-EN,7]=Arg1&~G[-EN,10]
					Do "Winner"
					ExitScript
				EndIf
			EndLoop
			Let E=1
		EndIf
	EndScript
	Routine "J_9"
		Do "Kill",46,Arg1
	EndScript
	Routine "Prefs"
		ShowPalette "Resources/Board.Pic"
		Local GP=Arg1
		Let G[0,0]=GP[0,0],G[0,-1]=GP[0,-1],G[0,-2]=GP[0,-2],MDS=0
		Loop CT,GP[0,0]
			Let G[-CT,6]=GP[-CT,6],G[-CT,7]=GP[-CT,7],G[-CT,11]=GP[-CT,11],G[-CT,12]=GP[-CT,12],G[-CT,13]=GP[-CT,13],G[-CT,14]=GP[-CT,14],G[-CT,15]=GP[-CT,15]
			If G[-CT,14]#""
				Let SG=G[-CT,14],MDS=MDS+1
			EndIf
		EndLoop
		Do "SetUp"
	EndScript
	Routine "Song"
		If G[-P,14]#""
			If SG#G[-P,14]
				Let SG=G[-P,14]
				PlaySong SG
			EndIf
			If G[-P,15]
				Let ContSg=1
			Else
				Dispose ContSg
			EndIf
		ElseIf ~ContSg
			Let SG=""
			PlaySong SG
			IfError
			EndIf
		EndIf
	EndScript
	Routine "Name"
		SetPen 58
		AreaRectangle 250,5,380,25
		SetPrintFont G[-P,16],G[-P,17]
		IfError
			SetPrintFont "sapphire",14
			IfError
				SetPrintFont "garnet",16
				IfError
					SetPrintFont "times",18
					IfError
						SetPrintFont "topaz",11
					EndIf
				EndIf
			EndIf
		EndIf
		GetTextExtent G[-P,6],W,H
		SetPen 0
		AreaRectangle 430-(W%2),7,W+20,H+3
		DrawBorder 430-(W%2),7,W+20,H+3,BEVEL ,18,19
		SetPen G[-P,7]
		PrintText G[-P,6],440-(W%2),10
	EndScript
	Routine "Next"
		ShowPalette "Resources/Board.Pic"
		If LargestChunkOfMemory<200000
			RamScram
		EndIf
		SendToSubDeck "Armies","Quit"
		IfError
		EndIf
		Dispose E,WN,MV,MF,Plyd
		Loop CT,T
			If S[CT,LastArrayIndex(S[CT]),1]>0;G[-CT,10]>0
				Let S1=0,S2=0
				Loop CNT,45
					Let CNT=IfThen(CNT=17,20,CNT)
					If G[CNT,1]=CT
						Let S1=S1+1,S2=S2+G[CNT,2]
					EndIf
				EndLoop
				Let S[CT,LastArrayIndex(S[CT])+1,1]=S1,S[CT,LastArrayIndex(S[CT]),2]=S2
			EndIf
		EndLoop
		Do "J_"||G[-P,8]
		IfError
			Do "J_"||G[-P,9]-37
		EndIf
		Do "Winner"
		Loop EN,T
			If G[-EN,9]&G[-EN,10]&EN#P
				Loop C,46,53
					If G[-EN,9]=C
						Loop CT,T
							If ~G[-CT,10]&G[-CT,7]=C
								Let P=EN,E=0
								Do "Winner"
							EndIf
						EndLoop
					EndIf
				EndLoop
			EndIf
		EndLoop
		If KI
			Loop CT,5
				If G[-P,CT]=""
					Let G[-P,CT]=G[0,LastArrayIndex(G[0])],KI=0
					DeleteArrayEntry G[0],LastArrayIndex(G[0])
					ExitLoop
				EndIf
			EndLoop
			;Do "Remark",G[-P,6]|||"recieved a card"
		EndIf
		Let LC=PreviousArrayIndex(G[0],-4),KI=0
		While SearchFound
			Dispose G[0,LC]
			Let LC=PreviousArrayIndex(G[0],LC)
		EndLoop
		Loop
			Let P=IfThen(P=T,1,P+1),GO=0
			If G[-P,10]
				ExitLoop
			EndIf
		EndLoop
		Do "Name"
		Do "Song"
		SetPrintFont G[0,-1],G[0,-2]
		IfError
			SetPrintFont "topaz",8
		EndIf
		Loop CT,45
			If G[CT,1]=P
				Let G[0,-5]=G[0,-5]+1
			EndIf
		EndLoop
		Let G[0,-5]=Max(3,G[0,-5]%3),G[0,-6]=5,G[0,-7]=5,G[0,-8]=7,G[0,-9]=2,G[0,-10]=3,G[0,-11]=2,AP=0,AC=0
		Do "Extra",1,9,Alias("G[0,-6]")
		Do "Extra",10,16,Alias("G[0,-7]")
		Do "Extra",20,31,Alias("G[0,-8]")
		Do "Extra",32,35,Alias("G[0,-9]")
		Do "Extra",36,41,Alias("G[0,-10]")
		Do "Extra",42,45,Alias("G[0,-11]")
		Loop CT,5
			If G[-P,CT]=""&G[-P,6]#"AMIGA"
				ExitLoop
			ElseIf G[-P,CT]#""&G[-P,6]="AMIGA"
				Let AC=AC+1
			EndIf
		EndLoop
		If (CT=6&G[-P,6]#"AMIGA")|AC=5
			Do "Cards",1
		ElseIf AC>2&Random(0,1)
			Do "Cards"
		EndIf
		If G[-P,6]#"AMIGA"
			ReattachObject "T_1"
		Else
			If ObjectAttached("T_1")
				DetachObject "T_1"
			EndIf
		EndIf
	EndScript
	Routine "Attack"
		Loop EN,Arg1,Arg2
			Let EN=IfThen(EN=17,20,EN)
			If G[EN,1]#P&(AB|G[-G[EN,1],7]=G[-P,9])
				Loop CT,7,NumberOfArrayEntries(G[EN])
					If G[G[EN,CT],1]=P
						If Arg3
							If ~AM
								ExitScript
							ElseIf G[G[EN,CT],2]+AM>G[EN,2]*1.2|Arg4
								If G[G[EN,CT],2]<G[EN,2]&~Random(0,T*2)&~Arg4
								Else
									Let DV=G[EN,CT],AR=Limit(1,AM,Max(3,Random(Absolute(G[EN,2]-G[DV,2]*Scared),AM)))
									Do "AEnter"
								EndIf
							EndIf
						ElseIf G[G[EN,CT],2]>G[EN,2]*Scared
							Let AG=G[EN,CT],AP=EN
							SetRGB AG,255,0,0
							SetRGB AP,0,0,255
							ExitScript
						EndIf
					EndIf
				EndLoop
			EndIf
		EndLoop
	EndScript
	Routine "SetUp"
		ClearWindow
		SetPrintFont G[0,-1],G[0,-2]
		IfError
			SetPrintFont "topaz",8
		EndIf
		SetPrintStyle PLAIN 
		SetDrawMode JAM1 
		GetTextExtent "1",W,H
		Let T=G[0,0],F=IfThen(F=T,1,F+1),P=F,G[0,-4]=2
		Dispose S
		Loop CT,8
			Dispose G[-CT,1],G[-CT,2],G[-CT,3],G[-CT,4],G[-CT,5]
		EndLoop
		Loop CT,14
			Let A=Alias("J"||CT),A=0
			Dispose G[-CT,8],G[-CT,9]
		EndLoop
		DetachObject "T_2"
		IfError
		EndIf
		Loop CT,T
			If G[-CT,6]="AMIGA"&~ObjectAttached("T_2")
				AttachObject "T_2"
			EndIf
			Loop
				Let JB=Random(1,14),A=Alias("J"||JB)
				If ~A
					If JB>8
						Loop EN,T
							If G[-EN,7]=37+JB
								Let G[0,-3]=1,G[-CT,9]=JB+37,G[-CT,10]=0,A=1
								ExitLoop
							EndIf
						EndLoop
					Else
						Let G[-CT,8]=JB,G[-CT,10]=0,A=1
					EndIf
					If A
						ExitLoop
					EndIf
				EndIf
			EndLoop
		EndLoop
		Loop CT,45
			Dispose G[CT,1],G[0,CT]
		EndLoop
		Loop CT,47
			Loop
				Let C=Random(1,44)
				If G[0,C]=""
					Let CT=IfThen(CT=17,20,CT),G[0,C]=CT
					ExitLoop
				EndIf
			EndLoop
		EndLoop
		Let P=Min(P,T)
		Loop CT,42
			Let P=IfThen(P=T,1,P+1)
			Loop
				Let C=Random(1,45)
				If G[C,1]=""
					If C#17&C#18&C#19
						Let G[C,1]=P,G[C,2]=1,G[-P,10]=G[-P,10]+1;A=Alias("FP"||P),A=A+1
						SetPen G[-P,7],C
						PrintText G[C,2],G[C,3]-(W%2),G[C,4]-(H%2)
						ExitLoop
					EndIf
				EndIf
			EndLoop
		EndLoop
		Loop CNT,T
			Let S1=0
			Loop CT,45
				Let CT=IfThen(CT=17,20,CT)
				If G[CT,1]=P
					Let S1=S1+1
				EndIf
			EndLoop
			Let S[CNT,0,1]=S1,S[CNT,0,2]=S1
		EndLoop
		Let P=Random(1,T),RestartG=G,RestartP=P,JS=1
		Do "Next"
	EndScript
	Routine "Winner"
		If ~E
			If ObjectAttached("T_2")
				DetachObject "T_2"
			EndIf
			ShowPalette "WB"
			OpenRequester "Informer","Reporter","© Donald Varnes 1996/2016"
			Delay 0,5,0
			SendToSubDeck "Informer","Print","The winner is / Der Gewinner ist..."|||G[-P,6]
			Delay 0,2,0
			Do "Name"
			SendToSubDeck "Informer","Print","this was his job / das war Sein Job"
			Delay 0,2,0
			SendToSubDeck "Informer","Quit"
			DoObjectScript "Job",ONCLICK ,"Won"
			StopScript
		EndIf
	EndScript
	Routine "GetRisk"
		Let Tmp=Limit(1,30,Arg1),Scared=1+(1/10*Tmp)
	EndScript
	Card "ARisk"
		BeforeAttachment
			;$VER: A-Risk 1.07 (05Jan16) © Donald Varnes 1996-2016
			Let G=LoadVariable("Resources/Values"),Scared=1.5
			LoadPalette "Resources/WBPalette","WB"
			LoadSubDeck "GamePrefs","Prefs"
			LoadSubDeck "Resources/Armies","Armies"
			LoadSubDeck "C:Informer","Informer"
		EndScript
		AfterAttachment
			ShowBrush "Resources/Copyright.bsh",111,100
		EndScript
		MessageFromSubDeck
			Do Arg1,Arg2,Arg3,Arg4,Arg5
		EndScript
		Window "UserWindow"
			Definition
				Origin 0,0
				BackgroundImage "Resources/Board.Pic"
				WindowFlags ACTIVATE BORDERLESS SEPARATESCREEN TOFRONT 
				VisualEffects NONE ,NONE 
				ScreenPubName "ARisk"
				ScreenFlags NOSHOWTITLE QUIET 
			EndScript
		EndObj
		AreaButton "Play"
			Definition
				Origin 0,0
				Highlight NONE 
				Size 483,502
			EndScript
			OnClick
				Let C=ColorOfPixel(MouseX,MouseY);,Trd=0
				If MV
					If G[C,1]=P&~MF&G[C,2]>1
						Let MF=C,TX=G[C,5],AR=1
					ElseIf C=MF&G[C,2]>1+AR
						Let AR=AR+1
					ElseIf C#MF&MF
						Loop CT,7,NumberOfArrayEntries(G[MF])
							If G[MF,CT]=C&G[C,1]=P
								Let G[MF,2]=G[MF,2]-AR,G[C,2]=G[C,2]+AR,TX=G[C,5]
								Do "Print",MF
								Do "Print",C
								Let MF=C
							EndIf
						EndLoop
					Else
						ExitScript
					EndIf
					SendToSubDeck "Armies","Print","Move from"|||TX|||AR
					IfError
						OpenWindow "Armies","Amount","Move from"|||TX|||AR
					EndIf
					ExitScript
				ElseIf ObjectAttached("T_1")
					If G[C,1]=P
						Let G[C,2]=G[C,2]+AR,G[-P,10]=G[-P,10]+AR,AG=C
						If AM
							Let AM=AM-AR
						EndIf
						If AM
							SendToSubDeck "Armies","Print",TX||AM
						ElseIf G[0,-5]=0
							Loop CT,-15,-6
								Dispose G[0,CT]
							EndLoop
							DetachObject "T_1"
							SendToSubDeck "Armies","Quit"
							SetRGB C,255,0,0
						Else
							Dispose G[0,-11-C]
							Let C=0
							DoObjectScript "T_1",OCCURRED 
						EndIf
						Do "Print",C
					EndIf
				ElseIf AG
					Dispose AP
					Loop CT,7,NumberOfArrayEntries(G[AG])
						If G[AG,CT]=C&G[C,1]#P&G[AG,2]>1
							Let AP=C,Plyd=1
							ExitLoop
						EndIf
					EndLoop
					If AP
						ShowPalette "Resources/Board.Pic"
						SetRGB AG,255,0,0
						SetRGB AP,0,0,255
						Do "Clip"
						ShowPalette "Resources/Board.Pic"
					ElseIf G[C,2]>1&G[C,1]=P
						ShowPalette "Resources/Board.Pic"
						Let AG=C
						SetRGB C,255,0,0
					EndIf
				ElseIf G[C,2]>1&G[C,1]=P
					ShowPalette "Resources/Board.Pic"
					Let AG=C
					SetRGB C,255,0,0
				EndIf
				
			EndScript
		EndObj
		AreaButton "Cards"
			Definition
				Origin 562,37
				Highlight NONE 
				Size 68,65
			EndScript
			OnClick
				If Plyd
					Do "Cards","Played"
				Else
					Do "Cards"
				EndIf
			EndScript
		EndObj
		AreaButton "Job"
			Definition
				Origin 581,122
				Highlight NONE 
				Size 30,43
			EndScript
			OnClick
				If G[-P,6]#"AMIGA" or Arg1="Won"
					SendToSubDeck "Armies","Quit"
					IfError
					EndIf
					If G[-P,8]
						Let Doc="Resources/J"||G[-P,8]||".txt"
					Else
						Let Doc="Resources/J"||G[-P,9]-37||".txt"
					EndIf
					LoadDocument Doc
					OpenRequester "Informer","About"
					SendToSubDeck "Informer","Attributes","","","","","",LinesInDocument
					SendToSubDeck "Informer","Return","Palette"
					SendToSubDeck "Informer","Enter",TextFromdocument(Doc)
					ShowPalette "WB"
					SendToSubDeck "Informer","Prep","Show",NoScroll
				EndIf
				
			EndScript
		EndObj
		AreaButton "Prefs"
			Definition
				Origin 576,180
				Highlight NONE 
				Size 41,43
			EndScript
			OnClick
				LoadSubDeck "GamePrefs","Prefs"
				ShowPalette "WB"
				OpenRequester "Prefs","GamePrefs",Alias("G"),P,T
				
			EndScript
		EndObj
		AreaButton "Status"
			Definition
				Origin 578,243
				Highlight NONE 
				Size 39,30
			EndScript
			OnClick
				OpenRequester "Armies","Statistic",G,S
			EndScript
		EndObj
		AreaButton "About"
			Definition
				Origin 580,295
				Highlight NONE 
				Size 40,37
			EndScript
			OnClick
				Local NL=Char(10)
				OpenRequester "Informer","About"
				SendToSubDeck "Informer","Attributes","","","","","",9
				SendToSubDeck "Informer","Enter",CanDoLegalNotice
				ShowPalette "WB"
				SendToSubDeck "Informer","Prep","Show"
				SendToSubDeck "Informer","Quit"
				MakeDocument "Doc"
				Type "Big> =================== <"||NL
				Type "Big>    A-Risk  V1.07<"||NL
				Type "Big> ===================<"||NL||NL
				Type "   $  Shareware  $ "||NL||NL
				Type "  Support Authors"||NL||NL
				Type "  Donald Varnes"||NL
				Type "  1 dollar or 1 Euro"||NL||NL||NL
				Type "Big>       H<ave"||NL
				Type "Big>            F<un!"||NL||NL
				OpenRequester "Informer","About"
				SendToSubDeck "Informer","Attributes","garnet",9,"garnet",16,"Embossed",6,3,2,1
				SendToSubDeck "Informer","Return","Palette"
				SendToSubDeck "Informer","Enter",TextFromDocument("Doc")
				SendToSubDeck "Informer","Prep","Show"
				Flush "Doc"
			EndScript
		EndObj
		AreaButton "Move"
			Definition
				Origin 578,348
				Highlight NONE 
				Size 46,43
			EndScript
			OnClick
				If ~ObjectAttached("T_1")
					ShowPalette "Resources/Board.Pic"
					Let MV=1,AR=0
				EndIf
				
			EndScript
		EndObj
		AreaButton "Next"
			Definition
				Origin 584,405
				Highlight NONE 
				Size 45,41
			EndScript
			OnClick
				Do "Next"
			EndScript
		EndObj
		AreaButton "NIX"
			Definition
				Origin 1,0
				Highlight NONE 
				Size 640,512
			EndScript
		EndObj
		AreaButton "Quit"
			Definition
				Origin 136,345
				Highlight NONE 
				Size 99,34
			EndScript
			OnClick
				;SetPen 17
				;FloodFill 140,350
				;ClipBrush 329,181,126,40,"Bsh3"
				;ShowBrushEfx "Bsh3",121,146,"Show",0.5,398,114
				;Delay 0,3,0
				Quit
				
			EndScript
		EndObj
		AreaButton "Risk"
			Definition
				Origin 405,342
				Highlight NONE 
				Size 104,38
			EndScript
			OnClick
				Flush "Resources/Copyright.bsh"
				SetDrawMode JAM1 
				SetPen 25
				AreaRectangle 407,345,99,34
				DetachObject "Risk"
				DetachObject "Quit"
				DetachObject "NIX"
				Delay 0,0,20
				ClearWindow
				ShowPalette "WB"
				OpenRequester "Prefs"
				
			EndScript
		EndObj
		IntervalTimer "T_1"
			NoAttach
			Definition
				Duration 0,0,10
			EndScript
			Occurred
				If C#ColorOfPixel(MouseX,MouseY)&MouseX<490
					Let C=ColorOfPixel(MouseX,MouseY),AR=1
					If G[0,-11-C]&C#0
						Dispose AM
						Let TX="2 Extra!",AR=2
					ElseIf G[0,-6]&C>0&C<10
						Let AM=Alias("G[0,-6]"),TX="North America:"
					ElseIf G[0,-7]&C>9&C<17
						Let AM=Alias("G[0,-7]"),TX="Europe:"
					ElseIf G[0,-8]&C>19&C<32
						Let AM=Alias("G[0,-8]"),TX="Asia:"
					ElseIf G[0,-9]&C>31&C<36
						Let AM=Alias("G[0,-9]"),TX="South America:"
					ElseIf G[0,-10]&C>35&C<42
						Let AM=Alias("G[0,-10]"),TX="Afrika:"
					ElseIf G[0,-11]&C>41&C<46
						Let AM=Alias("G[0,-11]"),TX="Australien:"
					ElseIf G[0,-5]&G[C,1]=P
						Let AM=Alias("G[0,-5]"),TX="Armies:"
					Else
						Dispose AM
						Let TX="?"
					EndIf
					SendToSubDeck "Armies","Print",TX||AM
					IfError
						OpenWindow "Armies","Amount",TX||AM
					EndIf
				ElseIf MouseX>550&AM
					Dispose AM
					Let TX="?"
					SendToSubDeck "Armies","Print",TX||AM
					IfError
						OpenWindow "Armies","Amount",TX||AM
					EndIf
				EndIf
			EndScript
		EndObj
		IntervalTimer "T_2"
			Definition
				Duration 0,1,0
			EndScript
			Occurred
				If G[-P,6]="AMIGA"
					Do "Amiga"
				EndIf
			EndScript
		EndObj
		RightMouseButton "UserRMB_1"
			OnClick
				Let C=ColorOfPixel(MouseX,MouseY);,Trd=0
				If MV
					Dispose MF,AR
					SendToSubDeck "Armies","Quit"
					IfError
					EndIf
				ElseIf ObjectAttached("T_1")
					If G[C,1]=P&~G[0,-11-C]&C#0
						Let AR=Min(AM,5)
						Let G[C,2]=G[C,2]+AR,G[-P,10]=G[-P,10]+AR,AG=C
						If AM
							Let AM=AM-AR
						EndIf
						If AM
							SendToSubDeck "Armies","Print",TX||AM
						ElseIf G[0,-5]=0
							Loop CT,-15,-6
								Dispose G[0,CT]
							EndLoop
							DetachObject "T_1"
							SendToSubDeck "Armies","Quit"
							SetRGB C,255,0,0
						Else
							Dispose G[0,-11-C]
							Let C=0
							DoObjectScript "T_1",OCCURRED 
						EndIf
						Do "Print",C
					EndIf
				EndIf
				
			EndScript
		EndObj
	EndObj
EndObj
