; $VER DC-Install 2.9 (18.01.99)

(if (> (exists ("env:Language")) 0) (set lanfound (getenv "Language")) )
(set lan 0)
(set #lanQuestion "Please select language:")
(set #nohelp "Sorry, no help-text.")
(if (= "deutsch" lanfound)
    (
        (set lan 1)
        (set #lanQuestion "Bitte Sprache auswählen:")
        (set #nohelp "Sorry, kein help-text.")
    )
)

(set DrawerNameParent @default-dest)
(set Update 0)
(set version 0)

(complete 0)

(set lan  (askchoice (choices "English" "Deutsch"
                         (prompt #lanQuestion)
                         (help #nohelp)
                         (default lan)
                     )
          )
)

(set #normal "normal")
(set #mwb1 "MagicWB (1)")
(set #mwb2 "MagicWB (2)")
(set #rare "RareDiamond")
(set #pal "PAL:LoRes")
(set #palh "PAL:HighRes")
(set #palhi "PAL:HighRes Interlace")
(set #ntsc "NTSC:LoRes")
(set #ntsch "NTSC:HighRes")
(set #ntschi "NTSC:HighRes Interlace")
(set #joy1 "Joystick Port 1")
(set #joy2 "Joystick Port 2")

(if (= lan 0)
    (
        (run "describe/sync SCRIPT=INSTALL.BATe") ; describe is (C) Dietmar Eilert
        (set #alreadyinstalled "You have already installed a version greater or equal 2.9!\n")
        (set #oldversion "You have already installed an older version of Diamond Caves.\nDo you want to update or install completely new?")
        (set #installnew "Install new")
        (set #update "Update")
        (set #copy "Copying..." )
        (set #deloldgfx "Deleteing old gfx-file")
        (set #regsource "english/Registration.txt")
        (set #guidesource "english/DiamondCaves.guide")
        (set #whichdrawer "In which drawer should Diamond Caves V2.9 be installed?\n(A directory called Diamond Caves will be created)")
        (set #createdrawer "Creating directory...")
        (set #whaticons "What kind of Icons do you want?")
        (set #noicons "none")
        (set #startupassign "An assign to \"DC:\" will be added to \"S:user-startup\" so that your system will be properly configured to use Diamond Caves.")
        (set #titlescreen "Which screenmode do you want the title screen of Diamond Caves to use?\n(Note that can select more screenmodes like DBL-PAL or CyberVision using the program Diamond Prefs)")
        (set #screenhelp "To play Diamond Caves with other Screenmodes (even gfx-board), use the program DiamondPrefs.")
        (set #settooltype "Setting ToolType...")
        (set #gamescreen "Which screenmode do you want the game to use?\n(Note that can select more screenmodes like DBL-PAL or CyberVision using the program Diamond Prefs)")
        (set #player1 "Which controlling device do you want Player 1 to use?")
        (set #player2 "Which controlling device do you want Player 2 to use?")
        (set #keyboard "Keyboard")
        (set #controlhelp "To redifine the controlling devices, use the program DiamondPrefs.")
        (set #end "\nDiamond Caves installation/update complete, please make sure to read at least the first chapter of the doc-file.\n\nIf you want to reconfigure Diamond Caves, please use the included program DiamondPrefs V1.4.\n\nAnd now enjoy Diamond Caves V2.9!")
    )
    (
        (run "describe/sync SCRIPT=INSTALL.BATd") ; describe ist (C) Dietmar Eilert
        (set #alreadyinstalled "Es ist schon eine Version größer oder gleich V2.9 installiert!")
        (set #oldversion "Es ist schon eine ältere Version von Diamond Caves installiert.\nWollen Sie dieVersion erneuern oder alles komplett neu installieren?")
        (set #installnew "neu installieren")
        (set #update "erneuern")
        (set #copy "Kopiere..." )
        (set #deloldgfx "Lösche alte gfx-datei")
        (set #regsource "deutsch/Registration.txt")
        (set #guidesource "deutsch/DiamondCaves.guide")
        (set #whichdrawer "Wo möchten Sie Diamond Caves V2.9 installieren?\n(Es wird eine Schublade mit dem Namen Diamond Caves erstellt)")
        (set #createdrawer "Erstelle Schublade...")
        (set #whaticons "Was für Icons möchten Sie?")
        (set #noicons "keine")
        (set #startupassign "Ein Assign auf \"DC:\" wird in die Datei \"S:user-startup\" eingefügt, damit Diamond Caves alle benötigten Dateien findet.")
        (set #titlescreen "In was für einem Screenmode soll das Hauptmenu von Diamond Caves laufen?\n(Andere Screenmodes wie DBL-PAL oder CyberVision können mit dem Programm Diamond Prefs eingestellt werden)")
        (set #screenhelp "Um Diamond Caves in einem anderen Screenmode zu betreiben (z.B. auf einer Grafikkarte), bitte das Programm Diamond Prefs benutzen)")
        (set #settooltype "Setze ToolType...")
        (set #gamescreen "In welchem Screenmode soll das Spiel laufen?\n(Andere Screenmodes wie DBL-PAL oder CyberVision können mit dem Programm Diamond Prefs eingestellt werden)")
        (set #player1 "Womit wollen Sie Spieler 1 steuern?")
        (set #player2 "Womit wollen Sie Spieler 2 steuern?")
        (set #keyboard "Tastatur")
        (set #controlhelp "Um die Steuerung nachträglich zu verändern, bitte das Programm Diamond Prefs benutzen")
        (set #end "\nDiamond Caves fertig installiert/erneuert, bitte lesen Sie als neuer Benutzer mindestens das einleitende Kapitel der Anleitung.\n\nUm die Vorgenommenen Einstellungen zu verändern, bitte das enthaltene Programm Diamond Prefs V1.4 benutzen.\n\nUnd nun viel Spaß mit Diamond Caves V2.9!")
    )
)

(if (exists "DC:" (NOREQ))
    (

        (set vernum (getversion "DC:DiamondCaves"))

        (set ver (/ vernum 65536))
        (set rev (- vernum (* ver 65536)))

        (set version (+ (* 100 ver) rev))

        (if (> version 208)
            (
                (message #alreadyinstalled)
                (exit (quiet))
            )
            (
                (set DrawerNameParent "DC:")
                (set DrawerName "DC:")
            )
        )

        (set Update
            (askchoice
                (prompt #oldversion)
                (choices #installnew #update)
                (help #nohelp)
                (default 1)
            )
        )
    )
)

(if (= Update 1)                  ; only Update according to present Version
    (
        (if (< version 103)         ; this is only for V1.0/1.1/1.2 (two fixed game levels)
            (
                (complete 30)
                (copyfiles
                    (prompt #copy)
                    (help #nohelp)
                    (source "UpdateFromV1.0-1.2")
                    (dest DrawerName)
                    (all)
                )
            )
        )

        (if (< version 104)         ; this is the update for V1.0-1.3 (ten new easy levels, one game level)
            (
                (complete 40)
                (copyfiles
                    (prompt #copy)
                    (help #nohelp)
                    (source "UpdateFromV1.0-1.3")
                    (dest DrawerName)
                    (all)
                )
            )
        )


        (if (exists "DC:gfx/x999" (NOREQ))
            (
                (complete 50)
                (delete
                        ("DC:gfx/x999")
                        (prompt #deloldgfx)
                        (help #nohelp)
                )
            )
        )

        (if (< version 106)         ; this is the update for V1.0-1.5 (new gfx-format)
            (
                (complete 60)
                (copyfiles
                    (prompt #copy)
                    (help #nohelp)
                    (source "UpdateFromV1.0-1.5")
                    (dest DrawerName)
                    (all)
                )
            )
        )

        (if (< version 107)         ; this is the update for V1.0-1.6 (all replay-files and new sfx-format)
            (
                (complete 70)
                (copyfiles
                    (prompt #copy)
                    (help #nohelp)
                    (source "UpdateFromV1.0-1.6")
                    (dest DrawerName)
                    (all)
                )
            )
        )

        (if (< version 108)         ; this is the update for V1.0-1.7 (2 replay-files)
            (
                (complete 75)
                (copyfiles
                    (prompt #copy)
                    (help #nohelp)
                    (source "UpdateFromV1.0-1.7")
                    (dest DrawerName)
                    (all)
                )
            )
        )

        (if (< version 200)         ; this is the update for V1.0-1.8 (sample y27)
            (
                (complete 80)
                (copyfiles
                    (prompt #copy)
                    (help #nohelp)
                    (source "UpdateFromV1.0-1.8")
                    (dest DrawerName)
                    (all)
                )
            )
        )

        (if (< version 203)         ; this is the update for V1.0-2.2 (gfx)
            (
                (complete 90)
                (copyfiles
                    (prompt #copy)
                    (help #nohelp)
                    (source "UpdateFromV1.0-2.2")
                    (dest DrawerName)
                    (all)
                )
            )
        )

        (if (< version 209)         ; this is the update for V1.0-2.7 (prg)
            (
                (complete 95)
                (copyfiles
                    (prompt #copy)
                    (help #nohelp)
                    (source "UpdateFromV1.0-2.7")
                    (dest DrawerName)
                    (all)
                )
            )
        )

        (if (exists "DC:Registration.txt" (NOREQ))
            (
                (copyfiles
                    (prompt #copy)
                    (help #nohelp)
                    (source #regsource)
                    (dest DrawerName)
                )
            )
        )

        (copyfiles                  ; new guide
            (prompt #copy)
            (help #nohelp)
            (source #guidesource)
            (dest DrawerName)
        )

    )

    (                             ; new install: copy everything to "DrawerName"
        (complete 0)
        (set DrawerNameParent
             (askdir
                 (prompt #whichdrawer)
                 (help #nohelp)
                 (default DrawerNameParent)
             )
        )

        (set DrawerName (tackon DrawerNameParent "DiamondCaves"))

        (complete 5)
        (makedir DrawerName
            (prompt #createdrawer)
            (help #nohelp)
            (infos)
        )

        (complete 10)
        (set icons
            (askchoice
                (prompt #whaticons)
                (choices #normal #mwb1 #mwb2 #rare #noicons)
                (help #nohelp)
                (default 0)
            )
        )

        (if (= icons 0)
            (set IconName "Icons/normal")
        )
        (if (= icons 1)
            (set IconName "Icons/MagicWB1")
        )
        (if (= icons 2)
            (set IconName "Icons/MagicWB2")
        )
        (if (= icons 3)
            (set IconName "Icons/RareDiamond")
        )

        (if (<> icons 4)
            (
                (copyfiles
                    (prompt #copy)
                    (help #nohelp)
                    (source IconName)
                    (dest DrawerName)
                    (pattern "DiamondCave#?")
                    (infos)
                )
                (copyfiles
                    (prompt #copy)
                    (help #nohelp)
                    (source IconName)
                    (dest DrawerName)
                    (pattern "Registration.tx#?")
                    (infos)
                )
                (copyfiles
                    (prompt #copy)
                    (help #nohelp)
                    (source IconName)
                    (dest DrawerName)
                    (pattern "Editor.pre#?")
                    (infos)
                )
            )
        )

        (complete 20)
        (copyfiles
            (prompt #copy)
            (help #nohelp)
            (source "new")
            (dest DrawerName)
            (all)
            (infos)
        )

        (complete 25)
        (copyfiles
            (prompt #copy)
            (help #nohelp)
            (source "UpdateFromV1.0-1.2")
            (dest DrawerName)
            (all)
        )

        (complete 30)
        (copyfiles
            (prompt #copy)
            (help #nohelp)
            (source "UpdateFromV1.0-1.3")
            (dest DrawerName)
            (all)
        )

        (complete 35)
        (copyfiles
            (prompt #copy)
            (help #nohelp)
            (source "UpdateFromV1.0-1.5")
            (dest DrawerName)
            (all)
        )

        (complete 40)
        (copyfiles
            (prompt #copy)
            (help #nohelp)
            (source "UpdateFromV1.0-1.6")
            (dest DrawerName)
            (all)
        )

        (complete 45)
        (copyfiles
            (prompt #copy)
            (help #nohelp)
            (source "UpdateFromV1.0-1.7")
            (dest DrawerName)
            (all)
        )

        (complete 50)
        (copyfiles
            (prompt #copy)
            (help #nohelp)
            (source "UpdateFromV1.0-1.8")
            (dest DrawerName)
            (all)
        )

        (complete 60)
        (copyfiles
            (prompt #copy)
            (help #nohelp)
            (source "UpdateFromV1.0-2.2")
            (dest DrawerName)
            (all)
        )

        (complete 65)
        (copyfiles
            (prompt #copy)
            (help #nohelp)
            (source "UpdateFromV1.0-2.7")
            (dest DrawerName)
            (all)
        )

        (copyfiles
            (prompt #copy)
            (help #nohelp)
            (source #regsource)
            (dest DrawerName)
        )

        (copyfiles
            (prompt #copy)
            (help #nohelp)
            (source #guidesource)
            (dest DrawerName)
        )


        (complete 70)
        (startup "Diamond Caves"
            (prompt #startupassign)
            (help #nohelp)
            (command "assign DC: \"" DrawerName "\"")
        )
        (makeassign "DC")
        (makeassign "DC" DrawerName)


        (complete 75)
        (if ((database "vblank") 50)
            (set DefaultMode 1)
            (set DefaultMode 3)
        )
        (set ScreenModeNum
            (askchoice
                (prompt #titlescreen)
                (choices #palh #palhi #ntsch #ntschi)
                (help #screenhelp)
                (default DefaultMode)
            )
        )
        (if (= ScreenModeNum 0)
            (set ScreenMode "PAL")
        )
        (if (= ScreenModeNum 1)
            (set ScreenMode "PAL-LACE")
        )
        (if (= ScreenModeNum 2)
            (set ScreenMode "NTSC")
        )
        (if (= ScreenModeNum 3)
            (set ScreenMode "NTSC-LACE")
        )
        (tooltype 
            (prompt #settooltype)
            (help #nohelp)
            (dest "DC:DiamondCaves")
            (settooltype "TITLEMODE" ScreenMode)
        )


        (complete 80)
        (if ((database "vblank") 50)
            (set DefaultMode 0)
            (set DefaultMode 2)
        )
        (set ScreenModeNum
            (askchoice
                (prompt #gamescreen)
                (choices #pal #palhi #ntsc #ntschi)
                (help #screenhelp)
                (default DefaultMode)
            )
        )
        (if (= ScreenModeNum 0)
            (set ScreenMode "PAL")
        )
        (if (= ScreenModeNum 1)
            (set ScreenMode "PAL-LACE")
        )
        (if (= ScreenModeNum 2)
            (set ScreenMode "NTSC")
        )
        (if (= ScreenModeNum 3)
            (set ScreenMode "NTSC-LACE")
        )
        (tooltype 
            (prompt #settooltype)
            (help #nohelp)
            (dest "DC:DiamondCaves")
            (settooltype "GAMEMODE" ScreenMode)
        )


        (complete 85)
        (set ControlNum
            (askchoice
                (prompt #player1)
                (choices #joy1 #joy2 #keyboard)
                (help #controlhelp)
                (default 1)
            )
        )
        (if (= ControlNum 0)
            (set ControlMode "JOY1")
        )
        (if (= ControlNum 1)
            (set ControlMode "JOY2")
        )
        (if (= ControlNum 2)
            (set ControlMode "KEYS")
        )
        (tooltype 
            (prompt #settooltype)
            (help #nohelp)
            (dest "DC:DiamondCaves")
            (settooltype "PLAYER1" ControlMode)
        )


        (complete 90)
        (set ControlNum
            (askchoice
                (prompt #player2)
                (choices #joy1 #joy2 #keyboard)
                (help controlhelp)
                (default 0)
            )
        )
        (if (= ControlNum 0)
            (set ControlMode "JOY1")
        )
        (if (= ControlNum 1)
            (set ControlMode "JOY2")
        )
        (if (= ControlNum 2)
            (set ControlMode "KEYS")
        )
        (tooltype 
            (prompt #settooltype)
            (help #nohelp)
            (dest "DC:DiamondCaves")
            (settooltype "PLAYER2" ControlMode)
        )

        (tooltype
            (prompt #settooltype)
            (help #nohelp)
            (dest "DC:DiamondCaves")
            (settooltype "GAMEPORTDEVICE" "OFF")
        )
    )
)

(complete 100)
(message #end)

(exit (quiet))
