;+----------------------------------------------------------------------------+
;|                                                                            |
;|                  PlayPac install script v1.3 (06-Feb-99)                   |
;|                                                                            |
;|                                                                            |
;#============================================================================#

;******************************************************************************
;Set strings
;******************************************************************************

(set #where-prompt 
  (cat "Where should I install PlayPac?\n A drawer called PlayPac will be created there.")
)

(set #abort-message
  (cat "PlayPac requires OS3.0 to run")
)

(set #gfxfile-prompt
  (cat "Please select graphic data")
)

(set #gfxfile-help
	(cat "PlayPac supports user selectable graphics." 
	     "This archive comes with three different graphic data: 'Lores16', 'Hires16' and 'Hires64'." 
	     "'Lores16' and 'Hires16' work on any Amiga, but 'Hires64' requires AGA chipset." 
	     "Please read PlayPac.guide for further info."
	)
)

(set #icon-prompt
  (cat "Please select icon outlook")
)

;******************************************************************************
;Here we go...
;******************************************************************************

(welcome)

(if (< (/ (getversion) 65536) 39) (abort #abort-message))

;******************************************************************************
;Ask destination directory
;******************************************************************************

(set where 
	(askdir 
		(prompt #where-prompt)
		(help @askdir-help)
		(default @default-dest)
	)
)

(if (exists (tackon where "PlayPac/"))
	(
		(delete (tackon where "PlayPac.info"))
		(foreach 
			(tackon where "PlayPac/") "#?"
			(delete (cat (tackon where "PlayPac/") @each-name))
		)
	)
	(
		(makedir (tackon where "PlayPac"))
	)
)

(set whereparent where)
(set @default-dest (tackon where "PlayPac/"))
(set where (tackon where "PlayPac/"))

;******************************************************************************
;Ask icon outlook
;******************************************************************************

(set iconchoice (askchoice (prompt #icon-prompt)
                           (help @askchoice-help)
                           (choices "Standard" "NewIcons" "MagicWB")
                           (default 0)
                )
)

;******************************************************************************
;Ask graphic data choice
;******************************************************************************

(set gfxchoice (askoptions (prompt #gfxfile-prompt)
                           (choices "Lores16" "Hires16" "Hires64")
                           (help #gfxfile-help)
                           (default 7)
               )
)

;******************************************************************************
;Copy files
;******************************************************************************

(copyfiles (source "Fonts/") (dest "SYS:Fonts") (all))
(copyfiles (source "PlayPac.guide") (dest where))
(copyfiles (source "PlayPac") (dest where))

;icon

(if (= iconchoice 0)
	(    
    		(copyfiles
			(source "Icons/Standard/PlayPac.info")
			(dest where)
		)
		(copyfiles
			(source "Icons/Standard/Guide.info")
			(dest where)
			(newname "PlayPac.guide.info")
		)
		(copyfiles
			(source "Icons/Standard/Drawer.info")
			(dest whereparent)
			(newname "PlayPac.info")
		)
	)
)

(if (= iconchoice 1)
	(    
    		(copyfiles
			(source "Icons/NewIcons/PlayPac.info")
			(dest where)
		)
		(copyfiles
			(source "Icons/NewIcons/Guide.info")
			(dest where)
			(newname "PlayPac.guide.info")
		)
		(copyfiles
			(source "Icons/NewIcons/Drawer.info")
			(dest whereparent)
			(newname "PlayPac.info")
		)
	)
)

(if (= iconchoice 2)
	(    
    		(copyfiles
			(source "Icons/MagicWB/PlayPac.info")
			(dest where)
		)
		(copyfiles
			(source "Icons/MagicWB/Guide.info")
			(dest where)
			(newname "PlayPac.guide.info")
		)
		(copyfiles
			(source "Icons/MagicWB/Drawer.info")
			(dest whereparent)
			(newname "PlayPac.info")
		)
	)
)

;graphics

(if (in gfxchoice 0) 
	(
		(makedir (tackon where "Lores16"))
		(copyfiles
			(source "Lores16/")
			(dest (tackon where "Lores16/"))
			(all)
		)
		(copyfiles
			(source "Lores16.init")
			(dest where)
		)
	)
)

(if (in gfxchoice 1)
	(
		(makedir (tackon where "Hires16"))
		(copyfiles
			(source "Hires16/")
			(dest (tackon where "Hires16/"))
			(all)
		)
		(copyfiles
			(source "Hires16.init")
			(dest where)
		)
	)
)

(if (in gfxchoice 2)
	(
		(makedir (tackon where "Hires64"))
		(copyfiles
			(source "Hires64/")
			(dest (tackon where "Hires64/"))
			(all)
		)
		(copyfiles
			(source "Hires64.init")
			(dest where)
		)
	)
)

;sound

(makedir (tackon where "Sound"))
(copyfiles 
	(source "Sound/")
	(dest (tackon where "Sound/"))
	(all)
)

;music

(makedir (tackon where "Music"))
(copyfiles 
	(source "Music/")
	(dest (tackon where "Music/"))
	(all)
)

;******************************************************************************
;That's it!
;******************************************************************************